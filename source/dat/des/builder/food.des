##########################################################################
# Helper function to weight the secondary food vaults properly.
# We want each level in the range to have a roughly even chance of placing
# the vault.
# The uppermost level we place it on is D:7, and the lowermost is D:14;
# thus the first level, D:7, has a 1 in 8 chance of placing it. If it
# doesn't place on D:7, D:8 has a 1 in 7 chance of placing it; D:9 has a
# 1 in 6 chance, etc. up until D:14 has a 100% chance.
# If the levels are traversed in order, there is therefore an even
# distribution of where the food vault is placed.
##########################################################################
{{
function food_weight(e, top)
  for i = 14, top, -1 do
    e.depth_weight("D:" .. i, 10000 / (15 - i))
  end
end
}}
##########################################################################
# Guarantee at least some really minimal food supply.
##########################################################################

NAME:   first_bread
DEPTH:  D:2-5
CHANCE: 2500, 3333 (D:3), 5000 (D:4), 10000 (D:5)
TAGS:   uniq_first_food extra transparent
ITEM:   bread ration no_mimic
MAP
d
ENDMAP

##########################################################################
# Midgame food supply.
#
# These should give around 20k nutrition.  Currently all strict
# carnivores have a large buffer so they can survive lulls in edible supply
# well until Crypt, so all-veggie is ok.
#
# normal/herbi/carni

##############################################################################
# A number of fruity vaults, by blue_anna
# [Condensed into one vault by HangedMan.]
# [Ruined by wheals.]
#
# The "fruiting_plant" tag uses this as a subvault for food vaults.
#
# TODO: Using KMONS and KITEM, we could have fruits on the same
#       square as a plant, though it'd change nutrition averages.
#
# The average of the old averages, for herbivore 3, seemed to have
# been ~10.1k nutrition. It now provides that much more directly.
#
NAME:   blue_anna_fruit_fields
TAGS:   fruiting_plant allow_dup unrand
KMONS:  P = plant
ITEM:   fruit q:4 no_mimic
NSUBST: . = 1:d / *:., " = 2:d / *:.
FTILE:  .dP = floor_grey_dirt / floor_grass w:20 / floor_dirt w:15
COLOUR: .dP = lightgrey / green w:20 / brown w:15
MAP
.""".
."P".
.""".
.....
ENDMAP

# 17.6k/26.6k/0
NAME:   the_bakery
DEPTH:  D:7-14
# Previously this was a 25% CHANCE; I'm assuming this was meant to weight this
# higher than the other food vaults, which mostly had a 15% CHANCE, but
# placing by chance tag doesn't work that way. Hence, there's also a WEIGHT
# tag to give it more weight than other food vaults.
: food_weight(_G, 7)
WEIGHT: 25
TAGS:   uniq_second_food extra chance_second_food
ITEM:   bread ration q:2 no_mimic
: if crawl.one_chance_in(8) then
:   dgn.delayed_decay(_G, 'e', 'spriggan name:baked n_adj n_spe n_des corpse')
: else
SUBST: =e = x
: end
SUBST: = = +
MONS:   spriggan col:white name:baker n_suf ; robe . dagger
MAP
xxxxxx
xdd=ex
x.xxxx
x.1xxx
x+xxxx
ENDMAP

# 14k
NAME:   david_nohive_1
DEPTH:  D:8-14
: food_weight(_G, 8)
WEIGHT: 15
TAGS:   uniq_second_food extra chance_second_food
MONS:   queen bee, killer bee
ITEM:   royal jelly no_mimic
COLOUR: a = yellow
TILE:   a = wall_wax
SUBST:  a = x
MAP
............
....aaaa....
..a2a2aaaa..
..aada2a2a..
.aa2ada2aaa.
.a2ada1ada2.
.aa2adada2a.
.aaa2a2a2aa.
..a2ada2aa..
...aa2aa2a..
....aaaa....
............
ENDMAP

# 20.6k/46.5k/0
NAME:     blue_anna_alchemist
DEPTH:    D:8-14
: food_weight(_G, 8)
WEIGHT: 15
TAGS:     uniq_second_food extra chance_second_food
SUBVAULT: X : fruiting_plant
SUBVAULT: W : fruiting_plant
KITEM:    p = potion of restore abilities w:2 / potion of poison w:3 / \
              potion of ambrosia w:1 / nothing
MONS:     big kobold ; wand of magic darts ident:type . short sword
MAP
XXXXX..WWWWW
XXXXX..WWWWW
XXXXX..WWWWW
XXXXX..WWWWW
x.x.c++c.x.x
ccccc..ccccc
cppc....cppc
cp.+.1..+.pc
cccc....cccc
cccccccccccc
ENDMAP

# 23.2k/19.9k/13k
# Not really a fish farm now that fish are gone...
NAME:   hangedman_fish_farm
DEPTH:  D:9-14
: food_weight(_G, 7)
WEIGHT: 15
TAGS:   uniq_second_food extra chance_second_food no_monster_gen no_pool_fixup \
        patrolling
KMONS:  A = merfolk ; spear
KFEAT:  g = shallow_water
ITEM:   meat ration q:2 no_mimic, bread ration q:3 no_mimic, throwing net
:       dgn.delayed_decay(_G, 'g', 'electric eel corpse')
MAP
xxxxxx
xgxedx
+..fAx
xWWWWx
xxxxxx
ENDMAP

# 18k/18.8k/16.6k (min 18k/12k/12k), plus perishable 6k/6k/0
NAME:   hangedman_pestilent_swarm
DEPTH:  D:8-14
: food_weight(_G, 8)
WEIGHT: 15
TAGS:   uniq_second_food extra chance_second_food no_monster_gen patrolling
MONS:   spider / brain worm w:5 / goliath beetle / worker ant
MONS:   wasp w:7 / boring beetle / soldier ant w:13
ITEM:   beef jerky q:3 no_mimic / fruit q:3 no_mimic
ITEM:   royal jelly q:3 no_mimic, royal jelly q:6 no_mimic
MAP
.xxxxxxx
@xx.x.xcccc
 ..x1x2cdec
@xx.x.x+fdc
.x1x.x2cccc
 xxxxxxx
ENDMAP

# 25k (min 20k)
NAME:   hangedman_little_slice_of_home
DEPTH:  D:8-14
: food_weight(_G, 8)
WEIGHT: 10
TAGS:   uniq_second_food extra chance_second_food
KMONS:  A = jelly
KMONS:  B = ooze
: if you.absdepth() > 10 then
KMONS:  C = slime creature
: end
KITEM:  C = royal jelly q:10 no_mimic
KFEAT:  C = altar_jiyva w:1 / . w:20
KMASK:  W = no_monster_gen
MARKER: + = lua:props_marker({ door_restrict="veto" })
MAP
 .Wxxxxx
.Wxxxxxxx
@.xBBAxxx
 W+BxA+Cx
@.xBBAxxx
.Wxxxxxxx
 .Wxxxxxx
ENDMAP

# 22.3k/18.8k/19k (min 9.6k)
NAME:     hangedman_ranch
DEPTH:    D:8-14
: food_weight(_G, 8)
WEIGHT:   10
TAGS:     uniq_second_food extra chance_second_food no_monster_gen patrolling
SUBVAULT: Z : fruiting_plant
KMONS:    A = three-headed hydra / w:5 two-headed hydra
KMONS:    B = sheep
KMONS:    C = yak
KMONS:    D = big kobold
KMONS:    E = big kobold ; nothing
KMONS:    F = plant
KITEM:    C = slice of pizza q:3 no_mimic
KITEM:    E = falchion ego:flaming ident:type / \
              hand axe ego:flaming ident:type
KITEM:    H = meat ration q:2 no_mimic
KITEM:    I = halberd
KFEAT:    EF = shallow_water
MAP
cccccccccc
cAH+...CCc
cIDcD..BBc
c+ccm+++cc
x.WFZZZZZx
xFWWZZZZZx
xWEFZZZZZx
xFW.ZZZZZx
xFFW.W.W.x
xxxxx@xxxx
ENDMAP

# 15.3k/34.2k/0
NAME:   hangedman_tree_tricks
DEPTH:  D:8-14
: food_weight(_G, 8)
WEIGHT: 10
TAGS:   uniq_second_food extra chance_second_food no_monster_gen
NSUBST: T = 1:. / 1:t
KMONS:  1 = wandering mushroom
KMONS:  2 = oklob sapling
KITEM:  12A_ = fruit q:2 no_mimic
KFEAT:  _ = altar_fedhas
MAP
 tttttttttt
tttt.ttttttt
@tt1t.tttttt
t.tt.t.Tt2tt
t.t.ttT.1.tt
t.t.ttt1_Att
t.t1tt2.Attt
tt.ttttttttt
 tttttttttt
ENDMAP

# 16.8k/21.3/8k
NAME:   hangedman_tell_no_tales
DEPTH:  D:9-14
: food_weight(_G, 9)
WEIGHT: 10
TAGS:   uniq_second_food extra chance_second_food
TAGS:   no_monster_gen no_item_gen patrolling
MONS:   spriggan skeleton / centaur skeleton
MONS:   troll skeleton / hungry ghost
MONS:   shadow imp
KMONS:  A = trapdoor spider w:6 / unseen horror / nothing w:4
ITEM:  fruit q:1 no_mimic
ITEM:  fruit no_mimic q:1
ITEM:  royal jelly q:2 no_mimic, bread ration q:3 no_mimic
SUBST:  D : DDDDE
NSUBST: D = 1:A / 2:d / 2:f / 1:h, E = 1:d / 1:e / 2:f / 2:h
: if crawl.coinflip() then
:     dgn.delayed_decay(_G, 'h', 'spriggan skeleton')
: else
:     dgn.delayed_decay(_G, 'h', 'ogre skeleton')
: end
: if crawl.coinflip() then
:     dgn.delayed_decay(_G, 'A', 'spriggan skeleton')
: else
:     dgn.delayed_decay(_G, 'A', 'ogre skeleton')
: end
MAP
 cccccccccc
ccxx1xh=DDc
c.x.x..mDDc
@.d.x2.mDDc
c.x.x..cc+c
ccxx1x.c3gc
 cccccccccc
ENDMAP

# 20.6k/23k/12.5k (min 18k/6k/0)
NAME:    hangedman_stone_soup
DEPTH:   D:10-14
: food_weight(_G, 8)
WEIGHT:  10
TAGS:    uniq_second_food extra chance_second_food patrolling no_rotate no_vmirror
# Branch representatives working together to cook some soup:
# D, Orc, Elf, Swamp, Shoals, Vaults, Crypt, Hells.
KMONS:   A = kobold
KMONS:   B = orc
KMONS:   C = deep elf fighter
KMONS:   D = spriggan
KMONS:   E = merfolk
KMONS:   F = human
KMONS:   G = necrophage
KMONS:   H = crimson imp
KITEM:   1 = stone q:1 no_pickup / large rock q:1 w:5
KITEM:   Ya = bread ration no_mimic / meat ration no_mimic
KITEM:   b = royal jelly no_mimic / fruit q:1 no_mimic
KFEAT:   1Yab = shallow_water
KMASK:   1Yac = no_monster_gen
MARKER:  Y = lua:fog_machine { cloud_type = "steam", pow_min = 1, \
                 pow_max = 10, delay = 60, size = 1, walk_dist = 0 }
MARKER:  Z = lua:fog_machine { cloud_type = "flame", pow_min = 100, pow_max = 100, \
                 delay = 10, size = 1, walk_dist = 0, excl_rad = 0 }
NSUBST:  c = 2:a / *:b
SHUFFLE: ABCDEFGH
MAP
 .......
..A...B..
..vv.vv..
..vcccv..
..vc1cv..
..vcYcv..
.CvvvvvD.
...Z.Z...
 .......
ENDMAP

# 22.0k/29.5k/0
# A tengu has staked out a little piece of the dungeon.
NAME:   grunt_tengu_nest
DEPTH:  D:10-14
: food_weight(_G, 10)
WEIGHT: 15
TAGS:   uniq_second_food extra chance_second_food no_monster_gen patrolling
MONS:   tengu warrior / tengu conjurer, raven, plant
NSUBST: 3 : 3:3 / *:.
ITEM:   bread ration no_mimic / fruit q:7
MAP
 xx+xx
xx3.3xx
x33333x
x33133x
xd2t2dx
xxdddxx
 xxxxx
ENDMAP

# 22.2k/17.7k/12.6k
# An ogre roasts intruders over his little campfire.
NAME:   grunt_ogre_cave
DEPTH:  D:7-14
: food_weight(_G, 7)
WEIGHT: 15
TAGS:   uniq_second_food extra chance_second_food no_monster_gen no_trap_gen \
        patrolling
{{
  local twochance = you.absdepth() - 8
  local onechance = 7 - twochance
  if twochance <= 0 then
    mons("ogre")
  elseif onechance <= 0 then
    mons("two-headed ogre")
  else
    mons("ogre w:" .. onechance .. " / two-headed ogre w:" .. twochance)
  end
  dgn.delayed_decay(_G, 'g', 'orc skeleton / human skeleton / elf skeleton'
                             .. ' / dwarf skeleton')
}}
ITEM:   bread ration q:3 no_mimic, beef jerky q:3 no_mimic, throwing net
MARKER: O = lua:fog_machine { cloud_type = "flame", pow_min = 10, \
                              pow_max = 10, delay = 10, size = 1, \
                              walk_dist = 0, excl_rad = 0 }
MAP
 xxxxx
xxxxxxx
xxeegxx
xx1Ogxx
xxdf.xx
 xxx.xx
 xx.xxx
xxx.xxx
xx.xxx.
 .g.
  @
ENDMAP

# 20.8k/23.8k/12.0k
# Welcome to Pandemonium Pizzeria; is this for carry-out or delivery?
# (Ideally the cashier would have a silly hat, but they can't wear hats.)
NAME:   grunt_pizzeria
DEPTH:  D:8-14
: food_weight(_G, 8)
WEIGHT: 10
TAGS:   uniq_second_food extra chance_second_food no_monster_gen no_trap_gen \
        patrolling
MONS:   deep elf fighter ; rapier | falchion / \
        human ; short sword | rapier / \
        orc warrior ; dagger / \
        spriggan ; dagger
ITEM:   slice of pizza q:8 no_mimic, bread ration q:2 no_mimic
MARKER: O = lua:fog_machine { cloud_type = "flame", pow_min = 10, \
                              pow_max = 10, delay = 10, size = 1, \
                              walk_dist = 0, excl_rad = 0 }
MAP
xxxxxxx
xvvvdex
xvO+..x
xvvv.xx
x$1...x
xmmm.xx
x.....x
xmm+mmx
 ..@..
ENDMAP

# 22.6k/22.6k/15k (+ perishable up to 16k, avg 6k)
NAME:   grunt_livestock
DEPTH:  D:7-14
: food_weight(_G, 7)
WEIGHT: 15
TAGS:   uniq_second_food extra chance_second_food no_monster_gen no_trap_gen \
        patrolling
MONS:   human ; animal skin . hand axe
{{
  local sheep_weight = 14 - you.absdepth()
  if sheep_weight <= 0 then
    mons("hog / yak")
  else
    mons("sheep w:" .. sheep_weight .. " / hog w:1 / yak w:1")
  end
}}
ITEM:   slice of pizza q:7 no_mimic, bread ration q:2 no_mimic
ITEM:   beef jerky q:2 no_mimic
KFEAT:  K = iron_grate
KFEAT:  - = open_door
MAP
.xxxxxxx
@+...fdx
.x...1ex
 xKK-KKx
 x2...2x
 x.2.2.x
 xxxxxxx
ENDMAP

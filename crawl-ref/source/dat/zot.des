##############################################################################
# zot.des: The ultimate goal. The Orb must be placed (Z). These maps can and
#          should be serious challenges! Players dying on Zot:5 are okay :)
##############################################################################


###########################
#
# Zot petite vaults
#
NAME:   lemuel_baited_zot_trap
DEPTH:  Zot:*
WEIGHT: 20
TAGS:   allow_dup
KFEAT:  * = Zot trap
KITEM:  * = any good_item
MAP
*
ENDMAP

NAME:   lemuel_zot_downstairs
DEPTH:  Zot:1-4
ORIENT: float
TAGS:   allow_dup
SUBST:  . = .^
KFEAT:  ^ = Zot trap
SUBST:  > = }])
MAP
xxxxxxx
@....>x
xxxxxxx
ENDMAP

NAME:   lemuel_zot_upstairs
DEPTH:  Zot:2-5
ORIENT: float
TAGS:   allow_dup 
SUBST:  . = .^
KFEAT:  ^ = zot trap
SUBST:  < = ([{
MAP
xxxxxxx
@....<x
xxxxxxx
ENDMAP


###########################
# Halls of Wrath
#
NAME:   lemuel_halls_of_wrath
DEPTH:  Zot:1-4
ORIENT: float
TAGS:   no_rotate
NSUBST: 1 = 6:2 / *=1
SUBST:  1 = 1 .:5
SUBST:  1 = 1 3
MONS:   troll /deep troll / iron troll / ogre / two-headed ogre / w:2 iron devil
MONS:   moth of wrath
MONS:   hill giant / stone giant / fire giant / frost giant / ettin /\
        w:5 efreet / w:3 titan
NSUBST: C = 2:= / *=c
NSUBST: D = 2:= / *=c
MAP
cccccccccccccccccccccccc
n111111111111111111.c..c
n111111111111111111.c.)c
cCCCCCCCCCCCCCCcc++cc..c
n....F....F....F....c..c
+...................+..c
+...................+.}c
+...................+..c
n....F....F....F....c..c
cDDDDDDDDDDDDDDcc++cc..c
n111111111111111111.c.]c
n111111111111111111.c..c
cccccccccccccccccccccccc
ENDMAP

###########################
# Lich library
#
NAME:   lemuel_lich_library
DEPTH:  Zot:1-4
TAGS:   no_monster_gen
MONS:   flying skull, lich, ancient lich, necromancer, vampire mage, mimic
ITEM:   any book, any scroll
SUBST:  w:w?
SUBST:  ?=. 1:1
SUBST:  x:xc
SUBST:  +=+=
SUBST:  d = d 6:1
SUBST:  4 = 4 5:2 .:4
SUBST:  d = d:20 e
MAP
wwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwww
wwxxxxxxxxx+xxxxxxxxxww
wwx....x.......x....xww
wwx....+...F...+....xww
wwx.4..x.......x..4.xww
wwx4.4.xxxx+xxxx.4.4xww
wwx.4.4xxx...xxx4.4.xww
wwx4.4.xxx.2.xxx.4.4xww
wwxxxxxxxxx+xxxxxxx+xww
wwxxxxxxxxx.xxxxxx$$xww
wwxdddxxxxx.x...xx$$xww
wwx.2.xxxxx.+.F.xxxxxww
wwx...xxxxx.x...xxxxxww
wwx...xxxxx.xxxxxxxxxww
wwxx+xxxxxx.xxxdddddxww
wwxx.xxxxxx.xxxd232dxww
wwxx.xxxxxx.xxxd...dxww
wwxx......+...+....dxww
wwxxxxxxxxxmxxxd...dxww
wwxxxxxxxxxFxxxdddddxww
wwxxxxxxxxxxxxxxxxxxxww
wwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwww
ENDMAP

###########################
# Lake of fire
#
NAME:  lemuel_lake_of_fire
DEPTH: Zot:*
SUBST: * =*l
KMONS: * = orb of fire
KFEAT: * = l
SUBST: .=.l
MAP
 ..ll..
..llll..
.llllll.
.ll**ll.
.ll**ll.
.llllll.
..llll..
 ..ll..
ENDMAP

###########################
# Globe of electric golems
#
NAME:  lemuel_golem_globe
DEPTH: Zot:*
TAGS:  no_rotate
KFEAT: T = teleport trap
SUBST: 1 = 1.
MONS:  electric golem
MAP
  mmmm
 mm..mm
mm.11.mm
m.1TT1.m
mm.11.mm
 mm..mm
  mmmm
ENDMAP

###################################################################
# Draconian Sentry Boxes
# There is an 8/27 chance all three downstairs will be here.
#
NAME:   lemuel_draconian_sentries
DEPTH:  Zot:1-4
ORIENT: float
TAGS:   uniq_sentry no_rotate
MONS:   red draconian scorcher / green draconian zealot / yellow draconian caller
SUBST:  }=}}> , )=))> , ]=]]> , 1=123, w:w. , m:mn
MAP
............................
..mmm..................mmm..
..m1m..................m1m..
..mmm...mmmwwwwwwmmm...mmm..
........m1mwwwwwwm1m........
........mmmwwwwwwmmm........
mmm.....www...}..www.....mmm
m1m.....www..)...www.....m1m
mmm.....www...]..www.....mmm
........mmmwwwwwwmmm........
........m1mwwwwwwm1m........
..mmm...mmmwwwwwwmmm...mmm..
..m1m..................m1m..
..mmm..................mmm..
............................
ENDMAP

NAME:   lemuel_draconian_sentries_small
DEPTH:  Zot:1-4
ORIENT: float
TAGS:   uniq_sentry no_rotate
MONS:   red draconian scorcher / green draconian zealot / yellow draconian caller
SUBST:  }=}}> , )=))> , ]=]]>, 1=123 , w:w. , m:mn
MAP
................
................
..mmmwwwwwwmmm..
..m1mwwwwwwm1m..
..mmmwwwwwwmmm..
..www...}..www..
..www..)...www..
..www...]..www..
..mmmwwwwwwmmm..
..m1mwwwwwwm1m..
..mmmwwwwwwmmm..
................
................
ENDMAP


###########################
# Acid trip
# chance for having all downstairs in the vault is 12.5%
#
NAME:   lemuel_acid_trip
DEPTH:  Zot:1-4
ORIENT: float
MONS:   oklob plant, acid blob, jelly, yellow draconian
MONS:   any nonbase yellow draconian
SUBST:  1 = 1:2 .
SUBST:  . = .:20 3:3 2:1
SUBST:  }=}>, )=)>, ]=]>
MAP
       xx++xx
     xxx....xxx
   xxx..4444..xxx
  xx....4554....xx
 xx.....cccc.....xx
 x....ccc..ccc....x
xx...cc......cc...xx
x...cc..1111..cc...x
x...c...1}11...c...x
x.5.c...11]1...c.5.x
x...c...1)11...c...x
x...cc..1111..cc...x
xx...cc......cc...xx
 x....cc....cc....x
 xx....c....c....xx
  xx............xx
   xxx..4444..xxx
     xxx4554xxx
       xxxxxx
ENDMAP

###########################
# Firehouse
#
NAME:   lemuel_firehouse
DEPTH:  Zot: 1-4
ORIENT: float
MONS:   orb of fire
MONS:   fire elemental
MONS:   red draconian / weight:2 mottled draconian
MONS:   any nonbase red draconian
MONS:   efreet
SUBST:  1=122, 3=322
SUBST:  4 = 2:20 4:5 3:15 .:20
MAP
          lllll
       lllllllllll
     lllll.....lllll
    llll....x....llll
   llll....xxx....llll
   llll...xx}xx...llll
  llll...xx]2)xx...llll
  llll..xx2...2xx..llll
 llll..xx...5...xx..llll
 llll..x...2.2...x..llll
llll...x.2xx+xx2.x...llll
llll...x.xx444xx.x...llll
llll...x.x44444x.x...llll
 llll..x.x44F44x.x..llll
 llll..x.x44444x.x..llll
  llll.x.xx444xx.x.llll
  llll.x.1x...x1.x.llll
   llllxxxx+++xxxxllll
    llll333...333llll
     lllll.....lllll
       lllllllllll
          ll>ll
ENDMAP


##############################################################################
# hall_of_Zot_5

NAME:       hall_of_Zot
PLACE:      Zot:5
ORIENT:     north
LFLAGS:     no_tele_control
MARKER:     Z = lua:item_pickup_change_flags { \
                level_flags="!no_tele_control", item="Orb of Zot" \
            }
LFLOORCOL:  magenta
LROCKCOL:   lightmagenta
LFLOORTILE: floor_tomb
LROCKTILE:  wall_zot_magenta

#traps
SUBST:    C = c:1000 =
KFEAT:    1 = any trap / w:20 floor
KMONS:    1 = patrolling Orb Guardian
KFEAT:    8 = any trap / w:20 floor
SHUFFLE:  ABab / ^^FF / ^^FF / ^^FF
SHUFFLE:  Aa / Bb 
KFEAT:    A = any trap / w:30 floor
KFEAT:    B = any trap / w:10 floor
SUBST:    a=F, b=6 F
SUBST:    ^ = ^ .:10
SUBST:    ! = ^ .:40
SUBST:    . = .:500 ^

#monsters
MONS:     patrolling Orb Guardian, Killer Klown, electric golem, orb of fire
MONS:     ancient lich, orange crystal statue / silver statue / ice statue
SUBST:    8=8., 8=8., 5=5.
SHUFFLE:  234
SUBST:    ? = .:8 ?
SUBST:    ? : KLM
SUBST:    K = 4:30 5 8 9
SUBST:    L = 4 5:30 8 9
SUBST:    M = 4 5 8:30 9

MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxXXXXXXXXXXXXXXXxxxxxxxxxxxxxxxxxxxxxxxXXXXXXXXXXXXXXXxxxxxxxxxxxxx
xxxxxxxxxxxXXXX.............XXXXxxxxxxxxxxxxxxxxxXXXX.............XXXXxxxxxxxxxx
xxxxxxxxxxXX...................XXXxxxxxxxxxxxxxXXX...................XXxxxxxxxxx
xxxxxxxxxXX..........3...........XXxxxxxxxxxxxXX...........3..........XXxxxxxxxx
xxxxxxxxxX..8.....................XXXXXXXXXXXXX.....................8..Xxxxxxxxx
xxxxxxxxxX.......?..?.....8.8.....XXX...1...XXX.....8.8.....?..?.......Xxxxxxxxx
xxxxxxxxxXX.8...........?.........XX..1...1..XX.........?...........8.XXxxxxxxxx
xxxxxxxxxxXX......................X1...!!!...1X......................XXxxxxxxxxx
xxxxxxxxxxxXX.....4....2....8.8......1.!Z!.1......8.8....2....4.....XXxxxxxxxxxx
xxxxxxxxxxXX....?...?.....?...?...X1...!!!...1X...?...?.....?....?...XXxxxxxxxxx
xxxxxxxxxXX.......?....?..........XX..1...1..XX..........?....2.......XXxxxxxxxx
xxxxxxxxxX.....8..........8.8.....XXX...1...XXX.....8.8..........8.....Xxxxxxxxx
xxxxxxxxxX...8....................cXXXXXXXXXXXc....................8...Xxxxxxxxx
xxxxxxxxxXX.....8.8.8..8.........ccccccccccccccc........8...8.8.8.....XXxxxxxxxx
xxxxxxxxxxXX...............8...ccccccccccccccccccc...8............8..XXxxxxxxxxx
xxxxxxxxxxxXX........8..8...ccccccccccccccccccccccccc...8..8........XXxxxxxxxxxx
xxxxxxxxxxxxXXa111acCCCCCCCcccccccccccccccccccccccccccCCCCCCCcb111bXXxxxxxxxxxxx
xxxxxxxxxxxcc...............AA1.ccccccccccccccccc.1BB...............ccxxxxxxxxxx
xxxxxxxxxxcc...8.8..8.8.....cc1...ccccccccccccc...1cc.......8..8.8.8.ccxxxxxxxxx
xxxxxxxxxcc.8...8......8.8...ccc...ccccccccccc...ccc...8.8.8..8.......ccxxxxxxxx
xxxxxxxxxc.8..8....8.8........ccc...............ccc.....8....8..8.8....cxxxxxxxx
xxxxxxxxxc..8..8.8....8.8.8.5.cxcc.............ccxc.5.8.8.8....8.8...8.cxxxxxxxx
xxxxxxxxxc..8.....8.8.......8.cxxc.............cxxc8........8.....8.8..cxxxxxxxx
xxxxxxxxxc.8...8......8.8.....cxxcc.1...1...1.ccxxc......8.......8.....cxxxxxxxx
xxxxxxxxxc8..8...8.8.8.8...8.8cxxxc...........cxxxc8.8..8..8.8.8...8.8.cxxxxxxxx
xxxxxxxxxc.....8.........8....cxxcc...........ccxxc....8.8.............cxxxxxxxx
xxxxxxxxxcc...5..8..8.8....8.ccxxc.............cxxcc8.....8....8..5.8.ccxxxxxxxx
xxxxxxxxxxcc8......8....8.8.ccxxcc.............ccxxcc.8.8....8....8..ccxxxxxxxxx
xxxxxxxxxxxcc..............ccxxxc...............cxxxcc..............ccxxxxxxxxxx
xxxxxxxxxxxxccccccccccccccccxxxxcccccccc@ccccccccxxxxccccccccccccccccxxxxxxxxxxx
ENDMAP


################################################################
# For colouring the walls and floors of the first four levels

NAME:       hall_of_Zot_1
PLACE:      Zot:1
TAGS:       transparent
ORIENT:     float
LFLOORCOL:  lightgrey
LROCKCOL:   lightgrey
LFLOORTILE: floor_tomb
LROCKTILE:  wall_zot_gray
MAP
.
ENDMAP

NAME:       hall_of_Zot_2
PLACE:      Zot:2
TAGS:       transparent
ORIENT:     float
LFLOORCOL:  lightgrey
LROCKCOL:   blue
LFLOORTILE: floor_tomb
LROCKTILE:  wall_zot_blue
MAP
.
ENDMAP

NAME:       hall_of_Zot_3
PLACE:      Zot:3
TAGS:       transparent
ORIENT:     float
LFLOORCOL:  blue
LROCKCOL:   lightblue
LFLOORTILE: floor_tomb
LROCKTILE:  wall_zot_cyan
MAP
.
ENDMAP

NAME:       hall_of_Zot_4
PLACE:      Zot:4
TAGS:       transparent
ORIENT:     float
LFLOORCOL:  lightblue
LROCKCOL:   magenta
LFLOORTILE: floor_tomb
LROCKTILE:  wall_zot_blue
MAP
.
ENDMAP

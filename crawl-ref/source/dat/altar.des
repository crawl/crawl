###############################################################################
# altar.des: This is the place where minivaults (and floating vaults) 
#            containing altars should go. Vaults particular to a specific
#            branch (Beogh altar minivaults for Orc:*, for example), may
#            go in the corresponding des-file. A few entry vaults contain
#            altars; these should always be in entry.des.
#
#            There are two reasons for keeping altar vaults separate:
#            * This makes it easier to adapt for new gods.
#            * Easier tweaking of altar chances.
###############################################################################

######################################
# I    Temples (Multi-altar vaults)
######################################

NAME:   minitemple
TAGS:   allow_dup
# More common than the others.
CHANCE: 20
MAP
cccccccccc
cBcBcBcBcc
G.c.c.c.Bc
@.......Bc
@.......Bc
G.c.c.c.Bc
cBcBcBcBcc
cccccccccc
ENDMAP

NAME:    jmf_multi_god_temple
SHUFFLE: abc
SUBST:   a:=, b:x, c:x
DEPTH:   D:8-21, Vault
MAP
............
.axxxxxxxxa.
.x9......9x.
.bT......Tb.
.x..C..C..x.
.cT......Tc.
.xxxxmmxxxx.
.xxx$$$$xxx.
.xx8....8xx.
..xx....xx..
...xG..Gx...
............
ENDMAP

NAME:  jmf_multi_god_temple2
DEPTH: D:8-21, Vault
MAP
............
..vvvvvvvv..
.vv......vv.
.v..x..x..v.
.v.Cx..xC.v.
.v..x..x..v.
.vT8x..x8Tv.
.vvvx==xvvv.
...Gx99xG...
...+*99*+...
...GxxxxG...
............
ENDMAP


######################################
# II   General altars
######################################

NAME: old_standard_altar
TAGS: allow_dup
SHUFFLE: cvxx
MAP
...........
.xxxx.xxxx.
.xx.....xx.
.x.......x.
.x.......x.
.....C.....
.x.......x.
.x.......x.
.xx.....xx.
.xxxx.xxxx.
...........
ENDMAP

NAME:    lemuel_round_altar
TAGS:    allow_dup
SHUFFLE: cvxx
SUBST:   X:x.
MAP
   xxXxx
  xx...xx
 xx.....xx
 x.......x
 X...C...@
 x.......x
 xx.....xx
  xx...xx
   xxXxx
ENDMAP

NAME: lemuel_crystal_altar
TAGS: allow_dup
MAP
b.b
.C.
b.b
ENDMAP

NAME:   lemuel_altar_in_water
DEPTH:  D:2-18, Swamp, Shoal
CHANCE: 9
: local brnd = crawl.random2(13)
: if brnd > 10 then
TAGS: no_monster_gen no_rotate
# this case almost always has altar access
: else
TAGS: no_monster_gen no_pool_fixup no_rotate
: end
MAP
   .........   
  ...wwwww...  
 ..wwwwwwwww.. 
..wwwwwwwwwww..
.wwwwwwwwwwwww.
.wwwwwwCwwwwww.
.wwwwwwwwwwwww.
..wwwwwwwwwww..
 .wwwwwwwww..
 ...wwwww... 
   ........  
ENDMAP

NAME:   lemuel_altar_in_water2
TAGS:   no_pool_fixup no_monster_gen
DEPTH:  D:2-18, Lair, Snake, Swamp, Shoal
CHANCE: 1
MAP
   www
  wwwww
 wwwwwww
 @..Cwww
 wwwwwww
  wwwww
   www
ENDMAP

NAME:  lemuel_oklob_altar
DEPTH: D:6-18, Lair, Snake, Shoal
MONS:  oklob plant, plant
MAP
 2
2C1
 2
ENDMAP

NAME:    lemuel_hidden_altar
DEPTH:   D:1-18, Vault, Lair, Elf, Crypt
SHUFFLE: XY
SUBST:   X = .
SUBST:   Y = =
MAP
.........
.xxxXxxx.
.xxx.xxx.
.xxxYxxx.
.X.YCY.X.
.xxxYxxx.
.xxx.xxx.
.xxxXxxx.
.........
ENDMAP

NAME:   lemuel_sealed_altar_a
CHANCE: 3
DEPTH:  D:2-18, Vault, Elf, Crypt
MAP
xxxxxxxxxxxxx
x...........x
x...........x
x...........x
x...........x
x.....>.....x
x.....C.....x
x...........x
x...........x
x...........x
x...........x
xxxxxxxxxxxxx
ENDMAP

NAME: lemuel_sealed_altar_b
CHANCE: 7
DEPTH: D:2-18, Vault, Elf, Crypt
MAP
xxxxxxxxxxx
x.........x
x.........x
x....>....x
x....C....x
x.........x
x.........x
xxxxxxxxxxx
ENDMAP

NAME:  lemuel_tele_altar
DEPTH: D:2-18, Vault 
SUBST: Y = X x
KFEAT: X = teleport trap
MAP
.............
.xxxxxXxxxxx.
.x.........x.
.x.........x.
.x.........x.
.Y....C....Y.
.x.........x.
.x.........x.
.x.........x.
.xxxxxYxxxxx.
.............
ENDMAP

NAME:  lemuel_statue_altar
DEPTH: 2-18, Vault, Elf
: if you.absdepth() < 10 then
SUBST: F = G
: end
MAP
.........
.ccccccc.
.F...>Cc.
.ccccccc.
.........
ENDMAP

NAME:   lemuel_coloured_pillars_altar
DEPTH:  D:2-18, Vault, Elf
TAGS:   no_rotate
COLOUR: c = blue / yellow / red / cyan
MAP
xxxxxxxxxxxxxxxxxxxx
...................x
c...c...c...c...c..x
@.................Cx
c...c...c...c...c..x
...................x
xxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   lemuel_upstairs_altar
DEPTH:  D:2-12, Vault, Elf, Crypt
ORIENT: float
SUBST:  T:T., )=)}], c:ccx
MAP
ccccccc
cT...Tc
c.....ccccccc
c..C..+....)c
c.....ccccccc
cT...Tc
ccccccc
ENDMAP

######################################
# III  Special altars
######################################

NAME:  lemuel_angel_altar
DEPTH: D:3-18, Vault
MONS:  angel/daeva
KFEAT: C = altar_elyvilon / altar_zin / altar_shining_one
MAP
   .....
  ..xmx..  
 ..xx>xx.. 
..xxx.xxx..
.xxxx1xxxx.
.m>..C..>m.
.xxxx.xxxx.
..xxx.xxx..
 ..xx>xx.. 
  ..xmx..
   .....
ENDMAP

NAME:  lemuel_hellish_altar
DEPTH: D:2-21, Crypt, Geh
MONS:  iron devil/orange demon, beast/rotting devil, lemure/manes, hellion
SUBST: 2 = 23
SUBST: 2 = 12
SUBST: 3 = 3L
: if you.absdepth() >14 then
SUBST: 1 = 1:20 4
: end
SUBST: L = l.
SUBST: C = C / P:1
KFEAT: C = altar_lugonu/altar_yredelemnul/altar_kikubaaqudgha/altar_makhleb
KFEAT: P = enter_abyss
MAP
  ..LLllLL.... 
  .LllllllLLL..
..Lll323lllLLL.
.LLl32123lllLL.
.LLl33C33llLL..
..Lll323llLL...
..LLll3llL.....
...LLlllL...   
....LLLL....   
   ..LLLLL..   
   ....LLLL.   
ENDMAP

NAME:  david_defended_altar_orc
DEPTH: D:7-20, Orc
MONS:  orc priest
TAGS:  uniq_defended_altar
KFEAT: D = altar_beogh / w:1 altar_okawaru / w:1 altar_makhleb / w:1 altar_trog
MAP
ccccc...
c1..c...
c1D.+.I@
c1..c...
ccccc...
ENDMAP

NAME:  david_defended_altar_elf
DEPTH: D:7-20, Elf
MONS:  deep elf priest / w:30 nothing
TAGS:  uniq_defended_altar
MAP
ccccc...
c1..c...
c1C.+.G@
c1..c...
ccccc...
ENDMAP

NAME:   lemuel_blue_sif_altar
DEPTH:  D:2-18, Elf, Vault
TAGS:   no_monster_gen mini_float
COLOUR: . = blue
KFEAT:  C = altar_sif_muna
MAP
xxxxxxxxxxxxxx
...........xxx
@..........+Cx
...........xxx
xxxxxxxxxxxxxx
ENDMAP

NAME:  lemuel_dead_fish_altar
DEPTH: D:2-18, Crypt
TAGS:  no_pool_fixup
KMONS: 1 = giant goldfish zombie / big fish zombie / nothing
KFEAT: 1 = deep_water
KFEAT: C = altar_kikubaaqudgha / altar_yredelemnul
MAP
xxxxxxxxxxxxxxxx
xxxx11111111xxxx
xxxxwwwwwwwwxxxx
@.............Cx
xxxxwwwwwwwwxxxx
xxxx11111111xxxx
xxxxxxxxxxxxxxxx
ENDMAP

# The mix of deep and shallow water is intended to keep the centaurs from 
# escaping without requiring the player to be able to cross deep water.
NAME:  lemuel_centaur_altar
DEPTH: D:7-20, Shoal, Lair
TAGS:  no_pool_fixup no_monster_gen
MONS:  centaur
MAP
xxxxxxxxxxxxx
xxxxxxxxxxwxx
xxwx.1.1.xxwx
xwxx1.C.1xwwx
xwwxWwwWwWwxx
xwwWwwwwwwxxx
xxwWwWWwwWxxx
xxxwWwwWwxxxx
xxxwwwWwWxxxx
xxx......xxxx
ENDMAP

NAME:   lemuel_mausoleum_altar
DEPTH:  D:7-20, Crypt
MONS:   w:50 human zombie/orc zombie/elf zombie/w:5 ogre zombie/w:5 troll zombie
MONS:   w:50 human skeleton/orc skeleton/elf skeleton/w:5 ogre skeleton/w:5 troll skeleton
MONS:   mummy, wight, wraith, necrophage, ghoul
NSUBST: ? = 1:C / *:1
SUBST:  1 = 1:25 2 3 4:8 5:6 6:4 7:2
KFEAT:  C = altar_kikubaaqudgha / altar_yredelemnul
KMONS:  C = human zombie
MAP
ccccccc
c?+.+?c
ccc.ccc
c?+.+?c
ccc.ccc
c?+.+?c
ccc.ccc
c?+.+?c
ccc.ccc
c?+.+?c
ccc.ccc
c?+.+?c
ccc.ccc
ccc+ccc
cGc@cGc
ENDMAP

###############################################################################
# snake.des: All maps and branch endings relating to the Snake Pit.
###############################################################################

##############################################################################
# Snake Pit entry vaults
##############################################################################

NAME:   dummy_snake_entry
TAGS:   snake_entry
KFEAT:  O = enter_snake_pit
MAP
O
ENDMAP

NAME:   minmay_snake_entry_guardpost_small
TAGS:   snake_entry
ORIENT: float
MONS:   naga, naga / nothing w:3
KFEAT:  O = enter_snake_pit
MAP
ccc.@.ccc
c.......c
c.2...2.c
.........
@...O...@
.........
c.1...2.c
c.......c
ccc.@.ccc
ENDMAP

NAME:   minmay_snake_entry_guardpost_medium
TAGS:   snake_entry
WEIGHT: 5
ORIENT: float
MONS:   naga, naga / nothing
KFEAT:  O = enter_snake_pit
MAP
   ccccccc
 ccc.....ccc
cc2.......2cc
c...c2.1c...c
@...c.O.c...@
c...c1.2c...c
cc2.......2cc
 ccc.....ccc
   ccccccc
ENDMAP

NAME:   minmay_snake_entry_guardpost_large
TAGS:   snake_entry
WEIGHT: 2
ORIENT: float
MONS:   naga warrior, naga / nothing
KFEAT:  O = enter_snake_pit
MAP
c@ccccccc
c.......cc
c..c.c..2cc
c.........cc
c..c.c.c..2cc
c......2....cc
cc2..c1O2c..2cc
 cc....2......c
  cc2..c.c.c..c
   cc.........c
    cc2..c.c..c
     cc.......c
      ccccccc@c
ENDMAP

NAME:   minmay_snake_entry_six_corridors
TAGS:   snake_entry ruin_lair
ORIENT: float
MONS:   ball python, adder, water moccasin, black mamba
KMONS:  P = plant / fungus w:1 / nothing
SUBST:  1 = 1., 2 = 2., 3 = 3.
SUBST:  1 = 122223333, 2 = 122333344, 3 = 33444
KFEAT:  O = enter_snake_pit
MAP
xxxx.@1xxxxxxP@xxxxx
xxxx1.PxxxxxP..xxxxx
xxxxx..xxxxx1.xxxxxx
xxxxxP..xxxx..xxxxxx
xxxxxP.1xxxx2..xxxxx
xxxxxx..Pxxxx1Pxxxxx
xxxxxxx2.xxxP.xxxxxx
xxxxxxxP1xx1..xxxxxx
xxxxxxxx...3.xxxxxxx
xxxxxxxP1..P1.xxxxxx
xx21xxP..PO.P..2xx1P
P.....1.3.1.xx.....@
@.1PxxxP..xxxx.Px1P.
xxxxxxxx.1xxxx2xxxxx
xxxxxxxx..Pxx..xxxxx
xxxxxxxx..PxxP.1xxxx
xxxxxxx1.2xxxx..xxxx
xxxxxxxP.xxxxxx1..Px
xxxxxxx.1xxxxxxxP...
xxxxxx.@.Pxxxxxxxx.@
ENDMAP

NAME:   minmay_snake_entry_spellcaster
TAGS:   snake_entry
ORIENT: float
WEIGHT: 2
MONS:   naga mage / guardian serpent
KFEAT:  O = enter_snake_pit
MAP
...
.O1
...
ENDMAP

NAME:   guppyfry_snake_entry_island
TAGS:   snake_entry patrolling no_monster_gen
ORIENT: float
MONS:   naga / nothing, plant
KFEAT:  O = enter_snake_pit
SUBST:  - = . W:2 2:1
MAP
   -WWWWW-
  -WWWWWWW-
 -WWW---WWW-
-WWW-----WWW-
-WW---1---WW-
@WW--1O1--WW@
-WW---1---WW-
-WWW-----WWW-
 -WWW---WWW-
  -WWWWWWW-
   -WWWWW-
ENDMAP

NAME:   guppyfry_snake_entry_garden
TAGS:   snake_entry patrolling no_monster_gen
ORIENT: float
MONS:  naga / nothing, plant
KFEAT:  O = enter_snake_pit
SUBST:  - = wW2t.
MAP
 xxxx@xxxx
xx---.---xx
x----.----x
x--tt.tt--x
x--t1.1t--x
@....O....@
x--t1.1t--x
x--tt.tt--x
x----.----x
xx---.---xx
 xxxx@xxxx
ENDMAP

##############################################################################
# Snake Pit specific non-ending non-entry vaults
##############################################################################

NAME:   minmay_snake_pond
DEPTH:  Snake
MONS:   ball python w:3 / adder / water moccasin
SUBST:  w = wwWW.1
MAP
  wwww
 wwwwww
wwwwwwww
wwwwwwww
wwwwwwww
wwwwwwww
 wwwwww
  wwww
ENDMAP

NAME:   minmay_snake_river
TAGS:   no_monster_gen generate_awake
DEPTH:  Snake
ORIENT: south
# Black mambas can't swim, so none of those
MONS:   ball python w:3 / adder / water moccasin / nothing w:5
MONS:   anaconda / nothing w:5
SUBST:  w = wwW
KPROP:  12 = no_rtele_into
MAP
xxxxxxxxxxxxxxxxxxxxxx..@..xxxxxxxxxxxxxxxxxxxx..@.xxxxxxxxxxxxxxxxxxx
xx211xxxxxxxxxxxxxxwwwWWWWWwwwwxxxxxxxxxxxxxwwwWWWWwwxxxxxxxxxxx112xxx
xx211wwwwxxxxxwwwwwwwwwxxxxxwwwwwwwwxxxxwwwwwwxxxxwwwwwwxxxxxxww112xxx
xx211xwwwwwwwwwwwwxxxxxxxxxxxxxwwwwwwwwwwwwxxxxxxxxxxxwwwwwwwwwx112xxx
xxxxxxxxxxwwwwwxxxxxxxxxxxxxxxxxxxwwwwwwwxxxxxxxxxxxxxxxxwwwwxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   hangedman_venom_bottle
TAGS:   patrolling
DEPTH:  Snake
WEIGHT: 5
KMONS:  123 = water moccasin / black mamba / nothing
KMONS:  4 = guardian serpent
KMONS:  5 = naga mage / nothing
KITEM:  1235 = potion of poison / potion of strong poison / \
               potion of confusion / nothing w:20
KITEM:  4 = any weapon good_item ego:venom q:1 / \
            any weapon good_item ego:venom q:2
KFEAT:  1234 = shallow_water
KMASK:  123 = no_monster_gen
COLOUR: 1234 = lightgreen
SUBST:  1 = 1 2:1 3:1
MARKER: 2 = lua:fog_machine { cloud_type = "poison gas", pow_min = 5, \
                pow_max = 30, delay = 30, size = 2, walk_dist = 2 }
MARKER: 3 = lua:fog_machine { cloud_type = "noxious fumes", pow_min = 5, \
                pow_max = 20, delay = 40, size = 3 }
MAP
     ...
    .....
   ..+++..
  ..m+2+m..
  ..mm1mm..
 ..mm111mm..
..mm11111mm..
..mm11111mm..
..mm11411mm..
..mm11111mm..
..mmm111mmm..
 ..mmmmmmm..
  ....5....
   .......
ENDMAP

NAME:   nicolae_snake_lava_pool
DEPTH:  Snake
TAGS:   mini_float
SUBST:  L = l1
SUBST:  1 = 1.
MONS:   adder / ball python / black mamba / anaconda w:5 / \
        water moccasin / nothing w:40
KMONS:  l = lava snake / nothing w:50
KFEAT:  l = lava
MAP
    .......
  ...........
 .............
 .....111.....
.....1LLL1.....
....1LlllL1....
...1LlllllL1...
...1LlllllL1...
...1LlllllL1...
....1LlllL1....
.....1LLL1.....
 .....111.....
 .............
  ...........
    .......
ENDMAP

NAME:   nicolae_snake_springs
DEPTH:  Snake
TAGS:   mini_float
SUBST:  W = w1
SUBST:  1 = 1.
MONS:   adder / ball python / black mamba / anaconda w:5 / \
        water moccasin w:15
KMONS:  w = sea snake / nothing w:90
KFEAT:  w = deep_water
MAP
    .......
  ...........
 .............
 .....111.....
.....1WWW1.....
....1WwwwW1....
...1WwwwwwW1...
...1WwwwwwW1...
...1WwwwwwW1...
....1WwwwW1....
.....1WWW1.....
 .....111.....
 .............
  ...........
    .......
ENDMAP

NAME:   nicolae_sea_snake_s
MONS:   naga mage, naga
ORIENT: float
DEPTH:  Snake:2-4
TAGS:   no_monster_gen
SHUFFLE: {[(
SHUFFLE: (<
KFEAT:  q = w
KMONS:  q = w:15 sea snake / w:5 nothing
MAP
xxxwwwwwwwxxxxxx
@..wwwqwww.....xx
@...wwwww.......xx
@....wqw.........x
x..2.www....x....x
xx...wqw...xxx...x
 x...www...xxx...x
 x.2.wqw...xxx...x
 x...www...xxx...x
 x...wqw...xxx...x
 x.2.www...xxx...x
 x....w....xxx...x
 x.........xxx{.[x
 xx...1...xxxx...x
  xx.....xx  xx(xx
   xxxxxxx    xxx
ENDMAP

NAME:   nicolae_snake_horde_hoard
DEPTH:  Snake
KITEM:  s = $ / nothing w:25 / * w:1
KMONS:  s = adder w:5 / ball python w:5 / black mamba / anaconda / \
           water moccasin / sea snake w:1 / nothing w:20
MAP
 xxxxxxx
xxsssssxxxxxx
xssssssss=..@
xssssssss=.0@
xssssssss=..@
xxsssssxxxxxx
 xxxxxxx
ENDMAP

NAME:   nicolae_cobra_stairs
DEPTH:  Snake:3-4
ORIENT: float
TAGS:   no_pool_fixup patrolling
SHUFFLE: }])
SUBST:  ) = >>)
MONS:   guardian serpent, greater naga, naga mage, naga warrior
MAP
       xxxxxxxx
     xxx......xx
   xxx..4.www.2xx
xxxx.9....w3w...x
@.....wwwwwWw...xx
@..0.ww.....1...}x
@..0.W.4.4.3.2..]x
@..0.ww.....1...)x
@.....wwwwwWw...xx
xxxx.9....w3w...x
   xxx..4.www.2xx
     xxx......xx
       xxxxxxxx
ENDMAP

NAME:   nicolae_snake_hallway
DEPTH:  Snake
ORIENT: float
SUBST:  A : @xx
SUBST:  B : @xx
SUBST:  C : @xx
SUBST:  T : TTTVVUY
MONS:   naga, naga warrior
MONS:   naga mage w:25 / greater naga w:5
MAP
             xxxxxx
           xxx.G.GxxxxxAAxx
xxx      xxx.G..1..Gxx.1..xxx
@Gxxx  xxx.G....1....G...T..xx
@..Gxxxx.G.2...G.G.....3...1.C
@..1.G.G.3.2.Gxxxx.G...3...1.C
@G.1...2.3.Gxxx  xxx.G...T..xx
xx.G...2.Gxxx      xxx.1..xxx
 xxx.G.Gxxx          xxBBxx
   xxxxxx
ENDMAP

# The disgraced guardian serpent Aizul has been
# demoted to watching the stairs.
NAME:   nicolae_aizul_guard
DEPTH:  Snake:2-4
TAGS:   no_rotate no_hmirror no_vmirror
ORIENT: float
SHUFFLE: ]})>
SUBST:  ) = )>
: if you.absdepth() > 14 then
KMONS:  A = patrolling Aizul, patrolling greater naga
KMONS:  B = guardian serpent / greater naga
: else
KMONS:  A = patrolling Aizul, patrolling guardian serpent
KMONS:  B = naga mage / guardian serpent
: end
NSUBST: B = A / B
MAP
 xxx.@.xxx
xx.B...B.xx
x.........x
xB.......Bx
....].>....
@.........@
....).}....
xB.......Bx
x.........x
xx.B...B.xx
 xxx.@.xxx
ENDMAP

NAME:   nicolae_snake_shapes
TAGS:   no_pool_fixup mini_float
DEPTH:  Snake
NSUBST: 0 = 2:9 / 1:8 / *:0
SUBST:  x : xcvmlwWb
MAP
      .....
   ...........
 ......xxx......
 .......0x.......
...xxx..xx..xxx...
....0x..x0...0x...
....xx..xxx..xx....
 ...x0...0x..x0....
 ...xxx..xx..xxx...
  .......x0.......
   ......xxx......
     ...........
        .....
ENDMAP

NAME:   nicolae_coiled_stairwell_down
DEPTH:  Snake:1-4
TAGS:   uniq_coiled_stairwell
SUBST:  } = }])>
MAP
  xxxxxxx
 xx.....xx
xx.xxxxx.xx
x.xx...xx.x
x.x.xxx.x.x
x.x.x}x.x.x
x.xx..x.x.x
xx.xxxx.x.x
 xx....xx.x
  xxxxxx.xx
    x...xx
   xx.xxx
   x.0.x
   x...x
   xx@xx
ENDMAP

NAME:   nicolae_coiled_stairwell_up
DEPTH:  Snake:2-
TAGS:   uniq_coiled_stairwell
SUBST:  { = {[(<
MAP
  xxxxxxx
 xx.....xx
xx.xxxxx.xx
x.xx...xx.x
x.x.xxx.x.x
x.x.x{x.x.x
x.xx..x.x.x
xx.xxxx.x.x
 xx....xx.x
  xxxxxx.xx
    x...xx
   xx.xxx
   x.0.x
   x...x
   xx@xx
ENDMAP

NAME: nicolae_snakescales
DEPTH: Snake
ORIENT: float
SUBST: 0 = 0., 9 = 9., 8 = 8.
MAP
        xxx       xxx       xxx
       x...x     x...x     x...x
      x.....x   x.....x   x.....x
   xxx...0...xxx...9...xxx...0...xxx
  x...x.....x...x.....x...x.....x...x
 x.....x...x.....x...x.....x...x.....x
x...0...xxx...9...xxx...9...xxx...0..x
 x.....x...x.....x...x.....x...x.....x
  x...x.....x...x.....x...x.....x...x
   xxx...0...xxx...8...xxx...0...xxx
  x...x.....x...x.....x...x.....x...x
 x.....x...x.....x...x.....x...x.....x
x...0...xxx...9...xxx...9...xxx...0..x
 x.....x...x.....x...x.....x...x.....x
  x...x.....x...x.....x...x.....x...x
   xxx...0...xxx...8...xxx...0...xxx
  x...x.....x...x.....x...x.....x...x
 x.....x...x.....x...x.....x...x.....x
x...0...xxx...9...xxx...9...xxx...0..x
 x.....x...x.....x...x.....x...x.....x
  x...x.....x...x.....x...x.....x...x
   xxx...0...xxx...9...xxx...0...xx
      x.....x   x.....x   x.....x
       x...x     x...x     x...x
        xxx       xxx       xxx
ENDMAP

NAME: nicolae_snake_nest
DEPTH: Snake
ORIENT: float
MAP
         .........
    ......xx..xxx..
  ...xxxxxx..xx.xx..
.....x......xx...xx..
.xx..xx...............
..xx....xxxx..xxxx..x..
 ..x..xxx..x.....xx.xx..
 ..x..x....xx.x...x..x...
 ..x....xx....xx..x..xxx.
 .xx.xx.x......x.xx....x.
 .x..x..x..xxxxx.x.....x.
 .x..x..x.xx........x.xx.
 ....x..x....x..xxx.x....
 .xx.xx.xx...x.xx.x.xxx..
 .x...x..xxx.x....x...x.
 .xx..xx.....x....x.x.x.
 ..x...x.x...x..xxx.xxx.
  .xx....xxxxx..........
  ..x...........x..xx..
   .x.xxx...xxx.x..x..
   .x...xxxxx...x..x.
   .............xxxx.
     ..xx..xxxx......
      ..xxxx..x.
       .........
ENDMAP

NAME:    guppyfry_snake_island
DEPTH:   Snake
TAGS:   transparent
MONS:  plant
SUBST:   . = . W:1 1:1, - = . W:3 0:1 ~:1 %:1
SHUFFLE: 0~
MAP
@.WWWWWWWW.@
.WWW.....WW.
WWW--WW-00WW
WW..WWWW..WW
WWW--WWWW~WW
WWWW..WWWWWW
WWWWW--WWWWW
W~WWWW..WWWW
W--WWWW--WWW
WW..WW..WWWW
.WW----WWWW.
@.WWWWWWWW.@
ENDMAP

####

NAME:    hangedman_snake_corridor_cut
TAGS:    no_monster_gen transparent
DEPTH:   Snake
MONS:    naga, patrolling naga mage
MONS:    black mamba / anaconda, patrolling guardian serpent
MONS:    patrolling adder / patrolling ball python / \
         patrolling water moccasin w:5
SHUFFLE: hH / hH / hH, iI / iI / Ii, jJ / jJ / Jj, kK / kK / Kk
SHUFFLE: 1234 / 1234 / 1212 / 3434
SUBST:   hi = ., H : xx., I : xx., J : xx., K : xx., % = $%
MAP
xxxxxxxxx@5@xxx
xccccccccc...xxx
xc*2%...KxJ...xxx
xc2*1...xxx.j..xxx
xc%1....JxKk....xxx
xcHxI.....kKxJ...xx
@cxxx.i1.j.xxx.j..x
5.IxHh..%..JxKk...@
@...hHxI.1%..kKxJ.5
x..i.xxx.i3.j.xxxc@
xx...IxHh..%..JxKcx
xxx....hHxI.3....cx
 xxx..i.xxx.i....cx
  xxx...IxHh.....cx
   xxx....hHxI.3%cx
    xxx..i.xxx3*4cx
     xxx...IxH%4*cx
      xxx...ccccccx
       xxx@5@xxxxxx
ENDMAP

NAME:    hangedman_snake_corridor_split
TAGS:    no_monster_gen transparent allow_dup
DEPTH:   Snake
MONS:    naga, black mamba, naga warrior, anaconda, naga mage, guardian serpent
SUBST:   X : xxc, Y : x:33 c:66 w:1, 0 = 12
SHUFFLE: 34, 56, 3456 / 3456 / 3456 / 3535 / 4646 / 3636 / 4545 / 3434 / 5656
MAP
    xxxxxxx
   xxxxxxxxx
  xxxx...xxxx
xxxxx..X..xxxx
xxxx..X%X.3xxxxx
xx1..X%..X.4xxxx
xx2YY..x..X.0.xxxxxxxxx
x..YY.xxx..YY.xxxxxxxxx.@.
..X.21xxxx.*Y...xxxxxx..X@
@X..xxxxxx...Y*.xxxx12.X..
.@.xxxxxxxxx.YY..xxx.YY..x
   xxxxxxxxx.0.X..x..YY2xx
          xxxx6.X..%X..1xx
          xxxxx5.X%X..xxxx
            xxxx..X..xxxxx
             xxxx...xxxx
              xxxxxxxxx
              xxxxxxxx
ENDMAP

NAME:    hangedman_snake_sidewinding
TAGS:    allow_dup
DEPTH:   Snake:1-4, Zot:1-4
: if you.branch() == "Zot" then
KMONS:  A = base draconian / green draconian w:4
KMONS:  B = swamp dragon w:5 / dragon / ice dragon / storm dragon w:5
KMONS:  C = nonbase green draconian / draconian annihilator
KMONS:  D = golden dragon / shadow dragon / quicksilver dragon w:5 / \
            iron dragon w:5
: else
KMONS:  A = naga warrior / naga mage / naga
KMONS:  B = black mamba w:6 / anaconda
KMONS:  C = greater naga / naga mage w:5 / guardian serpent w:5
KMONS:  D = guardian serpent / anaconda w:2
: end
SHUFFLE: @123<= / &456"-, EF
SUBST:   = = +, 1 = A, 2 = B, 3 = C, < : ~~_..., &E = x, - = c
SUBST:   456 = ', "F = ., y : x:2 G:1 .:6, Y : z', Z : z', z : xc
NSUBST:  * = **|, ~ = 1:( / 1:(<.., _ = 1:) / 1:)>.
KMASK:   '%*d = no_monster_gen
MAP
x@@xxxxxxxxxxxxxxxxxxxx
x""xxcccccccccccccccccx
x..xxxcc13121'''%%%**cx
xy..A%c--32z'''Z''BD*cx
xy...B%--c1''z'''z'B%cx
x4.xxxxxccc''z''zYz'%cx
x..xxEExxccc'''zYz''%cx
xy...EE.A%ccc'''z''Z'cx
xy...FF..B%ccc'''''''cx
x4.xxFFxxxxxccc''zz''cx
x1.xxEExxEExxccc''''4cx
xy...EE..EE.A%ccc''z5cx
xy...FF..FF..B%ccc454cx
x..xxFFxxFFxxxxxccc66cx
x1.xxEExxEExxEExxc==4cx
xy...EE..EE..EE.A%==ccx
xy...FF..FF..FF..B%cccx
x..xxFFxxFFxxFFxxxxxccx
x<<xxxxxxxxxxxxxxxxxxcx
x&&xxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: nicolae_snake_serpentine_hallways
TAGS: transparent extra
DEPTH: Snake
MAP
        x@@@@xxxxx
        x........xxx
       xxx.........xx
    xxxxxxxx........x
xxxxxxxxxxxxxxxx....xx
@..............xx....x
@...............xx...x
xxxxxxxxxxx......x...x
xxxxx.....xxx...xx...x
xxx.........xx.xx....x
xx...........x+x....xx
xx....x+x...........xx
x....xx.xx.........xxx
x...xx...xxx.....xxxxx
x...x......xxxxxxxxxxx
x...xx...............@
x....xx..............@
xx....xxxxxxxxxxxxxxxx
 x........xxxxxxxx
 xx.........xxx
  xxx........x
    xxxxx@@@@x
ENDMAP

NAME: nicolae_snake_linked_coils
TAGS: transparent extra
DEPTH: Snake
SUBST: + : +x
MAP
    xxxxxxxxxxxxxx
  xxx.....xxx....xxx
 xx.........+x.....xx
xx...xxxxx...xxxx...xx
@...xxxx.+xx..xxxx...@
@..xxxxx..xx..xxxxx..@
xxxxxxx..xxxx..xxxxxxx
xxxxxxx..xxxx..xxxxxxx
xxxxxxx..xxxx..xxxxxxx
xxxxxxx..xxxx..xxxxxxx
xxxxxxx..xxxx..xxxxxxx
@..xxxxx..xx..xxxxx..@
@...xxxx...x+.xxxx...@
xx...xxxx...xxxxx...xx
 xx.....x+.........xx
  xxx....xxx.....xxx
    xxxxxxxxxxxxxx
ENDMAP

NAME: nicolae_snake_single_loop
TAGS: transparent extra
DEPTH: Snake
MAP
 xxxxxxx
xx.....xx
x.......xx
@...x...xxx
@..xxx..xxxx
xxxxxx..xxxxx
xx...+..+...xx
x....+..+....x
x...xx..xx...x
x..xxx..xxx..x
x...x...xx...x
x.......x....x
xx.....xx...xx
 xxxxxxxx@@xx
ENDMAP

NAME: nicolae_snake_double_loop
DEPTH: Snake
TAGS: transparent extra
SHUFFLE: AB, CD
SUBST: AC = ., BD = +
MAP
 xxxxxxx    xxxxxxx
xx.....xxxxxx.....xx
x.......xxxx.......x
@...x...xxxx...x...x
@..xxx..xxxx..xxx..x
xxxxxxBBxxxxCCxxx..x
xx...A..A..D..D....x
x....A..A..D..D...xx
x...xxBBxxxxCCxxxxxx
x..xxx..xxxx..xxx..@
x...x...xxxx...x...@
x.......xxxx.......x
xx.....xxxxxx.....xx
 xxxxxxx    xxxxxxx
ENDMAP

NAME: nicolae_snake_round_shape_thing
TAGS: transparent extra
DEPTH: Snake
SHUFFLE: bc
SUBST: a : x., b : x@, c : @
MAP
       xxbbbxx
     xxx.....xxx
    xx.........xx
    x...........x
   xx....xxx....xx
   x....xxxxx....x
   x...xxxxxxx...x
   x...xxxxxxx...x
  xx...xxxxxxx...xx
 xx.....xxxxx.....xx
xx.......xxx.......xx
c....a.........a....c
c...aaa.......aaa...c
c....a.........a....c
xx.......xxx.......xx
 xx.....xxxxx.....xx
  xx...xxxxxxx...xx
   x...xxxxxxx...x
   x...xxxxxxx...x
   x....xxxxx....x
   xx....xxx....xx
    x...........x
    xx.........xx
     xxx.....xxx
       xxbbbxx
ENDMAP

NAME: nicolae_snake_biting_tails
TAGS: transparent extra
DEPTH: Snake
SUBST: G : GT
MAP
xxxxx     xxxxxxx     xxxxxxx     xxxxxxx
@...xxxxxxx.....xxx xxx.....xxx xxx.....xxxxxxxx
xx....xx..........xxx.........xxx.........xx...x
 xxx.x......xxx....x....xxx....x....xxx..x.....x
   xx.G....xxxxxx.x...xxxxxxx.x...xxxxxxx.G....x
   xx.....xxxxxxxx...xxxxxxxxx...xxxxxxxx.....xx
   x....G.xxxxxxx...x.xxxxxxx...x.xxxxxx....G.xx
   x.....x..xxx....x....xxx....x....xxx......x.xxx
   x...xx.........xxx.........xxx..........xx....xx
   xxxxxxxx.....xxx xxx.....xxx xxx.....xxxxxxx...@
          xxxxxxx     xxxxxxx     xxxxxxx     xxxxx
ENDMAP

NAME: nicolae_snake_salamander_stairs
DEPTH: Snake, !Snake:$
: local level = you.depth() ; local weight = level * level
KMONS: 1 = salamander
SHUFFLE: ])}
: kmons("2 = nothing w:21 / salamander w:" .. weight )
NSUBST: l = 1 / 1:2 / 1:2l, a = 2:. / *:x, b = 2:. / *:x, c = @ / .
SUBST: L = l.
KPROP: 12l = no_cloud_gen
KFEAT: 12 = l
MAP
 xxxxxxxxxx
xxllllL.a.bc
x.llllL.a.bc
x]llllL.a.bc
x.llllL.a.bc
xxllllL.a.bc
 xxxxxxxxxx
ENDMAP

################################################################################
# Snake endings
################################################################################

################################################################################
# Snake Pit: the hunt
#
NAME:    snake_hunt
PLACE:   Snake:$
WEIGHT:  20
TAGS:    no_rotate
ORIENT:  northwest
KFEAT:   Z = weight:2 teleport trap / .
KFEAT:   z = teleport trap / Zot trap / weight:20 .
MONS:    naga / nothing
MONS:    greater naga / nothing
MONS:    guardian serpent / nothing
MONS:    naga warrior / nothing
MONS:    naga mage / nothing
MONS:    greater naga / guardian serpent
SHUFFLE: 'X / "Y
SHUFFLE: 'X / 'X / AB, "Y / CD
SUBST:   A=x, B=x, C=x, D=x
SUBST:   ' = .:250 1 2 3 4:15 5:5
SUBST:   " = .:250 1 2 3 4:5  5:15
SUBST:   X=xc..++, Y=xc++
SUBST:   T : T U V .:30 b
SUBST:   _:.!, !:!.x, !=x .:2
SHUFFLE: 123, 45, ([<
KITEM:   O = serpentine rune of Zot
#
validate {{ return glyphs_connected('{', 'O') }}
#
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxcccccxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx''''''c4O4c""""""xxxxxxxxxxx
xxxxxxxxxxx''''''xxxx664z466xxxx""""""xxxxxx
xxxxxxxx''''xxxxxxxx.5665665.xxxxxxxx""""xxx
xxxxxxx''xxxx''''xxx..45554..xxx""""xxxx""xx
xxxxxx''xxx''''''''xxx.666.xxx""""""""xxx""x
xxxxxx'xxx''''''''''xxxcXcxxx""""""""""xxx"x
xxxxxx''x''''''''''''xx.3.xx""""""""""""x""x
xxxxxxx'Y'''''''''''''45354"""""""""""""Y"xx
xxxxxxxxxx''''''''''xx.....xx""""""""""xxxxx
xxxxxxxxxxx''''''''xxxxx.xxxxx""""""""xxxxxx
xxxxxxxxxxxxx''''xxxxxxx.xxxxxxx""""xxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx+xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxx!.!xxxxxx.....xxxxxx!.!xxxxxxxx
xxxxxxxxxx!....._xx...43335...xx_...!!!xxxxx
xxxxxxxx!......_x.11.4..3..5.22.x_......!xxx
xxxxxxx!...2...x..11.........22..x...1...!xx
xxxxxxx!..2..._x......T...T......x_...1..!xx
xxxxxx!...2...x..2.............1..x...1...!x
xxxxxx!..2....x.........{.........x....1...!
xxxxxx!.......x....T...(.[...T.............@
xxxxxx!..2....x.......ZZZZZ.......x....1...!
xxxxxxx!..2..._x.................x_...1..!xx
xxxxxxx!...2..._xx....T.}.T....xx_...1...!xx
xxxxxxx!........._xx.........xx_.........!xx
xxxxxxxx!.........._xxx...xxx_..........!xxx
xxxxxxxxx!.............x.x.............!xxxx
xxxxxxxxxx!!.........................!!xxxxx
xxxxxxxxxxxx!!!.....3.......3.....!!!xxxxxxx
xxxxxxxxxxxxxxx!!!!!..3...3..!!!!!xxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx!!!...!!!xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxx!@!xxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Snake Pit: old level
#
NAME:   snake_pit
PLACE:  Snake:$
WEIGHT: 20
ORIENT: southwest
TAGS:   no_rotate
MONS:   greater naga / nothing
MONS:   naga
MONS:   naga mage / nothing
MONS:   naga warrior / nothing
SUBST:  c = c .:1
SUBST:  v = v .:1
SUBST:  v = x
KITEM:  O = serpentine rune of Zot
MAP
xxxxxxxxxxxxxxxxxxxxxxx..@.xxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx.............xxxxxxxx
xxxxxxxxxxxxxx....v.............v..xxxxx
xxxxxxxxxxxx....2.v.............v.2..xxx
xxxxxxxxxxx.....2.v....v.....v..v.33.xxx
xxxxxxxxxxx.....22v.............vv.2..xx
xxxxxxxxxxx.......vv..v........vv.33..xx
xxxxxxxxxx.....x233vv....T...vvv4444..xx
xxxxxxxxxx......4444v.........v.333....x
xxxxxxxxxx......33v4...v.......4x4.....x
xxxxxxxxxx.......334.....v.....4333....x
xxxxxxxxxx..c......3.3..4.4..v.......c.x
xxxxxxxxx...cc.....1...1.1.1...133..cc.x
xxxxxxxxx...cc....3.....4.4.........cc.x
xxxxxxxxx...cc...3...v........2.....cc.x
xxxxxxxxx...cc.........1...1.......cc..x
xxxxxxxxxx..cc.....1.....1.....1..ccc.xx
xxxxxxxxxx...ccc......3333333.....cc..xx
xxxxxxxxxx....cccc....3333333.....cc..xx
xxxxxxxxxx.....cccccc............cc...xx
xxxxxxxxxx........ccccc..O....cccc....xx
xxxxxxxxxxx........cccccccccccccc....xxx
xxxxxxxxxxx..........ccccccccccc.....xxx
xxxxxxxxxxxxx.......................xxxx
xxxxxxxxxxxxxxxx..................xxxxxx
xxxxxxxxxxxxxxxxxxxxx.......xxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Snake Pit: Coiled (minmay)
#
NAME:    minmay_snake_end_coiled
PLACE:   Snake:$
TAGS:    no_rotate
ORIENT:  southwest
MONS:    naga, naga warrior, naga mage, greater naga
SUBST:   1 = 11., 2 = 23., 4 = 444123..
SUBST:   c : c:20 v:5
KITEM:   O = serpentine rune of Zot
MAP
xxxxxxxxxxxxxxxxxc.@..cxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxc....cxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxcc....ccxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxc......cxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxc..1...cxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxc...21.cxxxccccccccxxxxxx
xxxxxxxxxxxcccccc......ccccc......cccxxxx
xxxxxxxccccc...2........2.....1.....ccccc
xxxxcccc............4....................
xxccc.....4.....2.....2.......2.1.......@
xcc...............2.2........1...........
xc..........ccccc......cccccc......cccccc
cc..1....ccccxxxc......cxxxxccccccccxxxxx
c.......ccxxxxxxc....1.cxxxxxxxxxxxxxxxxx
c...4...cxxxxxxxc......cxxxxxxxxxxxxxxxxx
c.......ccxxxxxxc.1.2..cxxxxxxxxxxxxxxxxx
cc....1..ccccxxxc......cxxxxxxxxxxxxxxxxx
xc..........ccccc......ccccccxxxxxxxxxxxx
xcc......4..................cccccxxxxxxxx
xxccc...........................ccccxxxxx
xxxxcccc..1....4...........1.......cccxxx
xxxxxxxccccc.........4...............ccxx
xxxxxxxxxxxcccccc......ccccc.....1....cxx
xxxxxxxxxxxxxxxxc......cxxxcccc.......ccx
xxxxxxxxxxxxxxxxc..1...cxxxxxxcc...4...cx
xxxxxxxxxxxxxxxxc...21.cxxxxxxxc.......cx
xxxxxxxxxxxxxxxxc..1...cxxxxxxcc...1...cx
xxxxxxxxxxxxxxxxc......cxxxcccc.......ccx
xxxxxxxxxxxcccccc..4...ccccc..........cxx
xxxxxxxccccc.........................ccxx
xxxxcccc2.......1.......2.....4....cccxxx
xxccc........1....4.............ccccxxxxx
xcc.......4.................cccccxxxxxxxx
xc.....1....ccccc......ccccccxxxxxxxxxxxx
cc.......ccccxxxc....1.cxxxxxxxxxxxxxxxxx
c....1..ccxxxxxxc.1....cxxxxxxxxxxxxxxxxx
c.......cxxxxxxxc....2.cxxxxxxxxxxxxxxxxx
c...4...ccxxxxxxc..1...cxxxxxxxxxxxxxxxxx
cc.......ccccxxxc2...1.cxxxxxxxxxxxxxxxxx
xc......2...ccccc......ccccccxxxxxxxxxxxx
xcc..1............1.......4.cccccxxxxxxxx
xxccc..........1.....4......1...ccccxxxxx
xxxxcccc.4...2..4..........12.4....cccxxx
xxxxxxxccccc..1.....2..1....1....4..Ocxxx
xxxxxxxxxxxcccccccccccccccccccccccccccxxx
ENDMAP

################################################################################
# Salamanders (by Mu.)
# by popular request
#
NAME:   snake_pit_salamanders_mu
PLACE:  Snake:$
ORIENT: north
TAGS:   no_rotate
MONS:   naga
MONS:   naga mage / nothing
MONS:   naga warrior / nothing
MONS:   greater naga / nothing
MONS:   guardian serpent / nothing
MONS:   w:7 anaconda / black mamba / nothing
KITEM:  O = serpentine rune of Zot
KMONS:  O = guardian serpent
KMONS:  S = guardian serpent
KFEAT:  7 = lava
KMONS:  7 = salamander / w:5 lava snake / nothing
KMASK:  l7 = no_monster_gen
NSUBST: l = 8:7
NSUBST: . = 6:1 / .
MAP
              cccccc
            ccc2452ccc
           cc..2232..cc
          cc..llllll..cc
          c..llllllll..c  ccccc
   ccccc cc.ll7llll7ll.cccc666cc
  cc...ccc..llllllllll..cc66666cc
 cc.222.cc.llllllllllll.+666S666c
 c..444..c3llllllllllll.+666O666c
 c.42443.+5llllllllllll.+666S666c
 c.43323.c3llllllllllll.cc66666cc
 cc.....cc..llllllllll..ccc666cc
 @cc...+ccc.ll7llll7ll.ccccccccccc
c+cccccc..c..llllllll..c...+.....c
c..lllc.3.cc..llllll..cc...cl.33.c
c.3lllcc...cc..2232..cc...ccll...c
cc..lllcc...ccc2452ccc...cclll..cc
 cc..l7lcc....cccccc....ccl7l..cc
  cc..lllcc.....44.....cclll..cc
   cc..lllcc....54....cclll..cc
    cc..lllccc..44..ccclll..cc
     cc...lllcccccccclll...cc
      cc...llllllllllll...cc
       cc..lllll77lllll..cc
        c....llllllll....c
        c......llll......c
        c.2242..ll..2452.c
        c.3222..ll..2223.c
        c......llll......c
        cccc+cllllllc+cccc
         c....ll77ll....c
         cc.3.llllll.3.cc
          cc..4llll4..cc
           cc..4224..cc
            cc......cc
             cccccccc
ENDMAP

################################################################################

NAME:   grunt_snake_rune_spirals
PLACE:  Snake:$
ORIENT: centre
MONS:   naga / nothing, naga warrior / naga, naga mage / naga
MONS:   greater naga / guardian serpent
MONS:   greater naga / naga warrior w:5 / naga mage w:5
NSUBST: O = 1:A / 1:B
KITEM:  A = serpentine rune of Zot
KITEM:  B = serpentine rune of Zot mimic / w:90 acquire any
MAP
        cccccccc     c.@..c       cccccccc
     cccc..1...ccc   c....c    cccc..2...ccc
   ccc.....2.....ccc c....c   cc.....3.....cc
  cc.......1.......ccc....c  cc......2......cc
  c1....ccccccc....1cc....c  c1....cccccc..51c
 cc.51ccc..2..cc..5..c....c cc.2.ccc....cc12.cc
 c...cc...23...cc1........c c.5.1c.......cc...c
cc...c2....2....cc........c c...cc.......4c...cc
c...cc52..ccc..22cc....32.c c232c......4..cc...c
c...c...2ccccc25..cc......c c...c.4.O.4c.2.c...c
c232c...cc.5.cc...ccc..5..ccc...cc.....c323c...c
c...c323c.....cc...ccc..5..ccc...cc.5.cc...c232c
c...c.2.c4.O.4.c...c c......cc..52ccccc2...c...c
c...cc..4......c232c c.23....cc22..ccc..25cc...c
cc...c4.......cc...c c........cc....2....2c...cc
 c...cc.......c1.5.c c........1cc...32...cc...c
 cc.21cc....ccc.2.cc c....c..5..cc..2..ccc15.cc
  c15..cccccc....1c  c....cc1....ccccccc....1c
  cc......2......cc  c....ccc.......1.......cc
   cc.....3.....cc   c....c ccc.....2.....ccc
    ccc...2..cccc    c....c   ccc...1..cccc
      cccccccc       c..@.c     cccccccc
ENDMAP

################################################################################

NAME: grunt_snake_rune_pools
PLACE: Snake:$
TAGS: no_trap_gen
ORIENT: north
MONS: greater naga / naga warrior
MONS: greater naga / guardian serpent
MONS: naga warrior / naga
MONS: naga mage / naga
MONS: naga

KMONS: 6 = sea snake / nothing w:20
KFEAT: 6 = deep_water
KMASK: 6w = no_monster_gen

KMONS: S = guardian serpent / w:20 anaconda / w:30 black mamba

KITEM: O = serpentine rune of zot
MAP
          ccccccccc
        ccc.6w6w6.ccc
      ccc.5..www..5.ccc
      c4.www1.2.1www.4c
   ccccw.w6w..O..ww6.wcccc
 ccc.w6wS6www212www6Sw6w.ccc
 c.+.www.wwwwwwwwwww.www.+.c
cc.+....3w6www6ww6ww3....+.cc
c..cccwww.ww6www6ww.wwwccc..c
c..cxcc6w..2.5.5.2..w6ccxc..c
c..ccxcwwwwwnnnnnwwwwwcxcc..c
cc15cccww6wnn666nnw6wwccc51cc
 cc34cccwwnn.w6w.nnwwccc43cc
  c..ccccnn4.2.2.4nncccc..c
  c...cc..5..S.S..5..cc...c
  cc...+...3..1..3...+...cc
   cc..+..5.......5..+..cc
    cccccc.........cccccc
         cccc+++cccc
         ccc4...4ccc
        cc...www...cc
        cSn3.w6w.3nSc
        cSn.16w62.nSc
        cSn..w6w..nSc
        cc...www...cc
         ccc.....ccc
          ccc+++ccc
          cc..@..cc
ENDMAP

################################################################################

NAME:   grunt_snake_rune_serpentine_throne
PLACE:  Snake:$
ORIENT: north
KMONS:  L = Lamia
KITEM:  O = serpentine rune of Zot
KFEAT:  A = altar_okawaru / altar_makhleb / altar_trog
MONS:   greater naga / naga warrior w:5 / naga mage w:5
MONS:   naga warrior / naga, naga mage / naga, naga / nothing
MONS:   anaconda / guardian serpent
veto {{ return you.uniques("Lamia") }}
MAP
     ccccccc
   ccc..O..ccc
  cc4.3...3.4cc
  c..1.ccc.1..c
 cc.3.cc.cc.3.cc
 c...cc._.cc...c
cc......L......cc
c..5..G...G..5..c
c..T.2.1.1.2.T..c
c...3.G...G.3...c
c..4.2.1.1.2.4..c
c.4.3.G...G.3.4.c
c..4T2.1.1.2T4..c
c.4.3.G...G.3.4.c
c..4.2.1.1.2.4..c
c...3.G...G.3...c
cc.T.2.....2.T.cc
 c....G.5.G....c
 cc.5..5.5..5.cc
  c.....T.....c
  cc..4...4..cc
   ccc.4.4.ccc
     cc...ccxx
     x.......xx
     xx....5..xx
      xxxx...4.xx
         xxx3.1.x
   xxxxxxxx..2.4x
 xxx...5.5....3xx
xx.4....5....xxx
x.1.3xxxxxxxxx
x4.2........xx
xx3..........x
 xxxxxxx..@..xx
ENDMAP

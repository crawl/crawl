################################################################################
# lair.des: All maps relating to the Lair branch. Branches off of Lair can be
#           found in their relevant file:
#
#               Swamp - swamp.des                Shoals - shoals.des
#               Snake Pit - snake.des            Spider Nest - spider.des
#               Pits of Slime - slime.des
################################################################################

{{
function wad_woods_setup(e, uniq, weight, mist, delaynum, sizenum, spreadnum)
    if uniq == "decor" then
        e.tags("uniq_wad_woods_decor decor extra")
    elseif uniq ~= "other" then
        e.tags("uniq_wad_woods_" .. uniq)
    end
    e.weight(weight)
    e.mons("plant")
    e.mons("bush")
    e.mons("fungus")

    if you.in_branch("Lair") then
       e.mons("spriggan")
       e.mons("wolf / black bear w:4 / yak w:2")
    else
        if you.absdepth() >= 13 then
            e.mons("centaur warrior / hill giant")
            e.mons("hell rat")
        elseif you.absdepth() >= 8 then
            e.mons("centaur / troll")
            e.mons("porcupine")
        elseif you.absdepth() >= 5 then
            e.mons("gnoll / ogre w:1")
            e.mons("river rat")
        else
            e.mons("gnoll")
            e.mons("quokka")
        end
    end

    e.ftile(".t+%*|q012345678COGTU = floor_grass")

    e.kfeat("O = shallow_water")
    if mist then
        e.lua_marker("O", fog_machine { pow_min = 3, pow_max = 7, delay = delaynum,
                          size = sizenum, walk_dist = 1, spread_rate = spreadnum } )
    end
end

function lair_small_ending(e)
    e.tags('lair_end_small')
    e.weight(5)

    if e.is_validating() then return end

    e.hook("post_place", function ()
        if not dgn.persist.lair_second_ending_built then
            dgn.persist.lair_second_ending_built = true
            dgn.place_maps { tag = "lair_end_small" }
            dgn.persist.lair_second_ending_built = nil
        end
    end)
end

function beast_lair_populate_mons_large(e)
    local set = crawl.random2(3)
    if you.depth() > 3 then
      set = set + 3
    end
    e.tags("no_monster_gen")
    e.mons("plant") -- 1 is always plant.
    if set == 0 then
        e.mons("hound / wolf")
        e.mons("wolf")
        e.mons("wolf / warg w:1")
    elseif set == 1 then
        e.mons("black bear")
        e.mons("black bear / polar bear")
        e.mons("polar bear")
    elseif set == 2 then
        e.mons("iguana")
        e.mons("iguana / komodo dragon")
        e.mons("komodo dragon w:20 / basilisk w:1 / hydra w:1")
    elseif set == 3 then
        e.mons("elephant")
        e.mons("elephant")
        e.mons("elephant / dire elephant w:1")
    elseif set == 4 then
        e.mons("yak")
        e.mons("yak / death yak")
        e.mons("death yak / catoblepas w:1")
    else
        e.mons("water moccasin")
        e.mons("water moccasin / black mamba w:5")
        e.mons("black mamba / anaconda w:1")
    end
end

function beast_lair_populate_mons_small(e)
    if crawl.one_chance_in(25) then
        beast_lair_populate_mons_large(e)
    else
        local monster_list = {}
        if you.depth() < 4 then
            monster_list = { "porcupine", "porcupine", "iguana", "bullfrog",
                             "wolf", "yak", "komodo dragon", "spiny frog",
                             "crocodile" }
        else
            monster_list = { "yak", "yak", "polar bear", "death yak",
                             "elephant", "spiny frog", "komodo dragon",
                             "wyvern" }
        end
        e.mons(plant)
        e.mons(monster_list[crawl.random2(#monster_list) + 1])
        e.mons(monster_list[crawl.random2(#monster_list) + 1])
        e.mons(monster_list[crawl.random2(#monster_list) + 1])
    end
end

function beast_lair_randomise(e)
    -- Tools for randomizing environment
    -- A has a 33% chance of being a plant, 66% chance of being floor.
    e.subst("A = 1:5 .")
    -- B has a 23% chance of being a wall, 77% chance of being floor.
    e.subst("B = x:3 .")
    -- C has a 23% chance of being shallow water, 77% chance of being floor.
    e.subst("C = W:3 .")
    -- D has even odds of being shallow water, a wall, a plant of empty floor.
    e.subst("D = Wx1.")
    -- 25% chance that all instances of E in the vault will be plants.
    e.subst("E : 1:3 .:9")
    -- 25% chance that all instances of F in the vault will be walls.
    e.subst("F : x:3 .:9")
    -- 25% chance that all instances of G in the vault will be shallow water.
    e.subst("G : W:3 .:9")
    -- Tools or randomizin monsters and items
    -- M, for monster. 33% chance to be 2, 67% chance to be floor.
    e.subst("M = 2:4 .:8")
    -- P, for pack. 17% chance to be 2, 83% chance to be floor.
    e.subst("P : 2:2 .")
    -- L (Loot) tiny chance to be good item, high chance to be normal item.
    e.subst("L = *:1 % .:5")
    -- J (Junk) small chance to be an item, high chance to be nothing.
    e.subst("J = %:6 .:28")
    -- Universal settings
    e.tags("no_trap_gen")
    e.depth("Lair, !Lair:$")
end
}}

################################################################################
# Lair entries
################################################################################
# Dummy Lair entry
NAME:   lair_0_dummy
TAGS:   lair_entry extra
WEIGHT: 30
ORIENT: float
KFEAT:  O = enter_lair
MAP
O
ENDMAP

##############################################################################
# Generic Lair entry
#
# This could probably use some randomization.
NAME:    david_lair_generic
TAGS:    lair_entry
MONS:    crocodile / iguana, nothing
MONS:    nothing / yak / weight:2 death yak, plant / fungus / w:20 nothing
MONS:    elephant
SHUFFLE: 12
ORIENT:  float
KFEAT:   O = enter_lair
MAP
xxxx..@..xxxx
xx.........xx
x..42...44..x
..44..1......
@....1O3....@
...4..5...4..
x...4..2....x
xx.........xx
xxxx..@..xxxx
ENDMAP

##############################################################################
# Shrubbed Lair entry
#
NAME:   david_lair_shrubbed
TAGS:   lair_entry
MONS:   plant, komodo dragon
SUBST:  ' = 1.
ORIENT: float
KFEAT:  O = enter_lair
MAP
xxxxxxxxxxxxx
xx111111111xx
x112.'111111x
x11.O.'111''.
x11'..111'..@
x111111''....
x1''11......x
xx.''..'...xx
xxxx..@..xxxx
ENDMAP

##############################################################################
# Lair monsters near stair
#
NAME:    dpeg_lair_monsters
TAGS:    lair_entry
ORIENT:  float
MONS:    w:100 nothing / w:40 yak / death yak
MONS:    w:100 nothing / w:20 iguana / w:20 crocodile /\
         komodo dragon / basilisk w:5
MONS:    river rat / porcupine / hell rat / quokka / rat
MONS:    w:90 nothing / w:30 bullfrog / blink frog / spiny frog
SHUFFLE: 1234
KFEAT:   O = enter_lair
MAP
111...@......
111..........
11........2..
.............
@.....O.....@
...2.........
........2....
.............
....@........
ENDMAP

##############################################################################
# Obligatory Oklob Plant Lair Entry Vault,
#
NAME:   dpeg_oklobed_lair
TAGS:   lair_entry no_monster_gen no_pool_fixup no_rotate
ORIENT: float
: if you.absdepth() >= 11 then
MONS:   plant, oklob plant / w:50 plant
: elseif you.absdepth() >= 10 then
MONS:   plant, oklob plant / w:150 plant
: else
MONS:   fungus, plant
:end
SUBST:  ? = 2.
KFEAT:  O = enter_lair
MAP
...xxxxxxx...
..x.......x..
....x...x....
...xx???xx...
.x..?111?..x.
.x..?1O1?..x.
.x..?111?..x.
...xx???xx...
....x...x....
..x.......x..
...xxxxxxx...
ENDMAP

##############################################################################
# Grove Lair entry
#
NAME:   minmay_lair_entry_grove
TAGS:   lair_entry
ORIENT: float
MONS:   plant / bush w:2 / fungus w:2
MONS:   crocodile / basilisk w:5
MONS:   yak
MONS:   iguana
MONS:   rat w:2 / porcupine / river rat / quokka w:1
SUBST:  . = .:400 tt111123355
SUBST:  ' = .:200 tt111123355
SUBST:  3 : 334
KFEAT:  O = enter_lair
validate {{ return has_exit_from_glyph('O') }}
MAP
     .....
   .........
  ...........
 .............
 .....'''.....
.....'''''.....
....'''''''....
....'''O'''....
....'''''''....
.....'''''.....
 .....'''.....
 .............
  ...........
   .........
     .....
ENDMAP

##############################################################################
# Lava Lair entry
#
NAME:   minmay_lair_entry_lava
TAGS:   lair_entry no_monster_gen
ORIENT: float
KMONS:  1 = hell hound
SUBST:  L = l.
KFEAT:  O = enter_lair
MAP
.........
...lLll..
..lllllL.
..ll1Lll.
.LlLO1ll.
.ll.Llll.
.l.llll..
..lllL...
.........
ENDMAP

##############################################################################
# Welcome to Lair Island. We make great pancakes.
#
NAME:   erik_lair_island
TAGS:   lair_entry
ORIENT: float
MONS:   troll
MONS:   w:30 hound / jackal / w:20 wolf
SUBST:  ? = w.
KFEAT:  O = enter_lair
MAP
.@........wwwwww?.....
..?.?.?..wwwwwww.?..?.
..??w..wwwwwwwww?.??..
...??w.wwwwwwww?www?..
...wwww?wwwwwwwwww??..
..wwwwww??w??w2wwww..@
..wwwwww??....wwww???.
....1....2.2O2.wwww...
..?wwwww???2.2www.ww?.
.....wwww2..w.2www....
..wwwwwwwww?.wwww???..
@..wwwwwwwwwwwwwww....
....?ww..ww?www..w....
....?......??.w..???..
...........?...@......
ENDMAP

##############################################################################
# Dragon's Lair, for generous values of Dragon
#
NAME:    erik_lair_dragon
TAGS:    lair_entry no_rotate
ORIENT:  float
MONS:    steam dragon / mottled dragon / swamp drake
MONS:    w:20 iguana / komodo dragon / leopard gecko / crocodile / nothing
SUBST:   ? = x.c
SHUFFLE: %*RO
KITEM:   R = royal jelly
KFEAT:   O = enter_lair

MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx?xxx?x?xx?x.2xx?xxx?x*?xxx?.2.xxxx
xxxxxxxxxxxxxxx.2.......?xx.x.?xxx?.??xxxx?x.?xxx
xxxxxxxxxxxxx?....xxx...2....??????..????x..%cccc
xx?xx???xxxxx..xx??xx??..........?...???..??c+.?c
@......xxx.2.xx?xxxxxx?x?x??.......1...2.x.ccc.?c
xxx?xxx...xxxxxxxxxxxx?xxxx.??.2...?......?ccc$cc
xxxxxx?xxx?xxxxxxxxxxxx....xx??...?....??..??cc?c
xxxxxxxxxxxxxxxxxxxxx??.xxx?xx????..???2??.2O?ccc
xxxxxxxxxxxxxxxxxxxxx?R?xxxxxxxxxx.xxx?xxx?xxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Orcish Takeover!
#
NAME:   erik_lair_orcish_takeover
TAGS:   lair_entry
ORIENT: float
MONS:   orc
MONS:   warg
MONS:   orc warrior / warg
MONS:   orc priest / orc wizard / plant
KFEAT:  O = enter_lair
MAP
xxxx..@..xxxx
xx.........xx
x...2...44..x
...4..1......
@....1O3....@
..........4..
x...4..2....x
xx.........xx
xxxx..@..xxxx
ENDMAP

################################################################################
# Plant/fungus maze (due)
#
NAME:   due_lair_maze_entry_diamond
TAGS:   lair_entry
MONS:   plant / fungus
MONS:   w:100 bat / w:100 rat / w:30 river rat / crocodile /\
        yak / nothing
ORIENT: float
SUBST:  . = G:20 B:15 .
SUBST:  1 = 1:60 .
SUBST:  2 = 2:60 1 .
SUBST:  . = .:60 1:20 2:5
COLOUR: G = green
FTILE:  G = floor_grass
SUBST:  G = .
COLOUR: B = brown
FTILE:  B = floor_dirt
SUBST:  B = .
KFEAT:  O = enter_lair
MAP
       ..1.1..
     ...........
   ..1.1.11.11....
 .....11..11211..1.
 .1..1111.11.111...
....1.1111211.111....
.1.11121.111.1.111...
...1111.1211.1.111...
...111.111O.11.1.1...
.1.12....1111211.1.1.
..111111.111.11.1....
 ....1111.1.11.1..1.
 1....1111.11.1..1..
   .1..11111.1....
     ......1....
       ..1....
ENDMAP

################################################################################
# A guard hut placed over the entrance of the lair,
# and a guard to watch over it.
NAME:   grunt_lair_entry_orc_guard_hut
ORIENT: float
TAGS:   lair_entry transparent
MONS:   plant / fungus / nothing
MONS:   w:100 bat / w:100 rat / w:30 river rat / crocodile / yak / w:20 nothing
MONS:   patrolling orc warrior
KFEAT:  O = enter_lair
SUBST:  D = 1:5 2:2 .
SUBST:  . = 1:2 2:2 .
SUBST:  . = G:10 B:5 .
COLOUR: G = green
FTILE:  G = floor_grass
SUBST:  G = .
COLOUR: B = brown
FTILE:  B = floor_dirt
SUBST:  B = .
SUBST:  C = .
KFEAT:  O = enter_lair
validate {{ return has_exit_from_glyph('O') }}
MAP
    ...@..
  ..D.1.1...
 ..D.1D1D1...
 .D.1D221D1..
.D.1D1D1D1D1..
..1D1ccccD1D1.
@1D2DcO3c121..
..121c%CcD2D1@
.1D1Dcc+c1D1..
..1D1D1C1D1.D.
 ..1D122D1.D.
 ...1D1D1.D..
  ...1.1.D..
    ..@...
ENDMAP

################################################################################
# Lair-specific non-entry/ending vaults
################################################################################

###############################################################################
# Rats! (Lemuel), slightly easier now, [combined both Lair and D versions]
#
NAME:    lemuel_ratsnest
ORIENT:  northeast
DEPTH:   D:5-8, Lair
SHUFFLE: 23, 34
MONS:    rat, rat / river rat w:5, river rat, hell rat
ITEM:    slice of pizza w:80 / fruit
SUBST:   d = d$..., e = d$
: if you.in_branch("Lair") then
TAGS:    extra
SUBST:   4 = 40, 3 = 34., 2 = 24., 1 = 13., 0 = 1
CLEAR:   "
: else
SUBST:   1 = 1., 2 = 2., 3 = 3., 4 = 41111
SUBST:   " = x
: end
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxx.eexxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxx343dxx.xxxxxxxx.334433xxxxxxxxx23..exx
xxd222...xxxxxxxx..224422xxxxxx1111...xxx
x..111..xxxxxxxxx..111111.xxx.111...xxxxx
xx.111..xxxxxxxxxx.11111..x......xxxxxxxx
xxxx..xx.x.xxxxxx.xx..........xxxxxxxxxxx
xxxxx.xxx.x..x.xx.x.xx....x.xxxxxxxxxxxxx
xxxxx...x.xxx.x33xx.xxxx.xxxxxxxxxxxxxxxx
xxxxxxx.x.xxxx221x.xxx...xxxxxxxxxxxxxxxx
xxxxxxxx.xxxxx111.xxxx.xxxxxxxxxxxxxxxxxx
"""""""xx..xxx.x.x.xxx.xxxxxxxxxxxxxxxxxx
"""""""xxx..x.xx.x.xx.xxxxxxxxxxxxxxxxxxx
"""""""xxxxx..xx.xx.x.xxxxxxxxxxxxxxxxxxx
"""""""xxxxxxxx.xxxx.xxxxxxxxxxxxxxxxxxxx
"""""""""""""xx.xxxxxxxxxxxxxxxxxxxxxxxxx
"""""""""""""xxx.xxxxxxxxxxxxxxxxxxxxxxxx
"""""""""""""xxx.xxx"""""""""""""""""""xx
"""""""""""""xxxx.xx"""""""""""""""""""xx
"""""""""""""xxx.xxx"""""""""""""""""""xx
"""""""""""""xxxx.xx"""""""""""""""""""xx
"""""""""""""xxxx@xx"""""""""""""""""""xx
ENDMAP

##############################################################################
# A comfortable, reclusive cottage for the summer holidays (Eino)
#
NAME:    pond_and_an_island_with_a_cottage
ORIENT:  float
DEPTH:   Lair:3-, !Lair:$
SUBST:   % = %%%%%$$$**|
MONS:    patrolling necromancer / necromancer
SHUFFLE: 23
MONS:    elephant zombie w:2 / nothing
MONS:    crocodile zombie / basilisk zombie / \
         komodo dragon zombie w:4 / nothing
MAP
             wwwwwwwww
        wwwwwwwwwwwwwwwww
     wwwwwwwwwwwwwwwwwwwwww
    wwwww..........wwwwwwwww
  wwwwww.xxxxxxxxxx.wwwwwwwww
 wwwwwww.x.3.2+.1.x..wwwwwwww
 wwwwwww.x+xxxx2.3x..wwwwwwwww
wwwwwwww.x.%%%x.32x..wwwwwwwww
wwwwwwww.xxxxxxx+xx..wwwwwwwww
wwwwwwwww...........wwwwwwwwww
 wwwwwwwww....WW....wwwwwwwww
  wwwwwwwwww..WWW..wwwwwwwww
  wwwwwwwwwww..W..wwwwwwww
   wwwwwwwwwww...wwwwwww
    wwwwwwwwwww..wwwww
     wwwwwwwww...www
       wwwwww.@.www
         www
ENDMAP

##############################################################################
# Pointless little "vaults" by Lemuel
#

# Three separate decorative bubbles mixed into one vault.
NAME:   lemuel_fruit_fields
TAGS:   allow_dup extra transparent decor
DEPTH:  Lair
ORIENT: float
WEIGHT: 9
MONS:   plant, butterfly
ITEM:   fruit q:1 / fruit q:2 w:7 / fruit q:3 w:3 /\
        fruit q:4 w:1 / fruit q:5 w:1 / fruit q:6 w:1
KFEAT:  ^~ = .
: local size = crawl.random2(9)
: if size > 7 then
SUBST:  '`"- = ^:20 ~:15 ., . = d:6 .:150 1:10 2:4
: elseif size > 3 then
SUBST:  "` = ^:20 ~:15 ., . = d .:50 1:8 2:4
: else
SUBST:  '" = ^:20 ~:15 ., . = d .:50 1:8 2:4
: end
CLEAR:  `'"-
COLOUR: ^ = green
FTILE:  ^ = floor_grass
COLOUR: ~ = brown
FTILE:  ~ = floor_dirt
MAP
       -------
     -----------
   -``------------
 -``````-----------
 ```````"'''-------
-`````"""'''''-``----
--```"""'''''""````--
----`"''''''"""`````-
-----'''''''"""`````-
-----'""'''''""````--
----``""""''''-``----
 --`````"""'--------
 --````````---------
   -``````--------
     -``--------
       -------
ENDMAP

##############################################################################
# Plantage
#

# Two separate small decorative vaults mixed into one:
NAME:   lemuel_fruit_tree
TAGS:   extra transparent decor
DEPTH:  Lair, Snake
ORIENT: float
MONS:   plant, fungus
KITEM:  a : fruit q:1
: if crawl.one_chance_in(5) then
SUBST:  '"~ = . 1
SUBST:  0`-. = .:90 a:10 2:1
SUBST:  _ = .:90 a:4
: else
CLEAR:  '-_
SUBST:  `" = . a 2:1
SUBST:  ~. = .:90 a:10 2:1
SUBST:  0 = 1
: end
MAP
   ________
  _--------_
 _---..~..--_
_--'.......--_
_---..`"`.~--_
_---..`0`..--_
_---..`"`..--_
 _--~....~.-_
  _--.....-_
   ________
ENDMAP

##############################################################################
# Ice dragon lair with assorted imp pets
#
NAME:   bobbens_ice_dragon_lair
DEPTH:  Lair:4-
TAGS:   no_monster_gen
MONS:   ice dragon
MONS:   w:9 white imp/w:1 nothing
MONS:   white imp
KPROP:  _` = no_tele_into
SUBST:  ! = $:3 *:2
SUBST:  ' = w:15 x:12 `:1
SUBST:  ; = x:2 .:3
SUBST:  _ = %:1 `:3
SUBST:  " = G:1 .:3
MAP
;;..;..;;;.;;.;;;
;;...wwww.;;..ww.;;
;..ww'''ww..wwwww;;;;
;.ww'xxxwwwwwwwww;....
;.ww'x!xx''''''w...ww.
;..wwx!!xxxxxx'w..www.
;;..wx!1x..>xx'w"www.;
;;;.wx!!.2x3.x'wwww.;
;;;wwxxw2.xW.''www.;;
;..www'xxxxxxw'wwww.;;
..wwww''xxxxwww''ww.;;
.wwwwwww...www'_'ww.;
..wwwww....www'''w.;;
.;www...V..wwwwww.;;
;............www.;;;
;;   ;;;....w..;
ENDMAP

##############################################################################
# Dragon lair embedded in wall
#
NAME:   bobbens_dragon_lair_wall
DEPTH:  Lair:2-
TAGS:   no_monster_gen
: if you.depth() >= 7 then
MONS:   fire dragon w:9 / lindwurm w:1
MONS:   rime drake / w:2 mottled dragon / w:4 nothing
: else
MONS:   mottled dragon / steam dragon
MONS:   rime drake / w:15 nothing
: end
ITEM:   any good_item
SUBST:  ! = $:3 d:2
SUBST:  ' = l:15 x:9 c:3
SUBST:  ? = x:3 c:1
SUBST:  ; = x:3 c:1 .:4
SUBST:  / = l:3 .:1
SUBST:  _ = %:1 .:3
MAP
xxxxx?????????xxxxx
xxx?????..//???xxxx
xx???'.../?/!????xx
x???;.2./???!!???xx
x??;.1../;??!!???xx
x??;.../''???!???xx
x??;'.../'???????xx
x???;/..???????xxxx
x??????;.?????xxxxx
xx?????.;?????xxxxx
xx????./??????xxlxx
xx????.;_????xxxlxx
xx?./'.2///???x?xlx
x?//.././///x??xl/x
.._....//..//?x//..
.././...G./..//....
./........./.......
ENDMAP

##############################################################################
# Drake nest (minmay)
#
NAME:   minmay_lair_drake_nest
DEPTH:  Lair:4-
MONS:   rime drake, plant w:20 / fungus w:1 / bush w:1
NSUBST: 1 = 1:1 / * = 1.., . = 1:2 / * = ...2, . = 1:% / * = ...%
MAP
   ...
  .....
 .......
...111...
...111...
...111...
 .......
  .....
   ...
ENDMAP

##############################################################################
# Venomous thicket (minmay)
#
NAME:   minmay_lair_venomous_thicket
TAGS:   ruin_lair
DEPTH:  Lair
ORIENT: north
MONS:   black mamba / spiny frog / wolf spider
MONS:   basilisk / komodo dragon / hornet
MONS:   adder
MONS:   plant / fungus w:2 / bush w:1, bush
SUBST:  4 = 444. 5:2, . = ...4 5:2
SUBST:  1 = 1., 2 = 2., 3 = 3.
MAP
xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx
xxxxxxx4444xxxxxxx
xxxxx4441%%34xxxxx
xxxx444%3.1.44xxxx
xxxx4.31.3.3.4xxxx
xxx4.2.3444.144xxx
xxx.3.444444324xxx
xxx2..4444444.4xxx
xxx43.444444434xxx
xxx442.444444.4xxx
xx444..344443..4xx
xx44..........3.xx
..3..3....3.......
ENDMAP

##############################################################################
# Snail & Pal
#
NAME:   pf_torpor_hydra
TAGS:   no_monster_gen no_item_gen
DEPTH:  Lair:3-, !Lair:$
MONS:   torpor snail, hydra
# A decent item visible from the door, a superb item not visible, and a couple
# of floor items anywhere
NSUBST: . = 1:* / *:.
NSUBST: _ = 1:| / *:.
NSUBST: . = 2:% / *:.
MAP
x xx  x
 xxxxxx x
xx.2..xxx
x__x1..=xx
x______x@x
xx__x_xx
 xxxxxx
   x  x
ENDMAP

##############################################################################
#       Hibernating monster in a tunnel, can be pretty dangerous
#
NAME:   hibernation_bobbens
DEPTH:  Lair
TAGS:   allow_dup no_monster_gen no_item_gen
MONS:   ice dragon / fire dragon / swamp dragon / anaconda w:5
MONS:   swamp dragon / hydra / catoblepas
MONS:   swamp drake / w:20 spiny frog / w:20 komodo dragon /\
        w:20 basilisk / w:20 black mamba
MONS:   plant
: if you.absdepth() >= 17 then
SUBST:  M = 1:1 2:9 3:20
: else
SUBST:  M = 2:1 3:4
: end
SUBST:  ; = x:3 .:1
MAP
xxxxxxxxxx
xxx....xxx
xx.....%xx
xx44xx%Mxx
xxx..xxxxx
xxx;.;xxxx
xxx;..xxxx
xxxx..;xxx
xxx....xxx
ENDMAP

##############################################################################
# Oklob tunnel
#
NAME:   minmay_lair_oklob_tunnel
ORIENT: southwest
DEPTH:  Lair
MONS:   plant / oklob plant w:1 / nothing w:15
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xllllllllxxxxxxxxxxxxxxxxxxx11xxx111.....
xlllllllxxxxxxxxxxxxxx111xxxx111xx111...@
xlllllxxxx111xxx11xxxxx111111111111111..x
xllxxxxxx11xxxx11xxxx111111111111111111xx
xllllxxxx1xxxxxx11111111111111111111x1xx
xllxxxxxx11xx1111111111111xxx11111xxxxxx
xllx11xx1111111111111111111xxxxxx111x
xlxxx1111111111111xx111xxx11xxxxxxxxx
xlx%+11111111111xxxxxx11xxxxx
xlx%xxx11111xxx11xxxxxxxxxx
xlx|xx11xxx11xxxxx
xlxxxxxxxxxx111xx
xllllllxxxxxxxxx
xxxlllxxxx
xxxxxxxx
ENDMAP

##############################################################################
# Items with an oklob
#
NAME:   minmay_lair_oklob_items
DEPTH:  Lair
ORIENT: float
MONS:   plant / fungus w:3 / bush w:5
KMONS:  | = oklob plant
KITEM:  | = |
SUBST:  X = xx_
CLEAR:  _
SUBST:  % = ....%%%%**
MAP
XXXXXXXXX
Xxxxxxxxx.
Xx%%%1.x.@
Xx%%%xx.x.
Xx|%%xxxx
XxxxxxXXX
XXXXXXX
ENDMAP

##############################################################################
# Oklob-based vaults.
# Multiple vaults differing in size were combined into one.

NAME:   minmay_lair_oklob_chamber
TAGS:   luniq_oklob no_monster_gen
DEPTH:  Lair
ORIENT: north
KMONS:  1234 = plant
KMONS:  P = oklob plant
: local size = crawl.random2(10)
: if size > 8 then
SUBST:  '`" = ., yza = x, XYZ = ?
NSUBST: ? = 1:% / 4:P / * = ?
: elseif size > 6 then
SUBST:  '` = ., XY = ?, yzZ4 = x
NSUBST: ? = 1:% / 3:P / * = ?
: elseif size > 3 then
SUBST:  ' = ., X = ?, 3yY = x
NSUBST: ? = 1:% / 2:P / * = ?
: else
SUBST:  X2 = x
NSUBST: ? = 1:% / 1:P / * = ?
: end
CLEAR:  '`"34yzaYZ
SUBST:  ? = ....x11$$%%*|
MAP
aaaaaaa
zZZZZZa
yYYYYZa
xXXXYZa
x??XYZa
x??XYZa
x11234a
....'`"
 .@'`"
ENDMAP

NAME:   minmay_lair_oklob_bushes
TAGS:   luniq_oklob
DEPTH:  Lair
MONS:   oklob plant, bush
: local size = crawl.random2(10)
: if size > 8 then
SUBST:  `'" = .
: elseif size > 6 then
SUBST:  `' = .
: elseif size > 3 then
SUBST:  ` = .
: end
CLEAR:  `'"
MAP
       """
    """""""""
   """""""""""
  """""'''"""""
 """"'''''''""""
 """'''```'''"""
 """''`.%.`''"""
"""''`.222.`''"""
"""''`%212%`''"""
"""''`.222.`''"""
 """''`...`''"""
 """'''```'''"""
 """"'''''''""""
  """""'''"""""
   """""""""""
    """""""""
       """
ENDMAP

###############################################################################
# Catoblepas lookout (blackcustard)
NAME: lookout_lair_type
DEPTH: Lair:4-
TAGS: extra no_pool_fixup
MONS: w:1 generate_awake patrolling catoblepas / w:4 nothing
SUBST: a = w W .
MAP
  aaa aa
 aawwwwaa
 aaw1.wa
aaaw..ww
 aaw...Wa
  awwwww@
  a aaaa
     aa
ENDMAP

###############################################################################
# Two stream minivaults
NAME:   guppyfry_lair_drakes
DEPTH:  Lair
WEIGHT: 15
: if you.depth() > 3 and crawl.coinflip() then
MONS:   rime drake / swamp drake / mottled dragon
MONS:   steam dragon
MONS:   plant
SUBST:  -_ = wwWW..%
SHUFFLE: .t
: else
SUBST:  12 = 0, _ = wW.%, 3- = .
: end
MAP
.3WW-3.....
.ttWW-ttt..
.3ttW-tt.t.
.ttWWtt...t
tt1Wttt2.tt
.ttWW-t3.t.
..ttWW_-1t.
.tt1_WWttt.
..tt-_Wtt3.
.tt3.tWWttW
.ttt.ttWWW-
....3.3-W-.
ENDMAP

###############################################################################
# A polar bear lost in the jungle, how odd
NAME:  roderic_lost_polar_bear
TAGS:  no_monster_gen
DEPTH: Lair:3-, !Lair:$
MONS:  plant, polar_bear, bush
SUBST: ? = 1t
SUBST: ' = .:80 1 3:3
MAP
??'.
.'2'
'??.
ENDMAP

###############################################################################
# Wood themed vaults. Can generate in Forest, Lair, and occasionally D.
# uniq_ tagged by tier: large, medium, small, rock, and decor.
#
NAME:   wad_woods_pond
DEPTH:  D:2-, Lair, Forest
ORIENT: float
: wad_woods_setup(_G, "large", 1, true, 21, 2, 36)
FTILE:  ' = floor_dirt
MAP
                 .         tt.
             tttt. tt  ttttt...ttt    tt   tt
         tttttttt...ttttttt....tttt....t. ttt
      tttt11.tt.t...t11t.......tt.ttttt..t..tt
     ttttttt..tt.t1...t.1t...t...t...tttt..t.tt
   ttt.tt.ttt1t1t.t.....tt.........t....ttt....
  ttt....tttt.t.........Wt.t............t..t.t
 ttttt..ttt.t...t.W.OWtWt1t....1........t....tt
 tt11.tttt1.t..t...W1tWWtW1tWt....1....'.''..tt
ttt1.t.1t.t......WtWWWwWWtWWtWtW.W......''...t
 tt...t........tWtWWww.w..WWWWWWWt.1......'..t
  tttt...t...t....ttWWtwwtWWWtWW1W......11t1.tt
  ttt...11t1t.t...W.W.WWWWtW1tW.tt.......t.tttt
 tt.t.t.tt.t1tttt....W.tWtW1WO.t....1...t1.t...
ttt..tttttt.tt.t.t.1.........1.........ttt..1t
 tt.t...tttt.t1.11................t.t.t.ttttt
 tt...t..t.tt1t11..1t..........t1t.t.t1t.tt1.
 tt....1t..tt.t1...ttt1...1...t.t.t.t.t.t.t...
ttt....tt1tt.t......1ttt1...tttt.ttt1t.t.t.tt..
 tt...t11tt..1t........tt.1t.tt.tt.tt1t.t.t.tt.
 .t......t..1t1.....1..1t.t.tt.t.t.t.t.t.t.t
...t.......t1.........1t1tt..1tt..t.ttt.t.t.
 .t1t....1.....'.....t.1..tt.t..t..11ttt1t.t
  1.tt.1.....''''...tt..t..tt..t1.1tt..tt1t.t
     ......'''''.....tt.t.11tt..t..t.1t..t.t.t
      ..1...'.'''..t..t.11.t.tt...t1.ttt..t..t
      t.........1...tt.1.tt.tt..t.tttt...t..tt
      .t1.1..1.......t..t.t.1tt.t11..t.t..1t
       ..1.11..1.11t.1t.tttt.t.t1.t.ttt..1ttt
          ..111.1.ttttttt    ttttt tttttt.tt
            ......ttttt       ttt  tttttt..
              ..   ttt                    .
ENDMAP

NAME:   wad_woods_settlement
DEPTH:  D:2-, Lair, Forest
ORIENT: float
: wad_woods_setup(_G, "large", 1, true, 21, 2, 36)
NSUBST: 4 = 2:4 / 1 = 4' / *:', 6 = 4
FTILE:  '4*+ = floor_dirt
MAP
               ..  tttt   ...   ...
          .tt.t...t11t.......tt.ttt
       .tt..tt.t1...t.1t...t...t...ttt
     tt.ttt1t1t.t................t....ttt.
      .t.tt.t.......xxx...............t..t.
    ..ttt.t...xxx...+4x'''''''1......x.t..
  1.tt.t1.....x4+'''xxx'....x+xx1...x.....
t1.t.1t.t.....xxx..'1'''..1.x''x..........
t...t..............'''1..T..x*6x..1.x......
tttt...t...t.........'.%....xx+x1...x.11t1.
 tt...11t1t.t.....x..''''''''''........t.ttt
..t.t.tt.t1tt.t....x''.xxxx.......1...t1.t.
t..tt...t.tt.t.t.1''x''+%4x..........ttt..1
t.t...tt.tOt1.11.'....1xxxx1....t.t.t.ttttt
 ...t..t.tt1t11.'1t..........t1t.t.t1t.tt1.
  ...1t.%tt.t1..'tt.1....1..t.t.t.t.t.tOt...
  ...tt1tt.t....'.1ttt1...tttt.ttt1t.t.t.tt
  ..t11tt..1t...'....tt.1t.tt%tt.tt1t5t.t
 ......t..1t1..G4G1..1t.t.tt.t.t.t.t.t.t.t
.t.......t1.....'...1t1tt..1tt..t.ttt.t.t
t1t....1........'..t.1..tt.tO.t..11ttt1t.
1.tt.1....xxx..'..tt..t..tt5.t1.1tt..tt1tt
  t.......x4+''....tt.t.11tt..t..t.1t..t.t.
  tt..1...xxx....t..t.11.t.tt...t1.ttt..t...
.t1.t.........1...tt.1.tt.tt..t.tttt...t..tt
1.t1.t1.1..1.......t%.t.t.1tt.t11..t.t.51ttt
tt.11..1.11..1.1  .1t.ttt      .t.ttt..1tt
.1t   .1  111. .     tt        tt.t ttt.tt
ENDMAP

NAME:   wad_woods_tower
DEPTH:  D:2-, Lair, Forest
ORIENT: float
FTILE:  q13%OG. = floor_woodground
: wad_woods_setup(_G, "med", 2, true, 21, 2, 36)
FTILE:  '*+T4 = floor_dirt
MAP
   tttt....   .tt1   tt.t    tttt  ..t
t.1.tttt.tt.11O..t.11ttt..  15tttt....t
t..11xxx.t.tttt.tttt....t1t..11t.t..ttt.
 .t.xx4xxt.1ttt..t1t.tt.ttt.tttt.1.ttt1
 .1x+'*'+x..t....t..t..1.t..11tt...t....
  .txx4xx.....t.1ttt.1ttt.1..t.t...t.1
t.15txxx.t.t.t..tt1.1G...t.t.1tt..ttt.
 ..t.t...tt..t..1...'''%..1t..t..t...t.
  .tt.t1...1t.1t...G'T'G.tt......t......
 ...t..t.tt..111.t..'''.11.1..t.t.1..t.
 ....t.t.t.ttt1.tt.1.G1t.t....t.t.5ttt..
..t.tt.t.t..t1t..t.t1tt.ttt.t...1..ttt
.11O...t..5.t..11t..t....1..11tt.1.t..t.
t..t1t..t...11t..ttt..ttt....tOt...t.11t
  1..t..t.  t..tt. 1t..  tt..tt  ttt.tt
   t.t.1.   .t1.  ..     .11    .t.1...
ENDMAP

NAME:    wad_woods_stonehenge
DEPTH:   D:2-, Lair, Forest
ORIENT:  float
: wad_woods_setup(_G, "med", 2, true, 21, 2, 36)
SHUFFLE: 4% / '"
SUBST:   '" = .
MAP
       .....t..tt1.t  t.t    .. tt...
      tt1tt.1tOt.t.11ttt..1.15tttt.1..
     ttt%t.tttt.tttt....t1t..11t.t.1tt..
    ttttttt1ttt..t1t.tt.ttt.tttt.1.ttt1t
    ttttttt.t.t..t..t..1.t..11tt1..t.1.ttt
   tttttt1t.1.t.1ttt.1t..1..t.t...1t11..tt
  15tttttt1t.t...t1..G...t.t......tt..1t11
t.tt.t.t.tt..t..1......c...c...t..t..t.t
.t11t.t1.t.1t.1t.........%.......t..1...
t1..t1.t.tt.t111.t...c..4C4..c..1t.1.tt.t.
 .1t.t.11t.ttt1.t..1.....%......t.5tttt...
  t.tt.t.t..t1t......1.c...c...1.1..tt1.
  1O.ttt1.5.t..11t1....1.1..11tt.1.t1...
   t1tttt.1.11t..ttt1.tt11tt.tOt1.tt.11
    tt..t.t1   tt.11t1.1    .tt.1ttt.
      .t.t  .   .t..t        .t1.t
ENDMAP

NAME:   wad_woods_01
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "small", 2, true, 21, 2, 8)
MAP
     tt.tt  tt.1ttt
    tt...tt1tOtt.1.t
   ..1ttt....tttt...
 t4t.ttt.tttttt4t.t.1t
 t.tt..1tt1.tt.t.tt.tt
 .tt.%1...tt.1ttt..t.tt
tO1.tt..tt.t.t.tttt4t1
t.t.tt.t.tt.tG..ttt.tt
tt.ttt.1..ttt..ttt.1.tt
t.tt.ttttt.tt1ttt1.t.tt
ttt.tt4ttt..%.4ttOttt
  ..1tt.t1t..G...t.t
   .tt..4t1t.t.tt1t
    .ttt  .t..ttt
ENDMAP

NAME:   wad_woods_02
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "small", 2, false, 0, 0, 0)
MAP
   .1t1.t   .t.1t
   t.1...t.1t1tt.1
   151t.t.1..t1tt...
  1t.ttt.tt.1t1t..t1
 t.tt.11tt1....t.tt.t
t.tt.%4..WWW......1t.tt
tt1.....WWwWW..t.tt.t1.
.1t.tt...WWW...tt1.t.t4
 t.t.t.1..W.4..t1t.1.tt
t.tt.t1t..1..1t.t1.t.
...1t11t1t.1%.ttt...
 t1.1tt.t1t..G..1..
  t11..1tt    .1.1t
    .1..1       .1.
ENDMAP

NAME:   wad_woods_03
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "rock", 3, false, 0, 0, 0)
FTILE:  '%0+14 = floor_dirt
MAP
          .t t.tt'xx
   .''t...tt'''''t.1x
@'''.t''''''ttttx11x1tx
  ttt..t.tttxttxx11xx1x
  ...1t.tttxxxxxxx+xxx1
  .t..tttxtx%'4x''''x1x
  t.tt.tttxx%'0+'''0xx1
  .tt.tttttxxxxxxxxxxxx
   ....tt..tttxxtxxxxxx
    ..tttt..1xtttxxxxxx
    ...t1.ttt.1ttxxxxxx
      ..t.tt1txxtxxxxxx
         ttxxxxxxxxxxxx
            t1txxtttxtx
ENDMAP

NAME:   wad_woods_04
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "rock", 2, false, 0, 0, 0)
FTILE:  '+*0 = floor_dirt
MAP
            .tttttt.ttxtx
          t1t.tttxtt.tttx
      tt1t11tt11ttxxx.xxx
     tt.tt..tttttttx.x.xx
    tt1t..tt11txxtt.ttx..
    tttt.tttttxxxx.xxxxxx
   tt1t2tttx.xxxx....xxxx
   ttt11tt1.t..t.txxx.x.x
  ..tttt1t.ttxx.xxxxxx.xx
 .t.1.tt....ttxttxt..xx.x
.ttttt..tttttttt.xxx..1x.
tt1t.t.txx..xx..xxxxxxxxx
tt...tttttxx.t.xxtxxxxxxx
x.tt.x.xxxxx..xxxxx''*xxx
.xx1xxxxxxxxxxx101x'0'xxx
xxxx1.........4xxx+'''xxx
xxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   wad_woods_06
TAGS:   extra
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "small", 2, false, 0, 0, 0)
MAP
    tt2ttt  1
   tt.ttttt1t
  tttt.ttt1tt
tt1tt.1.tttt
tttt0...2tt1
 tt...G0.1ttt
 ttt02..1ttt
tt1tt...ttttt
t1tt1ttt21tt
   tttttt.t
     tt1t.t
ENDMAP

NAME:   wad_woods_antlair
TAGS:   uniq_anthill
DEPTH:  Lair:2-, Forest
: wad_woods_setup(_G, "rock", 5, false, 0, 0, 0)
MONS:   worker ant, soldier ant
KMONS:  8 = entropy weaver
KMONS:  Q = queen ant
: if (you.branch() == "Lair" and you.absdepth() < 14)
:  or you.absdepth() < 12 then
SUBST:  8' = .
: else
SUBST:  ' = %
: end
MAP
xxxxxxxxxxx1.xxxx
xxx1xxx1xt.t.txxx
x1xx1%..t1t.1x.tx
xx..1.t...WWt%.x.
xxxx.t.t.1tWWtt1.
xxxxxxx1.....t.t.
xxxxxx.x.x.1x...t
xxxx.x.7xxx.xx.xx
xxx6x.x.xxx.x.xxx
xxxx.x.6x7..x.x7x
x'x.x6x.W.x7x%86x
x7.'x.xWWW.x86Q*x
xx6xxxxxxxx6xx*%x
xxxxxxxxxxxxxxxxx
ENDMAP

NAME:   wad_woods_river_01
TAGS:   extra
DEPTH:  D:2-, Lair, Forest
ORIENT: float
: wad_woods_setup(_G, "med", 1, false, 0, 0, 0)
MAP
      tttt       ttttt
   t.tt1t.tt.   tt.t..tt
  t..t1ttt.t.t.tt11t.tt.t
 .1tt.ttttttt.t.t..t.t.t...
 .t.tttttt.t.tt1....t.t.t...
  t.t1tttt.1ttt.t.ttt.t.tt1t.
.ttt.t.tt1t.1t.tt1t0t.tt.t.ttt
t.1t5tt.tt.tt.t.ttt%t.t.t1tttt
ttt1t......0..1tWWWWWWWWWWWttt
 tt...tWWWWWWWWWWwwwwwwwwwWWWW
WWWWWWWWwwwwwwwwwwWWWWWWwwwwww
wwwwwwwwwwwWWWWWWWWW1.tWWWWWWW
WWWWWWWWWWWWWt....1.......ttt
tttt..tt.t..%.t.t..ttt1..t.tt
ttt5tt.t.tt1t1t.t%1tt.t5tttttt
 tt1t.tttttt.t.tttt.t.t1t1.ttt
  ttt.t.t1t.t.t.1tt.t.tt11tt
  tt.1tt1t.ttttt.t.t.t1t.ttt
    .t1tttttttttttt.ttt.t.tt
    .tttt   tt ttttt.ttttt
   .ttt           ttt.tt
ENDMAP

NAME:   wad_woods_river_02
TAGS:   extra
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "small", 1, true, 32, 2, 24)
MAP
      tttttttttt1tt
   tt1ttt..WWWW..0tt
ttt1ttt0.WWWwwWWW..tt
t11t.WWtWWWwwwwwWWWtt
.tt.WWWWWWwww|twwwWWt
WWWWWWwwwwwww>OwwwWWt
wwwwwwwwwwwwwwwwwwWWt
WWWWWWwwwwwwwwwwWWW1t
..ttWWWwwwwwwwWWW..tt
tt.t.tWWWwWWWWW.t.ttt
ttt1ttt.WWWt.ttt0t1t
  tttt1t0.ttttttt1tt
    tttttttttttttt
ENDMAP

NAME:   wad_woods_river_03
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "decor", 1, false, 32, 3, 24)
MAP
   1t.tWwW.t.t.t
   t.ttWwWt.tt1t
   t.ttWwW.t11tt
  t11.WWwWWt.t1t
tt1t.WWwwWW.ttt
t1ttWWwwWW.tttt
t.WWwwwWWtOttt
WWwwwWWtt.t.t
wwwWWtt.tttt.
WWWWt.t.t1t
.tt.t.t1tt
ENDMAP

NAME:   wad_woods_river_04
TAGS:   no_vmirror no_rotate
DEPTH:  D, Lair, Forest
TILE:   X = wall_brick_dark_leak
TILE:   x = wall_brick_dark
SUBST:  X = x
: wad_woods_setup(_G, "decor", 1, false, 0, 0, 0)
MAP
   ttttxxxtttt
 tt1txxxxxxxtt
ttt1xxxxXxxxx1tt
tttt1xxxWxxx1tt
ttt.t.1tWt.1t1tt
tt.t1tt1WW1tt.tt
.1ttt1tWWtt1tt.t
ttt11tttWWttt1t1
tt.ttt1WWWtt*ttt
t.ttttWWWttttt1t
 tttt.WwWttt.tt
   tttWwW1tttt
   tttWwW..ttt
    ttWwWt..t
ENDMAP

NAME:   wad_woods_road_01
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "decor", 2, true, 32, 3, 24)
FTILE:  @' = floor_dirt
MAP
    t1t.t1  tt.1tt   .@
  .1tt1.1   tOtt.1.tt.'
 tt..2ttt.1..tttt...%'.
 t2t.ttt.ttt1tt2t.t.'.
1t.tt..1tt1.tt.t.tt'.1
t.tt.%1..1tt.1ttt''t1tt
.........G.....1'...1..
@'''''''''''''''''''''@
.1...1...T3............
t.t.tt.t.tt.tG..t.t.ttt
tt.ttt.1..ttt..ttt.1.tt
 .tt.t2ttt.tt1t1t1.t.t
 tt.tt2ttt..%.2ttOttt
 t..1tt.t1t..G...t.t
  t.tt..2 1t.t.tt1t
  t21t      ..tttt
ENDMAP

NAME:   wad_woods_road_02
TAGS:   extra
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "small", 2, false, 32, 0, 0)
FTILE:  @' = floor_dirt
MAP
      ttt      t
   ttttt1t  111ttt
 t.tttt1ttttt1t1t.t
t1t.tt1ttttttt1ttt.t
t....t.'''''0t....t.
@'''''''.G%''''''''@
t....t0'''''.t....t.
ttt..ttt1tt11tt.t.tt
tt.tt.t1tt1tt1ttttt
tt1tt1ttt303tttttt
 ttt1ttt3.U.3t1ttt
   tttt1t3.3ttt1tt
     tt ttttt1 tt
ENDMAP

NAME:   wad_woods_road_03
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "decor", 2, false, 0, 0, 0)
FTILE:  @' = floor_dirt
MAP
       tt..@.tt
     1tt..'..t.t
   ttt1t..'.t.ttt
 tt1ttt..'.t0t.t
tt1tt...'.tttt
ttt..'''.t1t
....'..11ttt
.'''..ttt1t
@....tt1t
...1tttt
tttt1tt
ENDMAP

NAME:   wad_woods_road_04
DEPTH:  Lair, Forest
KMONS:  P = oklob plant
: wad_woods_setup(_G, "small", 1, false, 0, 0, 0)
FTILE:  @'+T = floor_dirt
MAP
  ttt..@..ttttttttt
 t11...'.ttt1ttP11t
 tttt1..'.tttt1t*1tt
ttt1tttt.'..ttttt1t
  ttttttt.'..1tt1ttt
   tt1t...'tttt..t..
  t1tttt.''''''''...
  ttttt..'.t1.tt.''@
   ttt1t.'tttttttttt
   tt1ttt.'.ttttttt
  tt1tttttt'..ttttt
  ttt1tt1..'.ttttt
   ttt1tt..@ttt
ENDMAP

NAME:   wad_woods_road_05
DEPTH:  Lair, Forest
KMONS:  * = 9
KITEM:  * = *
: wad_woods_setup(_G, "small", 2, false, 0, 0, 0)
FTILE:  @'*+T = floor_dirt
MAP
       .ttttt1.@.tt
     .1ttttt..'t1tt
    .tt..1t.''tttttt
   tt1tttt.'.t.tW1tt
ttt.tt....'.t1t1WWtt
tt.t.tt..'..tttttWWW
 tttt...'..t1tttxtWw
 t.1t.''tt1t1ttxxxtW
 ttt.'.t1tttt1x+*xxt
ttt.'.tttt   t1xxxtt
tt.@t1ttt    tttxttt
ENDMAP

NAME:   wad_woods_road_06
DEPTH:  Lair, Forest
: wad_woods_setup(_G, "decor", 1, false, 0, 0, 0)
FTILE:  @'*+T = floor_dirt
MAP
   tt.'@@..tt1ttttt..@@@..ttt
  tttt.'..ttttt1t11..'...1t11t
 tt1tt..'...tt1t1t...'..tt1tx1t
 tttt1t..'....tttt...'..tttxxx1
 ttttttt..''...ttt..'...ttttx1t
  tttttt1t..'...t..'..1.tttt%tt
   tttt.t1tt.''.%.'....t1tt1ttt
     t1ttttttt.'''...tt111t1tt
      ttttttttt1....tt1ttt1tt
       tttttttttttt1tttttt
           tttttttttttt
ENDMAP

NAME:   wad_woods_minihive
TAGS:   uniq_beehive
DEPTH:  D:8-, Lair, Forest
ORIENT: float
MONS:   killer bee, queen bee
KMONS:  8 = meliai
KITEM:  8 = %
KFEAT:  a = x
: wad_woods_setup(_G, "med", 5, false, 0, 0, 0)
: if (you.branch() == "Lair" and you.absdepth() < 14)
:  or you.absdepth() < 12 then
SUBST:  8 = 6
: end
KMASK:  W = no_monster_gen
COLOUR: a = yellow
TILE:   a = wall_wax
MAP
        ttttttt..t    tt.
      .ttt.t.tt...t  ttt..
    ttt.tt..tt.1t.t.1t.1.t...
 t.t1ttt.1..1WW.1t....t1...t...
t.1t..t.1..tWWW.t.1.1..t.tt..tt.
ttt.t11.1t...tWW.1t.t1..tt...ttt
tttt.1..aaamaaa1Wt.t...t..tt1ttt
t.t1.t1aa6.a66aaWWt1.1t.ttttttt
 tt.ttaaa.aba.aaaWW.t..ttttt1ttt
  tt.aa86a7a.a.6aaWtt1t...tt.tt
  t.aaaaaab|baaaaaa.t.......ttt
 t1.1aa66a.a.a.8aa.t...t.t.ttttt
tt.1..aaa.aba6aaa.11.t..tttt%ttt
t.t.1..aa.8a6.aaWW.t.tt.tt11t
tt.t.1.1aaaaaaaWWwW1.t..t.tt
 tt.t111....t1WWwW.....1ttt
  11.t.tt1.tt.tWWt1.1tt...
    t.tttxtttt1ttttttxx.
      .txxxt   ttxxtttt.
ENDMAP

NAME:   wad_woods_structure_01
TAGS:   no_monster_gen
DEPTH:  D:10-, Lair, Forest
ORIENT: float
: wad_woods_setup(_G, "other", 5, false, 0, 0, 0)
FTILE:  "2T = floor_grass
FTILE:  @'0+ = floor_dirt
MAP
   ttxcxxxxxxxxxxxxxxxxxxxxxxxcxxxxxct
 ttttxc''''+''0'xxxxxx'''+''0'cx0'0'ctt
 t.ttxc''0'xxxx'x''00x'''x''''cx''''ctt
 tt.txc'0''x''x'x''''x''0x''''cx''''ct1
tt.t.xc''''x''+'+'''0x'''x''''cc+cc+ct1tt
t.t.txccc++ccccccxcxcxcxcc++cccx'x2'2tt1tt
tttt.t.tt''tttttttcttttttt''ttx''x"'"ttt1t
  ttt..tt''''''tttcttt''''''ttxxxx2'2tt1tt
 ttt.tt..ttttt''ttctt''t...t.ttt.2"'"2.tt
 t.tt.t.t.t..tt''tct''ttt..tttx22""'""22tt
 tt.tt.ttt...t.t''c''t..t.t..tx2""'''""2tt
 ttt.ttt..t.t.t..'''tt.t.ttt..x"2''T''2"tt
  ttt...ttt.tt...0'0t.tt...tt.x2""'''""2t
  ttt.t.t..t.t...'''ttt..tt..tx22""'""22tt
tcccccccccccccccc+c+ccccccccccccccc+ccccct
 tttttt.111ttt.1x'x'xtttttttttttttt1ttttt
 ttt%1.tttt1..tt.'''.ttttttttttttt1.1tt
  ttt.tt..1txxxc.'''.cxxxttt1tttttt.tttt
 tt.tttt%.ttt1tt.'''.tttttt.t1ttttt.%*tt
  tt.1ttt.tt1txx.'''.xxtt..ttt%tttttt..t
  ttt.%.tt.tttcG.'''.Gct1tttttt.tttt..ttt
 ttt.t1t1t.tttxx.'''.xx1ttttttt.ttttt.ttt
 tt.tttt*.tt1ttt.'''.11ttttttt.tt1t.1tttt
 ttt1tttttttt1xx.'''.xxtttttt.t.%t1tt
 ttt..ttttttttcG.'''.Gcttttttt%.t.
  tttt1.%tt   xx..'..xxtttt
   ttttttt     1t.@.tttt
ENDMAP

###############################################################################

NAME:   kennysheep_rathole
TAGS:   no_monster_gen
DEPTH:  Lair
ORIENT: float
MONS:   river rat band, hell rat, river rat / rat
KMONS:  P = plant / fungus w:2
KITEM:  d = slice of pizza / beef jerky / bread ration
NSUBST: W = 1:1 / 2:P / 2:2 / 2:3 / 2:d
MAP
 xxxx
 xxxxxx
xxd.Wxxxx
xx..WWWxx
xxWWW.WWxx
xxxWWWWWxx
  xWxW.WWx
  xxxW...x
     @  @
ENDMAP

# contains food and one ok item to make the hell rats a little more worth it
NAME:   kennysheep_abandoned_house
TAGS:   ruin
DEPTH:  Lair
ORIENT: float
MONS:   river rat / rat, hell rat, rat
KITEM:  d = slice of pizza / beef jerky / bread ration
SUBST:  d = d123..., 2 = 2.
NSUBST: 1 = *:1
MAP
      .......
.......ccccc.........
.ccccccccccccccccccc.
.cccccccdddccccccccc.
.ccdddccdddcc..dddcc.
.ccdddccdddcc..dddcc.
.cc...cc...+...dddcc.
.cccc.cccc.ccccccccc.
.cccc+cccc+ccccccccc.
.cc.....+...+...ddcc.
.ccddddcc...cc..ddcc.
.ccddddcc...cc..ddcc.
.cccccccc+++cccccccc.
.ccccccc.....ccccccc.
.....................
ENDMAP

# a sealed arena with some loot
# contains one catoblepas, three other monsters and four items
# the walls are usually thick enough to make this only accessible from the floor below
NAME: kennysheep_catoblepas_arena
DEPTH: Lair:4-, !Lair:$
TAGS:  no_monster_gen ruin
MONS:  catoblepas
NSUBST: d = 4=%* / 3:0 / *:.
MAP
xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx
xxxxxxxdd1ddxxxxxxx
xxxxxcdddddddcxxxxx
xxxxxdcccccccdxxxxx
xxxddcdddcdddcddxxx
xxxddcdddcdddcddxxx
xxxccdcccdcccdccxxx
xxx..c...c...c..xxx
xxx..c...c...c..xxx
xxxxx.ccc.ccc.xxxxx
xxxxxc.......cxxxxx
xxxxxxx..}..xxxxxxx
xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx
ENDMAP

# contains up to to 4 basilisks, standing around an item
NAME:   kennysheep_basilisk_garden
DEPTH:  Lair:3-
MONS:   basilisk
NSUBST: . = 1:1 / 3 = 111.
SUBST:  . = .........G
MAP
   .....
 ....G....
 .G.....G.
.....G.....
...........
.G.G.*.G.G.
...........
.....G.....
 .G.....G.
 ....G....
   .....
ENDMAP

# an altar to chei, and a few followers
NAME:  kennysheep_snail_temple
DEPTH: Lair
MONS:  torpor snail / nothing
KFEAT: a = altar_cheibriados
MAP
 @
x.x   xxxxxxxxx
x.x  xx...1....@
x.x xx.xxxxxxxx
x.x x.xxbbbbbbxxxx
x.xxx.xbbWWWWW...xx
x.xx.xbbWbbbbbbxx.xxx
x...1.WWW1a1WWW..1...@
xxx.xxbbbbbbWbbx.xxxx
  xx...WWWWWbbx.xx
   xxxxxbbbbbxx.x
   xxxxxxxxxxx.xx
  @.......1...xx
   xxxxxxxxxxxx
ENDMAP

## A den of bears.
NAME:   gammafunk_lair_bear_cave
DEPTH:  Lair, !Lair:$
ORIENT: float
TAGS:   patrolling no_monster_gen
MONS:   black bear / polar bear
KFEAT:  " = W
KMONS:  " = electric eel w:40 / nothing
KITEM:  % = meat ration / w:1 beef jerky
SUBST:  % = %., * = *:40 |, B = 1., E = tE
CLEAR:  E
MAP
EEEEEEEEEEEEEEEEEEEEEEEEE
EtttttxxtttxxxxxxttxxtttE
EttxxxxxxxxxxxxxxxxxxxxxE
Etxxxxxxxxxxx..xxxxxxxxtE
Etxxxxx"Wxx......x.xxxttE
Exxx%...1.....x....xxxxtE
Exxx%B.1...xxxxx.....xxtE
Exxxx%%%*xxxxxxxxxxx..xtE
Etxxx*xxxxxxxtxxxxxxx...@
Etxxxxxxxxxxtttttttxxx..@
EttxxxxxttttttttttttttttE
EEEEEEEEEEEEEEEEEEEEEEEEE
ENDMAP

# A hidden river runs under a mountain, feeding into an enchanted lake.
NAME:   gammafunk_lair_enchanted_lake
TAGS:   no_pool_fixup
DEPTH:  Lair:2-, !Lair:$
ORIENT: north
KMONS:  p = plant / fungus / toadstool
KMONS:  q = plant / fungus / toadstool
KMONS:  1 = electric eel / rime drake / elephant / hippogriff w:5
KMONS:  2 = lindwurm / dire elephant / catoblepas
KITEM:  2 = * / |
KFEAT:  q = W
KFEAT:  1 = w
KFEAT:  h = runed_door
# Set these  before the NSUBST
FTILE:  iG = floor_pebble_brown
SUBST:  p = p., q = qW, r = tx, E = tE, * = *|, i : Gt, j = Gtx-
NSUBST: . = 2:9 / 3:0 / ., 1 = 3:1 / * = 1ww
CLEAR:  E
KMASK:  Ww = no_monster_gen
COLOUR: -|*2 = brown
FTILE:  -|*2h = floor_pebble_brown
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxx
ttccccccccccccccccccccccctt
Etci-WWWWW1jxxwWW1xxjww1ctE
Etc*2-WWxWWxxwWWwWWxxwwwctE
Etc*2-wxxWWxWWWjjwwWWwWWctE
Etci-wwjxxWWW1xxxx1wWWWwctE
EtccccccccccccccccccccWqxtE
EtxxxxxxxxxxxxxxxxxxxxxhxtE
EttttxrrrrrrrrrrrrrrttWWttE
Ettttt.qWWwWttqWtwwwqWWwttE
Ettt...qWwWwWwWwWwwwWwwwttE
Ettp..WWwwwqqWwqqWWWqqqtttE
Ettp..WqwwwttqqtttqqtttttE
Ettp..WqwwwttttttttttttttE
Ettp...WwwwwWqqqttttttttE
Ett.....Wwwwwqq..pttttttE
Etttp....qWwwwWq..ppttttE
Etttp......WwwWW...ptttttE
Etttp.....WWwwWW....pptttE
Ettttp...WWWwwwWW....ptttE
Ettttp...WWwwwwWW.....pttE
Ettttt...WWwwwwWW....ptttE
 Ettttp...WWwwWWW...pptttE
  Ettttp..WWWWWW...pttttE
  Etttttp..WWWW....pttttE
   Ettttp.........ptttEE
    Etttt........tttttE
     EEEE..@@@@..EEEEE
ENDMAP

####

NAME: nicolae_quartered_pool_little
DEPTH: Lair
TAGS: transparent extra decor luniq_qpool
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm, Nn, Oo, Pp, Qq
SUBST: abcdefghijklmnopq = ., ABCDEFGHIJKLMNOPQ = W
NSUBST: 7 = t@
MAP
    777777
   7qolLOQ7
  7pnkiIKNP7
 7pmjhfFHJMP7
7qnjgedDEGJNQ7
7okhecbBCEHKO7
7lifdbaABDFIL7
7LIFDBAabdfil7
7OKHECBbcehko7
7QNJGEDdegjnq7
 7PMJHFfhjmp7
  7PNKIiknp7
   7QOLloq7
    777777
ENDMAP

NAME: nicolae_quartered_pool_medium
DEPTH: Lair
TAGS: transparent extra decor luniq_qpool
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm
SHUFFLE: Nn, Oo, Pp, Qq, Rr, Ss, Tt, Uu
SUBST: abcdefghijklmnopqrstu = ., ABCDEFGHIJKLMNOPQRSTU = W
NSUBST: 7 = t@
MAP
      7777
    77spPS77
   7urolLORU7
  7tqnkiIKNQT7
 7uqmjhfFHJMQU7
 7rnjgedDEGJNR7
7sokhecbBCEHKOS7
7plifdbaABDFILP7
7PLIFDBAabdfilp7
7SOKHECBbcehkos7
 7RNJGEDdegjnr7
 7UQMJHFfhjmqu7
  7TQNKIiknqt7
   7UROLloru7
    77SPps77
      7777
ENDMAP

NAME: nicolae_quartered_pool_super
DEPTH: Lair
TAGS: transparent extra decor luniq_qpool
SHUFFLE: Aa, Bb, Cc, Dd, Ee, Ff, Gg, Hh, Ii, Jj, Kk, Ll, Mm
SHUFFLE: Nn, Oo, Pp, Qq, Rr, Ss, Tt, Uu, Vv, Ww, Xx, Yy, Zz
SHUFFLE: 12, 34, 56
SUBST: abcdefghijklmnopqrstuvwxyz135 = ., ABCDEFGHIJKLMNOPQRSTUVWXYZ246 = W
NSUBST: 7 = @ / t@
MAP
      777777
    771xtTX277
   75zwspPSWZ67
  73yvrolLORVY47
 75yuqnkiIKNQUY67
 7zvqmjhfFHJMQVZ7
71wrnjgedDEGJNRW27
7xsokhecbBCEHKOSX7
7tplifdbaABDFILPT7
7TPLIFDBAabdfilpt7
7XSOKHECBbcehkosx7
72WRNJGEDdegjnrw17
 7ZVQMJHFfhjmqvz7
 76YUQNKIiknquy57
  74YVROLlorvy37
   76ZWSPpswz57
    772XTtx177
      777777
ENDMAP

NAME: nicolae_lair_grove_arrangements
TAGS: transparent decor extra luniq
DEPTH: Lair
SHUFFLE: abcdefghi/''''T''''/T'''''''T/T'''T'''T/T'T'''T'T/T'T'T'T'T/\
         TTT'''TTT/'T'T'T'T'/T'T''''T'/'T'TTT'T'/'T'''''T'/TTTT'TTTT/\
         T'TTTTT'T/TT'TTT'TT/'''TTT'''/T'TT'TTTT/TT'T'T'TT/TT'''''TT/\
         TT''T''TT/TT''TT'T'/T''''T'T'
SUBST: abcdefghiT = ttttt., ' = ., _ = ._
CLEAR: _
MAP
__________________
_................_
_..aa...bb...cc.._
_.aaaa.bbbb.cccc._
_.aaaa.bbbb.cccc._
_..aa...bb...cc.._
_................_
_..dd...ee...ff.._
_.dddd.eeee.ffff._
_.dddd.eeee.ffff._
_..dd...ee...ff.._
_................_
_..gg...hh...ii.._
_.gggg.hhhh.iiii._
_.gggg.hhhh.iiii._
_..gg...hh...ii.._
_................_
__________________
ENDMAP

NAME: nicolae_lair_yak_garden
DEPTH: Lair:4-
TAGS: no_monster_gen no_item_gen no_trap_gen
SUBST: P = P.
NSUBST: D = 1:D / *:.
KITEM: f = fruit / nothing
KMONS: Y = yak
KMONS: D = death yak
KMONS: C = catoblepas
KMONS: P = plant
MAP
        .@@@.
      .........
    tt...P.P....tt
   tttttPPPPPPPtttt.
  .ttDfPP.Y....tDtt...
 .PPttP..P.P.PP.ftP...
 .P....PPP...PPP.PP..@
..PP..PPPwwwC.P..PPP.@
......Y.wwwwww.Y.ttP.@
.@.PP..Pwwwwww...fttt.
 @..P..PP.wwwPP.ttttt
  @.PP.PPP..PP..PPttt
  ...Ptt.P.PPPPPP....
  ...ttfDttPP.......
    .tttttt......@
     ..tt.......
        .....
ENDMAP

#####
NAME:   grunt_forest_stairs_basic
TAGS:   transparent extra luniq_staircase decor
DEPTH:  Lair
MONS:   plant
SUBST:  1 = 1.
: if you.depth() == dgn.br_depth(you.branch()) then
SUBST:  ) = (
: end
MAP
.........
..t...t..
.t11.11t.
..1t.t1..
....)....
..1t.t1..
.t11.11t.
..t...t..
.........
ENDMAP

NAME:   grunt_forest_stairs_clearing
DEPTH:  Lair:2-, !Lair:$
MONS:   plant / bush, wolf, black bear, polar bear, spriggan
SUBST:  t = t.
NSUBST: Q = 3:2 / 1:3 / 1:4 / 1:5 / *:.
MAP
.tt...tt.
t..Q.Q..t
t.t.Q.t.t
.Q.1.1.Q.
..Q.).Q..
.Q.1.1.Q.
t.t.Q.t.t
t..Q.Q..t
.tt...tt.
ENDMAP

# A secluded grove.
NAME:   forest_entry_grove
DEPTH:  Lair:2-, !Lair:$
MONS:   centaur w:4 / spriggan
MONS:   wolf, black bear, polar bear
KMONS:  P = plant / bush
NSUBST: Q = 2:1 / *:.
NSUBST: R = 3:2 / 1:3 / 1:4 / *:.
MAP
   xxxxxxxxx
  xxxttttxxxx
 xxxttttttttxx
xxtttt...tttxxx
xttt..Q.Q..ttxx
xttt.Q.R.Q.tttx
xtt.........ttx
xtt.R..)..R.ttx
xtt..R...R..ttx
xttt..R.R..tttx
xttt.......tttx
xxtttt...ttttxx
 xxxtt..ttxxxx
  xxxtt..ttxx
   xxtt@@ttx
ENDMAP

NAME:    bh_autumn_forest_stairs
TAGS:    extra luniq_staircase decor
ORIENT:  float
DEPTH:   Lair
WEIGHT:  5
COLOUR:  t = lightred / red w:3
SHUFFLE: Aaq / Bbr / Ccs
SUBST:   q = @
SUBST:   A = ., bBcCdDrs = t
COLOUR:  t = lightred / red w:3 / green w:3 / lightgreen w:1
TILE:    t = dngn_tree_lightred / dngn_tree_red w:4 / dngn_tree_yellow w:4
COLOUR:  a = lightred / red / green / lightgreen
TILE:    a = dngn_tree_lightred / dngn_tree_red / dngn_tree_yellow / dngn_tree
SUBST:   a = t
MAP
  aaaa qqq aaaa
 caaaaaAAAaaaaab
cccaaaaAAAaaaabbb
ccc...........bbb
ccc...........bbb
 cc..xxxxxxx..bb
sCC..x.....x..BBr
sCC..x..{..x..BBr
sCC..x.....x..BBr
 cc..xV...Vx..bb
ccc..xx+++xx..bbb
ccc...........bbb
ccc...........bbb
ccctttttttttttbbb
 cctttttttttttbb
  ctttt    tttb
ENDMAP

NAME: grunt_forest_small_clearings
TAGS: transparent extra decor
DEPTH: Lair
MONS: plant / bush
KITEM: d = fruit
KMASK: G = no_monster_gen
SHUFFLE: Ww. / ''` / ''`
SUBST: t = t.., '` = 1:49 d:36 .:405
SUBST: G = w., W = WW., w = wwW
MAP
 tt@@@tt
t.......t
t..WWW..t
@.WwwwW.@
@.WwGwW.@
@.WwwwW.@
t..WWW..t
t.......t
 tt@@@tt
ENDMAP

NAME: grunt_forest_small_clearing_treed
TAGS: transparent patrolling
DEPTH: Lair
MONS: plant / bush
SUBST: t = t..
KPROP: * = no_tele_into
MAP
 tt@@@tt
t.......t
t.1...1.t
@..ttt..@
@..t*t..@
@..ttt..@
t.1...1.t
t.......t
 tt@@@tt
ENDMAP

# Prviously large_clearing_fruit, _statues, _slash, _guards
NAME:    grunt_forest_large_clearings
TAGS:    transparent
DEPTH:   Lair
WEIGHT:  20
MONS:    plant / bush, wolf
MONS:    black bear / polar bear, spriggan / centaur w:4
KMONS:   9 = 9
KMONS:   * = spriggan rider / faun w:4
ITEM:    fruit q:1
KITEM:   9* = *
KFEAT:   9 = W
SHUFFLE: wW'"-~-_9 / ''GG''''* / '''0''''% / ``````22G
SUBST:   t = t.., '. = 1:49 d:36 .:405, W = WW., "w = wwW
NSUBST:  2 = 3:2 / 2:3 / 1:4 / *:.
MAP
      tt@@@tt
    ttt.....ttt
  ttt.........ttt
  tww...........t
 ttwww..........tt
 t..www..'.......t
tt...w"w....'....tt
t.....ww-._.......t
@......-~W-_......@
@....'..W9W..'....@
@......_-W~-......@
t.....'._.-ww.....t
tt.........w"w...tt
 t.......'..www..t
 tt..........wwwtt
  t...........wwt
  ttt.........ttt
    ttt.....ttt
      tt@@@tt
ENDMAP

NAME:  grunt_forest_large_clearing_hut
TAGS:  transparent
DEPTH: Lair
MONS:  plant / bush, spriggan / centaur w:2
SUBST: t = t..
SUBST: . = 1 .:90
COLOUR: -*29 = lightgrey
FTILE:  -*29 = floor_normal
MAP
      tt@@@tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t......ccccc......t
@......c-9*c......@
@......+-*2c......@
@......c-9*c......@
t......ccccc......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt@@@tt
ENDMAP

####
# Some "beast lair" vaults, originally submitted as a serial vault.
NAME:   beast_lair_carwin_large_1
TAGS:   transparent luniq_beast_lair
ORIENT: float
: beast_lair_populate_mons_large(_G)
: beast_lair_randomise(_G)
MAP
                E.EEJ
      1     1  ..E.ME
...........1.xxx.ME.
 .Axxxxx..xxxx.xxxA..
..xxJ..xx.xx.B.FFxx1.E
 ..xxx.B........GGxx...
 .xxA.....FFxxG..Gxxx.
 .x...423...xGGG.GGGx.
..x.AxxxFFFxxG4223Axx.1
..xx..AFFFxx%LL4xxxx..
..Axx..MFxxxxxxxx.xxx.
 ..x..xxxxAxx......A..
 .A.............  .
      .            ..
ENDMAP

NAME:   beast_lair_carwin_large_2
TAGS:   transparent luniq_beast_lair
ORIENT: float
: beast_lair_populate_mons_large(_G)
: beast_lair_randomise(_G)
MAP
  ..1.. 1
   .x........
  ...xxxxxxx.
. ..xxxLxxJxx.
 ..Ax44....Ex.
 .Gxx%4xx..xx.
 GGGxLxxxE.xx.
 .GGxxxx...xx.
 .Gxxxx..EEx..A
..xx31xx..xx....
1.x32..x.xx..  A
 .xxx....xx1.
 ..xxxxEMExx..
 .............
ENDMAP

NAME:   beast_lair_carwin_large_3
TAGS:   transparent luniq_beast_lair
ORIENT: float
: beast_lair_populate_mons_large(_G)
: beast_lair_randomise(_G)
MAP
            ..
        .......
 .........Mxxx...
 x  xxx.xxxxxxxxx
 xxxxM...A......xx
 .xxA......xx..M.xx.
..x...xxAA..x22.Mx...
..xx.xxxxxx.xxx..xx..
 .xx.xx44LA...x.......
 .x..xx%4...x.xxxxx.
......xxxxLx......x..
..xx.MM.2xxx....xxxx.
   x...24x....xxx  ..
   xxxxxxx.xxxxxx x
    ................
   ...         .
ENDMAP

# This vault is kinda neat because if the Fs become walls (1/4th of the time)
# it's a maze-like warren, but if they become floors it's an open "village".
NAME:   beast_lair_carwin_large_4
TAGS:   transparent luniq_beast_lair
ORIENT: float
: beast_lair_populate_mons_large(_G)
: beast_lair_randomise(_G)
SUBST: T:5 6:3
MAP
  .          .A1
.........  ........x    .
 ...xxxA.....xxxx.......x
 ...x3xxFFFAxx4x...xxxx..x.
 ..xx.xx..Fx...xFFxx24xA.....
...x3..........2xF....x...x
..FxxF11..FFxxx.x.....xxx.
.......Fx...LF....xx.xx  .
.x...xxx..x4Tx.F...F...D 1.
x.Mxxx...xxxxxxx.FFF.D.2WWW
xxxx....x.24xxxxxF...MWWWW
2....xx...Mxxxxx.D..3WWWWWWW
4..       .  Ax ..M1WWW W
ENDMAP

NAME: beast_lair_carwin_1
TAGS: transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
MAP
     ..
  1......
 ..xxxxx..
 .xxM3Axx1..
 .xAMJ4xFx.
.Axx.xxxFF.1
..1x..x...
 ..xx..x...
  .....
ENDMAP

NAME:   beast_lair_carwin_2
TAGS:   transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
ITEM: w:7 meat ration / beef jerky
SUBST: ? : .
MAP
  ...
 ..2..
..3d2..
 ..2..
  ...
ENDMAP

NAME: beast_lair_carwin_3
TAGS: transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
MAP
  1
   ..
....B..
 .xxxx..
..x33xx.1
.xxM.Ax.
A.xx.xx.
 ..B...B
ENDMAP


NAME:   beast_lair_carwin_4
TAGS: transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
MAP
  ...  A
 ..x....
A.xxxFA..
..x32FFB.
..xxM1x...
.xx..x1.
....FxA
ENDMAP

NAME:   beast_lair_carwin_5
TAGS: transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
MAP
 .
 ...1...
..xxxx......
 ...3x.xBA.
 .x2xx.xxxB
 .xJ.2...
1.xxBxxxA
 ........A
ENDMAP

NAME:   beast_lair_carwin_6
TAGS: transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
MAP
  .........
   .Axxxx.1
 .....GGxx.
 ..xxxxGGx...
..1xxGG.Gxx.
 .xxD24Jxx..
 ..xx32xxA.
 .A.FFFFF..
  ......
ENDMAP

NAME: beast_lair_carwin_7
TAGS: transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
MAP
.x....
...x....
..1x.x....
 .xx.xx.x..
..xPPxxxxx..x .
Exx3PPx1Jx..x.
E.xxxxx....x.
 E.EEx42Pxxx..
 ..E.xxxxx.x...1
  ...........
     A
ENDMAP

NAME:   beast_lair_carwin_8
TAGS:   transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
MAP
. .. . ..   1   .......   1
 ..Axxxx.... .  xxxxxx....
..xxGGGxx.xxxxxxA.1.xxx.
..xGGGGG..M243.....F..x..
.xxxGGxxxxxxxxxxxx...xxx..
....G..1         FFA......
ENDMAP

NAME:   beast_lair_carwin_9
TAGS:   transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
MAP
 .   1    .
 ........  .
...xxxxx.....
 .AAxxxxxxxx.
 ..1xxxx4Lx....
..xxx.M44xx.
 x.....xxx..
...xxxxx... A
...xxxx..x.s
      .
ENDMAP

NAME: beast_lair_carwin_10
TAGS: transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
MAP
  AA
 .......
 .x..xx.xx
...x..xx.
 .Axx.4x.
 ..Jx3xx.
...xxxx1x
  .x43xx.1
 .xxx.Ax..
 .xxx.xx.1
1.........
ENDMAP

NAME: beast_lair_carwin_11
TAGS: transparent luniq_beast_lair
: beast_lair_populate_mons_small(_G)
: beast_lair_randomise(_G)
MAP
     .E
. .......xx
 ..xxxxx..E
..xx2x4xE..
.xx...2xx.
..B.xxxxxx..
 ...    .....E
ENDMAP

NAME:    hangedman_lair_generics_rect
TAGS:    no_monster_gen
DEPTH:   Lair
MONS:    plant / fungus w:6
NSUBST:  ' = 2:c / 2:. / * = c+....
SHUFFLE: yY / yY / Yy
SUBST:   y = ., Yz = xxxx1., Z = xx`
CLEAR:   `
MAP
.............xZZ
.c'c'c'c'c'cxxxZZ
.'...........zxxZ
.c..0.....0..zxxx
.'...Wxxxx...xx%x
.c...xxxxx...x9.x
.'...xxxxW..xx.9x
.c..0......Y.x..x
.x........x.y.xxx
xxzzzzzzxxxx..%xx
Zxxxxxxxx9..x%0xx
ZZxxxxxx%.9.xxxzx
 ZZZZZxxxxxxxxxxx
ENDMAP

NAME:    cheibrodos_lair_reptile_den
TAGS:    no_item_gen no_monster_gen
DEPTH:   Lair, !Lair:$
MONS:    adder / water moccasin, komodo dragon / black mamba, plant, fungus
SHUFFLE: "', ABC
SUBST:   A = ., B = x, C = x, D = x .:2
NSUBST:  " = 7:1
NSUBST:  ' = 2:2
NSUBST:  - = 6:3
NSUBST:  ; = 5:3
SUBST:   3 = 3 4:5
MAP
  ...........
 ..DDDDCDDDD..
..DxxxxCxxxxD..
.Dxxx"""""xxxD.
.Dxx";;;;;"xAD.
.Ax";;;;;;;"xA.
.DA";;---;;"xD.
.Dx";;-(-;;"xD.
.Dx";;---;;"xD.
.Dx";;;;;;;"xD.
.Dxx";;;;;"BBD.
.Dxxx"""""BBBB.
..DxxxAxxxBBB..
 ..DDADDDDDB..
  ...........
ENDMAP

NAME:    cheibrodos_lair_caveside_slide
TAGS:    extra decor
DEPTH:   Lair, !Lair:$
MONS:    fungus w:3 / nothing, plant w:3 / nothing
SHUFFLE: 12
SUBST: " = x.
MAP
    ...xx
  ....xxx
 xx..1Wxxx
 xxx.1Wxxx
 xxx".WWxxx
  xxx.1Wxxx
  xxx.1WWxxx
  xxx".1WWxx
 xxxx"".1Wxx
xxxx""".1Wxxx
xxx"""..1WWxx
xxW1....1WWxx
xxWW(111WWxxx
xxWWWWWWWxxx
xxxWWWWWxxx
 xxxxxxxxx
  xxxxxx
ENDMAP

# Not very randomized, but the unusual shape, wall-plant use,
# and location should be variable enough. Intentionally lacks
# the transparent tag, to minimize niches in walls behind plants.
NAME:    hangedman_lair_unloving_vines
TAGS:    extra decor
DEPTH:   Lair
KMONS:   P = plant
KMASK:   ` = no_monster_gen
SHUFFLE: xP'. / xP'. / .'xP
SUBST:   '` = .
MAP
           xPx
         xxPxPxx    ````..@
        xPP'.'PPx   ....'''
      xxP''.'.''Px...'''PPP
   xxxPP'```.``'Px.''PPPxxx
xxxPPP''.xP'``.```'PPxxx
PPP'''...xP''.'.''Pxx
'''....   xPP'.'PPx
@..````    xxPxPxx
             xPx
ENDMAP

NAME:    minmay_guarded_unrand_snakebite
DEPTH:   Lair
TAGS:    ruin
ORIENT:  southeast
: if you.unrands("snakebite") then
# a randart venom demon whip is unlikely to be useful for someone with snakebite
# so just do this
ITEM:    demon whip good_item
: else
ITEM:    snakebite no_pickup
: end
KMONS:   A = adder
KMONS:   B = water moccasin
KMONS:   C = black mamba
SUBST:   1 = A B:2 .:48, 2 = A B:5 .:60, 3 = A B C:2 .:88, 4 = A:2 B C:4 .:64
SUBST:   5 = A:1 B C .:84, 6 = B:5 C .:60, 7 = B.
MAP
xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxx222xxx
@11xxxxxx222xxx3xx
xxx1xx112xxxxxxx3x
xxxx11xxxxxxxxxx3x
xxxxxxxxxxxxxxxx3x
xxxxxxxxxx44xxx3xx
xxxxxxx444xx433xxx
xxxx554xxxxxxxxxxx
xxx5xxxxxxxxxxxxxx
xx5xxxxxxxxxxxxxxx
xx5xxxxxxxxxx77xxx
xxx5xxxxxxxx7xx7dx
xxxx56xxx666xxxxxx
xxxxxx666xxxxxxxxx
xxxxxxxxxxxxxxxxxx
ENDMAP

###############################################################################
# Lair endings
###############################################################################

##############################################################################
# Vaguely Jungle Book (R. Kipling) inspired. Map by due.
#
NAME:   due_jungle_book
ORIENT: north
PLACE:  Lair:$
TAGS:   no_pool_fixup no_wall_fixup no_monster_gen
MONS:   plant, elephant
SUBST:  X = xxx.., P = 1.........
SUBST:  T = tttxxc
KFEAT:  S = deep_water
KMONS:  S = anaconda
# The anaconda's name is Kaa.
KITEM:  U = superb_item
KITEM:  Q = acquire any
MARKER: S = lua:MonPropsMarker:new { anaconda_guard="anaconda_guard" }
KFEAT:  o = granite_statue
KPROP:  U = no_tele_into
{{
function elephant_statue()
  return
end

set_feature_name("granite_statue", "elephant-shaped statue")
}}
MARKER: o = lua:portal_desc { slaved_to="elephant" }
MARKER: G = lua:props_marker { veto_fragmentation="veto", \
                               veto_disintegrate="veto", \
                               veto_shatter="veto" }
COLOUR: oG = green
TILE:   oG = dngn_statue_elephant_jade
MARKER: o = lua:monster_on_death { \
            target="anaconda_guard", new_monster="generate_awake dire elephant", \
            message_seen="The elephant guardians awaken!", \
            message_unseen="You hear a distant trumpeting.", \
            master_name="elephant", \
            veto_fragmentation="veto", veto_disintegrate="veto", \
            veto_shatter="veto", \
         }
MAP
  xxxxxxxxxxx
 xxxxxXX.XXXxx
xxxxX.....PPXxx
xxtt.........Xxx                           xxxx
xttGtxx.X..X..Xxx                         xxT.xxx...@
xtUttxccc+cc++ccccccccccccccccccccccccccccTT.TT.xxx..
xtttxccco.Gc....GccPWWcco..%cccP..ccP....P.G.o.TTxxx.
xttxcccc...c.P...+P.WwW+...%Qc.....+.....c.c...TTTx..
xxccccccPc.ccccPGc.WwwS+...%|c.22..c.Pcccc.P.cTTccc..
xccccccc+c.P.PccccPwwwwco..%cc.22..cccc.P..c+ccccccc.
xcc1...PPccccP.P.cWwWWPccccccc%...PcP.PPccccPP1..%cc.
xcc......cccccccPcccW.ccco.occcP.ccc.ccccccc......cc.
xccP.1..%ccttTccP.PcccccG.V.GcccccP.PccTttcc1....Pcc.
xxcccccccct....cccP.P.P1P...P1P.P.Pccc....tcccccccc.
 TxTcccTt.......tcccccccc+++cccccccct.......tcccTt..@
   @........@@...........@@@....T......@@..........@
ENDMAP

##############################################################################
# Evil forest
#
NAME:    evil_forest
PLACE:   Lair:$
TAGS:    no_pool_fixup no_wall_fixup no_monster_gen no_rotate
ORIENT:  southeast
MONS:    hydra / rime drake / dire elephant
MONS:    red devil / ice devil w:15 / orange demon w:15 / hell hound w:5
MONS:    hellwing, sun demon, sixfirhy, smoke demon, hell beast
KMONS:   8 = wizard
# XXX: no_pickup is incompatible with acquire any.
# Should be used here, loot is easily missed.
KITEM:   / = acquire any, any good_item / no_pickup gold good_item w:15
KFEAT:   / = enter_hell
KFEAT:   Q = granite_statue
KFEAT:   _ = altar_makhleb
KFEAT:   L = endless_lava
NSUBST:  1 = 3:1 / *:.
SHUFFLE: 345 / 345 / 345 / '"7, 3456
SUBST:   T = ttt., ' = ;, " = 3
KPROP:   Q = bloody
KMASK:   :;2345678 = no_item_gen
COLOUR:  _;/234567 = red
COLOUR:  :8 = lightgrey
MAP
  ...TTTT....  .............................
 ..TttttttT.......Tt.TtT....TT..TT......Tt....
..TtttttttttT..TTtttttttT..TttttttTT..TTtttT...
..Tttttttttttt0ttttttttttttttttttttttttttttttT..
.Tttttttttttttt.ttttttttttttttttT.Ttttttttttttttt
..Ttttttttttttt.tttttttttttttttT.1.Tttttttttttttttxx
.....ttttttttt..tttttttttttttttT....ttttttttttttttxx
..Ttt0.tttttt....ttttttttttttttt..Tt..ttttttttttttxx
..Ttttt.......t.0.tttttttttttt...ttttt.0ttttttttttxx
..ttttttt..0tttt.t..ttttttttt.0.tttttttt..ttttttttxx
.Tttttttttttttt..ttt...ttt....ttttttttttT.1Tttttttxx
...Ttttttttttt..ttttt.......tttttttttttT....Ttttttxx
...tttttttttt.1ttttttt0tt.tttttttttcccccc++cccccccxL
..Tttttttttt..tttttttttttt+ccccccccc:::::::::::::clL
..Ttttttttt.0t..ttttttttttc::::::::c:::Q;;;;Q::::clL
.Tttttttttt..tt..tttttttttc::G:G:::+:::::::::::::clL
..Tttttttt..tttt.tttttttttc:::G:G::ccccccccccccccclL
.Tttttttt..ttttt..ttttttttc::G:G:::c;;;;;;;2lllllllL
..Ttttt.0ttttttt...tttttttc:::G:G::c;;;;;;;;22lllllL
....T..tttttttt....tttttttc::G:G:::+;;;;_;4352/llllL
...T0tttttttttt*Q1ttttttttc:::G:G::c;;;;;2222llllllL
..Ttttttttttttttttttttttttc:::::::8c;;;;2llllllllllL
ttttttttttttttttttttttttttcccccccccccccllllllllllllL
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxllllllllllllllllllL
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxLLLLLLLLLLLLLLLLLLL
ENDMAP

##############################################################################
# Oh worms!
#
NAME:    wormcave
PLACE:   Lair:$
TAGS:    no_wall_fixup no_monster_gen no_rotate
ORIENT:  southeast
MONS:    torpor snail, tyrant leech, lindwurm, hornet
MONS:    fire dragon / ice dragon
KMONS:   p = plant
SHUFFLE: ABC
NSUBST:  A = 3:4 / 1:23 / * = ....4
NSUBST:  B = 2:2 / 1:1 / * = ....4
NSUBST:  C = 1:1 / 1:2 / 1:3 / *:.
SUBST:   P = pp., X = x., z = x'
CLEAR:   '
NSUBST:  " = 1:GU / *:.
KMASK:   $5 = no_trap_gen
: lair_small_ending(_G)
MAP
            zzzzzxxzzxxzzxxxxzzxxxzzzxxx
 @........@xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
 ..PP.XX..Px..xxxxxxxxxxxxxxxxxXAxxxxxxx
 ..PP.XX..PPxxxxxxxxXXBBXxxxxxA*..xxxxxx
 ...x.....PPP.xxxxxP4B..BXxxxXAA.%Xxxxxx
 ..XxX..X.....xxxxxXB....Bxxx4AA..xxxxxx
 .Pxxxxx.x...xxxxxxx%...*XxxxxX.p..xxxxx
 xxccccxx..XPPxxxxxPpp.XPxxxxxxxXP.xxxxx
zxcc$$ccxxXxx..xxxxxXp..xXxxxxXxx.pxxxxx
zxc$$$$ccxxx...Xxxxxxx1..x.xxxx..1xxxxxx
xxc$$$$$cxxx..xxxxxxxXx...xXxx...xxxxxxx
xxcc$5$$cxx..xxxxxxxxxxx.P..X...xXxxxxxx
xxxcc$$ccxX..Xxxxxxxxxxx......xxxxxxxxxx
xxxxccc=c"..xxxxxxxxxXx...P.xxxxCCCxxxxx
xxxxxxxcp".xxxxxxxxxXx...XxxXxx4CCC*xxxx
xxxxxxx"""xxXxxxxXxx....xxxxxxx*...xxxxx
xxxxxxxxX.1.xxxxxx....x..xxxxxxxx..xxxxx
xxxxxxxxxX...xXx....Xxx....pxxxx...xxxxx
xxxxxxxxxxx.......Xxxxxx1.........xxxxxx
xxxxxxxxxxXxx.p.xxXxxxxxxxxx....xxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Dogs by themselves get repetitive, so do bears. What if we combine them?...
# Previously "kennels" and "bearwithus", both by due.
NAME:    hangedman_lair_caniforms_friends
PLACE:   Lair:$
TAGS:    no_monster_gen no_rotate patrolling
ORIENT:  southwest
MONS:    hound, wolf, warg, hell hound, raiju
MONS:    black bear, polar bear
KMONS:   P = plant
# The one big reward and the several smaller stacks of items
# are randomly split between the mostly-dog and mostly-bear sections.
KITEM:   $ = any, any w:1 / nothing w:5, beef jerky q:3
KITEM:   % = any, any w:1 / nothing w:5, royal jelly q:2
KITEM:   *| = gold, gold, gold, acquire any
NSUBST:  3 = 1:4 / 1:5 / 2 : 3:4 4:3 5:1 / *:3
SHUFFLE: %'*" / '$"|
SUBST:   X = xx., z = xx`, ' = ., " = x, 0 = 4:2 5:1, p = PP.
CLEAR:   `
: lair_small_ending(_G)
MAP
xxxxxxxxxxxxxxxxxxxxzzz  @  @  @
xxxxxxxxxxxxxxxxxxxxxxxxxp..p..
xxxxxxxxxxxxxxxxxxxxxxxxxxp..p.
xxxxxxXxxxxxxxxxxxxxxxxxxxx....
xx%'67xxxxXXxxxxxxxxxxxxxxxP..p@
xx7..67xXx66xPxxxxxxxxxx...xP..
xxx...xxx%...xPxx1x.x1x..1..xP.
xxXx3..xxx...Px6x.........1..xp@
xxxxxx.......Px........x...xxxx
xxxxX0.xx...6xP6..x1..x1xxxxxxx
xxxxxx20xx.%XXxx..xxxxxxxxxxxxxz
xxxxx"x.7..xxxxx..xx2x1xx.xxxxxz
xxxxXx....6Xxxxx....x.x....x1xxz
xxxxx%....xxxxxxx.............xz
xxxxx6....6xXxxxxx............xz
xxxxXx6....7xxxxxxxx2x...x3x..xx
xxxxxxP7.%7xXxxxxxxxxxx.xxxxx..x
xxxxxPxPxPxxxxxxxxxxxxxxxxxxx.1x
xxxxxx7x6xxxxx2'xxxxxx..x.x...xx
xxxxx..6xxxxxx..x.x2x...33....xz
xx*x2.....'xx111...x....2....xxz
xxx3.......1x..1..........3..xxx
xx0.4.2.xx....xx.....xx....xxxxx
xxx5.3'xxxx..xxx....x.xx2x2xxxxx
xxx.0.xxxxxx3xxxx'.xxxxxxxxxxxxx
xxx'.%xxxxxxxxxxxx3xxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Frog pond
#
NAME:   minmay_lair_end_frog_pond
PLACE:  Lair:$
ORIENT: float
WEIGHT: 5
MONS:   bullfrog, spiny frog, vampire mosquito
MONS:   blink frog, komodo dragon / hydra
KMONS:  6 = patrolling tyrant leech
KMONS:  p = plant
ITEM:   any good_item
KFEAT:  4p = W
# Previously 11.6 bullfrogs, 16.6 spiny frogs,
# and 7.1 vampire mosquitoes on average.
NSUBST: XW. = 6:1 / 8:2 / 4:3 / 12 = 11... / 16 = 22... / 8 = 44...
NSUBST: W = 3 = 66w / *:W, 5 = 1:5 / *:4
NSUBST: % = 1:d / 1:* / * = %%*|
SUBST:  X = x:80 W:20 .:5 p:10
SUBST:  W = W:80 w:25 p:10
SUBST:  . = .:85 W:20 p:10
MAP
          ............xxx
       .................xxx
   .........WWWWW.........xxxx
  .......WWWWWWWWWWWWWWW.......
 x.....WWWWWWWWWWWWWWWWWWW.....
 x...WWWWWWWWWWWWWWWWWWWWWW.....
 x..WWWWWWWWWWWWWWWWWWWWWWWW....
xxxxxWWWWWWWWWWWWWWWWWWWWWWW....
x.XXxxxxWWWWWWWWWWWWWWWWWWWWW...
x.WWXXXxxxxxxxpp4WWWWWWWWWWWW...
..WWWWWXXXxxppppp5WWWWWWWWWWW...
..WWWWWWWW4pp%%%pp4WWWWWWWWWW...
..WWWWWWWW6pp%%%%pp6WWWWWWWWWW..
..WWWWWWWW4ppp%%%pp4WWWWWWWWWW..
.WWWWWWWWWW4ppppppxxXXWWWWWWWW..
.WWWWWWWWWWW54ppxxxxxxXXXWWWWW..
.WWWWWWWWWWWWWWWWWWWWxxxxXXXWW.x
.WWWWWWWWWWWWWWWWWWWWWWWxxxxXX.x
..WWWWWWWWWWWWWWWWWWWWWWWWWxxxxx
..WWWWWWWWWWWWWWWWWWWWWWWWW...x
...WWWWWWWWWWWWWWWWWWWWWW.....x
....WWWWWWWWWWWWWWWWWWW.......x
 .....WWWWWWWWWWWWWW.........
  xxx......WWWWWW.........
    xxxxx..............
        xxxx.......
ENDMAP

##############################################################################
# A mock-up of Forest, made well before serious attempts at the branch.
#
NAME:   minmay_lair_end_enchanted_forest
PLACE:  Lair:$
ORIENT: northwest
MONS:   black bear / polar bear w:12, wolf, redback / hornet w:20
MONS:   spriggan, spriggan rider, boggart
KITEM:  * = gold no_pickup, star_item no_pickup, star_item no_pickup
KITEM:  | = gold no_pickup, superb_item no_pickup, star_item no_pickup
KITEM:  $ = gold no_pickup, star_item no_pickup
SUBST:  T : T:20 .:1 t:1, T = tt..., E = t_
CLEAR:  _
NSUBST: . = 4:1 / 4 = 1. / 1:3 / 3 = 3.. / *:.
NSUBST: 2 = 8:2 / 20 = 2. / *:., 4 = 4:4 / 8 = 4. / *:.
NSUBST: 5 = 1:5 / *:4, 6 = 2:6 / 2 = 56 / *:4
NSUBST: * = 1:| / *:*
MAP
ttttttttttttttttttttttttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttttttttttttt
tttttttttT*G*Ttttttt....ttttttttt.....ttTTtttttTT
ttttttttt4.*.6TTT....ttT5..TTT....ttT.2222TTtt..@
tttttttTT.4.4....TTttttttT..4...TttttttTT222..ttT
tttttT.....6..4TtttttttttT4...4ttttttttt22ttttttt
ttttt..ttT4.4.TtttttttttT....4.tttttttt..tttttttE
tttt.ttttttttttttttttttt....tt..TttT...TT.tttttt
tttT2ttttttttttttttttT...TTttttt.....ttttT.Tttt
ttT22tttT.6TtttttttT....tttttttttTTttttttt..ttE
tt22ttt$.4..tttttt5.TTT.ttttttttttttttttttt.tt
tt22tttT6.4.ttttT.Tttttt.22tttttttttttttttT.tt
tt2tttttt..TtttT2TtttttttT22TttttttttttttT.ttt
tt2ttttttt.TtTT22tttttttttT222ttttttttttt.tttt
tt..tttttTT..222Ttttttttttttt22ttTTTTtt..TtttE
ttT.tttt.....TTT222TttttttttttT...TT...TttttE
ttt..tt...TTttttttt..TTttttttttt..4...Tttttt
tttT....TtttttttttttT@   EEttttt46.46ttttttE
ttttT..tttttttttttttE      EEttttT.$tttttE
ttttT..TtttttttttttE          ttttttttt
ttttt...ttttttttttE            ttttttt
tttttT..TttttttE                 EEE
ttttttt.@ttE
ttttttE
tttE
tE
t
ENDMAP

##############################################################################
# Catoblepas ending by evilmike.
#
# Contains 3 catoblepas with one being semi-randomly placed. The last
# one is likely to breathe on you in an area that is hard to escape from.
#
NAME:   evilmike_catoblepas_cave
PLACE:  Lair:$
TAGS:   no_monster_gen no_rotate no_item_gen
ORIENT: northwest
FTILE:  d|*$1c- = floor_cobble_blood
COLOUR: d|*$1c- = lightgrey
ITEM:   acquire any
MONS:   patrolling catoblepas
MONS:   death yak / w:5 yak, death yak w:5 / yak
MONS:   yak / death yak
NSUBST: 3 = 1:1 / *:2
# These are still living plants for all intents and purposes...
MONS:   plant perm_ench:petrified dbname:petrified_plant col:lightgrey tile:mons_withered_plant / nothing
SUBST:  - = .
MAP
xxxxxxcccxxxxxxxxxxxxxxxx
xxxxccc*cccxxxxxxxxxxxxxxx
xxxxcd|$$-cxxxxxxxxxxxxxxxx
xxxxcc$--1ccxxxxxxxxxxxxxxx
xxxxxcc----ccxxxxxxxxxxxxxxxxx
xxxxxxccc---cxxx%5xx5xxxxxxxxxxx
xxxxxxxxcc-ccxx5......5xxxxxxxxxxx
xxxxxxxxxc-cxx..3.3.x...xxxxxxxxxxx
xxxxxxxxxc-cx5.2.2.xxx5..xxxx55.%xxxx
xxxxxxxxxx.cxx5...xxxxx...xxxx4.4xxxx
xxxxxxxxxx.xxxxx..xxxxxxx...xx.4.4xxx
xxxxxxxxxx.xxxxxx...xxxxxxx......xxxx
xxxxxxxxxx.xxxxxxxx..xxxxxx...xx55xxx
xxxxxxx.....xxxxxxx..5xxxx..xxxxxxxxxx
xxxxxx32.x..xxxxxx5..xxxx..xxxxcccxxxx
xxxxxx2xxxx..xxxxxx..5xxx.xxxccc1ccxxx
xxxxxxxxxx..xxxxxx........xxcc----ccxx
xxxxxxxxx5..5xxxx32xxx5...5cc------cxx
xxxxxxxxx.....xx.3xxxxxx5..c------ccx
xxxxxxxx5.G.G.....xxxxxxx..---cc--cxx
xxx%xxx..G.54.G.xxxxxxxxxxx-ccc---cxx
x%525xx..4..G....xxxxxxxxxxxxc---ccxx
xx2..3....5..G..55xxxxxx55xxx...xxxx
xxx53...xxx5..5xxxxxxxxxx5.....xxxxx
xxxxx55xxxxxxxxxxxxxxxxxxx......xxx
xxxxxxxxxxxxxxxxxxxxxxxx......G...
xxxxxxxxxxxxxxxxxxxxxx....G.......
xxxxxxxxxxxxxxxx     ........G....
xxxxxxxxx             ..G........
xxx                   .........G.
x                           .....
ENDMAP

##############################################################################
# Dragon's lair.
#
NAME:   guppyfry_lair_end_dragon
PLACE:  Lair:$
TAGS:   patrolling no_monster_gen
ORIENT: southeast
KMONS:  1 = rime drake / swamp drake / mottled dragon / steam dragon
KMONS:  2 = fire dragon
KMONS:  3 = plant
KMONS:  4 = basilisk / komodo dragon
KMONS:  5 = iguana / crocodile
SUBST:  - = wWW...%3t554
SUBST:  w = wwwW
SUBST:  ? = 145*
: lair_small_ending(_G)
MAP
@..@..@..xxxxxxxxxxxxxxxxx
x3WW-3.xxxxttttttttttxxxxx
xttWW-tttttttt3WW-..ttxxxx
xtttW-tt.tttt-WwwW3..ttxxx
xtttWWtt...t--WwwwW-.tttxx
xtt1Wttt1.tt.WWtt3WW..tttx
xttWw-t3.t..Wttt--WWW.tttx
xxttWWt-1t..W--t3.WwwWttxx
xtt1-Wwttt.-wWtt-WwwwWttxx
xxtt--Wtt3.wWtttWwwwwWtttx
xtt3.tWWttwW3tt1WWWwwWttxx
xttt.ttWww-.tttt3WwwwWtttx
xxtt3.3-W-.tt??..WwwW.ttxx
xxtt...3WWttt$%.WwwW.ttxxx
xxttt.t.-Wtt2**.WwwW.ttxxx
xttttt..Wwttt$%..WW.tttxxx
xxxxtt..-WWtt??..WwWW.ttxx
xxxxxtt..-WWttt3WWwW..ttxx
xxxxxxtt..3WWt1WWwW.tttxxx
xxxxxxxttt..WWtWW-..ttxxxx
xxxxxxxxttt.WwwwW3.ttxxxxx
xxxxxxxxxtt.-WwW..ttttxxxx
xxxxxxxxxttttWW-.tttxxxxxx
xxxxxxxxxxtttttttttxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Reminders of progress.
#
NAME:    hangedman_lair_in_review
TAGS:    no_monster_gen no_item_gen patrolling
ORIENT:  float
PLACE:   Lair:$
# mons are in groups of four, each ordered from least to most dangerous.
# mythological or dragon-y reptiles
MONS:    basilisk, rime drake, komodo dragon, hydra
# mythological non-reptiles
MONS:    hippogriff, manticore
KMONS:   8 = catoblepas
# fast things
KMONS:   9 = water moccasin
KMONS:   0 = blink frog
KMONS:   A = spiny frog
KMONS:   B = black mamba
# beetles/worms
KMONS:   C = swamp worm
KMONS:   D = torpor snail
KMONS:   E = tyrant leech
KMONS:   F = lindwurm
# yaks & pals
KMONS:   G = yak
KMONS:   H = elephant
KMONS:   I = death yak
KMONS:   J = dream sheep
# some non-set monsters right outside the vault
# to make sure you don't miss that there's a vault here maybe
KMONS:   K = crocodile
KMONS:   L = river rat
KMONS:   M = wolf
KMONS:   N = wyvern
KMONS:   P = plant w:12 / fungus w:8 / nothing
KITEM:   ~ = any good_item
SHUFFLE: 1'2"3`4^ / 5e6f7g8h / 9i0jAkBl / GGHHIIJJ
SHUFFLE: 5e6f7g8h / 5e6f7g8h / C-D-E-F-
SHUFFLE: KLMN, %*~| / %*~| / |~*%
NSUBST:  a = 2:. / 1 = x:9 .:1 / *:x, b = 2:. / 1 = x:9 .:1 / *:x
NSUBST:  c = 2:. / 1 = x:9 .:1 / *:x, d = 2:. / 1 = x:9 .:1 / *:x
SUBST:   X = xx., '"`^efghikl- = ., j = 0, O = 4448BFJ, $ = $$%.
: lair_small_ending(_G)
MAP
               xxx  xxx   xxx  xxx
             xxxxxxxxxxxxxxxxxxxxxxx
            xxxxxxxxxx.P.xxxxxxxxxxxx
           @..xxxxXXP.....XxB84xXPXxxx
          ....XxxXX.......Xxlh^xX.Xxxx
         @....XxXX.........d.X$x..Xxx
        ......XxXP.........ddddd..Xxx
       .......xxX....ccccc........Xxxx
      .......Kxx.....c.X$x........PXxx
     ...x...L.xxP....x`gkxX.......Xxxx
  xxxxx...xxxxxxxx...x37AxX..x....Xxx
 xxX....x..Xx962x...xxxxxxxx...xxxxxx
xxxX.......Xxjf"x....PxxXX.%.x..Pxxx
xxXP........x$X.b.....xxXP*.....xxxx
xxxX........bbbbb....Xxx.~.....xxx
 xxX..aaaaa.........PXxx|O...Pxxx
 xxX.Xx$X.a.........XXxx..0.7xxxx
xxxX.Xx'eixX.......XXxxxP..Axxx
xxxXPXx150xX.....PXXxxxxXP3xxx
 xxxxxxxxxxxx.P.xxxxxxxxxxxxxx
  xxxxxxxxxxxxxxxxxxxxxxxxxx
    xx   xxxx xx xxxx   xx
ENDMAP

##############################################################################
# The beastmaster's lair.
# Inspired by discussions of deep troll professionals and the prospect of a
# "beastmaster"; here he lives, with packs of beasts and one powerful
# personal pet.
#
# On his own the boss shouldn't be *extremely* threatening - it's his pet
# and the inner welcoming party that add enough danger that this ending
# stands on its own.
#
NAME:    grunt_lair_end_beastmaster
TAGS:    no_monster_gen no_item_gen no_trap_gen patrolling
PLACE:   Lair:$
ORIENT:  northeast
MONS:    deep troll shaman, dire elephant / hydra / lindwurm
MONS:    catoblepas, death yak band
MONS:    elephant, wolf, black bear / polar bear
KMONS:   P = plant / fungus w:2 / bush w:1 / nothing w:13
SHUFFLE: 567
SUBST:   C = cx..
SUBST:   P = P:19 t:1
NSUBST:  % = 2:| / 3:* / *:%
NSUBST:  5 = 2:5 / 3 = 5. / *:.
NSUBST:  6 = 3:6 / 4 = 6. / *:.
NSUBST:  7 = 1:7 / 2 = 7. / *:.
{{
  dgn.delayed_decay(_G, 'd', "human skeleton / orc skeleton / elf skeleton"
                             .. " / dwarf skeleton / ogre skeleton w:1"
                             .. " / naga skeleton w:1 / minotaur skeleton w:1"
                             .. " / merfolk skeleton w:1")
}}
MAP
                xxxxxx
          xxxxxxx5P.Pxx
         xxP..5.5..P..xxxxxx
        xxP5d5.5.5P.P..xxP.xxx
       xx.5.5.5ccccccc....P..x
       x...5.ccc.d...ccc.P..xx
      xx.P.ccc..d......d...Px
     xx..ccc...G..C..3..d..xx
    xx..ccc.G....CcC..4..c.x
    x.P.ccc..d..CcCcC...cc.x
    x..ccc%.G..G.CcC...ccP.x
   xx..ccc%%.2.dG.C..d.c...x
   x...cccc%%.1.....G.ccP..x
   x.P.ccccc%%.Gd.G...c..P.x
  xx....ccccc%%.....ccc...xx
 xx.d..Pccccccc%cccccP.6..x
xx.P..7..cccccccc.P.6.6.Pxx
..7..7.7P..ccP..P6.6d6.Pxx
.P.7..7..d....P...6.6.xxx
d.P.7P..P.xxxx.P..P6.xx
.P.7..7..xx  xxx.P..xx
..P.P..xxx     xxxxxx
.P.P..xx
..P.dxx
@....x
ENDMAP

################################################################################
# Some of the most famously dangerous lair monsters are small, fast,
# and incidentally poisonous. Let's compensate for frog_pond's change in status.
# Inspired by minmay_lair_venomous_thicket.
NAME:    hangedman_lair_tendril_chambers
PLACE:   Lair:$
ORIENT:  float
MONS:    worker ant w:16 / adder w:24 / river rat w:2
MONS:    water moccasin / wyvern, hornet
MONS:    spiny frog, black mamba, redback w:5, wolf spider
KMONS:   8 = guardian serpent
KMONS:   f = fungus
KMONS:   P = plant
SHUFFLE: AB / CD / EF / GH, DFH
SUBST:   P = P:35 @:25, f = f:4 ":1, z = x:49 f:1, BCDEG = x, AH = ., F : xxP
NSUBST:  1 = 8:1 / 4 = 2. / 4:. / * = 11., 0 = 8:2 / 2:3 / 3:. / * = 221..
NSUBST:  9 = 1:367 / 2:4 / 2:5 / * = 33445, 8 = 1:8 / 1:7 / 2 = 45 / * = 45 6:15 "
NSUBST:  % = 10:% / 2 = %" / *:", x = 6:c / * = x
KMASK:   %*" = no_monster_gen
: lair_small_ending(_G)
MAP
      PPPcccxxcccPPP
   PPPP.cc%cccc%cc.PPPP
  zz....c8$%**%$8c....zz
 Pzc....cc8$**$8cc....czP
PP.xx....c8""""8c....xx.PP
P...xx1.0cc""""cc0.1xx...P
P....xx1.0c8""8c0.1xx....P
x....1xx1.cc""cc.1xx1....x
xzx.0.1xx%%c""c%%xx1.0.xzx
P.xzx0.%BB9c""c9DD%.0xzx.P
 P..xzx%9AAxffxCC9%xzx..P
 P....xxxAA""""CCxxx....P
  ......xc0"ff"0xx......
  ......xx""ff""cx......
 P....xxxEE"00"GGxxx....P
 P..xzx%9EExccxGG9%xzx..P
P.xzx0.%FF%9xx9%HH%.0xzx.P
xzx.0.1xx.%xxxx%.xx1.0.xzx
x....1xx1.0x..x0.1xx1....x
P....xx1.0xx..xx0.1xx....P
P...xx1...z....z...1xx...P
PP.xx....xx....xx....xx.PP
 Pzc.....z......z.....czP
  zz....xx......xx....zz
   PPPP.z..P  P..z.PPPP
      PPcPP    PPcPP
ENDMAP

# An ancient temple flooded with waters brought forth by a mystic avatar.
NAME:    gammafunk_lair_ancient_temple
PLACE:   Lair:$
TAGS:    patrolling no_pool_fixup no_monster_gen
ORIENT:  float
KMONS:   ~ = plant
KMONS:   1 = water moccasin / crocodile / hornet / wyvern / spiny frog
KMONS:   2 = elephant / komodo dragon / polar bear / rime drake
KMONS:   3 = sea snake / manticore / hydra / wind drake / harpy
KMONS:   4 = dire elephant
KMONS:   5 = merfolk avatar
KFEAT:   ~12345%*|! = W
KITEM:   % = any no_pickup
KITEM:   * = star_item no_pickup
KITEM:   | = superb_item no_pickup
KITEM:   ! = any good_item no_pickup
# Two of three locations will be collapsed wall openings, and the remaining one
# will be a closed wall. Each opening will have 1-4 wall tiles replaced with
# land, with the corresponding walls tiles having been moved nearby.
SHUFFLE: defghijkopqr / suyzABCDEFHI / JKLMNOPQRSTU
SHUFFLE: def / pqr, ghi / jko, suy / FHI, zAB / CDE
SHUFFLE: defghijkopqr / defghijkoVYY / defghiVYYVYY / defVYYVYYVYY
SUBST:   KLNOQRTUY = -, JMPSV = c
SHUFFLE: suyzABCDEFHI / suyzABCDEVYY / suyzABVYYVYY / suyVYYVYYVYY
SUBST:   dgjpszCF = `, ehkquADHY = -, V = c, a = ccc-, G = GGGW`, " = .-
NSUBST:  f = 1:c / -, i = 1:c / -, o = 1:c / -, r = 1:c / -
NSUBST:  y = 1:c / -, B = 1:c / -, E = 1:c / -, I = 1:c / -
NSUBST:  - = 10:1 / 6:2 / 8 = 12WW / 4 = 3W / W
NSUBST:  ' = 4:2 / 4 = 23W / 3 = 33334 / 1:5 / W, % = 1:! / 1:* / %%*|
SUBST:   W = W:125 ~:10
COLOUR:  c = white
MAP
""""""""""""""""""""""""
"aa--aa--aa--aa--aa--aa"
"aa--aa--aa--aa--aa--aa"
"----------------------"
"----aa---a--a---aa----"
"aa--c------------c--aa"
"aa--c----yBDI----c--aa"
"----cG---uBEH---Gc----"
"----cccccszCFccccc----"
"aa--cG'''uADH'''Gc--aa"
"aa--c''''yAEI''''c--aa"
"----c''''''''''''c----"
"---edeff'%%%%'LLKJK---"
"aa-ighhi'%ww%'ONNMO-aa"
"aa-ojkok'%ww%'QRQPR-aa"
"---qpqrr'%%%%'UUTST---"
"----c''''''''''''c----"
"aa--c''''''''''''c--aa"
"aa--cG''''''''''Gc--aa"
"----ccccc----ccccc----"
"----cG----------Gc----"
"aa--c-----GG-----c--aa"
"aa--c------------c--aa"
"----aa-aaa--aaa-aa----"
"----------------------"
"aa--aa--aa--aa--aa--aa"
"aa--aa--aa--aa--aa--aa"
""""""""""""""""""""""""
ENDMAP

##############################################################################
# The Hall of Blades
##############################################################################
# Guarantee two rare base types with a brand
{{

function hall_of_blades_weapon(e)

  local long_blade_type = crawl.one_chance_in(2) and "double sword"
                                                  or "triple sword"
  local types = {"quick blade", long_blade_type,
                 "executioner's axe", "eveningstar", "bardiche",
                 "lajatang"}
  local egos = {"flaming", "freezing", "electrocution", "venom",
              "holy_wrath", "pain", "vampiricism", "draining",
              "anti-magic", "distortion"}
  local weapon1 = util.random_from(types)
  local weapon2 = weapon1
  while weapon2 == weapon1 do
    weapon2 = util.random_from(types)
  end
  local ego1 = util.random_from(egos)
  local ego2 = ego1
  while ego2 == ego1 do
    ego2 = util.random_from(egos)
  end

  e.mons("dancing weapon; good_item " .. weapon1 .. " ego:" .. ego1)
  e.mons("dancing weapon; good_item " .. weapon2 .. " ego:" .. ego2)
end

}}

######################################
# Entry vaults

NAME:     vaults_mumra_blade_bloodbath
TAGS:     vaults_entry_blade allow_dup no_item_gen
KFEAT:    O = enter_hall_of_blades
SUBST:    1 = ..'
SUBST:    2 = .''
SUBST:    3 = .'''
KPROP:    ' = bloody
SUBST:    ' = ...'
KITEM:    ' = short sword / long sword / morningstar / mace / scimitar / dagger / bardiche / battleaxe / war axe
MAP
xx...xxx1..xx
x.1...x11...x
..11..121....
..1222222..11
...223332221.
x..23333322.x
xx2233O332.xx
x122333332..x
11122333222..
....222.211..
....11...11..
x...1.x...11x
xx..1xxx..1xx
ENDMAP

NAME:     vaults_mumra_blade_sword
TAGS:     vaults_entry_blade allow_dup no_item_gen
KFEAT:    O = enter_hall_of_blades
SUBST:    : : ''-
SUBST:    ' : ll.
FTILE:    - = floor_hall
SUBST:    . : ..W
MAP
     :
    ':'
    ':'
    ':'
    ':'
   '':''
   '':''
   '':''
   '':''
-----O-----
 ....-....
    .-.
    .-.
    .-.
     -
ENDMAP

# Based on vaults_rooms_7_spin_blade_pillars
NAME:     vaults_blade_entry_pillars
TAGS:     vaults_entry_blade allow_dup no_monster_gen
SUBST:    y : x...
SUBST:    1 = 1.
MONS:     place:Blade:1
KFEAT:    O = enter_hall_of_blades
FTILE:    cxyO1. = floor_hall
MAP
xy...yx
y..c..y
..1.1..
.c.O.c.
..1.1..
y..c..y
xy...yx
ENDMAP

# Based on vaults_room_7_spin_blade_grunt
NAME:     vaults_blade_entry_basic_grunt
TAGS:     vaults_entry_blade allow_dup no_item_gen no_monster_gen
NSUBST:   1 = 2:1 / *:.
MONS:     dancing weapon
KFEAT:    O = enter_hall_of_blades
MAP
.......
.1...1.
.......
...O...
.......
.1...1.
.......
ENDMAP

NAME: nicolae_vaults_blade_pointy_end
TAGS: vaults_entry_blade allow_dup no_monster_gen
KFEAT: O = enter_hall_of_blades
KMONS: 01 = dancing weapon
FTILE: .O1 = floor_hall
MAP
xxxx
xO.xxx
x....xxx
xx.1...xxx
 x.......xx
 xx..1.....
  x........
  xx...1...
   x.......
   xx....1.
    x......
ENDMAP

NAME: nicolae_vaults_blade_hurt_lockers
TAGS: vaults_entry_blade allow_dup no_monster_gen vaults_orient_w
SHUFFLE: Aa/Bb/Dd, TG
SUBST: BD = x, bd = 1, A = +, a = ., T : T., G : G.
KFEAT: O = enter_hall_of_blades
KMONS: 1 = dancing weapon
FTILE: O' = floor_hall
MAP
xx..+aA''cxx
x..Gxxx''ccx
...Txxx'''cx
....+bB''Ocx
...Txxx'''cx
x..Gxxx''ccx
xx..+dD''cxx
ENDMAP

NAME: nicolae_vaults_blade_armed_guards
TAGS: vaults_entry_blade allow_dup no_monster_gen
KMONS: 1 = vault guard ; battleaxe | broad axe | flail | glaive | great mace | \
   halberd | hand axe | long sword | morningstar | scimitar | short sword | \
   trident | war axe . chain mail | plate armour . \
   large shield | shield w:20 | nothing w:60
KFEAT: O = enter_hall_of_blades
MAP
xxx...xxx
x...c...x
x.c.c.c.x
...1.1...
.cc.O.cc.
...1.1...
x.c.c.c.x
x...c...x
xxx...xxx
ENDMAP

NAME: nicolae_vaults_blade_crossed_weapons
TAGS: vaults_entry_blade allow_dup no_monster_gen
NSUBST: O = O / .
KFEAT: O = enter_hall_of_blades
KMONS: 1 = dancing weapon ; long sword
FTILE: .O1 = floor_hall
MAP
...............
.v.v.......v.v.
..v...vOv...v..
.v.v..vvv..v.v.
....v..c..v....
.....1.c.1.....
..vv..OcO..vv..
..OvcccccccvO..
..vv..OcO..vv..
.....1.c.1.....
....v..c..v....
.v.v..vvv..v.v.
..v...vOv...v..
.v.v.......v.v.
...............
ENDMAP

NAME:   vaults_nooodl_blade_preview
TAGS:   vaults_entry_blade allow_dup no_item_gen no_monster_gen
SUBST:  C : cc.
NSUBST: 1 = 2:1 / *:.
MONS:   place:Blade:1
KFEAT:  O = enter_hall_of_blades
MAP
ccccccccccccccc
cCCCCCCCCCCCCCc
c.............c
c.c1.c1.1c.1c.c
+......O......+
c.c1.c1.1c.1c.c
c.............c
cCCCCCCCCCCCCCc
ccccccccccccccc
ENDMAP

######################################
# Main layout

NAME:    hall_of_blades
TAGS:    no_dump no_monster_gen
PLACE:   Blade
ORIENT:  encompass
: hall_of_blades_weapon(_G)
SHUFFLE: CD / CD / DC, {([
SUBST:   C : cc., D = ., E : c.., F : c.., c : cv, ([ = .
NSUBST:  { : 1:{ / 1:( / 1:[ / 1:<, . : 6:0 / 6 = 0. / 6 = 0... / *:.
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccxxx
xxxcccc....cccc.cccc{cccc.cccc.F.cccc.F.cccc.cccc{cccc.cccc.....cccxxx
xxxccc......cc...cc...cc...cc....(cc(....cc...cc...cc...cc.......ccxxx
xxxcc.............................................................cxxx
xxxc......c..............c..................c..............c......cxxx
xxxc.....ccc............ccc................ccc............ccc.....cxxx
xxxcE...ccccc..........ccccc..............ccccc..........ccccc...Ecxxx
xxxcE...ccccc...........ccc................ccc...........ccccc...Ecxxx
xxxc....0ccc.............c..................c.............ccc0....cxxx
xxxcE....ccc..............................................ccc....Ecxxx
xxxcE.....c................................................c.....Ecxxx
xxxc.............................[CC[.............................cxxx
xxxc.............DD..............CCCC..............DD.............cxxx
xxxcE1...0......DDDD.............CCCC.............DDDD......0...2Ecxxx
xxxc.............DD..............CCCC..............DD.............cxxx
xxxc.............................[CC[.............................cxxx
xxxcE.....c................................................c.....Ecxxx
xxxcE....ccc..............................................ccc....Ecxxx
xxxc....0ccc.............c..................c.............ccc0....cxxx
xxxc....ccccc...........ccc................ccc...........ccccc....cxxx
xxxcE...ccccc..........ccccc..............ccccc..........ccccc...Ecxxx
xxxcE....ccc............ccc................ccc............ccc....Ecxxx
xxxc......c..............c..................c..............c......cxxx
xxxc..............................................................cxxx
xxxcc.......cc...cc...cc...cc....(cc(....cc...cc...cc...cc.......ccxxx
xxxccc.....cccc.cccc{cccc.cccc.F.cccc.F.cccc.cccc{cccc.cccc.....cccxxx
xxxcccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

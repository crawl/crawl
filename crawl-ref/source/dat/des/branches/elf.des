##############################################################################
# elf.des: Special maps for Elf:5, arrival vaults for Elf:1, and (mini)vaults
#          particular to the Elven Halls.
##############################################################################

##############################################################################
# Elf:1 arrival vaults
#
# The concept:
# There are no entry vaults for the Elven Halls. Instead, there is a "border
# check" vault on the other side, in the Elf:1 level. Note that elves and orcs
# are not necessarily at war or have big enmities against each other. It's
# just a border check.
#
# The intention:
# I (Eino here, hi!) lost a lot of characters trying to do the Halls right
# after the Mines until I realized the Halls are significantly harder. A
# welcoming party on the other side will hopefully warn the player about this.
#
# Honour this intention. For instance, put lots of space between the monsters
# and the stairs, so the players have many turns to realize their mistake.
# Note that designing can be a challenge with the eight space LOS. What might
# make the player realize it's better to flee: facing a nasty summon, taking
# a few hits from conjurations.. they might engage melee opponents, at which
# point it's quite difficult to flee. Please keep that in mind. Note also,
# that a corridor is usually easier for the player than an open space.
#
# This is an instance where an entry (arrival) vault really does benefit from
# having monsters, even a nasty bunch. However, don't go overboard trying to
# make it really hard. And try to make it seem like a plausible border guard.
#
##############################################################################

##############################################################################
# Dummy Elf arrival vault

NAME:   elf_arrival_dummy
TAGS:   dummy
PLACE:  Elf:1
WEIGHT: 50
ORIENT: float
MAP
{
ENDMAP

##############################################################################

NAME:    elf_arrival_001
PLACE:   Elf:1
ORIENT:  west
TAGS:    patrolling
SHUFFLE: asd/x$=
SHUFFLE: fgh/x$=
SUBST:   a = x, s = x, d = x, f = x, g = x, h = x
MONS:    deep elf soldier
MONS:    deep elf mage / deep elf priest / deep elf summoner w:5
MAP
xxxxxxxxxxxxxxxaaa
xxxxxxxxxxxxxxxasa
xxxxxxxxxxxxxxxxdx
x...xxxxx..1.....@
x.{........G1.G2.x
x...xxxxx..1.....@
xxxxxxxxxxxxxxxxhx
xxxxxxxxxxxxxxxfgf
xxxxxxxxxxxxxxxfff
ENDMAP

##############################################################################

NAME:    elf_arrival_002
PLACE:   Elf:1
ORIENT:  south
TAGS:    patrolling
MONS:    deep elf soldier, deep elf mage
MAP
xxxxx@xxx@xxx@xxxxx
xxxx...........xxxx
xx....G..2..G....xx
x...1.........1...x
x.G.............G.x
x.................x
x.......G.G.......x
x........[........x
x.....G.....G.....x
xx.1.....{.....1.xx
xxxx...........xxxx
xxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################

NAME:    eino_elf_arrival_customs
ORIENT:  southwest
PLACE:   Elf:1
TAGS:    patrolling
MONS:    deep elf soldier / deep elf mage w:5
MONS:    deep elf summoner / nothing
MAP
xxxxxxxxx
x.+.....x
x.x..x..x
x.m.xxx.x
x.x..x..x
x.m1..1.x
x.x.2x..x
x.m1xxx.x
x.x.1x..x
x{m...2.+
xxxxxxxxx
ENDMAP

##############################################################################

NAME:    eino_elf_arrival_shootout
PLACE:   Elf:1
ORIENT:  float
TAGS:    patrolling
MONS:    deep elf soldier / deep elf mage w:3 / nothing w:6
MONS:    deep elf mage / deep elf priest w:3 / nothing w:6
MAP
xxxxxxxx@@@xxxxxxxx
xxxx...........xxxx
xx.......2.......xx
x.....1.....1.....x
xxxx.....G.....xxxx
xxxxxxxx...xxxxxxxx
xxx[.....{.....(xxx
xxxxxxxx...xxxxxxxx
xxxx.....G.....xxxx
x.....1.....1.....x
xx.......2.......xx
xxxx...........xxxx
xxxxxxxx@@@xxxxxxxx
ENDMAP

##############################################################################

NAME:   eino_elf_arrival_sip
PLACE:  Elf:1
ORIENT: south
TAGS:   patrolling
TAGS:   no_monster_gen
KMONS:  Z = deep elf soldier / nothing w:7
KFEAT:  Z = W
MAP
 .........
x..WWWWW..x
xWWWZWZWWWx
xWWZWZWZWWx
xWWWWWWWWWx
x..WWWWW..x
x.........x
x....[....x
xx...{...xx
xxx.....xxx
xxxxxxxxxxx
ENDMAP

##############################################################################

NAME:   eino_elf_arrival_the_gate
PLACE:  Elf:1
ORIENT: south
TAGS:   patrolling
MONS:   deep elf soldier / deep elf mage w:5
MONS:   deep elf mage / deep elf priest w:3
MAP
x.........x
@.........@
x....2....x
x...1.1...x
x.G..1..G.x
x.........x
xxx+++++xxx
xxx.....xxx
xxx..[..xxx
xxx.....xxx
xxx..{..xxx
xxxx...xxxx
xxxxxxxxxxx
ENDMAP

##############################################################################
# A nice scenic altar

NAME:    eino_elf_arrival_altar
ORIENT:  west
PLACE:   Elf:1
TAGS:    patrolling
SHUFFLE: 1/2
KFEAT:   C = altar_vehumet
MONS:    deep elf soldier, deep elf mage
MONS:    deep elf priest
MAP
xxxxxxxxxxxxxxxxxxxxxxx@@@xxxx
xxxxxxxxxxxxx...xxxxxx.....xxx
xxxxxxxxxxx.22x...xx.........x
xxxxxxxxx...xxxxx.x..........x
x...xxxxx.xxxxxxx.m..........x
x.[.......xxxxxx....1..GmG...@
x....xxxxxxxxxxx..m..C3mbm...@
x.{.......xxxxxx....2..GmG...@
x...xxxxx.xxxxxxx.m..........x
xxxxxxxxx...xxxxx.x..........x
xxxxxxxxxxx.11x...xx.........x
xxxxxxxxxxxxx...xxxxxx.....xxx
xxxxxxxxxxxxxxxxxxxxxxx@@@xxxx
ENDMAP

###########################################################################
# Two arrival vaults by Lemuel, using fog generators.

NAME:   elf_arrival_freeze
PLACE:  Elf:1
ORIENT: float
ITEM:   nothing
MARKER: d = lua:fog_machine { cloud_type = "freezing vapour", \
             pow_min = 10, pow_max = 10, delay = 10, \
             size = 2, walk_dist = 0, spread_rate= 0 }
MAP
xx@@@xx
xx...xx
xx...xx
xx.d.xx
xx...xx
xx...xx
xx...xx
xx...xx
xx...xx
xx...xx
xx...xx
xx.{.xx
xxxxxxx
ENDMAP

NAME:   elf_arrival_flame
PLACE:  Elf:1
ORIENT: float
ITEM:   nothing
MARKER: d = lua:fog_machine { cloud_type = "flame", \
             pow_min = 10, pow_max = 10, delay = 10, \
             size = 2, walk_dist = 0, spread_rate= 0 }
MAP
xxxxxxxxxxxxx
xxxxxxxxxxxxx
@...........x
@..d.......{x
@...........x
xxxxxxxxxxxxx
xxxxxxxxxxxxx
ENDMAP

NAME:   minmay_elf_arrival_circle
ORIENT: float
PLACE:  Elf:1
TAGS:   patrolling
MONS:   deep elf soldier / deep elf fighter, deep elf mage / deep elf summoner
SUBST:  B : b.
MAP
       xx+xx
    xxxx...xxxx
   xx....2....xx
  xx...........xx
 xx.............xx
 x...BbB...BbB...x
 x...bbb...bbb...x
xx...BbB...BbB...xx
x.................x
+.1......{......1.+
x.................x
xx...BbB...BbB...xx
 x...bbb...bbb...x
 x...BbB...BbB...x
 xx.............xx
  xx...........xx
   xx....2....xx
    xxxx...xxxx
       xx+xx
ENDMAP

NAME:   minmay_elf_arrival_plus
ORIENT: float
PLACE:  Elf:1
TAGS:   patrolling
MONS:   deep elf soldier / deep elf fighter / deep elf mage / \
        deep elf summoner / deep elf priest / deep elf conjurer / nothing w:40
MAP
     x@...@x
     x.....x
     x.1T1.x
     x.....x
    xx.....xx
xxxxxmmm=mmmxxxxx
@....m.....m....@
..1..m.....m..1..
..T..=.({[.=..T..
..1..m.....m..1..
@....m.....m....@
xxxxxmmm=mmmxxxxx
    xx.....xx
     x.....x
     x.1T1.x
     x.....x
     x@...@x
ENDMAP

NAME:   minmay_elf_arrival_summoners
ORIENT: float
PLACE:  Elf:1
TAGS:   patrolling
MONS:   deep elf summoner / deep elf mage / deep elf priest
MAP
  xxxxxxx
  x.....x
  x.({[.x
xxx.....xxx
@1m.....m1@
xxx.....xxx
@1m.....m1@
xxx.....xxx
@1m.....m1@
xxx.....xxx
  x.....x
  x@...@x
ENDMAP


##############################################################################
# Elf minivaults
##############################################################################

# Total weight of these is 30.
NAME:   minmay_elf_crystal_square
DEPTH:  Elf
ORIENT: float
MONS:   deep elf mage / nothing
MAP
+bbbbbbbbbbbbbbb+
b...............b
b.bbbb..bbbbbb..b
b.b1....b..11b..b
b.bb....b..bbb..b
b.b1............b
b.b...bbbbb..b..b
b....bb......b..b
b...bb....1..b..b
b..bb...b....b..b
b..b.1..bb..1b..b
b..b.....bb..b..b
b..b1U.1..b..b..b
b..b.......1.b..b
b..bbbbbbbbbbb..b
b...............b
+bbbbbbbbbbbbbbb+
ENDMAP

NAME:   minmay_elf_crystal_pentagon
DEPTH:  Elf
ORIENT: float
WEIGHT: 8
MONS:   deep elf mage / deep elf summoner / nothing w:25
MAP
   +bbbbbbbbbbb+
   b...........b
   b...........b
  bb...bbbbb...bb
  b1..bb1.1bb..1b
  b...b1.U.1b...b
 bb...bb1.1bb...bb
 b.....bb.bb.....b
 b...............b
bb..bb..1........bb
+...b1....bbb.....+
bb..bbbb...1b...1bb
 bb1..1bb..bb...bb
  bbb...b..b1.bbb
    bb.......bb
     bb.....bb
      bbb.bbb
        b+b
ENDMAP

NAME:   minmay_elf_crystal_hexagon
DEPTH:  Elf:2-
ORIENT: float
WEIGHT: 6
MONS:   deep elf mage / deep elf summoner / deep elf conjurer / nothing w:45
MAP
    +bbbbbbbbb+
    b...1b1...b
   bb...1b1...bb
   b....bbbb...b
  bb...bb.1....bb
  b....b.1......b
 bb....b...bb...bb
bb.....b.1..bb...bb
+.......1.1..bb...+
bb...bb1.U.1bb...bb
 bb...bb1.1bb1..bb
  b...1bb1bb....b
  bb...1bbb....bb
   b..1.1b1....b
   bb.bbbb....bb
    b.........b
    +bbbbbbbbb+
ENDMAP

NAME:   minmay_elf_crystal_heptagon
DEPTH:  Elf:3-
ORIENT: float
WEIGHT: 4
MONS:   deep elf mage / deep elf summoner / deep elf conjurer / \
        deep elf death mage / nothing w:70
MAP
    +bbbbbbb+
   bb.......bb
   b.........b
  bb..bbbbb..bb
 bb...b.1.b...bb
bb...bb1.1bb...bb
+....b1.U.1b.1..+
b.b1.b1.1.1b1bb.b
b.bb1bb1.1bb.1b.b
b1....bb.bb1....b
bb..1.....11b..bb
 b.bb1....bbb..b
 b..b.1b11.....b
 +b1...bbb....b+
  bbb.......bbb
    bbb...bbb
      bb+bb
ENDMAP

NAME:   minmay_elf_crystal_octagon
DEPTH:  Elf:3-
ORIENT: float
WEIGHT: 2
MONS:   deep elf mage / deep elf summoner / deep elf conjurer / \
        deep elf death mage / deep elf sorcerer / nothing w:100
MAP
     +bbbbb+
    bb.....bb
   bb..bb...bb
  bb...1.1...bb
 bb...bbbbb...bb
+b....b111b...1b+
b...bb.1b1.bb...b
b..1b11...11b1b.b
b...b1b.U.b1b.b.b
b.b1b11...11b1b.b
b...bb.1b1.bb...b
+b....b111b....b+
 bb...bbbbb...bb
  bb1..1.1...bb
   bb..b.b..bb
    bb.....bb
     +bbbbb+
ENDMAP

NAME:  minmay_elf_necromancy
DEPTH: Elf
MONS:  elf zombie / necromancer w:1 / nothing
KMONS: 2 = deep elf death mage
KITEM: 2 = randbook disc:necromancy slevels:16
MAP
xxxxxxxxxxx
xxx1..x112x
xxx..1+111x
x.1.1.xxxxx
+......1..x
x..1.1....+
x.Y.....xxx
x11...1xxxx
xxxxxxxxxxx
ENDMAP

NAME:  minmay_elf_sorcery
DEPTH: Elf:2-
MONS:  deep elf summoner / deep elf conjurer / deep elf mage / nothing
KMONS: 2 = deep elf sorcerer w:90 / deep elf annihilator / deep elf demonologist
KITEM: 2 = randbook disc:conjuration disc2:summoning slevels:16
SUBST: x : xxc, m : mmcxb
MAP
...........1............
.mmm.x..x......x..x.mmm.
.mmm...........1....mmm.
.mmm1.x..x....x..x.1mmm.
1mmmm.....1......1.mmmm.
.mmmm1.x..x..x..x..mmmm.
..mmmm..1.........mmmm..
 ..mmmm......1...mmmm.1
  ..mmmmm......mmmmm..
   ...mmxxx++xxxmm...
     1..xx....xx...
      ..x......x..
       .x......x1
       .x......x.
       .x......x.
       .xx2...xx.
       .xxxxxxxx.
       ...1......
ENDMAP

NAME:  minmay_elf_annihilation
DEPTH: Elf:3-
MONS:  deep elf conjurer / nothing
KMONS: 2 = deep elf annihilator / deep elf sorcerer w:2
KITEM: 2 = randbook disc:conjuration slevels:16
SUBST: x : x c:1
MAP
        ...
        .x1
  ...............
  .x.xxxx+xxxx.x.
  ...x....1..x...
  .xxx1x...x.xxx.
  .x....1......x.
  .x.x..b+b..x.x.
...x1..bb.bb1..x...
.x.+...+.2.+...+.x.
...x...bb.bb..1x...
  .x.x1.b+b..x.x.
  .x.......1...x.
  .xxx.x...x.xxx.
  .1.x..1....x..1
  .x.xxxx+xxxx.x.
  ...............
        .x.
        ...
ENDMAP

NAME:  minmay_elf_demonology
DEPTH: Elf:3-
MONS:  deep elf summoner / nothing
KMONS: 2 = deep elf demonologist w:90 / deep elf sorcerer / deep elf high priest
KITEM: 2 = randbook disc:summoning slevels:16
MAP
        ..
       ....
      ......
     ..1xx...
    ...xxxx...
   ....xxxx1.1.
  ...1..xx......
 ..xx.....1..xx1.
..xxxx..xx..xxxx..
..xxxx1x.2x.xxxx..
 ..xx..x..x..xx..
  ..1...xx....1.
   ..xx....xx1.
   .xxxx..xxxx.
   .xxxx1.xxxx.
   ..xx...1xx..
    ..1.......
     ........
      ......
       ....
        ..
ENDMAP

# Dummies give 2 XP and halt autotravel for adjacent players, used for flavour
NAME:   minmay_elf_archery
DEPTH:  Elf:3-
ORIENT: float
MONS:   deep elf soldier; bow | longbow . arrow / \
        deep elf fighter; bow | longbow . arrow / nothing
MONS:   deep elf master archer
MONS:   training dummy tile:mons_training_dummy
MAP
x+xxxxxxxxxxxxxx
x...1..........x
x.1....1......3x
.....1.........x
@2............3x
...1...........x
x1....1.......3x
x...1..........x
x+xxxxxxxxxxxxxx
ENDMAP

NAME:    minmay_elf_blademastery
DEPTH:   Elf:4-
ORIENT:  float
MONS:    deep elf soldier; falchion | long sword | scimitar | sabre / \
         deep elf fighter; falchion | long sword | scimitar | sabre / nothing
MONS:    deep elf blademaster
MONS:    training dummy tile:mons_training_dummy
KITEM:   d = long sword race:elven, long sword race:elven / nothing, \
             long sword race:elven / nothing, long sword race:elven / nothing
KITEM:   e = falchion race:elven, falchion race:elven / nothing, \
             falchion race:elven / nothing, falchion race:elven / nothing
KITEM:   f = scimitar race:elven, scimitar race:elven / nothing, \
             scimitar race:elven / nothing, scimitar race:elven / nothing
KITEM:   g = sabre race:elven, sabre race:elven / nothing, \
             sabre race:elven / nothing, sabre race:elven / nothing
SHUFFLE: defg
MAP
xxxxxxxxxx+xxx
x.1..........+
x3..31.31....x
x............x
x..........2.x
x3.13..3.....x
x1.....1...x+x
x..........xdx
x31.31.3...xex
x.......1..xfx
xxxxxxxxxx+xxx
ENDMAP


##############################################################################
# Elf:5 branch endings
##############################################################################

##############################################################################
## Classical elf_hall
##
NAME:    elf_hall
PLACE:   Elf:5
ORIENT:  northwest
TAGS:    no_rotate
LFLAGS:  no_tele_control
MONS:    deep elf high priest, deep elf demonologist, deep elf annihilator
MONS:    deep elf sorcerer, deep elf death mage
MONS:    deep elf blademaster, deep elf master archer
SUBST:   1=1., 2=2., 3=3., 4=4., 5=5.
SHUFFLE: 23, 45
KMONS:   $ = weight:450 nothing / deep elf sorcerer / deep elf annihilator
KMONS:   * = weight:300 nothing / deep elf annihilator / deep elf death mage
KMONS:   | = weight:600 nothing / deep elf demonologist / deep elf death mage
KFEAT:   $ = $
KFEAT:   * = *
KFEAT:   | = |
SUBST:   v = v .:2
SUBST:   v : xvb
SUBST:   | = | *:2
SUBST:   $ = $:20 *:4 |:1
SUBST:   * = * |:3
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxcccccccccccccccccxxxxx
xxxxxxxxxxxxcc*|*|*|**|||||c$ccxxxx
xxxxxxxxxxxcc*$*|*|*|*|||||c$$ccxxx
xxxxxxxxxxcc*$|*$***$$|||||c|$$ccxx
xxxxxxxxxcc*$*|**ccccccccccc$$$$ccx
xxxxxxxxxc*|*$*$ccc..2..2..c+$|$$cx
xxxxxxxxxc$*$*ccc7........7.c$$$$cx
xxxxxxxxxc||**cc...5.5...4.4cc$|$cx
xxxxxxxxxc*$$cc..3.....3..ccccccccx
xxxxxxxxxc$+ccc.....2....cc.....5cx
xxxxxxxxxc$c....5.....5.cc....5..cx
xxxxxxxxxccc......5....cc..2....ccx
xxxxxxxxxxxc....5...2.cc.......ccxx
xxxxxxxxxxcc..1..U.....3.4..4..ccxx
xxxxxxxxxcc.....................ccx
xxxxxxxxxc..2........3........5..cx
xxxxxxxxxc.......2..2....3.......cx
xxxxxxxxxc..2................2..5cx
xxxxxxxxxc......v.........v..2...cx
xxxxxxxxxc..2..vv..1...1..vv.....cx
xxxxxxxxxc2..6vvv....1....vvv64.4cx
xxxxxxxxxc..vvvv...........vvvv..cx
xxxxxxxxxc.vvv.....cc.cc.....vvv.cx
xxxxxxxxxc.v.3...cccc.cccc.3...v.cx
xxxxxxxxxc.3...cccxxc.cxxccc.3.3.cx
xxxxxxxxxc...cccxxxxc.cxxxxccc...cx
xxxxxxxxxc.cccxxxxxxc.cxxxxxxccc.cx
xxxxxxxxxcccxxxxxxxxc.cxxxxxxxxcccx
xxxxxxxxxxxxxxxxxxxxx@xxxxxxxxxxxxx
ENDMAP


##############################################################################
## Alternative elf halls: gauntlets (David)
##
NAME:    elf_hall_gauntlet_wide
PLACE:   Elf:5
TAGS:    no_pool_fixup no_rotate
LFLAGS:  no_tele_control
ORIENT:  float
WEIGHT:  5
MONS:    deep elf high priest, deep elf demonologist
MONS:    deep elf annihilator, deep elf sorcerer
MONS:    deep elf death mage, deep elf blademaster
SHUFFLE: 345
SUBST:   4=4., 2=2., 5=5., 3=3.
KMONS:   $ = weight:115 nothing / deep elf annihilator
KMONS:   * = weight:105 nothing / deep elf death mage
KMONS:   | = weight:290 nothing / deep elf sorcerer
KFEAT:   $ = $
KFEAT:   * = *
KFEAT:   | = |
SUBST:   | = | *:2
SUBST:   * = * |:3
KMONS:   A = deep elf master archer
KFEAT:   A = |
KMONS:   B = deep elf blademaster
KFEAT:   B = |
MAP
cccccccccccccccccccccccccc
c2ccccccc........ccccccccc
c.cccc2...5....5...2cccccc
c.cc..................cccc
c.c....3....45....3....c3c
c.+........U..U........+.c
c.c......2.2..2.2......c.c
c.cc......3....4......cc.c
c.ccc4cccccc..cccccc.cc..c
c2ccc.c.3.ww..ww.4.ccc...c
c.ccc..3..ww..ww..5......c
c$$$ccc...ww..ww...ccccc.c
c$$$ccc.1.ww..ww.5.ccccc.c
c$$$cccccccc2.cccccc1262.c
ccc+cccccccc..cccccccccc.c
cB*|*|**|*cc..cc$|$*$|$|.c
c**|*|||||cc..cc|$|$*$*$.c
c*|*|||**|cc..cc$*$*$*$|.c
c|**|*|||Acc..ccA$|$|$|$2c
ccccccccccccWWcccccccccccc
ccccccccccccWWcccccccccccc
ccccccccccccWWcccccccccccc
xxxxxxxxxxxx@@xxxxxxxxxxxx
ENDMAP

NAME:    elf_hall_gauntlet_narrow
PLACE:   Elf:5
TAGS:    no_pool_fixup no_rotate
ORIENT:  float
WEIGHT:  5
MONS:    deep elf high priest, deep elf demonologist
MONS:    deep elf annihilator, deep elf sorcerer
MONS:    deep elf death mage, deep elf master archer
MONS:    deep elf blademaster
SHUFFLE: 345
SUBST:   4=4., 2=2., 5=5., 3=3.
SUBST:   l : lww
SHUFFLE: lww
SHUFFLE: nN / yY / zZ
SUBST:   N=@, n=., Y=c, y=c
SHUFFLE: zZ / nN
SUBST:   N=@, n=., Z=c, z=c
KMONS:   $ = weight:115 nothing / deep elf annihilator
KMONS:   * = weight:105 nothing / deep elf death mage
KMONS:   | = weight:290 nothing / deep elf sorcerer
KFEAT:   $ = $
KFEAT:   * = *
KFEAT:   | = |
SUBST:   | = | *:2
SUBST:   * = * |:3
KITEM:   6 = |
KITEM:   7 = |
KFEAT:   6 = 6
KFEAT:   7 = 7
LFLAGS:  no_tele_control
MAP
ccccccccccccccccccccccccc
c2ccccccc.......ccccccccc
c.cccc2...5...5...2cccccc
c.cc.................cc7c
c.c....3....45...3....c3c
c.+.........U.........+.c
c.c......2.2.2.2......c.c
c.cc......3...4......cc.c
c.ccc4cccccc.cccccc.cc..c
c2ccc.c.3.ww.ww.4.ccc...c
c.ccc..3..ww.ww..5......c
c$$$ccc...ww.ww...ccccc.c
c$$$ccc.1.ww.ww.5.ccccc.c
c$$$cccccccc.ccccccc122.c
ccc+cccccccc2cccccccccc.c
c7*|*|**|*cc.cc$|$*$|$|.c
c**|*|||||cc.cc|$|$*$*$.c
c||*||***|cc.cc$*$*$*$|.c
c|**|*||6*cc.cc6$|$|$|$2c
cccccccccccclcccccccccccc
ccccccccccclllccccccccccc
cccccccccc..c..cccccccccc
Yyyyyyyyyy.ccc.zzzzzzzzzZ
xxxxxxxxxxNncnNxxxxxxxxxx
ENDMAP

##############################################################################
## minmay's Elf hall
##
NAME:    minmay_elf_hall
PLACE:   Elf:5
ORIENT:  southwest
TAGS:    no_rotate
LFLAGS:  no_tele_control
MONS:    deep elf high priest, deep elf demonologist, deep elf annihilator
MONS:    deep elf sorcerer, deep elf death mage
MONS:    deep elf blademaster, deep elf master archer
SUBST:   1=11., 2=22., 3=33., 4=44., 5=543, 6=66., 7=77.
SHUFFLE: 12, 34, 67
KMONS:   $ = weight:450 nothing / deep elf sorcerer / deep elf annihilator
KMONS:   * = weight:300 nothing / deep elf annihilator / deep elf death mage
KMONS:   | = weight:600 nothing / deep elf demonologist / deep elf death mage
KFEAT:   $ = $
KFEAT:   * = *
KFEAT:   | = |
SUBST:   | = | *:2
SUBST:   $ = $:20 *:4 |:1
SUBST:   * = * |:3
MAP
xxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxc.cxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxc.cxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxc.cxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxc.cxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxc.cxxxxxxxxxxxxxxxxx
xxxcccccxxxxxxxxxc.cxxxxxxxxxcccccxxx
xccc...cccxxxxxccc.cccxxxxxccc...cccx
cc.......cccxccc.....cccxccc.......cc
c3.........ccc.........ccc.........4c
c....6......5...........5......6....c
c3.................................4c
cc......3.........7.........3......cc
xcc...............n...............ccx
xxc5............6nnn6............5cxx
xcc...............n...............ccx
cc.......4........7........4.......cc
c1.................................2c
c....7......5...........5......7....c
c1.........ccc.........ccc.........2c
cc.......cccxccc.....cccxccc.......cc
xccc...cccxxxxxccc.cccxxxxxccc...cccx
xxxcc+ccxxxxxxxxxc.cxxxxxxxxxcc+ccxxx
xxxc$*|cxxxxxxxxxc.cxxxxxxxxxc|*$cxxx
xxxc|*$cxxxxxxxxxc.cxxxxxxxxxc$*|cxxx
xxxcccccxxxxxxxxxc.cxxxxxxxxxcccccxxx
xxxxxxxxxxxxxxxxxc.cxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxc.cxxxxxxxxxxxxxxxxx
xxxxxxxcccccxxxccc.cccxxxcccccxxxxxxx
xxxxcccc|$*ccccc6...6ccccc*||ccccxxxx
xxccc*|$*|$$|+....U....+|$$|*$*$cccxx
xcc*$$*|$$|$$ccc7...7ccc|*|$$$||*$ccx
cc$*|*|*|*|cccxcccccccxccc$*$$*$$$|cc
c|*|$$|*ccccxxxxxxxxxxxxxcccc|*$||$*c
cccccccccxxxxxxxxxxxxxxxxxxxccccccccc
ENDMAP

##############################################################################
## minmay's more circular hall
##
NAME:    minmay_elf_hall_circle
PLACE:   Elf:5
ORIENT:  float
TAGS:    no_rotate
LFLAGS:  no_tele_control
MONS:    deep elf high priest, deep elf demonologist, deep elf annihilator
MONS:    deep elf sorcerer, deep elf death mage
MONS:    deep elf blademaster, deep elf master archer
SHUFFLE: 12345, 67
SUBST:   1=1., 2=2., 3=3., 4=4., 5=5.
KMONS:   $ = weight:450 nothing / deep elf sorcerer / deep elf annihilator
KMONS:   * = weight:300 nothing / deep elf annihilator / deep elf death mage
KMONS:   | = weight:600 nothing / deep elf demonologist / deep elf death mage
KFEAT:   $ = $
KFEAT:   * = *
KFEAT:   | = |
SUBST:   v = v .:2
SUBST:   v : xvb
SUBST:   | = | *:2
SUBST:   $ = $:20 *:4 |:1
SUBST:   * = * |:3
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxcccccccccccxxxxxxxxxxx
xxxxxxxxccc$|$|$|$|$cccxxxxxxxxx
xxxxxxccc***$|$|$|$|$|cccxxxxxxx
xxxxxcc******$|$|$|$|$|$ccxxxxxx
xxxxcc**cc+cc|$|$|$|$|$|$ccxxxxx
xxxcc**cc5..cccc|$|$|$|$|$ccxxxx
xxxc****cc...2.ccccc$|$|$|$cxxxx
xxcc*****ccc......3cccccc$|ccxxx
xxc*****cc6cccc........5cccccxxx
xcccccccc.....ccccc.5......6ccxx
xc2........2......cccccc.....cxx
xc..........4................cxx
xc..3..5.......U..2........5.cxx
xc7..................4....1..cxx
xc.....2...vv.....vv.........cxx
xc..........vvvvvvv..3.......cxx
xc....2.......vvv.........2..cxx
xc.........3...v...7.........cxx
xc2.........5....4....4......cxx
xcc.5....4..................ccxx
xxc....vv6...4.......6vv...2cxxx
xxcc...vvv2......5..2vvv.5.ccxxx
xxxc3..vvvvv2.....2vvvvv..3cxxxx
xxxcc....vvvvv...vvvvv....ccxxxx
xxxxcc3....vvvv1vvvv....3ccxxxxx
xxxxxcc.................ccxxxxxx
xxxxxxccc3...1...1...3cccxxxxxxx
xxxxxxxxccc.........cccxxxxxxxxx
xxxxxxxxxxccccc@cccccxxxxxxxxxxx
ENDMAP


##############################################################################
# (Mini)vaults specific to the Elven Halls
##############################################################################

default-depth: Elf:*

##############################################################################
# Some orcs on a diplomatic mission (Shiori)
# They have brought some gold from their mines as a gift
NAME:    orc_legates
DEPTH:   Elf:1-2
SHUFFLE: AB , 34 , 12
SUBST:   6=4
SUBST:   0= 0 .:9 9:1
SUBST:   5= 5 .:9 4:1
NSUBST:  1= 2:1 / *:.
NSUBST:  2= 2:2 / *:.
KITEM:   A= any good_item race:elven
KITEM:   B= any good_item race:elven
KMONS:   A= patrolling deep elf knight
KMONS:   B= patrolling deep elf high priest / patrolling deep elf demonologist
MONS:    ice dragon, dragon
MONS:    orc knight / orc sorcerer / orc high priest
MONS:    orc warrior / orc wizard / orc priest
MONS:    orc
ITEM:    q:120 gold / q:60 gold / q:30 gold
# being able to blink and become invisible, the deep elves do not need a
# hidden exit in back of their audience chamber.
MAP
cccccxxccccc
c111cGGc222c
cnnncABcnnnc
xG00B..A00Gx
xG00A..B00Gx
xx00x..x00xx
xG00.dd.00Gx
xG...34...Gx
xx.5x66x5.xx
xG.555555.Gx
xG.555555.Gx
xx++x++x++xx
ENDMAP

##############################################################################
# A high-level negotiation between orc and deep elf leaders (Shiori)
NAME:     elf_orc_summit_meeting
DEPTH:    Elf:4-5
SHUFFLE: 12 , 45
NSUBST:  7= 1=7 / *= 7:1 .
NSUBST:  6= 1=6 D:1 / *= 6 .:1
SUBST:   3 = 3 .:1
# the deep elf delegation:
MONS:    deep elf blademaster / deep elf master archer
MONS:    deep elf annihilator / deep elf sorcerer / deep elf death mage
MONS:    deep elf knight / w:1 deep elf high priest / \
         w:1 deep elf demonologist
# the orcish delegation:
MONS:    orc warlord / iron troll / w:1 fire giant / w:1 frost giant / \
         w:1 stone giant / w:1 ettin
MONS:    orc sorcerer / orc high priest / w:5 ogre mage
MONS:    orc knight / rock troll / w:1 orc sorcerer / w:1 orc high priest
# the arbiter(s):
MONS:    great orb of eyes / sphinx
# The orcs might have a unique with them. He or she will likely be outclassed
# by the others in attendance, but perhaps has some special testimony to
# present. None of the uniques really fits in with the deep elves...
KMONS:   D = Ijyb / Blork the Orc / Urug / Erolcha / Snorg
MAP
     xxxx
    xxGGxx
 xxxx3776xxxx
.GxT33..66TxG.
.xxx3x..x6xxx.
@+3+331466+6+@
@+3+332566+6+@
.xxx3x..x.xxx.
.GxT33..66TxG.
 xxxx3776xxxx
    xxGGxx
     xxxx
ENDMAP

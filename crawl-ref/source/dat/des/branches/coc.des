##############################################################################
# coc.des: Special maps for Cocytus.
# Content: (mini)vaults particular to Cocytus, and/or first appearing in
#          Coc (order: Dis, Geh, Coc, Tar)
##############################################################################

{{

function coc_setup(e)
   e.place("Coc:7")
   e.tags("no_rotate")
   e.lflags("no_tele_control")
   e.kmask('O = no_item_gen')
   e.kitem('O = icy rune of Zot')
   e.lrockcol("lightcyan")
   e.lfloorcol("white")
   e.lfloortile("floor_ice")
   e.colour("c = lightblue")
   e.set_feature_name("rock_wall", "ice-covered rock wall")
   e.set_feature_name("stone_wall", "ice-covered stone wall")
   e.set_feature_name("floor", "Frost-covered floor")
end

-- the Serpent should appear in exactly one hell end
-- XXX: are things like shafts going to break this?
function hell_branches_remaining()
   local hell_branches = { "Geh", "Coc", "Dis", "Tar" }
   local ret = #hell_branches
   for _, branch in pairs(hell_branches) do
      if travel.find_deepest_explored(branch) == 7 then
         ret = ret - 1
      end
   end
   return ret
end

function serpent_of_hell_setup(e)
   if not you.uniques("the Serpent of Hell") and
      crawl.one_chance_in(hell_branches_remaining()) then
      e.kmons('D = the Serpent of Hell')
   end
end

}}


##############################################################################
# Cocytus
##############################################################################

NAME:   grunt_cocytus_stairs_refrigerator
TAGS:   transparent mini_float
DEPTH:  Coc:1-6
COLOUR: c = blue
MONS:   Ice Fiend, blizzard demon, ice devil, blue devil, white imp
KMONS:  H = ice statue
MAP
xwww..4......
xwwwwww..5...
xwwwwwwww..5.
xwwwwwwwww...
xcccccHwwww4.
cc.H.ccwwww..
c}..2.cWWWW4.
c]1...+.3.3..
c)..2.cWWWW4.
cc.H.ccwwww..
xcccccHwwww4.
xwwwwwwwww...
xwwwwwwww..5.
xwwwwww..5...
xwww..4......
ENDMAP

NAME:    nicolae_coc_three_mouths
DEPTH:   Coc, !Coc:$
TAGS:    transparent extra mini_float decor
SUBST:   j = >}}, b = >]], c = >))
SHUFFLE: )}]
MAP
              .....
             ..x....
            ..xx.....
            .xx....x.
            .x..j..x.
         ....xx...xx.
        ...xxxxx.xx..
  ..........xxxxxx..
 ..xxx..x...xxxxx..
..xx.xxxxx...xxxxx.
......xxxx.........
....b..xxxx........
......xxxx.........
..xx.xxxxx...xxxxx.
 ..xxx..x...xxxxx..
  ..........xxxxxx...
        ...xxxxx.xx..
         ....xx...xx.
            .x..c..x.
            .xx....x.
            ..xx.....
             ..x....
              .....
ENDMAP

NAME:    nicolae_coc_ice_floe
DEPTH:   Coc:2-, !Coc:$
SHUFFLE: {[(, }]}
NSUBST:  D = 1:1 / 2:2 / *:34
KMONS:   1 = Ice Fiend
KMONS:   2 = blizzard demon
KMONS:   3 = ice devil / blue devil
KMONS:   4 = white imp
MAP
      wwwwwwwww
    wwwwwwwwwwwww
   wwwwwxxxxxwwwww
  wwww.xx...xx.wwww
 wwww.....D.....wwww
 www.D..x...x..D.www
www....xx.D.xx....www
wwwx..xx.....xx..xwww
wwxxDxx...x...xxDxxww
wwx.....4xxx......xww
wwx.{.3.xxxxx.1.}.xww
wwx.....4xxx......xww
wwxxDxx...x...xxDxxww
wwwx..xx.....xx..xwww
www....xx.D.xx....www
 www.D..x...x..D.www
 wwww.....D.....wwww
  wwww.xx...xx.wwww
   wwwwwxxxxxwwwww
    wwwwwwwwwwwww
      wwwwwwwww
ENDMAP

NAME:    nicolae_coc_snowflakes_chance_in_hell
DEPTH:   Coc, !Coc:$
ORIENT:  centre
TAGS:    transparent extra decor
SHUFFLE: abcdefghi, jklmno, ABCDEFGH
SUBST:   abc : x, d : ., e : .x, f : .x, g : .x, h : .x, i : .x
SUBST:   jk : x, l : ., m : .x, n : .x, o : .x, p : .x, q : .x, r : .x, s : .x
SUBST:   AB : x, C : ., D : .x, E : .x, F : .x, G : .x, H : .x
MAP
      .............
   .........n.........
  .........mom.........
 ....H....lkxkl....H....
 ....GE....jxj....EG....
 ..HGFDB.i..p..i.BDFGH..
....EDCA.hg.q.gh.ACDE....
.....BA..fedxdef..AB.....
..........cbxbc..........
.....ihf...axa...fhi.....
...l..gec...r...ceg..l...
..mkj..dba.sxs.abd..jkm..
.noxxpqxxxrxxxrxxxqpxxon.
..mkj..dba.sxs.abd..jkm..
...l..gec...r...ceg..l...
.....ihf...axa...fhi.....
..........cbxbc..........
.....BA..fedxdef..AB.....
....EDCA.hg.q.gh.ACDE....
 ..HGFDB.i..p..i.BDFGH..
 ....GE....jxj....EG....
 ....H....lkxkl....H....
  .........mom.........
   .........n.........
      .............
ENDMAP

NAME:    nooodl_cocytus_jagged_cave
ORIENT:  northwest
DEPTH:   Coc, !Coc:$
TAGS:    extra
SUBST:   < : AB
NSUBST:  A = 1:{ / 1:} / *:.
NSUBST:  B = 1:[] / *:.
SUBST:   . : ...W, ' : .W, _ : .W, " : .W, ; : .W
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx<x
xxxxxxxxxx<xxxxxxxxxxxxxxxxxxxxxxxxxxxx..x
xxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxx.xx
xxxxxxxxxx..xxxxxxx<xxxxx<xxxxxxxxxxxx..xx
xxxxxxxxxxx.xxxxxx.xxxxxx.xxxxxxxxxxxx.xxx
xxxxxxxxxxx..xxxx..xxxxx..xxxxxxxxxxx..xxx
xxxxxxxxxxx..xxxx.xxxxx...xxxxxxxxxxx..xxx
xxxxxx<xxxxx..xx..xxxxx...xxx.xxxxxx..xxxx
xx'xxx.xxxxx..x..xxxxx...xxxx.xxxxxx..xxxx
xx'xxx.xxxxx.....xxxxx...xxx...xxxx..xxxxx
xx''xx.xxxxx....xxx"xx"".xxx;;.xxxx..xxxxx
x''''...xxxx....xxx"xx""xxx;;;;xx....xxxxx
x''''''''''x.__xxxx"xx""xx;;;;;;x;..xxxxxx
x''''''''''x___xxx""xx"x""";;;;;;;;.xxxxxx
x''''x'''''____xxx"""""""""";;;;;;;xxxxxxx
xx'''x''''____x___"""""""""";;;;;;;.xxxxxx
xx'''xx''_________"""""""""xx;;;;;;.xxxxxx
xx''xxx'__________"xx""x""""xxx;;;...xxxxx
xxxxxxxx__________xxx"""x"""..xxx;....xxxx
xxxxxxxxxx___x___xxx""""xx"....xxxxx..xxxx
xxxxxxxxxxx..xx_xxxx.....xx......xxxxx<xxx
xxxxxxxxxxxx.xxxxxx......xxx......xxxxxxxx
xxxxxxxxxxxxx<xxxxx..xx...xxx...xxxxxxxxxx
xxxxxxxxxxxxxxxxxx..xxxx..xxxx<xxxxxxxxxxx
xxxxxxxxxxxxxxxxxx<xxxxx...xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx..xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx@@xxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Antaeus; bottom of Cocytus.
##############################################################################
# To do: this level has much shorter distances to travel, hence should
#        come up with harder resistance.

NAME:    coc_dpeg
ORIENT:  encompass
MONS:    patrolling Antaeus, Ice Fiend, ice dragon / nothing
: serpent_of_hell_setup(_G)
ITEM:    any, any misc, any misc, any misc
SHUFFLE: O1dD / e0%H / f9*H / g8|H
KMONS:   d = ice devil w:5 / Ice Fiend / nothing w:5
KITEM:   d = any
SUBST:   ' = w .
: coc_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwww.......wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwww....wwwww....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww''wwwwwwwwxxxx
xxxxwww...wwwwwwwwwww....wwwwwwwwwwwwwwwwwwwwwwwwwww'.......wwwwwwxxxx
xxxxwww.wwww......wwwwww..wwwwwwwwwwwwwwwwwwww'.......ccccc...wwwwxxxx
xxxxww..ww....cc......www.3wwwwwwwwwwwww..........ccccccccccc..wwwxxxx
xxxxw...ww..cccccc.....www.wwwwwwwwww'3..........cc....9.2..cc..wwxxxx
xxxxw..ww..ccccccc+c....ww.'wwwwwwwwwww''........ccf********cc..wwxxxx
xxxxw..ww..cc.%%%%.cc...ww..wwwwwwwwwwwwwwwww'....ccccccc+ccc...'wxxxx
xxxxw..ww..cc2.e0.2cc...ww..wwwwwwwwwwwwwwwwwwww'......c...c.....wxxxx
xxxxw..ww..cc.%%%%.cc..www.'wwwwwwwwwwwwwwwwwwwwwwwww'.cc+cc..H..wxxxx
xxxxw..www..c+cccccc...ww..wwwwwwwwwwwwwwwwwwwwwwwwwww..........wwxxxx
xxxxww..ww....ccccH...www.wwwwwwwwwwwwwwwwwwwwwwwwwwwww........wwwxxxx
xxxxww..www..........www..wwwwwwwwwwwwwwwwwwwwwwwwwwww'..2....w..wxxxx
xxxxwww..wwww......wwww..wwwwwwwwwwwwwwwwwwwwwwwwwww'............wxxxx
xxxxwwww..wwwwwwwwwwww..wwwwwwwwwwwwwwwwwwwwwwwwww..............wwxxxx
xxxxwwwww...'wwwwww....wwwwwwwwwwwwwwwwwwwwwwwwww............'wwwwxxxx
xxxxwwwwwwww........'wwwwwwwwwwwwwwwwwwwwwwww'3..'ww'....''wwwwwwwxxxx
xxxxwwwwwwwwwww'...3wwwwwwwwwwwwwwwwwwwwwwwwww''wwwwww''wwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwww........wwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwww............wwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwww...ccc+ccc...wwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwww...cc.....cc...wwwwwwwwwwwww'wwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwww...c.......c...wwwwwwwwwwwww3wwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwww...+.[.{.(.+...wwwwwwwwwwww...wwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwww...c.......c...wwwwwwwwwwww...wwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwww...cc.....cc...wwwwwwwwwww.....wwwwwwwwxxxx
xxxxwwwww'wwwwwwwwwwwwwwwwww...ccc+ccc...wwwwwwwwwwwww...wwwwwwwwwxxxx
xxxxwwwww3wwwwwwwwwwwwwwwwww............wwwwwwwwwwwwww...wwwwwwwwwxxxx
xxxxwwwww.wwwwwwwwww.3'wwwwwww.........wwwwwwwwwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww.wwwwwwww'.3.'wwwwwwwwwwwwwwwwwwww'wwwwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww.wwwwwwwww..'wwwwwwwwwwwwwwwwwwww'3wwwwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww..wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..wwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww...w.wwwww..wwwwwwwwwxxxx
xxxxwwww'...wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww....www...w.wwwwwwwxxxx
xxxxwwww....'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..ccccccc.c.wwwwwwwwxxxx
xxxxwwww.....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..cc$$$$$$.c..wwwwwwwxxxx
xxxxwww'.....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..c.$dddd$.c...wwwwwwxxxx
xxxxwww..cc..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwww'...$$$$$$cc....wwwwwxxxx
xxxxwww.cccc..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww'.ccccccc....wwwwwwxxxx
xxxxwww.cg.cc..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwww........ww..wwwwwwwxxxx
xxxxww'.c||.cc...'wwwwwwwwwwwwwwwwwwwwwwwwwwww.D...wwwwwwwwwwwwwwwxxxx
xxxxww'.c||.8cc......'wwwwwwwwwwwwwwwwwwwwwww'...w..wwwwwwwwwwwwwwxxxx
xxxxww..c||2.2cc..H.....'wwwwwwwwwwwwwwwwww....wwww..wwwwwwwwwwwwwxxxx
xxxxww..c||....cc..............3'wwwwwwwww...wwwwwww.....wwccccwwwxxxx
xxxxww..cccc+ccccc..........wwwwwwwwwww'3...wwwwwwwwwww.2.cc2dccwwxxxx
xxxxwww..ccc...........'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.+dd1Ocwwxxxx
xxxxwwww......'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcc2dccwwxxxx
xxxxwwwwww'''wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP


##############################################################################
# Original Antaeus level
##############################################################################

NAME:    coc_old
ORIENT:  encompass
SHUFFLE: $|, 2X
SUBST:   X=., 3=3.
MONS:    patrolling Antaeus, Ice Fiend, ice dragon, Ice Fiend
: serpent_of_hell_setup(_G)
KFEAT:   D = deep_water
NSUBST:  H = 1:D / *:w
: coc_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxx........................xxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx..........................xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx............................xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx..............................xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx................................xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx....cccccccccccc..cccccccccccc....xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx....ccccccccccccc2Xccccccccccccc....xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx....cc..........................cc....xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx....cc............................cc....xxxxxxxxxxxxxxx
xxxxxxxxxxxxxx....cc...wwwwwwwwwwwwwwwwwwwwwwww...cc....xxxxxxxxxxxxxx
xxxxxxxxxxxxx....cc...wwwwwwwwwwwwwwwwwwwwwwwwww...cc....xxxxxxxxxxxxx
xxxxxxxxxxxx....cc...wwwwwwwwwwwwwwwwwwwwwwwwwwww...cc....xxxxxxxxxxxx
xxxxxxxxxxx....cc...ww.......3....33....3.......ww...cc....xxxxxxxxxxx
xxxxxxxxxx....cc...ww............................ww...cc....xxxxxxxxxx
xxxxxxxxx....cc...ww....cccccccccccccccccccccc....ww...cc....xxxxxxxxx
xxxxxxxx....cc...ww....cccccccccccccccccccccccc....ww...cc....xxxxxxxx
xxxxxxx....cc...ww....cc......................cc....ww...cc....xxxxxxx
xxxxxx....cc...ww....cc...T................T...cc....ww...cc....xxxxxx
xxxxx....cc...ww....cc..........wwwwww..........cc....ww...cc....xxxxx
xxxx....cc...ww....cc.......wwwwwwwwwwwwww.......cc....ww...cc....xxxx
xxxx....cc...ww...cc.....wwwwwwwwwwwwwwwwwwww.....cc...ww...cc....xxxx
xxxx....cc..www..cc....wwwwwwwwwccccccwwwwwwwww....cc..www..cc....xxxx
xxxx....cc..www.cc....wwwwwwwwccc2O12cccwwwwwwww....cc.www..cc....xxxx
xxxx....cc..www.cc...wwwwwwwwcc4+....+4ccwwwwwwww...cc.www..cc....xxxx
xxxx....cc..www.cc...wwwwwwwwcc+cc++cc+ccwwwwwwww...cc.www..cc....xxxx
xxxx....cc..www3.c..wwwwwwwwwc|||c..c$$$cwwwwwwwww..c.3www..cc....xxxx
xxxx....cc..wwww.c.wwwwwwwwwwc|||cXXc$$$cwwwwwwwwww.c.wwww..cc....xxxx
xxxx....cc..wwww3c.wwwwwwwwwwcc||c..c$$ccwwwwwwwwww.c3wwww..cc....xxxx
xxxx....cc..wwww.c.wwwwwwwwwwwcccc++ccccwwwwwwwwwww.c.wwww..cc....xxxx
xxxx....cc..www3.c..wwwwwwwwwwwwww33wwwwwwwwwwwwww..c.3www..cc....xxxx
xxxx....cc..www.cc...wwwwwwwwwwwwwwwwwwwwwwwwwwww...cc.www..cc....xxxx
xxxx....cc..www.cc....wwwwwwwwwwwwwwwwwwwwwwwwwww...cc.www..cc....xxxx
xxxx....cc..www.cc....wwwwwwwwwwwwwwwwwwwwwwwwww....cc.www..cc....xxxx
xxxx....cc..www..cc....wwwwwwwwwwwwwwwwwwwwwwww....cc..www..cc....xxxx
xxxx....cc...ww...cc.....wwwwwwwwwwwwwwwwwwww.....cc...ww...cc....xxxx
xxxx....cc...ww....cc.......wwwwwwwwwwwwww.......cc....ww...cc....xxxx
xxxxx....cc...ww....cc..........wwwwww..........cc....ww...cc....xxxxx
xxxxxx....cc...ww....cc...T................T...cc....ww...cc....xxxxxx
xxxxxxx....cc...ww....cc......................cc....ww...cc....xxxxxxx
xxxxxxxx....cc...ww....ccccccccccc..ccccccccccc....ww...cc....xxxxxxxx
xxxxxxxxx....cc...ww....cccccccccc2Xcccccccccc....ww...cc....xxxxxxxxx
xxxxxxxxxx....cc...ww............................ww...cc....xxxxxxxxxx
xxxxxxxxxxx....cc...ww..........................ww...cc....xxxxxxxxxxx
xxxxxxxxxxxx....cc...wwwwwwwwwwwww..wwwwwwwwwwwww...cc....xxxxxxxxxxxx
xxxxxxxxxxxxx....cc...wwwwwwwwwHww..wwHwwwwwwwww...cc....xxxxxxxxxxxxx
xxxxxxxxxxxxxx....cc...wwwwwwwwwww..wwwwwwwwwww...cc....xxxxxxxxxxxxxx
xxxxxxxxxxxxxxx....cc............................cc....xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx....cc..........................cc....xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx....cccccccccccccccccccccccccccc....xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx....cccccccccccccccccccccccccc....xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx................................xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx..............................xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx............................xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx..........{.(.[...........xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP


##############################################################################
# Antaeus (Cocytus) by Mu
##############################################################################

NAME:    coc_mu
ORIENT:  encompass
MONS:    patrolling Antaeus, ice fiend
MONS:    ice devil, blue devil, white imp, ice dragon, freezing wraith
KMONS:   8 = ice statue
KMONS:   9 = frost giant
SUBST:   3 = 3333344445555666779
KMONS:   E = water elemental
KFEAT:   E = w
: serpent_of_hell_setup(_G)
SUBST:   ^ : ^^^W. , ^ = W.
SUBST:   X : XXXc. , X = c.
SUBST:   " = 8:1 .:10
SUBST:   ' = .cc
SUBST:   ; : ;;;;w , ; = wc
KFEAT:   # = permarock_wall
COLOUR:  # = lightcyan
NSUBST:  - = 1:+ / *:c
: coc_setup(_G)
MAP
################################################################################
################################################################################
################################################################################
################################################################################
################################################################################
################################################################################
######ccccc............wwwwwwwwwwwwwwwwxxxxxxxxxwwwwwww......3......cccccc######
######cccc..............wwwwwwwwwwwwwwwwxxxxxxxwwwwwwww.3.3......c...ccccc######
######ccc...wwwwwwwwww..wwwwwwwwwwwEwwwwwwxxxwwwwwwww....3..D.cccc....'.cc######
######ccc..wwwwww8www.2.wwwwwwwwwEw.wEwwwwwwwwwwwwww...3..3...cccccc..c..c######
######ccc..ww|$|www.......wwwwwwww...wwwwwwwwwwwwwwwww......ccccccc...cc.c######
######ccc..ww$O$....1..............8..Ewwwwwwwwwwwwwwww...ccccc"c"c.cccc.'######
######ccc..ww|$|www.......wwwwwwww...wwwwwwwwwwwwwwwwww.cccccc....c.cccc.'######
######ccc..wwwwww8www.2.wwwwwwwwwEw.wEwwwwwwwwwwwwwwwwwccccc...cc...."cc.c######
######ccc...wwwwwwwwww..wwwwwwwwwwwEwwwwwwwwwwwwwwwwwccccccc...ccccccccc.c######
######cccc..............wwwwwwwwwwwwwwwwwwwwwwwwwwwwcccccc......cccccc"...######
######ccccc............wwwwwwwwwwwwwwwwwwwwwwwwwwwwccccc..........ccccc"c.######
######cccccccccccccccccwwwwwwwwwwwwwwwwwwwwwwwwwwccccc.....333......ccccc.######
######ccccccccccccccccccwwwwwwwwwwwwwwwwwwwwwwwwcccc........333.......cc..######
######cccccccccccccccccc;wwwwwwwwwwwwwwwwwwwwwwcccc..................ccc.c######
######cccccc"c$.....ccc;cccwwwwwwwwwwwwwwwwwwccccc....................cc.c######
######ccccc$$$$........cccccccwwwwwwwwwwwwwcccccc....cccccc..........ccc..######
######ccc"$$$$...........ccccccccwwwwwwwcccccccc....cccccccc.......cccc...######
######cccc$$$$.............ccccccccccccccccccc.....cccccwwwcc....cccccc...######
######ccccc$$................ccccccccccccccc.....cccccccwwwwwwwcccccccc...######
######cccccc$...........wwwwwwwccccccccccc......ccccc.cwwcwwwwccccccccc..c######
######ccccccc.........wwwwwwwwwwww.............cccc333333cccwccccX.-....cc######
######ccccccccc.....wwwwwwwwwwwwwwww........ccccccc.2....cccc-XXX..cc...cc######
######ccccccccccccwwwwwwwwwwwwwwwwwwww.......cccccc333333ccccX....ccccX..c######
######ccccccccccwwwwww..wwwwwwwwwwwwwww........ccccc$$$$$cccX....cccccX...######
######cccccccccwwwwww..wwwwwwwwwwwwwwwww........cccc|||ccccc|.....ccc.....######
######ccccccccwwwwww..www...wwwwwwwwwwwwwww......ccccccccccc|......-......######
######ccccc..cwwww....ww....www..wwwwwwwwwwww.....ccccccccccX.2..Xcc......######
######ccccc..www..ccccww...www..wwwwwwwwwwwwwwww..cccccccccccXXXXcccc.....######
######cccc...ww..cc...ww...ww...ww..wwwwwwwwwwwwwwwcccccccccccccccccww...c######
######cccc..www.cc....www...www.ww.wwwwwwwwwwwwwwwwwwccccccccccccwwwwwwwcc######
######ccc...ww..c|..3..www..wwwwwwww^^^wwwwwwwwwwwwwwwccccccwwwccwwwwwwccc######
######ccc...ww..c|233..cwwwwwwwwwwwcc+ccccccccccccwwwwwccccwwwwwwwwwwwwccc######
######ccc..www..c|.33..c.wwwwwwwwwcc...2.$$$$$|||ccwwwwwwwwwwwwwwwwwwwcccc######
######ccc..www..cc..3.cc....wwwww.cc..33.$$$$$|||cc..wwwwwwwwwwwwwwwwccccc######
######ccc..wwww..cc$$cc....wwwwww.cc2333.$$$$$|||cc...wwwwwww^^wwwwwwccccc######
######ccc..wwww...cccc....wwwwww...ccccccccccccccc.....wwwww^..^wwwwwwcccc######
######ccc..wwwww........wwwwww......................c.wwwww^.6..^wwwwwcccc######
######ccc..wwwwwww....wwwwwww......................cccwwwww^....^wwwwwwccc######
######ccc..wwwwwwwwwwwwwwww........................cccwwwwww^..^wwwwwwwccc######
######ccc...wwwwwwwwwwwww.........333.3...........ccc.wwwwwww^^wwwwwwwwccc######
######cccc..wwwwwwwwwwww.............3............ccc.wwwwwwwwwwwwwwwww.cc######
######cccc...wwwwwwwww.........ccc....333........cccc..wwwwwwwwwwwwwwww.cc######
######cwccc....wwwww........cccccccc.....3......ccccc..wwwwwwwwwwwwwww..cc######
######cwwc;ccc...........ccccccccccccc....333..ccccccc..wwww^wwwwwwww..ccc######
######ccwwwcccccc.....cccccccccccccccccccc...ccccccccc..www^.wwwwww....ccc######
######ccwwwwwcccccccccccccccccccccccccccccc;ccccccccccc..wwwwwwwww...3cccc######
######cc.wwwwwcccccccccccccccccccccccccccccc;cccccccccccwwwwwwwwww..3ccccc######
######c...wwwwwww..ccccccccccccc...........wwXXXXXccccccXwwwwwwwww...3cccc######
######c....wwwww.....cccccccc...3.....wwwwwwwwwwwwXXccccXwwwwwwwwww....ccc######
######..{..wwww...333...............wwwwwwwwwwwwwwwwXcccXwwwwwwwwwwww..ccc######
######......www....333...........wwwwwwwwwwwwwwwwwwwwXccXwwwwwwwwwwwww..cc######
######.[....wwww.............wwwwwwwwwwwwwwwwwwwwwwwwwXXwwwwwwwwwwwwwww.cc######
######.......wwwww.......wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.^www.cc######
######..(....wwwwwwwwwwwwwwwwwwwwwwwwwwwwww....wwwwwwwwwwwwwwwwww^.wwwwccc######
######c.......wwwwwwwwwwwwwwwwwwwwwwwwww..........wwwwwwwwwwwwwwww^wwwwccc######
######cc.......wwwwwwwwwwwwwwwwwwwww........333......wwwwwwwwwww^wwwwwwccc######
######cc.........wwwwwwwwwwwwwwwww.........33.33.......wwwwwww^...wwwwcccc######
######ccccc.........wwwwwwwwwww............................wwww^^^wwwwcccc######
################################################################################
################################################################################
################################################################################
################################################################################
################################################################################
################################################################################
ENDMAP

##############################################################################
# Antaeus (Cocytus) by Grunt
##############################################################################

NAME:    coc_grunt
ORIENT:  encompass
MONS:    patrolling Antaeus, Ice Fiend, ice dragon / nothing
MONS:    ice devil w:5 / blue devil w:4 / white imp w:4 / ice dragon w:3 / \
         freezing wraith w:2 / frost giant w:1
: set_border_fill_type('open_sea')
KFEAT:   X = open_sea
NSUBST:  H = 1:D / 1:8 / *:.
KFEAT:   D = floor
: serpent_of_hell_setup(_G)
KMONS:   8 = ice statue
KITEM:   d = superb_item
KITEM:   e = gold
KMONS:   de = Ice Fiend
SHUFFLE: |d / $e
: set_border_fill_type("open_sea")
: coc_setup(_G)
MAP
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXcccccccccccc
Xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcccccccccc
Xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccccccc
Xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcccccccc
Xww...wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccccc
Xw..{..........wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccccc
Xw....(...............wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcccccc
Xw..[............4......wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccc
Xww......................wwwwwwwwwww.......wwwwwwwwwwwwwwwwwwwwwwwcccc
Xwwwwwwww......4...4......wwwww.........4.....wwwwwwwwwwwwwwwwwwwwcccc
Xwwwwwwwwwww.............wwww.........3........wwwww......wwwwwwwwwccc
cwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.....4.........wwww.....4....wwwwwwwwcc
cccwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.........wwwwwwwwwwwwww.....wwwwwwwwc
cccccwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww....wwwwwwwc
ccccccccwwwwwwwwcccccccccccccccwwwwwwwwwwwwwwwwwwwwwwwwwwww.2..wwwwwwc
cccccccccccccccccccccccccccccccccccccwwwwwwwwwwcccccccwwwwww...wwwwwwX
ccccccccccccccccccccccccccccccccccccccccccccccccccccccwwwwww.H.wwwwwwX
cccccccccccccccccccccccccccccccccccccccccccccccccccccwwwwww....wwwwwwX
cccccccccccccccccccccccccccccccccwwwwwwwwwwwwwwwwwwwwwwwww.....wwwwwwX
cccccccccccccccccwwwwwwcccccwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.4..wwwwwwwX
ccccccccccccccwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.....wwwwwwwwX
cccccwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.......wwwwww.....wwwwwwwwwX
cwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww......4.........wwwwwwwwwwwwwwwwcc
Xwwwwwwwwwwwwwwwwwwwwww.wwwwwww.........3.........wwwwwwwwwwwwwwwwcccc
Xwwwwwwwwwwwwwwwwww...4...www.........4.........wwwwwwwwwwwwwwwwcccccc
Xwwwwwww.....wwww...3....wwww..............wwwwwwwwwwwwwwwwwwwcccccccc
Xwwwwww.......www.4....wwwwwww......wwwwwwwwwwwwwwwwwwwwwwwwwccccccccc
Xwwww...4....wwwww...wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcccccccccccccc
Xwwww.....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccccccccccccc
Xwww....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccccccccccccccccccccccccccccc
Xwww...wwwwwwwwwwwwwwccccccccccccccccccccccccccccccccccccccccccccccccc
Xwww.H.wwwwwcccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
Xwww...wwwwwwwwwwwwwcccccccccccccccccccccccccccccccccccccccccccccccccc
Xwww..2.wwwwwwwwwwwwwwwwwwcccccccccccccccccccccccccccccccccccccccccccc
Xwwww...wwwwwwwwwwwwwwwwwwwwcccccccccccccccccwwwwwwwwwwwwwwwcccccccccc
Xwwww.4..wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccc
cwwwwww.....wwwww........wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwc
ccwwwwwww...ww......4......wwwwwwwwwwww..........wwwwwwwwwwwwwwwwwwwwX
ccccwwwwwwwwwwww......3.....wwwwwwwww......3.4......wwwwwwwwwwwwwwwwwX
ccccccccccwwwwwwww......4........ww......4...........wwwwwwwwwwwwwwwwX
ccccccccccccccccwwwwww..........wwwww........wwwwww..www...wwwwwwwwwwX
ccccccccccccccccccccwwwwww..wwwwwwwwww....wwwwwwwwwwwwwww...wwwwwwwwwX
cccwwwwwwwwwwwwwcccccccwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww....wwwwwwwX
ccwwwwwww......wwwcccccccccccwwwwwwwwwwwwwwwwwwwwwwwwwwwww..4..wwwwwwX
cwwwwwww........wwwccccccccccccccccccwwwwwwwwwwwwwwwwwwwwww....wwwwwwX
Xwwwwww.....3.....wwwccccccccccccccccccccccccccccccwwwwwwww..2..wwwwwX
Xwwwww..........4..wwwwcccccccccccccccccccccccccccccccccwww.....wwwwwX
Xwwwww....ccccc....wwwwwccccccccccccccccccccccccccccccccwww..H..wwwwwX
Xwwww....cc|d|cc...wwwwwwwwwwwcccccccwwwwwwwwwwwwwwwwwwwwww.....wwwwwX
Xwww.....c|||d|c....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.4..wwwwwwX
Xwww...4.+..1.Oc.3..wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww...wwwwwwwX
Xwww.....c$$$e$c....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww...wwwwwwwwc
Xwwww....cc$e$cc....ww.4..wwwwwwwwwwwwww..........wwwwwww...wwwwwwwwcc
Xwwww.....ccccc.....ww.....wwwwwwww......4........wwww.....wwwwwwwwccc
Xwwwww..........4..www..3.4..www.......3....ww...wwwwww..wwwwwwwwwcccc
Xwwwwww.....3.....wwwww......www.....4....wwwwwwwwwwwwwwwwwwwwwwwwcccc
Xwwwwwww........wwwwwwwwwwwwwwwwwww....wwwwwwwwwwwwwwwwwwwwwwwwwwccccc
Xwwwwwwww.....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccc
Xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccccc
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXcccccccc
ENDMAP

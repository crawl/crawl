##############################################################################
# coc.des: Special maps for Cocytus.
# Content: (mini)vaults particular to Cocytus, and/or first appearing in
#          Coc (order: Dis, Geh, Coc, Tar)
##############################################################################

: crawl_require('dlua/vault.lua')

{{
function coc_setup(e)
   e.place("Coc:$")
   e.tags("no_rotate")
   e.kitem("$ = $ no_pickup")
   e.kmask('O = no_item_gen')
   e.kitem('O = icy rune of Zot')
   e.lrockcol("lightcyan")
   e.lfloorcol("white")
   e.lfloortile("floor_ice")
   e.colour("c = lightblue")
   e.set_feature_name("rock_wall", "ice-covered rock wall")
   e.set_feature_name("stone_wall", "ice-covered stone wall")
   e.set_feature_name("floor", "Frost-covered floor")
end
}}

##############################################################################
# Cocytus
##############################################################################

NAME:   grunt_cocytus_stairs_refrigerator
TAGS:   transparent
DEPTH:  Coc:1-6
COLOUR: c = blue
MONS:   Ice Fiend, shard shrike, simulacrum, wendigo / nargun
KMONS:  P = pillar of rime
KMONS:  HI = ice statue
KFEAT:  PI = deep_water
MAP
xwww..3......
xwwwwww..4...
xPwwwwwww..4.
xwPwwwwwww...
xcccccIwwww3.
cc.H.ccwwww..
c...2.cWWWW3.
c}1...+.3.3..
c...2.cWWWW3.
cc.H.ccwwww..
xcccccIwwww3.
xwPwwwwwww...
xPwwwwwww..4.
xwwwwww..4...
xwww..3......
ENDMAP

NAME:    nicolae_coc_three_mouths
DEPTH:   Coc, !Coc:$
TAGS:    transparent
ORIENT:  centre
MONS:    pillar of rime
SHUFFLE: jbc
SUBST:   j = >}}, b = >1., c = >1.
MAP
              .....
             ..x....
            ..xx.....
            .xx....x.
            .x..j..x.
         ....xx...xx.
        ...xxxxx.xx..
  ..........xxxxxx..
 ..xxx..x...xxxxx..
..xx.xxxxx...xxxxx.
......xxxx.........
....b..xxxx........
......xxxx.........
..xx.xxxxx...xxxxx.
 ..xxx..x...xxxxx..
  ..........xxxxxx...
        ...xxxxx.xx..
         ....xx...xx.
            .x..c..x.
            .xx....x.
            ..xx.....
             ..x....
              .....
ENDMAP

NAME:    nicolae_coc_ice_floe
DEPTH:   Coc:2-, !Coc:$
ORIENT:  centre
TAGS:    transparent
NSUBST:  D = 3:110 / *:0
MONS:    ice fiend / shard shrike
KMONS:   P = pillar of rime
MAP
      wwwwwwwww
    wwwwwwwwwwwww
   WwwwwxxxxxwwwwW
  WWWw.xx.P.xx.wWWW
 wwWW.....D.....WWww
 www.D..x...x..D.www
www....xx.D.xx....www
wwwx..xx.....xx..xwww
wwxxDxx...x...xxDxxww
wwx.....0xxx......xww
wwx.{...xxxxx.1.}.xww
wwx.....0xxx......xww
wwxxDxx...x...xxDxxww
wwwx..xx.....xx..xwww
www....xx.D.xx....www
 www.D..x...x..D.www
 wwWW.....D.....WWww
  WWWw.xx.P.xx.wWWW
  @WwwwwxxxxxwwwwW
    wwwwwwwwwwwww
      wwwwwwwww
ENDMAP

NAME:    nicolae_coc_snowflakes_chance_in_hell
TAGS:    transparent extra decor
DEPTH:   Coc, !Coc:$
ORIENT:  centre
MONS:    pillar of rime
SHUFFLE: abcdefghi, jklmno, ABCDEFGH
SHUFFLE: {[(< / }])>
# you thought we'd be generous with hatches
SUBST:   [(<>]) = 11.
SUBST:   abc : x, d : ., e : .x, f : .x, g : .x, h : .x, i : .x
SUBST:   jk : x, l : ., m : .x, n : .x, o : .x, p : .x, q : .x, r : .x, s : .x
SUBST:   AB : x, C : ., D : .x, E : .x, F : .x, G : .x, H : .x
MAP
      ......]......
   .........n.........
  .........mom.........
 ....H....lkxkl....H....
 ...>GE....jxj....EG)...
 ..HGFDB.i..p..i.BDFGH..
....EDCA.hg.q.gh.ACDE....
.....BA..fedxdef..AB.....
..........cbxbc..........
.....ihf...axa...fhi.....
...l..gec...r...ceg..l...
..mkj..dba.sxs.abd..jkm..
{noxxpqxxxrxxxrxxxqpxxon}
..mkj..dba.sxs.abd..jkm..
...l..gec...r...ceg..l...
.....ihf...axa...fhi.....
..........cbxbc..........
.....BA..fedxdef..AB.....
....EDCA.hg.q.gh.ACDE....
 ..HGFDB.i..p..i.BDFGH..
 ...(GE....jxj....EG<...
 ....H....lkxkl....H....
  .........mom.........
   .........n.........
      ......[......
ENDMAP

NAME:    nooodl_cocytus_jagged_cave
TAGS:    ruin
ORIENT:  encompass
DEPTH:   Coc, !Coc:$
MONS:    pillar of rime
NSUBST:  < = 1:{ / 1:} / *:1
SUBST:   . : ...W, ' : .W, _ : .W, " : .W, ; : .W
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx<x
xxxxxxxxxx<xxxxxxxxxxxxxxxxxxxxxxxxxxxx..x
xxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxx.xx
xxxxxxxxxx..xxxxxxx<xxxxx<xxxxxxxxxxxx..xx
xxxxxxxxxxx.xxxxxx.xxxxxx.xxxxxxxxxxxx.xxx
xxxxxxxxxxx..xxxx..xxxxx..xxxxxxxxxxx..xxx
xxxxxxxxxxx..xxxx.xxxxx...xxxxxxxxxxx..xxx
xxxxxx<xxxxx..xx..xxxxx...xxx.xxxxxx..xxxx
xx'xxx.xxxxx..x..xxxxx...xxxx.xxxxxx..xxxx
xx'xxx.xxxxx.....xxxxx...xxx...xxxx..xxxxx
xx''xx.xxxxx....xxx"xx"".xxx;;.xxxx..xxxxx
x''''...xxxx....xxx"xx""xxx;;;;xx....xxxxx
x''''''''''x.__xxxx"xx""xx;;;;;;x;..xxxxxx
x''''''''''x___xxx""xx"x""";;;;;;;;.xxxxxx
x''''x'''''____xxx"""""""""";;;;;;;xxxxxxx
xx'''x''''____x___"""""""""";;;;;;;.xxxxxx
xx'''xx''_________"""""""""xx;;;;;;.xxxxxx
xx''xxx'__________"xx""x""""xxx;;;...xxxxx
xxxxxxxx__________xxx"""x"""..xxx;....xxxx
xxxxxxxxxx___x___xxx""""xx"....xxxxx..xxxx
xxxxxxxxxxx..xx_xxxx.....xx......xxxxx<xxx
xxxxxxxxxxxx.xxxxxx......xxx......xxxxxxxx
xxxxxxxxxxxxx<xxxxx..xx...xxx...xxxxxxxxxx
xxxxxxxxxxxxxxxxxx..xxxx..xxxx<xxxxxxxxxxx
xxxxxxxxxxxxxxxxxx<xxxxx...xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx.<xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   nicolae_coc_bergin
TAGS:   transparent no_pool_fixup
DEPTH:  Coc:2-
ORIENT: centre
KMONS:  0 = 0 w:12 / 9 w:3 / 8 w:1
KFEAT:  0 = W
NSUBST: { = 1={ / .
SUBST:  M = Wwww x:5 .:1, w = w:20 x:1, W = W 0:1
MAP
   wwM..@..Mw  wwM.@M
  wwwwM...MwwwwwwM..ww
 wwwwwwMWWwwwwwwwMWMww
wwwwwwwwMWwwwwwwwMWwww
wwwwwwwwMWwwwwwwMWMww
MMwMMwwwwWWMwwwMWMww
@.WWWWMMwwWwwwMWMwww
MMwwwMWWWWWwwwWMwwwww
wwwwMWWwwwWWwWWMwwwMww
 wwwMWMwwM..WWwwMMMW..
 wwwWMwwM.{{.wwwMWWW.@
 wwWWWwwM.{{.WWWWMwMMM
wwMWMwMwMM..WWMMMwwwww
MMWWwwwMwWWMMWWwwwwww
..WMwwwwWWMwwwWMwwww
@.MwwwwwMWwwwwMWMwww
MMwwwwwMWWwwwwwMWWww
wwwwwwwMWMwwwwwwMWMww
wwwwwMMWMwwwwwwwwWWMMM
 wwwwMWWMwwwwwwwwMWW.M
  wwwM...MwwwwwwwwWM..
   wM..@.Mww     wM..@
ENDMAP

# Inspired by the bit from the Inferno where the sinners in the Caina region
# of Cocytus are trapped in ice with only their heads sticking out. Granted,
# these heads are a little more mobile than those ones.
NAME:   nicolae_coc_caina
TAGS:   transparent
DEPTH:  Coc, !Coc:$
ORIENT: centre
MONS:   curse skull, laughing skull, weeping skull
NSUBST: x = 2:1 / 1 = 12 / 5 = 2233. / 2:3 / 4:x / 4:. / * = x:4 .:3
TILE:   x = wall_undead
MAP
     ..x.x
   .x.......
  ....x...x..x
 ..x....x.....
 x...x......x.
.......x.x....
x.x..x.........
........}..x.x.
.x...x.........
..........x..x
..x....x......
x...x.....x..
  x....x....x
  ..x.....x.
    ..x..
ENDMAP

NAME:   nicolae_coc_caroushell
TAGS:   transparent no_pool_fixup no_monster_gen
DEPTH:  Coc
ORIENT: centre
WEIGHT: 2
KMONS:  1 = diamond obelisk
SUBST:  ~ = w., M = w_, ` = ._
CLEAR:  _
MAP
          @
   MMM   `.`   MMM
  MwwwM `...` MwwwM
 Mwwwww~.....~wwwwwM
 Mwwwwww~...~wwwwwwM
 Mwwwwwww~.~wwwwwwwM
  MwwwwwwwWwwwwwwwM
   ~wwwwwwWwwwwww~
  `.~wwwwwWwwwww~.`
 `...~wwwwWwwww~...`
@.....WWWW1WWWW.....@
 `...~wwwwWwwww~...`
  `.~wwwwwWwwwww~.`
   ~wwwwwwWwwwwww~
  MwwwwwwwWwwwwwwwM
 Mwwwwwww~.~wwwwwwwM
 Mwwwwww~...~wwwwwwM
 Mwwwww~.....~wwwwwM
  MwwwM `...` MwwwM
   MMM   `.`   MMM
          @
ENDMAP

NAME:   nicolae_coc_city_of_iceburg
TAGS:   no_pool_fixup patrolling
DEPTH:  Coc:2-, !Coc:$
ORIENT: encompass
KMONS:  1 = ice statue
KMONS:  P = pillar of rime
KFEAT:  ~ = open_sea
: set_border_fill_type('open_sea')
NSUBST: D = + / nccc / c, E = + / nccc / c, F = + / nccc / c, H = + / nccc /c, \
        J = + / nccc / c, K = + / nccc / c, L = + / nccc / c, M = + / nccc /c, \
        N = + / nccc / c, O = + / nccc / c, a = 2=n / c, p = 2=n / c, \
        q = 2=n / c, r = 2=n / c, s = 3=n / c, y = 2=n / c, } = } / 0
SUBST:  Q = 009.., 0 = 009, 9 = 998
MAP
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
~wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww~
~wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww~
~wwwwwwwwwwwwwwwcppppppcwwwwwwwwwwwww~
~wwwwwwwwwwcccccc......cwwwwwwwwwwwww~
~wwwwww....M....a.}.Q..+......Qwwwwww~
~wwwwww.cLLc..}.a......cqqqqqc.wwwwww~
~wwwww..c..L....caaaaaac.....r.wwwwww~
~wwww...L.}cMMMMc......c...}.r.wwwwww~
~wwww...L..L....c..Q.}.c.....r.wwwwww~
~wwwx...c..L.Q..c......c.Q...r.wwwwww~
~wwwx...cLLc....cKKKKKKc.....r..Qwwww~
~wwxx..Q....!..........c++cccccc.wwww~
~wwxxw....................J....s.wwww~
~wxxww...cNNNc...VWWWV....J..}.s.wwww~
~wxxww...N...N...W.1.W..Q.J....s.wwww~
~wxxww...N.}.N...W1G1W....J....s.wwww~
~wxxww...N...N...W.1.W...ccccccc.wwww~
~wxxxww..cNNNc...VWWWV...H....s..wwww~
~wxxxxx..................H..}.sQwwwww~
~wwxxxw.Q..cDDDDDc.......H....s..wwww~
~wwxxwwccncc.....D...Q...cHHcFcc+cwww~
~wwxxwwc...c.Q.}.D..........F....swww~
~wwxxwwc...D.....D..........F..}.swww~
~wwxxwwn.}.D.....D...ccEcc..cFFsscwww~
~wwxxxwc...cDDDDDc...c...c......wwwww~
~wxxxxxc+ccc.........E...E.Q.xx.wwwww~
~wx...xx............xc...cxxxxx.wwwww~
~wxP{Pxxxx....G....Gxc...cccccxxwwwww~
~wx....xxxwwwwxx..xxxc.}.c.}.cxwwwwww~
~wxx.....xxxxxx...xxwc...c...ywwwwwww~
~wwxx............xxxwc+ccc...ywwwwwww~
~wwxxxx.........xxxwww...+...ywwwwwww~
~wwwxxxxxx.P.xxxxxxwwwwwwcyyycwwwwwww~
~wwwwxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwww~
~wwwwwwwwxxxxxwwwwwwwwwwwwwwwwwwwwwww~
~wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww~
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
ENDMAP

NAME:   nicolae_coc_cocytus_crossing
TAGS:   transparent no_pool_fixup
DEPTH:  Coc
ORIENT: centre
NSUBST: D = 2=W / w, E = 2=W / w, F = 2=W / w, H = 2=W / w, J = 2=W / w, \
        K = 2=W / w, L = 2=W / w, M = 2=W / w, N = 2=W / w, O = 2=W / w, \
        P = 2=W / w, Q = 2=W / w
SUBST:  p : x., q : x., r : wx, s : wx, y : x.
MAP
    wwww@@wwww
  wwwww....wwwww
 wwwwN..xx..Owwww
 www.NN....OO.www
www.y.NFFFFO.y.www
wwNN.sNHHHHOs.OOww
ww.NNNr....rOOO.ww
w...DE.q..q.KJ...w
@.x.DE..pp..KJ.x.@
@.x.DE..pp..KJ.x.@
w...DE.q..q.KJ...w
ww.QQQr....rPPP.ww
wwQQ.sQMMMMPs.PPww
www.y.QLLLLP.y.www
 www.QQ....PP.www
 wwwwQ..xx..Pwwww
  wwwww.@@.wwwww
    wwww@@wwww
ENDMAP

NAME:  nicolae_coc_downstairs_cove
TAGS:  transparent no_pool_fixup
DEPTH: Coc, !Coc:$
KFEAT: @ = W
SUBST: - = xx., ~ = ww., ! = wx., 0 = 0009, 9 = 998
MAP
 xxxxx@Www
xx-....Wwww
x-.0.~wwwww
x..!!xwwwww
x0xxxx~.~ww
x..-xx.}.ww
xx.-xx-..ww
x-..-xx8~ww
x-9...x.~ww
xx--x...~ww
 xxxxxxxxw
ENDMAP

NAME:    nicolae_coc_hellspring
TAGS:    transparent no_pool_fixup
DEPTH:   Coc, !Coc:$
ORIENT:  centre
KMONS:   1 = elemental wellspring w:70 / nothing
KFEAT:   1 = w
SHUFFLE: DEFH, Pq/Qq/Rr/Ss, JKLMNOZa
SUBST:   D = w, EFH = ., PQR = w, pqr = Ww, S = ., s = W., JKL = ., M : w., \
         N : w., Za = w
MAP
  wwwJJwKKwww
 wwwwJJwKKwwww
ww1D.......E1ww
wwDDD.....EEEww
ww.DDwPPPwEE.ww
aa..w1ppp1w..LL
aa..SsWWWqQ..LL
ww..SsW}WqQ..ww
ZZ..SsWWWqQ..MM
ZZ..w1rrr1w..MM
ww.HHwRRRwFF.ww
wwHHH.....FFFww
ww1H.......F1ww
 wwwwOOwNNwwww
  wwwOOwNNwww
ENDMAP

NAME:     nicolae_coc_icebean_singular
TAGS:     transparent no_pool_fixup allow_dup
DEPTH:    Coc
SUBVAULT: D : nico_icebean
MAP
  ......
 ..DDDD..
..DDDDDD.
.DDDDDDD.
.DDDDDDD.
.DDDDDD..
.DDDDD...
..DDD..
 .....
ENDMAP

NAME:     nicolae_coc_icebeans_plural
TAGS:     transparent no_pool_fixup
DEPTH:    Coc
ORIENT:   centre
KMONS:    1 = pillar of rime
KFEAT:    1 = w
SUBVAULT: D : nico_icebean
SUBVAULT: E : nico_icebean
SUBVAULT: F : nico_icebean
SUBVAULT: H : nico_icebean
SHUFFLE:  JK/1w/1w/xW/xW/ww/wW/wW/WW/-W/-W
SUBST:    JK = x, - = .
MAP
  .............
 ..DDDD...EEEE..
..DDDDDD.EEEEEE..
.DDDDDDD.EEEEEEE.
.DDDDDDD.EEEEEEE.
.DDDDDD...EEEEEE.
.DDDDD.....EEEEE.
..DDD..KKK..EEE..
.......KJK.......
..HHH..KKK..FFF..
.HHHHH.....FFFFF.
.HHHHHH...FFFFFF.
.HHHHHHH.FFFFFFF.
.HHHHHHH.FFFFFFF.
..HHHHHH.FFFFFF..
 ..HHHH...FFFF..
  .............
ENDMAP

NAME:    nicolae_sub_coc_subbean
TAGS:    unrand nico_icebean no_pool_fixup allow_dup
SHUFFLE: DE/xw/xw/xW/xW/ww/wW/wW/WW/.W/.W
SUBST:   DE = x
MAP
  EEEE
 EEDDEE
EEDDDDE
EDDDDEE
EDDDEE
EEDEE
 EEE
ENDMAP

NAME:    nicolae_coc_icecubes
TAGS:    transparent no_pool_fixup
DEPTH:   Coc
SHUFFLE: DEJ, FHM, NRS, QZa
SUBST:   D : .W, F : .W, N : .W, Q : .W, E : .xmwW, H : .xmwW, J : .xmwW, \
         K : .xmwW, L : .xmwW, M : .xmwW, O : .xmwW, P : .xmwW, R : .xmwW, \
         S : .xwmW, Z : .xmwW, a : .xmwW
MAP
DDDEEEFFFHHH
DDDEEEFFFHHH
DDDEEEFFFHHH
JJJKKKLLLMMM
JJJKKKLLLMMM
JJJKKKLLLMMM
NNNOOOPPPQQQ
NNNOOOPPPQQQ
NNNOOOPPPQQQ
RRRSSSZZZaaa
RRRSSSZZZaaa
RRRSSSZZZaaa
ENDMAP

NAME:   nicolae_coc_kraken_the_ice
TAGS:   transparent no_pool_fixup
DEPTH:  Coc, !Coc:$
KMONS:  1 = kraken simulacrum
KFEAT:  1 = w
SUBST:  ` = w., ' = w-
NSUBST: - = 1:} / ., w = 3:1 / 3 = 1w / *:w
MAP
`...`.`...`
.'w--'-w-'.
.www-'www'.
`--w'-'w--`
.-wwwwwwww.
.ww'w1w''w.
.w--www---`
`--ww'w'ww.
.-'w'-www'.
.www--w-''.
`...`..`..`
ENDMAP

NAME:   nicolae_coc_liches_on_tour
TAGS:   transparent no_monster_gen no_pool_fixup
DEPTH:  Coc:2-
KMONS:  1 = ice fiend
KMONS:  2 = hellion / tormentor
KMONS:  3 = ancient lich / dread lich
NSUBST: M = 2:1 / 3:2 / 3:3 / 2:. / * = 123..
SUBST:  ~ = w., - = x.
MAP
 .........
.M.~www~.M.
M.~wwwww~.M
.M~wwwww~M.
..M~www~M..
.M..~~~..M.
x.M-...-M.x
x.-.x.x.-.x
xx...{...xx
xxxx~~~xxxx
 xxxxxxxxx
ENDMAP

NAME:  nicolae_coc_melting_block
TAGS:  transparent allow_dup extra
DEPTH: Coc
SUBST: M = mxx, N = Wmm, O = .WW
MAP
..........
.OOOOOOOO.
.ONNNNNNO.
.ONMMMMNO.
.ONMxxMNO.
.ONMxxMNO.
.ONMMMMNO.
.ONNNNNNO.
.OOOOOOOO.
..........
ENDMAP

NAME:    nicolae_coc_naught_crossing
TAGS:    transparent no_pool_fixup extra
DEPTH:   Coc
ORIENT:  centre
SHUFFLE: w.
MAP
..xxxwwwwwxxx..
...wwwwwwwww...
x.WWwwwwwwwWW.x
xwWWWxxxxxWWWwx
xwwW..xxx..Wwwx
wwwx...x...xwww
wwwxx.....xxwww
wwwxxx...xxxwww
wwwxx.....xxwww
wwwx...x...xwww
xwwW..xxx..Wwwx
xwWWWxxxxxWWWwx
x.WWwwwwwwwWW.x
...wwwwwwwww...
..xxxwwwwwxxx..
ENDMAP

NAME:    nicolae_coc_on_the_berg
TAGS:    transparent no_pool_fixup
DEPTH:   Coc, !Coc:$
ORIENT:  encompass
KFEAT:   E = open_sea
KFEAT:   ! = W
: set_border_fill_type('open_sea')
SHUFFLE: Qq, Or
NSUBST:  S = { / } / !
SUBST:   ~ = w., D : w-, F : w-, H : w-, J : w-, K : w-, L : w-, \
         M : xxw--, N : xxw--, O : xxw--, P : xxw--, Q : xxw--, \
         R : xxw--, p : xxw--, qr : -, - = .:40 !:1 8:1
MARKER:  ! = lua:fog_machine { pow_max = 20, cloud_type = "freezing vapour", \
             delay_min = 50, delay_max = 250, size = 16, start_clouds = 1, \
             excl_rad = -1 }
MAP
EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE
EwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwE
EwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwE
Ewwwww~...DDDwwwwwwww~....~wwwwwwwE
Ewww~......DDDDwwww~...MM.....~wwwE
Ewww..S.....DDDDDDD...MMMM.....~wwE
Eww~.........DDDDD...MMMMM...S..wwE
Eww..........DDDDDD..MMMMM......wwE
Eww..pppp...DDDDDDD...MMM......~wwE
EwwLppppp....DDDDD...RRR......~wwwE
EwwLLppppp....DDD...RRRRR.....wwwwE
EwwLLLppp...........RRRRR...FFFwwwE
EwwwLLLLL.........qqRRRRRFFFFFFFwwE
EwwwwLLLLL....xxxqqqqRRRFFFFFFFFwwE
EwwwLLLLLL...xxxxxxqq...FFFFFFFFwwE
EwwLLLL....QQxxxxxxq....FFFFFFFFwwE
EwwL.....QQQQQxxxx......NNFFFFFFwwE
Eww......QQQQQQ.........NNNNNFFwwwE
EwwK..KKKKKQQxx.!.rrxxx..NNNNNFwwwE
EwwKKKKKKKKKxxxxrrrxxxxx..NNNHwwwwE
EwwwKKKKKKKKxxxxrrxxxxx...HHHHwwwwE
EwwwwKKKKKKxxxxxrrxxxxx..HHHHHHwwwE
EwwwwKKKKK..xxx..rrrxxx..HHHHHHHwwE
EwwwwKK..............xxxOOHHHHHHwwE
EwwwK....PP...JJJJJJ..xxOOO.HHHHwwE
Ewww....PPPP.JJJJJJJJ.OOOOO...HwwwE
Eww~...PPPPPJJJJJJJJJ..OOO.....~wwE
Eww.....PPPPJJJJJJJJ...OO.......wwE
Eww......PP..JJJJJ..........S..~wwE
Eww..S.........JJ.............~wwwE
Eww~....~ww~...JJJ..~www~....~wwwwE
Ewww~..~wwww~.JJJJJwwwwwwwwwwwwwwwE
EwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwE
EwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwE
EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE
ENDMAP

NAME:    nicolae_coc_statues_of_limitations
TAGS:    transparent allow_dup luniq
DEPTH:   Coc, !Coc:$
KMONS:   1 = ice statue
KFEAT:   1 = W
KFEAT:   ^ = shaft trap
SHUFFLE: DE
SUBST:   D : 1, E : GG1, } = }}^
MAP
WWWWW
WDEDW
WE}EW
WDEDW
WWWWW
ENDMAP

NAME:   nicolae_coc_three_more_mouths
TAGS:   transparent patrolling no_pool_fixup
DEPTH:  Coc, !Coc:$
ORIENT: centre
MONS:   ice fiend, pillar of rime
MAP
        wwwww
    x  ..xxxww
   xxx..xx.xxww
    x..xx..2xxw
  .....x..1..xw
 w.xxx......xx.
wwxx.......xx..
wxx........x..x
wx2.1...}....xxx
wxx........x..x
wwxx.......xx..
 w.xxx......xx.
  .....x..1..xw
    x..xx..2xxw
   xxx..xx.xxww
    x  ..xxxww
        wwwww
ENDMAP

NAME:   nicolae_coc_through_the_icewall
TAGS:   transparent no_pool_fixup
DEPTH:  Coc
ORIENT: centre
KMONS:  1 = 0 w:120 / 9 w:30 / 8
KMONS:  P = pillar of rime
KFEAT:  1 = W
KFEAT:  P = w
SUBST:  M = wx, ~ = .w, - = Wwxxx, W = W:15 1:1
MAP
  .......................
 ..wW~..............~w~...
 ~wWWw~..~wwww~.....~ww~..
 xwWWwxMwwwwwwwMxMwwwwwwMx
xxMWWMxxMwwwwMxxxxMwwwMxxxx
xxxWWxxxxMPMxxx--xxxxxxxxxx
xx-WW--xxxxxx-WWWWWWWWWW-xx
xx-WWWW------WWWWWWWWWWW-xx
xx-WWWWWWWWWWWWWxxxxx-WWW-x
xxx--WWWWWWWWWWxxMPMx-WWW-x
xxxxxxxxxxxxx--xMwwMxx-WW-x
xxxxxMMwwwwMxxxxMwwwMxwWWxx
 xxxwwwwwwwwMxxMwwwwMxwWWx
 ...~ww~.~ww~~~~~ww~.ww~..
 .........~w~........~w...
  .......................
ENDMAP

NAME:   nicolae_coc_two_mouths
TAGS:   patrolling transparent no_pool_fixup
DEPTH:  Coc:2-, !Coc:$
ORIENT: centre
KMONS:  P = pillar of rime
KFEAT:  P = w
SUBST:  0 = .009, 9 = 998
MAP
 wwwwwww             wwwwwww
wwxxxxxww           wwxxxxxww
wxx.0.xxww         wwxx.9.xxw
..0....xxw         wxx....9..
@.9..{..xP         Px..}..8.@
..0....xxw         wxx....9..
wxx.0.xxww         wwxx.9.xxw
wwxxxxxww           wwxxxxxww
 wwwwwww             wwwwwww
ENDMAP

NAME:   nicolae_coc_wendigon
TAGS:   transparent no_pool_fixup
DEPTH:  Coc
KMONS:  1 = simulacrum
KMONS:  2 = wendigo
SUBST:  ~ = Ww, M = W., S = 1., ` = wwwxx..., . = 1:6 2:2 .:50
MAP
  ww~WW~ww
 ww~M..M~ww
ww~M....M~ww
w~M......M~w
~M..S``S..M~
W...`{M`...W
W...`MM`...W
~M..S``S..M~
w~M......M~w
ww~M....M~ww
 ww~M..M~ww
  ww~WW~ww
ENDMAP

NAME:   tekkud_coc_lazy_river
TAGS:   transparent no_pool_fixup
DEPTH:  Coc:2-
KMONS:  1 = pharaoh ant
KMONS:  2 = wendigo
KMONS:  3 = juggernaut
KMONS:  4 = frost giant / titan
KMONS:  5 = shard shrike / azure jelly
KFEAT:  12345 = shallow_water
NSUBST: 5 = 3:5 / 2:W / * = 55W
MAP
      wwwww
     WwwwwwwWw
    wWWWw516WWw
   www54W2Wwwwww
  www5314wwwww
 www5255w5ww
wwwW4Ww3WwWww
wW15WwwwwwWW
WWwWwwww
  ww
ENDMAP

##############################################################################
# Antaeus; bottom of Cocytus.
##############################################################################

NAME:    coc_dpeg
ORIENT:  encompass
MONS:    patrolling Antaeus, Ice Fiend, wendigo band, titan
KMONS:   E = elemental wellspring
: serpent_of_hell_setup(_G)
KMONS:   defg = shard shrike w:5 / ice fiend / nothing w:5
KITEM:   defg = any misc
KFEAT:   E = w
SHUFFLE: O1dD / e4%H / f9*I / g8|J
SUBST:   ' = w ., 3 = 3 ., HIJ = 4 4 .
NSUBST:  w = 2:E / 2 = E w / *:w
: coc_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwww.......wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwww....wwwww....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww''wwwwwwwwxxxx
xxxxwww...wwwwwwwwwww....wwwwwwwwwwwwwwwwwwwwwwwwwww'.......wwwwwwxxxx
xxxxwww.wwWw......wwwwww..wwwwwwwwwwwwwwwwwwww'.......ccccc...wwwwxxxx
xxxxww..ww....cc......www.3wwwwwwwwwwwww..........ccccccccccc..wwwxxxx
xxxxw...ww..cccccc.....www.wwwwwwwwww'3..........cc....9.2..cc..wwxxxx
xxxxw..ww..ccccccc+c....ww.'wwwwwwwwwww''........ccf********cc..wwxxxx
xxxxw..ww..cc.%%%%.cc...ww..wwwwwwwwwwwwwwwww'....ccccccc+ccc...'wxxxx
xxxxw..ww..cc2.e4.2cc...ww..wwwwwwwwwwwwwwwwwwww'......c...c.....wxxxx
xxxxw..ww..cc.%%%%.cc..www.'wwwwwwwwwwwwwwwwwwwwwwwww'.cc+cc..I..wxxxx
xxxxw..www..c+cccccc...ww..wwwwwwwwwwwwwwwwwwwwwwwwwww..........wwxxxx
xxxxww..ww....ccccH...www.wwwwwwwwwwwwwwwwwwwwwwwwwwwww........wwwxxxx
xxxxww..www..........www..wwwwwwwwwwwwwwwwwwwwwwwwwwww'..2....w..wxxxx
xxxxwww..wwww......wwWw..wwwwwwwwwwwwwwwwwwwwwwwwwww'............wxxxx
xxxxwwwW..wwwwwwwwwwww..wwwwwwwwwwwwwwwwwwwwwwwwww..............wwxxxx
xxxxwwwWW...'wwwwww....wwwwwwwwwwwwwwwwwwwwwwwwww............'wwwwxxxx
xxxxwwwWWwww........'wwwwwwwwwwwwwwwwwwwwwwww'3..'ww'....''wwwwwwwxxxx
xxxxwwwWWwwwwww'...3wwwwwwwwwwwwwwwwwwwwwwwwww''wwwwwww.Wwwwwwwwwwxxxx
xxxxwwwWWwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwWWwwwwwwwwwxxxx
xxxxwwWWwwwwwwwwwwwwwwwwwwwwww........wwwwwwwwwwwwwwwwwWWwwwwwwwwwxxxx
xxxxwwWWwwwwwwwwwwwwwwwwwwww............wwwwwwwwwwwwwwwWWwwwwwwwwwxxxx
xxxxwwWWwwwwwwwwwwwwwwwwwwww...ccc+ccc...wwwwwwwwwwwwwWWwwwwwwwwwwxxxx
xxxxwww.wwwwwwwwwwwwwwwwwww...cc.....cc...wwwwwwwwwwwwWWwwwwwwwwwwxxxx
xxxxwww..wwwwwwwwwwwwwwwwww...c.......c...wwwwwwwwwwwwW3wwwwwwwwwwxxxx
xxxxwww...wwwwwwwwwwwwwwwww...+...{...+...wwwwwwwwwwww...wwwwwwwwwxxxx
xxxxwww.3.wwwwwwwwwwwwwwwww...c.......c...wwwWWwwWWwwW...wwwwwwwwwxxxx
xxxxwww...wWwwwWwwwwwwwwwwW...cc.....cc...WWWWWWWWWWW.....wwwwwwwwxxxx
xxxxwwww..WWWwWWWwwwwwwwwWWw...ccc+ccc...WWwwwWWwwwWww...wwwwwwwwwxxxx
xxxxwwww..WwWWWwWWwwwwWWWWww............wwwwwwwwwwwwww...wwwwwwwwwxxxx
xxxxwwwww.wwwWwwwWW'.3.WWwwwww.........wwwwwwwwwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww.wwwwwwwwW.3.'wwwwwwwwwwwwwwwwwwww'wwwwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww.wwwwwwwww'.'wwwwwwwwwwwwwwwwwwww'3wwwwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww..wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..wwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww...w.wwwww..wwwwwwwwwxxxx
xxxxwwww'...wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww....www...w.wwwwwwwxxxx
xxxxwwww....'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..ccccccc.c.wwwwwwwwxxxx
xxxxwwww.....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..cc$$$$$$.c..wwwwwwwxxxx
xxxxwww'.....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..c.$dddd$.c...wwwwwwxxxx
xxxxwww..cc..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwww'...$$$$$$cc....wwwwwxxxx
xxxxwww.cccc..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww'.ccccccc....wwwwwwxxxx
xxxxwww.cg.cc..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwww........ww..wwwwwwwxxxx
xxxxww'.c||.cc...'wwwwwwwwwwwwwwwwwwwwwwwwwwww.D...wwwwwwwwwwwwwwwxxxx
xxxxww'.c||.8cc......'wwwwwwwwwwwwwwwwwwwwwww'...w..wwwwwwwwwwwwwwxxxx
xxxxww..c||2.2cc..J.....'wwwwwwwwwwwwwwwwww....wwww..wwwwwwwwwwwwwxxxx
xxxxww..c||....cc..............3wwwwwwwwww...wwwwwww.....wwccccwwwxxxx
xxxxww..cccc+ccccc..........wwwwwwwwwwww3...wwwwwwwwwww.2.cc2dccwwxxxx
xxxxwww..ccc...........'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.+dd1Ocwwxxxx
xxxxwwww......'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcc2dccwwxxxx
xxxxwwwwww'''wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Original Antaeus level
##############################################################################

NAME:    coc_old
ORIENT:  encompass
MONS:    patrolling Antaeus, ice fiend, shard shrike, titan
KMONS:   0 = wendigo / simulacrum w:40
KMONS:   E = elemental wellspring
KMONS:   F = ice dragon
KFEAT:   E = w
KFEAT:   F = W
SHUFFLE: 1O26Y$| / 8~47Z%*, 1O, 8~, $|, %*
SHUFFLE: 5X, 6Y, 7Z, AB
SUBST:   % = $, ~ = |, 3 = 3:6 .:4
SUBST:   567 = 2, X8 = 4, AYZ = ., B = c , < = .
: serpent_of_hell_setup(_G)
KFEAT:   D = deep_water
NSUBST:  H = 1:D / *:w
NSUBST:  0 = 20:0 / 4:0. / 4:0.. / *:.
: coc_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx........................xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx............00............xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx.............00.............xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx..............00..............xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx................................xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx....ccAAccccccccccccccccccBBcc....xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx....cccAAccccccccccccccccccBBccc....xxxxxxxxxxxxxxx
xxxxxxxxxxxxxx....cc........X........5........cc....xxxxxxxxxxxxxx
xxxxxxxxxxxxx....cc............................cc....xxxxxxxxxxxxx
xxxxxxxxxxxx....cc...wwwwwwwwwwwwwwwwwwwwwwww...cc....xxxxxxxxxxxx
xxxxxxxxxxx....cc...wwwwwwwwwwwwwwwwwwwwwwwwww...cc....xxxxxxxxxxx
xxxxxxxxxx....cc...wwwwwwwwwwwwwwwwwwwwwwwwwwww...cc....xxxxxxxxxx
xxxxxxxxx....cc...ww.......3....33....3.......ww...cc....xxxxxxxxx
xxxxxxxx....cc...ww............................ww...cc....xxxxxxxx
xxxxxxx....cc0..ww....cccccccccccccccccccccc....ww..0cc....xxxxxxx
xxxxxx....cc0..ww....cccccccccccccccccccccccc....ww..0cc....xxxxxx
xxxxx....cc...ww....cc......................cc....ww...cc....xxxxx
xxxx....cc...ww...0cc...T................T...cc0...ww...cc....xxxx
xxx....cc...ww...0cc..........wwWWww..........cc0...ww...cc....xxx
xx....cc...ww....cc......wwwwwwWWWWwwwwww......cc....ww...cc....xx
xx....cc...ww...cc.....wwwWWWWWWwwWWWWWWwww.....cc...ww...cc....xx
xx....cc..www..cc....wwwwWcccccwwwwcccccWwwww....cc..www..cc....xx
xx....cc..www.cc....wwwwWcc%%cccwwccc$$ccWwwww....cc.www..cc....xx
xx....cc..www.cc...wwwwwWc%%%+4cccc2+$$$cWwwww....cc.www..cc....xx
xx....cc..www.cc...wwwwwWc%%%c+cccc+c$$$cWwwwww...cc.www..cc....xx
xx.00.cc..www3.c..wwwwwwWccccc.7cc6.cccccWwwwwww..c.3www..cc.00.xx
xx.00.cc..wwww.c.wwwwwwwF+.Z.+.8cc1.+.Y.+Fwwwwwww.c.wwww..cc.00.xx
xx.00.cc..wwww3c.wEwwwwwF+.Z.+.~ccO.+.Y.+FwwwwwEw.c3wwww..cc.00.xx
xx....cc..wwww.c.wwwwwwwwccccc.7cc6.cccccwwwwwwww.c.wwww..cc....xx
xx....cc..www3.c..wwwwwwwc***c+cccc+c|||cwwwwwww..c.3www..cc....xx
xx....cc..www.cc...wwwwwwc***+4cccc2+|||cwwwwww...cc.www..cc....xx
xx....cc..www.cc...wwwwwwcc**cccwwccc||ccwwwwww...cc.www..cc....xx
xx....cc..www.cc....wwwwwwcccccwwwwcccccwwwwww....cc.www..cc....xx
xx....cc..www..cc....wwwwwwwwwwwwwwwwwwwwwwww....cc..www..cc....xx
xx....cc...ww...cc.....wwwwwwwwwwwwwwwwwwww.....cc...ww...cc....xx
xx....cc...ww....cc......wwwwwwwwwwwwwwww......cc....ww...cc....xx
xxx....cc...ww....cc..........wwwwww..........cc....ww...cc....xxx
xxxx....cc...ww....cc...T................T...cc....ww...cc....xxxx
xxxxx....cc...ww....BB........5....X........AA....ww...cc....xxxxx
xxxxxx....cc...ww....cBccccccccccccccccccccAc....ww...cc....xxxxxx
xxxxxxx....cc...ww....BccccccccccccccccccccA....ww...cc....xxxxxxx
xxxxxxxx....cc...ww............0000............ww...cc....xxxxxxxx
xxxxxxxxx....cc...ww..........................ww...cc....xxxxxxxxx
xxxxxxxxxx....cc...wwwwwwwwwwwww..wwwwwwwwwwwww...cc....xxxxxxxxxx
xxxxxxxxxxx....cc...wwwwwwwwwHww..wwHwwwwwwwww...cc....xxxxxxxxxxx
xxxxxxxxxxxx....cc...wwwwwwwwwww..wwwwwwwwwww...cc....xxxxxxxxxxxx
xxxxxxxxxxxxx....cc............................cc....xxxxxxxxxxxxx
xxxxxxxxxxxxxx....cc...........0000...........cc....xxxxxxxxxxxxxx
xxxxxxxxxxxxxxx....cccccccccccccccccccccccccccc....xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx....cccccccccccccccccccccccccc....xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx..............0000..............xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx..............................xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx............................xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx............{<............xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Antaeus (Cocytus) by Mu
##############################################################################

NAME:    coc_mu
ORIENT:  encompass
MONS:    patrolling Antaeus, ice fiend
MONS:    nargun, azure jelly, simulacrum, wendigo, freezing wraith
KMONS:   8 = ice statue
KMONS:   9 = frost giant
KMONS:   0 = titan
KMONS:   e = water elemental
KMONS:   E = elemental wellspring
KFEAT:   eE = w
SUBST:   3 = 3:5 4:4 5:4 6:2 7:2 9:1
NSUBST:  0 = 5:0 / *:3
: serpent_of_hell_setup(_G)
SUBST:   ^ : ^^^W. , ^ = W.
SUBST:   X : XXXc. , X = c.
SUBST:   " = 8:1 .:10
SUBST:   ' = .cc
SUBST:   ; : ;;;;w , ; = wc
KFEAT:   # = permarock_wall
COLOUR:  # = lightcyan
NSUBST:  - = 1:+ / *:c
: coc_setup(_G)
MAP
########################################################################
#cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc#
#ccccc............Ewwwwwwwwwwwwwwwxxxxxxxxxwwwwwww......3......cccccccc#
#cccccc............wwwwwwwwwwwwwwwwxxxxxxxxxwwwwwww......3......ccccccc#
#ccccc..............wwwwwwwwwwwwwwwwxxxxxxxwwwwwwww.3.3......c...cccccc#
#cccc...wwwwwwwwww..wwwwwwwwwwwewwwwwwxxxwwwwwwww....3.0D.cccc....'.ccc#
#cccc..wwwwww8www.2.wwwwwwwwwew.wewwwwwwwwwwwwww...3..3...cccccc..c..cc#
#cccc..ww|$|www.......wwwwwwww...wwwwwwwwwwwwwwwww......ccccccc...cc.cc#
#cccc..ww$O$....1..............8..ewwwwwwwwwwwwwwwW...ccccc"c"c.cccc.'c#
#cccc..ww|$|www.......wwwwwwww...wwwwwwwwwwwwwwwwWW.cccccc....c.cccc.'c#
#cccc..wwwwww8www.2.wwwwwwwwweW.wewwwwwwwwwwwwwwWWwccccc...cc...."cc.cc#
#cccc...wwwwwwwwww..wwwwwwwwwwWewwwwwwwwwwwwwwwWWccccccc...ccccccccc.cc#
#ccccc..............wwwwwwwwwwWWwwwwwwwwwwwwwwWWcccccc......cccccc"...c#
#cccccc............wwwwwwwwwwwWWwwwwwwwwwwwwWWWccccc..........ccccc"c.c#
#ccccccccccccccccccEwwwwwwwwwwWWwwwwwwwwwWWWWccccc.....3330.....ccccc.c#
#cccccccccccccccccccwwwwwwwwwwwWWWwwwwWWWWwwcccc........333.......cc..c#
#ccccccccccccccccccc;wwwwwwwwwwwwWWWWWWwwwwcccc..................ccc.cc#
#ccccccc"c$.....ccc;cccwwwwwwwwwwwwwwwwwwccccc....................cc.cc#
#cccccc$$$$........cccccccwwwwwwwwwwwwwcccccc....cccccc..........ccc..c#
#cccc"$$$$0..........ccccccccwwwwwwwcccccccc....cccccccc.......cccc...c#
#ccccc$$$..............ccccccccccccccccccc.....cccccwwwcc....cccccc...c#
#cccccc$$................ccccccccccccccc.....cccccccwwwwwwwcccccccc...c#
#ccccccc$...........wwwwwwwccccccccccc......ccccc.cwwcwwwwccccccccc..cc#
#cccccccc.........wwwwwwwwwwww.............cccc333333cccwccccX.-....ccc#
#cccccccccc.....wwwwwwwwwwwwwwww........ccccccc.2....cccc-XXX..cc...ccc#
#cccccccccccccwwwwwwwwwwwwwwwwwwww.......cccccc333333ccccX....ccccX..cc#
#cccccccccccwwwwww..wwwwwwwwwwwwwww........ccccc$$$$$cccX.0..cccccX...c#
#ccccccccccwwwwww..wwwwwwwwwwwwwwwwW........cccc*|*ccccc|.....ccc.....c#
#cccccccccwwwwww..www...wwwwwwwwwwWWWww......ccccccccccc|......-......c#
#cccccc..cwwww....ww....WWW..wwwwWWwwwwww.....ccccccccccX.2..Xcc......c#
#cccccc..www..ccccww...wwW..wwwwWWwwwwwwwwww..cccccccccccXXXXcccc.....c#
#ccccc...Ww..cc...WW...ww...WW..Wwwwwwwwwwwwwwwcccccccccccccccccww...cc#
#ccccc..wwW.cc....www...www.ww.wwwwwwwwwwwwwwwwwwccccccccccccwwwwwwwccc#
#cccc...ww..c*..3..Www..wwwwwwww^^^wwwwwwwwwwwwwwwccccccwwwccwwwwwwcccc#
#cccc...ww..c|233..cWwwwwwwwwwwcc+ccccccccccccwwwwwccccwwwwwwwwwwwwcccc#
#cccc..www..c*.33..c.wwwwwwwwwcc...2...$$$*||ccwwwwwwwwwwwwwwwwwwwccccc#
#cccc..www..cc..3.cc....wwwww.cc..33..$$$$*||cc..wwwwwwwwwwwwwwwwcccccc#
#cccc..wwww..cc$$cc....wwwwww.cc2333.$$$$$*||cc...wwwwwww^^wwwwwwcccccc#
#cccc..wwww...cccc....wwwwww...ccccccccccccccc.....wwwww^..^wwwwwwccccc#
#cccc..wwwww........wwwwww......................c.wwwww^.6..^WWwwwccccc#
#cccc..wwwwwww....wwwwwww......................cc.wwwww^....^WWWWwwcccc#
#cccc..wwwwwwwwwwwwwwww........................cc.wwwwwW^..^wwWWWWwcccc#
#cccc...wwwwwwwwwwwww.........333.............ccc.wwwwwWW^^wwwwwWWWcccc#
#ccccc..wwwwwwwwwwww............03............ccc.wwwwWWwwwwwwwwwWW.ccc#
#ccccc...wwwwwwwww.........ccc....333........cccc..wwWWwwwwwwwwwwww.ccc#
#ccwccc....wwwww........cccccccc....03......ccccc..WWWwwwwwwwwwwww..ccc#
#ccwwc;ccc...........ccccccccccccc....333..ccccccc..Wwww^wwwwwwww..cccc#
#cccwwwcccccc.....cccccccccccccccccccc...ccccccccc..www^.wwwwww....cccc#
#cccwwwwwcccccccccccccccccccccccccccccc;ccccccccccc..wwwwwwwww...3ccccc#
#ccc.wwwwwcccccccccccccccccccccccccccccc;cccccccccccwwwwwwwwww..3cccccc#
#cc...wwwwwww..ccccccccccccc...........wwXXXXXccccccXwwwwwwwww...3ccccc#
#xc....wwwww.....cccccccc...3.....wwwwwwwwwwwwXXccccXwwwwwwwwww....cccc#
#x..{..wwww...333...............WwwwwwwwwwwwwwwwXcccXwwwwwwwwwwww..cccc#
#x......www....333...........wWWWwwwwwwwwwwwwwwwwXccXwwwwwwwwwwwWW..ccc#
#x......wwwW.............wwwwwwWWwwwwwwwwwwwwwwwwwXXwwwwwwwwwwWWWWW.ccc#
#x.......WWWWw.......wwwwwwwwwwwWWwwwwwwwwwwwwwwwwwwwwwwwwwwww.^www.ccc#
#x.......WWWwwwwwwwwwwwwwwwwwwwwWWwwwww....wwwwwwwwwwwwwwwwww^.wwwwcccc#
#xc.......WwwwwwwwwwwwwwwwwwwwwwWWWw..........wwwwwwwwwwwwwwwW^wwwwcccc#
#ccc.......wwwwwwwwwwwwwwwwwwwww........333......wwwwwwwwwww^Wwwwwwcccc#
#ccc.........wwwwwwwwwwwwwwwww.........33033.......wwwwwww^...wwwwccccc#
#cccccc.........wwwwwwwwwww............................WWWW^^^wwwwccccc#
#cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc#
########################################################################
ENDMAP

##############################################################################
# Antaeus (Cocytus) by Grunt
##############################################################################

NAME:    coc_grunt
ORIENT:  encompass
MONS:    patrolling Antaeus, ice fiend, shard shrike / nothing
KMONS:   8 = ice statue
KMONS:   9 = elemental wellspring
KMONS:   de = ice fiend
KMONS:   fg = titan
KITEM:   df = superb_item
KITEM:   egh = gold
: serpent_of_hell_setup(_G)
KFEAT:   D = floor
KFEAT:   X = open_sea
KFEAT:   9 = w
: set_border_fill_type('open_sea')
SHUFFLE: 1O|d$e / 5!*f%g, |d / $e
NSUBST:  H = 1:D / *:8
SUBST:   * = |, % = $, 5 = 2
: set_border_fill_type("open_sea")
: coc_setup(_G)
MAP
XXXXXXXXXXXcccccccccccXXXXXXXXXXXcccccccccXXXXXXXXXXXXXXX
Xwwwwwwwwww9cccccccccwwwwwwwwwwwwwccccccc9wwwwwwwwwwwwwwX
XwwwwwwwwwwwcccccccccwwwwwwwwwwwwwwcccccwwwwwwwwwwwwwwwwX
XwwwwwwwwwwwwccccccccwwwwwwwwwwwwwwccccwwwwwwwwwwwwwwwwwX
Xwwww...wwwwwccccccccwwwwwww.{.wwwwwcccwwwwwww...wwwwwwwX
Xwwww....wwwwwccccccwwwwww.......wwwcccwwwwww......wwwwwX
Xwww...0..wwwwccccccwwwwww.......wwwcccwwww.........wwwwX
Xwww.......wwwccccccwwwww....w....wwcccwww.....0.....wwwX
Xww..cc+cc..wwwcccccwwww..0.wwww.0wwcccww.............wwX
Xw..cc*.%cc..wwcccccwwww....wwwww..wcccw.....cc+cc.....wX
Xw..c**.%%c..wwcccccwwww.0.wwwwwwW0wwccw....cc|.$cc....wX
Xw.3cf*5%gc3.wwcccccwwwwW.WwwwwwwWWwwccw....c||.$$c....wX
Xw..c*f.g%c..wwcccccwwwwWWWwwcwwwWWwwccw..3.cd|1$ec.3..wX
Xw..cc*!%cc..wwcccccwwwwWWWwwcwwwwWWwccw....c|d.e$c....wX
Xww.0ccccc0.wwwccccwwwwwWWwwwcwwwwWWwccw....cc|O$cc...wwX
Xwww...3....wwwccccwwwwwWWwwwcwwwwWWwccww....ccccc....wwX
Xwwww......wwwcccccwwwwwWWwwwcwwwwWWwwccww.0.......0.wwwX
Xwwwww....wwwwccccwwwwwwWWwwwcwwwWWWwwccww.....3.....wwwX
Xwwwww....wwwwccccwwwwwW.0WwwcwwwW..Wwcccww.........wwwwX
Xwwwww....wwwwccccwwwww....wwcwww....wccccwwww.....WwwwwX
Xwwwww...0.wwwccccwwwww.3..wwcwww.0..wwcccwwwwWWWWWWwwwwX
Xwwwww.....wwwccccwwwww...wwwcwww....wwccccwwwwWWWWWwwwwX
Xwwwwww3...wwwccccwwwww0..wwwcwww..3..wccccwwwwwW...wwwwX
Xwwwwww....wwwcccccwwww.Wwwwwcwww.....wwccccwwwww0...wwwX
Xwwwwwww.0.wwwcccccwwwwWWwwwwcwww...0.wwcccccwwww..3.wwwX
XwwwwwwwW.WwwwcccccwwwwWWwwwwcwwww....wwcccccwwww....wwwX
XwwwwwwwwWWwwwcccccwwwwWWwwwwccwwwW...wwcccccwwwww.0.wwwX
XwwwwwwwwWWwwwcccccwwwwWWwwwwccwwwwWWWwwcccccwwwwww.WwwwX
XwwwwwwwwWWwwwccccwwwwwWWwwwwcccwwwWWWwwcccccwwwwwwWWwwwX
XwwwwwwwwWWwwwccccwwwwwWWwwwwcccwwwWWwwwwccccwwwwwwWWwwwX
XwwwwwwwwwWWwwccccwwwwwWWWwwwcccwwwWWwwwwcccccwwwwwWWwwwX
XwwwwwwwwwWWwwwcccwwwwWWWWwwwcccwwwWWwwwwcccccwwwwwWWwwwX
XwwwwwwwwwWWwwwcccwwwwW..WwwwcccwwwWWwwwwcccccwwwwwWWwwwX
XwwwwwwwwwWWwwwccwwwww....wwwcccwwwWWwwwwcccccwwwwwWWwwwX
XwwwwwwwwWWWWwwccwwwww....wwwcccwwwwWwwwwcccccwwwwwWWwwwX
XwwwwwwwW...Wwwccwwwww....wwwcccwwwwWwwwwcccccwwwwwWWwwwX
Xwwwwwww...0.wwccwwww....wwwwcccwwwwWwwwwcccccwwwwW..WwwX
Xwwwwwww.....wwwcwwww....wwwwcccwwwwWWwwwwcccwwwww..0.wwX
Xwwwwwww..3..wwwcwwww..0.wwwwcccwwwwWWwwwwcccwwwww....wwX
Xwwwwwww.....wwwcwwww....wwwccccwwwWWWwwwwcccwwwww.3.wwwX
Xwwwwwww.0...wwwcwwww.3..wwwccccwwWW.Wwwwwcccwwww....wwwX
Xwwwwwww.....wwwcwwww...WwwwccccwwW.0.wwwwcccwwww.0..wwwX
Xwwwwwww....wwwwcwwww0.WWwwwccccwwW...wwwwcccwwww...WwwwX
XwwwwwwwW..WwwwwcwwwwWWWwwwwccccww.3..wwwwcccwwwwW.WwwwwX
XwwwwwwwwWWWwwwwcwwwwWWwwwwwccccww....wwwwcccwwwwWWWwwwwX
XwwwwwwwwwWWwwwwcwwwwWWwwwwwcccwww.0.wwwwwcccwwwwWWWwwwwX
XwwwwwwwwwWWwwwwcwwwwWWwwwwwcccwww...wwwwwcccwwwwWWWwwwwX
XwwwwwwwwwwWwwwccwwwwWWwwwwwcccwww...wwwwwcccwwwwWWWwwwwX
XwwwwwwwwwwWWwwwcwwwwWWwwwwwcccwwwW..wwwwwcccwwwwWWWwwwwX
XwwwwwwwwwwWWwwwwwwwW.WWwwwwcccwwwWWWwwwwwcccwwwwWWwwwwwX
XwwwwwwwwwwWWwwwwwwwWW.WwwwwcccwwwWWwwwwwwwcwwwwwWWwwwwwX
XwwwwwwwwwwWWwwwwwwwwWWwwwwwcccwwwWWwwwwwwwwwwwwwWWwwwwwX
XwwwwwwwwwwWWwwwwwwwwWWwwwwwcccwwwWWwwwwwwwwwwwwWWWwwwwwX
XwwwwwwwwwwWWwwwwwwwwWWwwwwwcccwwwWWwwwwwwwwwwwwWWwwwwwwX
XwwwwwwwwwwWWwwwwwwwwWWwwwwwcccwwwWWWwwwwwwwwwwWWWwwwwwwX
XwwwwwwwwwwWWWwwwwwwWWWwwwwwccccwwwWWwwwwwwwwwwWWwwwwwwwX
XwwwwwwwwwwW..wwwwwWWWWwwwwcccccwwwWWwwwwwwwwwwWWwwwwwwwX
Xwwwwwwwwwww.0.wwww..WWwwwwcccccwwWWWWwwwwwwwwWWwwwwwwwwX
cwwwwwwwwwww....ww..0.WwwwwcccccwwWWW.wwwwwww.WWwwwwwwwwX
cwwwwwwwwwwww..2.......WwwwWcccWwwWW...........wwwwwwwwwX
cccwwwwwwwwwww...H...wWWWWWWWWWWWWW..0.....0..wwwwwwwwwwX
ccccwwwwwwwwwww.....wwwwWWWWWWWWWWww...2.H...wwwwwwwwwwwX
cccccwwwwwwwwwwwwwwwwwwwwcWWWWWcwwwww.......wwwwwwwwwwwwc
cccccccwwwwwwwwwwwwwwwwwwcc3..3cwwwwwww....wwwwwwwwwwwwcc
ccccccccwwwwwwwwwwwwwwwwwccchhccwwwwwwwwwwwwwwwwwwwwwwwcc
cccccccccwwwwwwwwwwwwwwwwccchhccwwwwwwwwwwwwwwwwwwwwwcccc
cccccccccccwwwwwwwwwwwwwwcccccccwwwwwwwwwwwwwwwwwwwcccccc
ccccccccccccwwwwwwwwwwwwwcccccccwwwwwwwwwwwwwwwwwwccccccc
cccccccccccccwwwwwwwwwwwwcccccccwwwwwwwwwwwwwwwwwcccccccc
ccccccccccccccXXXXXXXXXXcccccccccXXXXXXXXXXXXXXcccccccccc
ENDMAP

##############################################################################
# Antaeus (Cocytus) by HangedMan
##############################################################################

NAME:    coc_hangedman
TAGS:    no_pool_fixup
ORIENT:  encompass
MONS:    patrolling Antaeus, ice fiend, titan / shard shrike
MONS:    simulacrum
MONS:    wendigo / nargun
KMONS:   6 = ice statue
KMONS:   7 = patrolling kraken simulacrum / \
             patrolling elemental wellspring
KMONS:   89 = azure jelly / frost giant w:4
KMONS:   & = patrolling ice fiend
: serpent_of_hell_setup(_G)
KITEM:   d = $ no_pickup
KFEAT:   68 = w
KFEAT:   d7& = W
SHUFFLE: O1AB / eEFG, !?^~
SUBST:   AE = 2, B = 2333, F = 339, G = 995, !? = &,  ^~ = 6
SUBST:   e = |, y : yY, y = xxw, Y = xww
NSUBST:  a = + / c, - = + / c, 6 = 2=6 / 2=677 / 7
KPROP:   d7& = no_tele_into
: coc_setup(_G)
MAP
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
cccccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccccccccccccccccccccccccccc
cccccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccccccccccccccccccccccccccc
cccccccxxwyyyyyyyyywwwwwyyyyyyyyyyyywxxccccccccccccccccccccccccccccccc
cccccccxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxccccccccccccccccccccccccccccccc
cccccccxxd8wwwwwwww..w..wwwwwwwwwwwwwxxccccccccccccccccccccccccccccccc
ccxxxxxxxdwwwwwwww.cc.cc.wwwwwwwwwwwwxxxxxxxxxcccccccccccccccccccccccc
ccxxxxxxxd!wwwwww.c..c..c.wwwwwwwwwww6xxxxxxxxcccccccccccccccccccccccc
ccxxwwddddwwwwww.c.......c.wwwwwwwwwwwwwwwwwxxcccccccccccccccccccccccc
ccxxyw8w?wwwwww.c....5....c.wwwwwwwwwwwwwwwyxxcccccccccccccccccccccccc
ccxxywwwwwwwwww.c...535...c.wwwwwwwwwwwwwwwyxxcccccccccccccccccccccccc
ccxxywwwwwwwwww.c...x5x...c.wwwwwwwwwwwwwwwyxxcccccccccccccccccccccccc
ccxxywwwwwwwwww.c.........c.wwwwwwwwwwwwwwwyxxcccccccccccccccccccccccc
ccxxywwwwwwwwwww.c.......c.wwwwwwwwwwwwwwwwyxxcccccccccccccccccccccccc
ccxxywwwww....wWw.c.....c.wWw....wwwwwwwwwwyxxcccccccccccccccccccccccc
ccxxywwww.c..c.w.cccc-cccc.w.cccc.wwwwwwwwwyxxcccccccccccccccccccccccc
ccxxywww.c....c.cccc...cccc.c....c.wwwwwwwwyxxcccccccccccccccccccccccc
ccxxyww.c......cccB.....Bccc......c.wwwwwwwyxxcccccccccccccccccccccccc
ccxxww.c........cc.A..x|.cc........c.wwwwwwyxxcccccccccccccccccccccccc
ccxxww.c...5x...c...1xAx.cc...x4...c.wwwwwwyxxcccccccccccccccccccccccc
ccxxwww.c.535...-...xxx|$cc...54..c.wwwwwwwyxxcccccccccccccccccccccccc
ccxxww.c...5x...c..xAxO$|cc...x4...c.wwwwwwwxxcccccccccccccccccccccccc
ccxxww.c........cc.|x|$B|cc........c.wwwwww^xxcccccccccccccccccccccccc
ccxxyww.c......cccB..$||cccc......c.wwwwwwwwxxxxxxxxxxxxxxxxxxcccccccc
ccxxywww.c....c.ccccccccccc.c....c.wwwwwwwwwxxxxxxxxxxxxxxxxxxcccccccc
ccxxywwww.cccc.w.ccccccccc.w.cccc.8wwwwwwwwwww^wyyyyyyyyyyywxxcccccccc
ccxxywwwww....wWw.c.....c.www....wwwwwwwwwwwwwwwwwwwwwwwwwwwxxcccccccc
ccxxywwwwwwwwwww.c.......c.w.Wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxcccccccc
ccxxywwwwwwwwww.c.........c.W.Wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxcccccccc
ccxxywwwwwwwwww.c...x5x...c.wW.Wwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxcccccccc
ccxxywwwwwwwwww.c...444...c.wwW.Wxxwxxwwwwwwwwwwwwwwwwwwwwwwxxcccccccc
ccxxywwwwwwwwww.c.........c.wwwWx..x.Dxwwwwwwwwwwwwwwwwwwwwwxxxxxxxxcc
ccxxywwwwwwwwwww.c.......c.wwwwx......xwwwwwww..w..wwwwwwwww6xxxxxxxcc
ccxxywwwwwwwwwwww.c..c..c.8wwwwx...W.xwwwwwww.cc.cc.wwwwwwwwwwwwwwxxcc
ccxxywwwwwwwwwwwww.cc.cc.wwwwwwwx.W...xwwww8.c..c..c.wwwwwwwwwwwwyxxcc
ccxxwwwwwwwwwwwwwww..w..wwwwwwwx......xwwww.c.......c.wwwwwwwwwwwyxxcc
ccxxxxxxx6wwwwwwwwwwwwwwwwwwwcwW..x..xWwww.c.........c.wwwwwwwwwwyxxcc
ccxxxxxxxxwwwwwwwwwwwwwwwwwwccW.WxwxxW.Www.c...444...c.wwwwwwwwwwyxxcc
cccccccxxxwwwwwwwwwwwwwwwwwccW.WwwwwwwW.Ww.c....x....c.wwwwwwwwwwyxxcc
ccccccccxxwwwwwwwwwwwwwwwwccW.WccwwwwwwW.W.c....5x...c.wwwwwwwwwwyxxcc
ccccccccxxwwwwwwwwwwwwwWWccW.WccwwwwwwwwW.w.c.......c.wwwwwwwwwwwyxxcc
ccccccccxxwwwwwwwwwwwwWcccW.Wccwwwwww....www.c.....c.wWw....wwwwwyxxcc
ccccccccxxwyyyyyyyyywW~cWW.Wccwwwww8.cccc.w.ccccccccc.w.cccc.wwwwyxxcc
cccccccccccccccccccccccc.2Wccwwwwww.c....c.ccccccccccc.c....c.wwwyxxcc
ccccccccccccccccccccccc...WcWwwwww.c......cccc||$..Gccc......c.wwyxxcc
ccxx8w.......xx44........cccWwwww.c........cc||$Gx|.cc........c.wwxxcc
ccxx8w.......xxx.x......cc~Wwwwww.c...4....cc|$exFx..c....5x..c.wwxxcc
ccxxww.wxx....xxxwx.....ccWwwwwwww.c..4x5..cc$Gxxx...a...535.c.wwwxxcc
ccxxx...wxx....xxww.x...ccwwwwwww.c...4.x..cc.xFxE...c...x5...c.wwxxcc
cccxxx...wxx..xwwww.....ccywwwwww.c........cc.|x..F.cc........c.wwxxcc
ccccxxx...wxx..xww.wwx..ccywwwwwww.c......cccG.....|ccc......c.wwyxxcc
cccccxxx...wxx....wwwwx.ccywwwwwwww.c....c.cccc...cccc.c....c.wwwyxxcc
ccccccxxx...wxx...wwxx.4ccywwwwwwwww.cccc.w.ccccacccc.w.cccc.wwwwyxxcc
cccccccxxx...wxx..xwxxx4ccywwwwwwwwww....wWw.c.....c.wWw....wwwwwyxxcc
ccccccccxxx...wxx..x.xxxccywwwwwwwwwwwwwwww.c.......c.wwwwwwwwwwwyxxcc
cccccccccxxx.{.wxx....xxccywwwwwwwwwwwwwww.c.........c.wwwwwwwwwwyxxcc
ccccccccccxxx...wxx.....ccywwwwwwwwwwwwwww.c....5x...c.wwwwwwwwwwyxxcc
cccccccccccxxx...wxx....ccywwwwwwwwwwwwwww.c...535...c.wwwwwwwwwwyxxcc
ccccccccccccxxx...wxx...ccwwwwwwwwwwwwwwww.c...x5....c.wwwwwwd!wwyxxcc
cccccccccccccxxx...wxx..ccxxxxxx6wwwwwwwwww.c.......c.wwwwwwddd8wwxxcc
ccccccccccccccxxx...wx..ccxxxxxxxwwwwwwwwwww.c..c..c.wwwwwwddxxxxxxxcc
cccccccccccccccxxx...w..ccccccxxxwwwwwwwwwwww.cc.cc.wwwwwww?dxxxxxxxcc
ccccccccccccccccxxx.....cccccccxxwwwwwwwwwwwww..w..wwwwwwwww8xxccccccc
cccccccccccccccccxxx.wwwcccccccxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxccccccc
ccccccccccccccccccxxxw88cccccccxxwyyyyyyyyyyyywwwwwyyyyyyyyywxxccccccc
cccccccccccccccccccxxxxxcccccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccc
ccccccccccccccccccccxxxxcccccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccc
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

##############################################################################
# Antaeus (Cocytus) Cove by Grunt
##############################################################################

NAME:     coc_grunt_cove
ORIENT:   encompass
MONS:     shard shrike / nothing
MONS:     patrolling ice fiend, patrolling titan
KMONS:    8 = ice statue
KMONS:    9 = elemental wellspring
KFEAT:    9 = w
KFEAT:    X = open_sea
: serpent_of_hell_setup(_G)
SHUFFLE:  ABCEF
SUBVAULT: A : coc_grunt_cove_antaeus
SUBVAULT: B : coc_grunt_cove_fiend
SUBVAULT: C : coc_grunt_cove_fiend
SUBVAULT: E : coc_grunt_cove_fiend
SUBVAULT: F : coc_grunt_cove_fiend
: set_border_fill_type("open_sea")
: coc_setup(_G)
MAP
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwX
XwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwX
Xwwwwwwwwwww........wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwX
Xwwwwwwwwww...........wwwwwwwwwwwwwwwwwwwwwww......wwwwwwwwwwwwwwwwwwX
Xwwwwwwww........0.....wwwwwwwwwwwwwwwwwwww.........wwwwwwwwwwwwwwwwwX
Xwwwwwww.....0........wwwwwwwwwwwwwww......................wwwwwwwwwwX
Xwwwwww..............WWW.wwwwwwwww...........ccccc............wwwwwwwX
Xwwww.............wwwww...wwwwwwww..........ccccccc...2.......wwwwwwwX
Xwwww.....1......wwwwwww.wwwwwwww......ccccccccBBccc...........wwwwwwX
Xwww............wwwwwwwwWwwwww......ccccccccccBBBBcc$$.ccccc...wwwwwwX
Xwww...........wwwwwwwwwWWwww......cccccccccc.BBBBcc$$cccccc...wwwwwwX
Xww....1.....wwwwwwwwwwwWWw.......cccccccccc..cBBcccccccccccc...wwwwwX
Xww.........wwwwwwwwwwwwwW.......ccccccccccc.cccccccccccCCccc...wwwwwX
Xww........wwwwwwwwwwwwwwW......ccccAAccc......cccccc..CCCCccc..wwwwwX
Xw........wwwwwwwwwwwwww.....ccccccAAAA...c.....cc....cCCCCccc..wwwwwX
Xw..0....wwwwwwwwwwww.....cccccccccAAAAccccc....3..cccccCCcccc..wwwwwX
Xw......wwwwwwwwwwww.....cccccccccccAAccccccc......ccccccccccc..wwwwwX
Xw......wwwwwwwwwww....cccccccccccccccccccccc...8...cccccccccc..wwwwwX
Xw.....wwwwwwwwwwww...ccccccc.....cccccccccccc.......ccccccccc..wwwwwX
Xw.0..wwwwwwwwwwww....cccccc...........ccccccc..3.cc...ccEEccc..wwwwwX
Xw....wwwwwwwwwwww...ccccc...............ccccc...ccccc..EEEEcc..wwwwwX
Xw...wwwwwwwwwwww...ccccc.....www....3.....cc..c.cccccccEEEEcc..wwwwwX
Xww.wwwwwwwwwwww...ccccc...wwwwww...0......c..cc..cccccccEEccc..wwwwwX
Xww.wwwwwwwwwww...cccccc..wwwwwwww3..3.....c.ccc..cccccccccccc..wwwwwX
Xww.wwwwwwwwww...cccccc...wwwwwwwW.......0...cccc...cFFccccccc..wwwwwX
XwwWwwwwwwwww....ccccc...wwwww0wWwww...1.....ccccc..FFFFcccccc..wwwwwX
XwwWwwwwwwwww...ccccc....wwww.3.w9ww.0.3...2...cccccFFFFccccc...wwwwwX
XwwWwwwwwwww...ccccc....wwwww.0.wwwww....D......cccccFFcccc.....wwwwwX
Xww.wwwwwwww..cccccc...wwwwwWwwwwwwww........0...cccccccccc....wwwwwwX
Xww.wwwwwww...ccccc...wwwwwWwwwwwwwwwww...31........ccccccc....wwwwwwX
Xww..wwwwww..ccccc...wwwww..wwwwwwwwwww..0...3.3.....cccccc....wwwwwwX
Xww..wwwww...cccc...wwwwwww..wwwwwwwwwwwW.....0.......cccccc...wwwwwwX
Xww...wwww...cccc...wwwwwww..wwww...wwwWwww..3..........cccc....wwwwwX
Xww...wwww...ccc...wwwwwwww...www....wWwwww.............ccccc....wwwwX
Xww.0..ww...cccc...wwwwwwww.0.www.0...wwwwwwww...........cccc....wwwwX
Xww....ww...cccc..wwwwwwwww...www......wwwwwwwww.........ccccc...wwwwX
Xww..1.ww..ccccc..wwwwwwwww...www...1..wwwwwww9wwWwwwwww..cccc...wwwwX
Xww....ww..cccc..wwwwwwwww.1.wwwww......wwwwwwwwWwwwwwww..cccc...wwwwX
Xww..0.ww..cccc..wwwwwwww...wwwwwww..0..wwwwwwwWwwwwwwww..cccc...wwwwX
Xwww...ww..cccc..wwwwwww.0.wwwwwwww.....wwwww....wwwwwww..cccc...wwwwX
Xwww..www..cccc.wwwwwwww..wwwwwwwwww...wwwwww..0..wwwww...cccc...wwwwX
Xwwww.www..cccc.wwwwwwwWwwwwwwwwwwwwwwwwwwwww..3..wwwww..cccc....wwwwX
XwwwwWwwww.cccc.wwwww..wwwwwwwwwwwwwwwwwwwwwww....wwww...cccc...wwwwwX
XwwwwWwwww.ccc..wwww.0.wwwwwwwwwwwwwwwwwwwwwww..0.www....cccc...wwwwwX
XwwwwWwwww.ccc..wwww..wwwwwwwwwwwwww......wWWWw..wwww...cccc...WWW.wwX
Xwwww..www..cc.www..1.wwwwwwwwwwwwwW....1..wwwwwwwww....cccc...WW...wX
Xwww....www.cc.www....wwwwwwwww.0.Www.0....wwwwwwwww...ccccc..wwww.WwX
Xww..1..www....www.0..wwwwwww....wwwww.....wwwwwwww....cccc...wwwwWWwX
Xww....wwwww..wwwww..wwwwwww...1.wwwwww..0.wwwwwww....ccccc..wwwwwWWwX
Xww....wwwwwwwWwwwwwwww...0......wwwwww....wwwwww....ccccc..wwwwwwWWwX
Xw..0..wwwwwwwWwwwwwwwwww.......wwwwwwww...wwwww....cccccc..wwwwww..wX
Xw....wwwwwwwww.wwwwwwwwwwwwwwwwwwwwwwwwwwwwwww....cccccc..wwwwww...wX
Xww..wwwwwwwww.8.wwwwwwwwwwwwwwwwwwwwwwwwwwww.....cccccc...wwwww..0.wX
Xwww.wwwwwwwwww.wwwwwwwwwwwwwwwwwwwwwwwwwww.....ccccccc...wwwwww....wX
XwwwWwwww..wwwwwWwwww....wwwwwwwwwwwwww.......cccccccc...wwwwww....wwX
XwwwWWw..1.0wwwwwWw........wwwwwwww..........cccccccc...wwwwwww..0.wwX
XwwwwW.......wwwww....cc..............ccccccccccccc....wwwwwww.....wwX
Xwwww....0.1.wwwww..ccccccccc.....ccccccccccccccc.....wwwwwww......wwX
Xwwww.......wwwwww..cccccccccccccccccccccccccccc.....wwwwwww.......wwX
Xwww........wwwwwww..cccccccccccccccccccc..........wwwwwwww....1...wwX
Xwww........wwwwwwww......ccccccccccc............wwwwwwwww........wwwX
Xww........wwwwwwwww..........................wwwwwwwwwww........wwwwX
Xww.......wwwwww.wwwwww.................wwwwwwwwwwwwwwww....1..wwwwwwX
Xw........www.....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.........wwwwwwwX
Xw..{....WWW..0..1..wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww....0....wwwwwwwwX
Xw......wwww........WWW........wwww....0.....wwwww..0......wwwwwwwwwwX
Xww....wwwwwwwww..wwwww...0..1...WWWwww....1...WW.......wwwwwwwwwwwwwX
Xwwwwwwwwwwwwwwwwwwwwwww.........wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
ENDMAP

NAME:  coc_grunt_cove_antaeus
TAGS:  coc_grunt_cove_antaeus
MONS:  patrolling Antaeus, patrolling ice fiend
KITEM: $ = $ no_pickup
KITEM: O = icy rune of Zot
MAP
 $|
|1|$
$2O|
 |$
ENDMAP

NAME:  coc_grunt_cove_fiend
TAGS:  coc_grunt_cove_fiend allow_dup
MONS:  patrolling ice fiend
KITEM: $ = $ no_pickup
MAP
 $*
*1*$
$*|*
 *$
ENDMAP

##############################################################################
# coc.des: Special maps for Cocytus.
# Content: (mini)vaults particular to Cocytus, and/or first appearing in
#          Coc (order: Dis, Geh, Coc, Tar)
##############################################################################

{{
function coc_setup(e)
   e.place("Coc:7")
   e.tags("no_rotate")
   e.lflags("no_tele_control")
   e.kitem("$ = $ no_pickup")
   e.kmask('O = no_item_gen')
   e.kitem('O = icy rune of Zot')
   e.lrockcol("lightcyan")
   e.lfloorcol("white")
   e.lfloortile("floor_ice")
   e.colour("c = lightblue")
   e.set_feature_name("rock_wall", "ice-covered rock wall")
   e.set_feature_name("stone_wall", "ice-covered stone wall")
   e.set_feature_name("floor", "frost-covered floor")
end

-- the Serpent should appear in exactly one hell end
-- XXX: are things like shafts going to break this?
function hell_branches_remaining()
   local hell_branches = { "Geh", "Coc", "Dis", "Tar" }
   local ret = #hell_branches
   for _, branch in pairs(hell_branches) do
      if travel.find_deepest_explored(branch) == 7 then
         ret = ret - 1
      end
   end
   return ret
end

function serpent_of_hell_setup(e)
   if not you.uniques("the Serpent of Hell") and
      crawl.one_chance_in(hell_branches_remaining()) then
      e.kmons('D = the Serpent of Hell')
   end
end
}}

##############################################################################
# Cocytus
##############################################################################

NAME:   grunt_cocytus_stairs_refrigerator
TAGS:   transparent mini_float
DEPTH:  Coc:1-6
COLOUR: c = blue
MONS:   Ice Fiend, blizzard demon, ice devil, blue devil, white imp
KMONS:  H = ice statue
MAP
xwww..4......
xwwwwww..5...
xwwwwwwww..5.
xwwwwwwwww...
xcccccHwwww4.
cc.H.ccwwww..
c}..2.cWWWW4.
c]1...+.3.3..
c)..2.cWWWW4.
cc.H.ccwwww..
xcccccHwwww4.
xwwwwwwwww...
xwwwwwwww..5.
xwwwwww..5...
xwww..4......
ENDMAP

NAME:    nicolae_coc_three_mouths
DEPTH:   Coc, !Coc:$
TAGS:    transparent extra mini_float decor
SUBST:   j = >}}, b = >]], c = >))
SHUFFLE: )}]
MAP
              .....
             ..x....
            ..xx.....
            .xx....x.
            .x..j..x.
         ....xx...xx.
        ...xxxxx.xx..
  ..........xxxxxx..
 ..xxx..x...xxxxx..
..xx.xxxxx...xxxxx.
......xxxx.........
....b..xxxx........
......xxxx.........
..xx.xxxxx...xxxxx.
 ..xxx..x...xxxxx..
  ..........xxxxxx...
        ...xxxxx.xx..
         ....xx...xx.
            .x..c..x.
            .xx....x.
            ..xx.....
             ..x....
              .....
ENDMAP

NAME:    nicolae_coc_ice_floe
DEPTH:   Coc:2-, !Coc:$
SHUFFLE: {[(, }]}
NSUBST:  D = 1:1 / 2:2 / *:34
KMONS:   1 = Ice Fiend
KMONS:   2 = blizzard demon
KMONS:   3 = ice devil / blue devil
KMONS:   4 = white imp
MAP
      wwwwwwwww
    wwwwwwwwwwwww
   wwwwwxxxxxwwwww
  wwww.xx...xx.wwww
 wwww.....D.....wwww
 www.D..x...x..D.www
www....xx.D.xx....www
wwwx..xx.....xx..xwww
wwxxDxx...x...xxDxxww
wwx.....4xxx......xww
wwx.{.3.xxxxx.1.}.xww
wwx.....4xxx......xww
wwxxDxx...x...xxDxxww
wwwx..xx.....xx..xwww
www....xx.D.xx....www
 www.D..x...x..D.www
 wwww.....D.....wwww
  wwww.xx...xx.wwww
   wwwwwxxxxxwwwww
    wwwwwwwwwwwww
      wwwwwwwww
ENDMAP

NAME:    nicolae_coc_snowflakes_chance_in_hell
DEPTH:   Coc, !Coc:$
ORIENT:  centre
TAGS:    transparent extra decor
SHUFFLE: abcdefghi, jklmno, ABCDEFGH
SUBST:   abc : x, d : ., e : .x, f : .x, g : .x, h : .x, i : .x
SUBST:   jk : x, l : ., m : .x, n : .x, o : .x, p : .x, q : .x, r : .x, s : .x
SUBST:   AB : x, C : ., D : .x, E : .x, F : .x, G : .x, H : .x
MAP
      .............
   .........n.........
  .........mom.........
 ....H....lkxkl....H....
 ....GE....jxj....EG....
 ..HGFDB.i..p..i.BDFGH..
....EDCA.hg.q.gh.ACDE....
.....BA..fedxdef..AB.....
..........cbxbc..........
.....ihf...axa...fhi.....
...l..gec...r...ceg..l...
..mkj..dba.sxs.abd..jkm..
.noxxpqxxxrxxxrxxxqpxxon.
..mkj..dba.sxs.abd..jkm..
...l..gec...r...ceg..l...
.....ihf...axa...fhi.....
..........cbxbc..........
.....BA..fedxdef..AB.....
....EDCA.hg.q.gh.ACDE....
 ..HGFDB.i..p..i.BDFGH..
 ....GE....jxj....EG....
 ....H....lkxkl....H....
  .........mom.........
   .........n.........
      .............
ENDMAP

NAME:    nooodl_cocytus_jagged_cave
ORIENT:  northwest
DEPTH:   Coc, !Coc:$
TAGS:    extra decor
SUBST:   < : AB
NSUBST:  A = 1:{ / 1:} / *:.
NSUBST:  B = 1:[] / *:.
SUBST:   . : ...W, ' : .W, _ : .W, " : .W, ; : .W
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx<x
xxxxxxxxxx<xxxxxxxxxxxxxxxxxxxxxxxxxxxx..x
xxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxx.xx
xxxxxxxxxx..xxxxxxx<xxxxx<xxxxxxxxxxxx..xx
xxxxxxxxxxx.xxxxxx.xxxxxx.xxxxxxxxxxxx.xxx
xxxxxxxxxxx..xxxx..xxxxx..xxxxxxxxxxx..xxx
xxxxxxxxxxx..xxxx.xxxxx...xxxxxxxxxxx..xxx
xxxxxx<xxxxx..xx..xxxxx...xxx.xxxxxx..xxxx
xx'xxx.xxxxx..x..xxxxx...xxxx.xxxxxx..xxxx
xx'xxx.xxxxx.....xxxxx...xxx...xxxx..xxxxx
xx''xx.xxxxx....xxx"xx"".xxx;;.xxxx..xxxxx
x''''...xxxx....xxx"xx""xxx;;;;xx....xxxxx
x''''''''''x.__xxxx"xx""xx;;;;;;x;..xxxxxx
x''''''''''x___xxx""xx"x""";;;;;;;;.xxxxxx
x''''x'''''____xxx"""""""""";;;;;;;xxxxxxx
xx'''x''''____x___"""""""""";;;;;;;.xxxxxx
xx'''xx''_________"""""""""xx;;;;;;.xxxxxx
xx''xxx'__________"xx""x""""xxx;;;...xxxxx
xxxxxxxx__________xxx"""x"""..xxx;....xxxx
xxxxxxxxxx___x___xxx""""xx"....xxxxx..xxxx
xxxxxxxxxxx..xx_xxxx.....xx......xxxxx<xxx
xxxxxxxxxxxx.xxxxxx......xxx......xxxxxxxx
xxxxxxxxxxxxx<xxxxx..xx...xxx...xxxxxxxxxx
xxxxxxxxxxxxxxxxxx..xxxx..xxxx<xxxxxxxxxxx
xxxxxxxxxxxxxxxxxx<xxxxx...xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx..xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx@@xxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Antaeus; bottom of Cocytus.
##############################################################################

NAME:    coc_dpeg
ORIENT:  encompass
MONS:    patrolling Antaeus, Ice Fiend, ice dragon, blizzard demon
: serpent_of_hell_setup(_G)
ITEM:    any, any misc, any misc, any misc
SHUFFLE: O1dD / e4%H / f9*I / g8|J
KMONS:   d = ice devil w:5 / Ice Fiend / nothing w:5
KITEM:   d = any
SUBST:   ' = w ., 3 = 3 ., HIJ = 4 4 .
: coc_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwww.......wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwww....wwwww....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww''wwwwwwwwxxxx
xxxxwww...wwwwwwwwwww....wwwwwwwwwwwwwwwwwwwwwwwwwww'.......wwwwwwxxxx
xxxxwww.wwww......wwwwww..wwwwwwwwwwwwwwwwwwww'.......ccccc...wwwwxxxx
xxxxww..ww....cc......www.3wwwwwwwwwwwww..........ccccccccccc..wwwxxxx
xxxxw...ww..cccccc.....www.wwwwwwwwww'3..........cc....9.2..cc..wwxxxx
xxxxw..ww..ccccccc+c....ww.'wwwwwwwwwww''........ccf********cc..wwxxxx
xxxxw..ww..cc.%%%%.cc...ww..wwwwwwwwwwwwwwwww'....ccccccc+ccc...'wxxxx
xxxxw..ww..cc2.e0.2cc...ww..wwwwwwwwwwwwwwwwwwww'......c...c.....wxxxx
xxxxw..ww..cc.%%%%.cc..www.'wwwwwwwwwwwwwwwwwwwwwwwww'.cc+cc..I..wxxxx
xxxxw..www..c+cccccc...ww..wwwwwwwwwwwwwwwwwwwwwwwwwww..........wwxxxx
xxxxww..ww....ccccH...www.wwwwwwwwwwwwwwwwwwwwwwwwwwwww........wwwxxxx
xxxxww..www..........www..wwwwwwwwwwwwwwwwwwwwwwwwwwww'..2....w..wxxxx
xxxxwww..wwww......wwww..wwwwwwwwwwwwwwwwwwwwwwwwwww'............wxxxx
xxxxwwww..wwwwwwwwwwww..wwwwwwwwwwwwwwwwwwwwwwwwww..............wwxxxx
xxxxwwwww...'wwwwww....wwwwwwwwwwwwwwwwwwwwwwwwww............'wwwwxxxx
xxxxwwwwwwww........'wwwwwwwwwwwwwwwwwwwwwwww'3..'ww'....''wwwwwwwxxxx
xxxxwwwwwwwwwww'...3wwwwwwwwwwwwwwwwwwwwwwwwww''wwwwww''wwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwww........wwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwww............wwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwww...ccc+ccc...wwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwww...cc.....cc...wwwwwwwwwwwww'wwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwww...c.......c...wwwwwwwwwwwww3wwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwww...+.[.{.(.+...wwwwwwwwwwww...wwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwww...c.......c...wwwwwwwwwwww...wwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwww...cc.....cc...wwwwwwwwwww.....wwwwwwwwxxxx
xxxxwwwww'wwwwwwwwwwwwwwwwww...ccc+ccc...wwwwwwwwwwwww...wwwwwwwwwxxxx
xxxxwwwww3wwwwwwwwwwwwwwwwww............wwwwwwwwwwwwww...wwwwwwwwwxxxx
xxxxwwwww.wwwwwwwwww.3'wwwwwww.........wwwwwwwwwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww.wwwwwwww'.3.'wwwwwwwwwwwwwwwwwwww'wwwwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww.wwwwwwwww..'wwwwwwwwwwwwwwwwwwww'3wwwwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww..wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..wwwwwwwww.wwwwwwwwwwxxxx
xxxxwwwww..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww...w.wwwww..wwwwwwwwwxxxx
xxxxwwww'...wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww....www...w.wwwwwwwxxxx
xxxxwwww....'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..ccccccc.c.wwwwwwwwxxxx
xxxxwwww.....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..cc$$$$$$.c..wwwwwwwxxxx
xxxxwww'.....wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww..c.$dddd$.c...wwwwwwxxxx
xxxxwww..cc..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwww'...$$$$$$cc....wwwwwxxxx
xxxxwww.cccc..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww'.ccccccc....wwwwwwxxxx
xxxxwww.cg.cc..'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwww........ww..wwwwwwwxxxx
xxxxww'.c||.cc...'wwwwwwwwwwwwwwwwwwwwwwwwwwww.D...wwwwwwwwwwwwwwwxxxx
xxxxww'.c||.8cc......'wwwwwwwwwwwwwwwwwwwwwww'...w..wwwwwwwwwwwwwwxxxx
xxxxww..c||2.2cc..J.....'wwwwwwwwwwwwwwwwww....wwww..wwwwwwwwwwwwwxxxx
xxxxww..c||....cc..............3'wwwwwwwww...wwwwwww.....wwccccwwwxxxx
xxxxww..cccc+ccccc..........wwwwwwwwwww'3...wwwwwwwwwww.2.cc2dccwwxxxx
xxxxwww..ccc...........'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.+dd1Ocwwxxxx
xxxxwwww......'wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcc2dccwwxxxx
xxxxwwwwww'''wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwccccwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Original Antaeus level
##############################################################################

NAME:    coc_old
ORIENT:  encompass
MONS:    patrolling Antaeus, Ice Fiend, ice dragon, blizzard demon
KMONS:   0 = ice devil w:14 / blue devil / freezing wraith w:6 / \
         ice dragon skeleton w:8 / anaconda simulacrum w:6 / \
         titan simulacrum w:6 / hydra simulacrum w:6 / storm dragon simulacrum w:6
SHUFFLE: 1O26Y$| / 8~47Z%*, 1O, 8~, $|, %*, {[( / <<<
SHUFFLE: 5X, 6Y, 7Z, AB
SUBST:   % = $, ~ = |, 3 = 3:6 .:4
SUBST:   567 = 2, X8 = 4, AYZ = ., B = c , < = .
: serpent_of_hell_setup(_G)
KFEAT:   D = deep_water
NSUBST:  H = 1:D / *:w
NSUBST:  0 = 20:0 / 4:0. / 4:0.. / *:.
: coc_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx........................xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx............00............xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx.............00.............xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx..............00..............xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx................................xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx....ccAAccccccccccccccccccBBcc....xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx....cccAAccccccccccccccccccBBccc....xxxxxxxxxxxxxxx
xxxxxxxxxxxxxx....cc........X........5........cc....xxxxxxxxxxxxxx
xxxxxxxxxxxxx....cc............................cc....xxxxxxxxxxxxx
xxxxxxxxxxxx....cc...wwwwwwwwwwwwwwwwwwwwwwww...cc....xxxxxxxxxxxx
xxxxxxxxxxx....cc...wwwwwwwwwwwwwwwwwwwwwwwwww...cc....xxxxxxxxxxx
xxxxxxxxxx....cc...wwwwwwwwwwwwwwwwwwwwwwwwwwww...cc....xxxxxxxxxx
xxxxxxxxx....cc...ww.......3....33....3.......ww...cc....xxxxxxxxx
xxxxxxxx....cc...ww............................ww...cc....xxxxxxxx
xxxxxxx....cc0..ww....cccccccccccccccccccccc....ww..0cc....xxxxxxx
xxxxxx....cc0..ww....cccccccccccccccccccccccc....ww..0cc....xxxxxx
xxxxx....cc...ww....cc......................cc....ww...cc....xxxxx
xxxx....cc...ww...0cc...T................T...cc0...ww...cc....xxxx
xxx....cc...ww...0cc..........wwwwww..........cc0...ww...cc....xxx
xx....cc...ww....cc.......wwwwwwwwwwwwww.......cc....ww...cc....xx
xx....cc...ww...cc.....wwwwwwwwwwwwwwwwwwww.....cc...ww...cc....xx
xx....cc..www..cc....wwwwwcccccwwwwcccccwwwww....cc..www..cc....xx
xx....cc..www.cc....wwwwwcc%%cccwwccc$$ccwwwww....cc.www..cc....xx
xx....cc..www.cc...wwwwwwc%%%+4cccc2+$$$cwwwww....cc.www..cc....xx
xx....cc..www.cc...wwwwwwc%%%c+cccc+c$$$cwwwwww...cc.www..cc....xx
xx.00.cc..www3.c..wwwwwwwccccc.7cc6.cccccwwwwwww..c.3www..cc.00.xx
xx.00.cc..wwww.c.wwwwwww3+.Z.+.8cc1.+.Y.+3wwwwwww.c.wwww..cc.00.xx
xx.00.cc..wwww3c.wwwwwww3+.Z.+.~ccO.+.Y.+3wwwwwww.c3wwww..cc.00.xx
xx....cc..wwww.c.wwwwwwwwccccc.7cc6.cccccwwwwwwww.c.wwww..cc....xx
xx....cc..www3.c..wwwwwwwc***c+cccc+c|||cwwwwwww..c.3www..cc....xx
xx....cc..www.cc...wwwwwwc***+4cccc2+|||cwwwwww...cc.www..cc....xx
xx....cc..www.cc...wwwwwwcc**cccwwccc||ccwwwwww...cc.www..cc....xx
xx....cc..www.cc....wwwwwwcccccwwwwcccccwwwwww....cc.www..cc....xx
xx....cc..www..cc....wwwwwwwwwwwwwwwwwwwwwwww....cc..www..cc....xx
xx....cc...ww...cc.....wwwwwwwwwwwwwwwwwwww.....cc...ww...cc....xx
xx....cc...ww....cc.......wwwwwwwwwwwwww.......cc....ww...cc....xx
xxx....cc...ww....cc..........wwwwww..........cc....ww...cc....xxx
xxxx....cc...ww....cc...T................T...cc....ww...cc....xxxx
xxxxx....cc...ww....BB........5....X........AA....ww...cc....xxxxx
xxxxxx....cc...ww....cBccccccccccccccccccccAc....ww...cc....xxxxxx
xxxxxxx....cc...ww....BccccccccccccccccccccA....ww...cc....xxxxxxx
xxxxxxxx....cc...ww............0000............ww...cc....xxxxxxxx
xxxxxxxxx....cc...ww..........................ww...cc....xxxxxxxxx
xxxxxxxxxx....cc...wwwwwwwwwwwww..wwwwwwwwwwwww...cc....xxxxxxxxxx
xxxxxxxxxxx....cc...wwwwwwwwwHww..wwHwwwwwwwww...cc....xxxxxxxxxxx
xxxxxxxxxxxx....cc...wwwwwwwwwww..wwwwwwwwwww...cc....xxxxxxxxxxxx
xxxxxxxxxxxxx....cc............................cc....xxxxxxxxxxxxx
xxxxxxxxxxxxxx....cc...........0000...........cc....xxxxxxxxxxxxxx
xxxxxxxxxxxxxxx....cccccccccccccccccccccccccccc....xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx....cccccccccccccccccccccccccc....xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxx..............0000..............xxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxx..............................xxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxx............................xxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx..........<{<[<(..........xxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Antaeus (Cocytus) by Mu
##############################################################################

NAME:    coc_mu
ORIENT:  encompass
MONS:    patrolling Antaeus, ice fiend
MONS:    ice devil, blue devil, white imp, ice dragon, freezing wraith
KMONS:   8 = ice statue
KMONS:   9 = frost giant
KMONS:   0 = blizzard demon
SUBST:   3 = 3:5 4:4 5:4 6:2 7:2 9:1
NSUBST:  0 = 5:0 / *:3
KMONS:   E = water elemental
KFEAT:   E = w
: serpent_of_hell_setup(_G)
SUBST:   ^ : ^^^W. , ^ = W.
SUBST:   X : XXXc. , X = c.
SUBST:   " = 8:1 .:10
SUBST:   ' = .cc
SUBST:   ; : ;;;;w , ; = wc
KFEAT:   # = permarock_wall
COLOUR:  # = lightcyan
NSUBST:  - = 1:+ / *:c
: coc_setup(_G)
MAP
########################################################################
#ccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccc#
#ccccc............wwwwwwwwwwwwwwwwxxxxxxxxxwwwwwww......3......cccccccc#
#cccccc............wwwwwwwwwwwwwwwwxxxxxxxxxwwwwwww......3......ccccccc#
#ccccc..............wwwwwwwwwwwwwwwwxxxxxxxwwwwwwww.3.3......c...cccccc#
#cccc...wwwwwwwwww..wwwwwwwwwwwEwwwwwwxxxwwwwwwww....3.0D.cccc....'.ccc#
#cccc..wwwwww8www.2.wwwwwwwwwEw.wEwwwwwwwwwwwwww...3..3...cccccc..c..cc#
#cccc..ww|$|www.......wwwwwwww...wwwwwwwwwwwwwwwww......ccccccc...cc.cx#
#cccc..ww$O$....1..............8..Ewwwwwwwwwwwwwwww...ccccc"c"c.cccc.'x#
#cccc..ww|$|www.......wwwwwwww...wwwwwwwwwwwwwwwwww.cccccc....c.cccc.'x#
#cccc..wwwwww8www.2.wwwwwwwwwEw.wEwwwwwwwwwwwwwwwwwccccc...cc...."cc.cx#
#cccc...wwwwwwwwww..wwwwwwwwwwwEwwwwwwwwwwwwwwwwwccccccc...ccccccccc.cx#
#ccccc..............wwwwwwwwwwwwwwwwwwwwwwwwwwwwcccccc......cccccc"...x#
#cccccc............wwwwwwwwwwwwwwwwwwwwwwwwwwwwccccc..........ccccc"c.x#
#ccccccccccccccccccwwwwwwwwwwwwwwwwwwwwwwwwwwccccc.....3330.....ccccc.x#
#cccccccccccccccccccwwwwwwwwwwwwwwwwwwwwwwwwcccc........333.......cc..x#
#ccccccccccccccccccc;wwwwwwwwwwwwwwwwwwwwwwcccc..................ccc.cx#
#ccccccc"c$.....ccc;cccwwwwwwwwwwwwwwwwwwccccc....................cc.cx#
#cccccc$$$$........cccccccwwwwwwwwwwwwwcccccc....cccccc..........ccc..x#
#cccc"$$$$0..........ccccccccwwwwwwwcccccccc....cccccccc.......cccc...x#
#ccccc$$$..............ccccccccccccccccccc.....cccccwwwcc....cccccc...x#
#cccccc$$................ccccccccccccccc.....cccccccwwwwwwwcccccccc...x#
#ccccccc$...........wwwwwwwccccccccccc......ccccc.cwwcwwwwccccccccc..cx#
#cccccccc.........wwwwwwwwwwww.............cccc333333cccwccccX.-....ccc#
#cccccccccc.....wwwwwwwwwwwwwwww........ccccccc.2....cccc-XXX..cc...ccc#
#cccccccccccccwwwwwwwwwwwwwwwwwwww.......cccccc333333ccccX....ccccX..cx#
#cccccccccccwwwwww..wwwwwwwwwwwwwww........ccccc$$$$$cccX.0..cccccX...x#
#ccccccccccwwwwww..wwwwwwwwwwwwwwwww........cccc*|*ccccc|.....ccc.....x#
#cccccccccwwwwww..www...wwwwwwwwwwwwwww......ccccccccccc|......-......x#
#cccccc..cwwww....ww....www..wwwwwwwwwwww.....ccccccccccX.2..Xcc......x#
#cccccc..www..ccccww...www..wwwwwwwwwwwwwwww..cccccccccccXXXXcccc.....x#
#ccccc...ww..cc...ww...ww...ww..wwwwwwwwwwwwwwwcccccccccccccccccww...cx#
#ccccc..www.cc....www...www.ww.wwwwwwwwwwwwwwwwwwccccccccccccwwwwwwwccc#
#cccc...ww..c*..3..www..wwwwwwww^^^wwwwwwwwwwwwwwwccccccwwwccwwwwwwcccc#
#cccc...ww..c|233..cwwwwwwwwwwwcc+ccccccccccccwwwwwccccwwwwwwwwwwwwcccc#
#cccc..www..c*.33..c.wwwwwwwwwcc...2...$$$*||ccwwwwwwwwwwwwwwwwwwwccccc#
#cccc..www..cc..3.cc....wwwww.cc..33..$$$$*||cc..wwwwwwwwwwwwwwwwcccccc#
#cccc..wwww..cc$$cc....wwwwww.cc2333.$$$$$*||cc...wwwwwww^^wwwwwwcccccc#
#cccc..wwww...cccc....wwwwww...ccccccccccccccc.....wwwww^..^wwwwwwccccc#
#cccc..wwwww........wwwwww......................c.wwwww^.6..^wwwwwccccc#
#cccc..wwwwwww....wwwwwww......................cccwwwww^....^wwwwwwcccc#
#cccc..wwwwwwwwwwwwwwww........................cccwwwwww^..^wwwwwwwcccc#
#cccc...wwwwwwwwwwwww.........333.............ccc.wwwwwww^^wwwwwwwwcccc#
#ccccc..wwwwwwwwwwww............03............ccc.wwwwwwwwwwwwwwwww.ccc#
#ccccc...wwwwwwwww.........ccc....333........cccc..wwwwwwwwwwwwwwww.ccc#
#ccwccc....wwwww........cccccccc....03......ccccc..wwwwwwwwwwwwwww..ccc#
#ccwwc;ccc...........ccccccccccccc....333..ccccccc..wwww^wwwwwwww..cccc#
#cccwwwcccccc.....cccccccccccccccccccc...ccccccccc..www^.wwwwww....cccc#
#cccwwwwwcccccccccccccccccccccccccccccc;ccccccccccc..wwwwwwwww...3ccccc#
#ccc.wwwwwcccccccccccccccccccccccccccccc;cccccccccccwwwwwwwwww..3cccccc#
#cc...wwwwwww..ccccccccccccc...........wwXXXXXccccccXwwwwwwwww...3ccccc#
#xc....wwwww.....cccccccc...3.....wwwwwwwwwwwwXXccccXwwwwwwwwww....cccc#
#x..{..wwww...333...............wwwwwwwwwwwwwwwwXcccXwwwwwwwwwwww..cccc#
#x......www....333...........wwwwwwwwwwwwwwwwwwwwXccXwwwwwwwwwwwww..ccc#
#x.[....wwww.............wwwwwwwwwwwwwwwwwwwwwwwwwXXwwwwwwwwwwwwwww.ccc#
#x.......wwwww.......wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.^www.ccc#
#x..(....wwwwwwwwwwwwwwwwwwwwwwwwwwwwww....wwwwwwwwwwwwwwwwww^.wwwwcccc#
#xc.......wwwwwwwwwwwwwwwwwwwwwwwwww..........wwwwwwwwwwwwwwww^wwwwcccc#
#ccc.......wwwwwwwwwwwwwwwwwwwww........333......wwwwwwwwwww^wwwwwwcccc#
#ccc.........wwwwwwwwwwwwwwwww.........33033.......wwwwwww^...wwwwccccc#
#cccccc.........wwwwwwwwwww............................wwww^^^wwwwccccc#
#cccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxcccc#
########################################################################
ENDMAP

##############################################################################
# Antaeus (Cocytus) by Grunt
##############################################################################

NAME:    coc_grunt
ORIENT:  encompass
MONS:    patrolling Antaeus, Ice Fiend, ice dragon / nothing
MONS:    ice devil w:6 / blue devil w:4 / bog body w:4 / ice dragon w:3 / \
         freezing wraith w:2 / frost giant w:1
KMONS:   8 = ice statue
KMONS:   de = Ice Fiend
KMONS:   fg = blizzard demon
KITEM:   df = superb_item
KITEM:   egh = gold
: serpent_of_hell_setup(_G)
KFEAT:   D = floor
KFEAT:   X = open_sea
: set_border_fill_type('open_sea')
SHUFFLE: 1O|d$e / 5!*f%g, |d / $e
NSUBST:  H = 1:D / *:8
SUBST:   * = |, % = $, 5 = 2
: set_border_fill_type("open_sea")
: coc_setup(_G)
MAP
XXXXXXXXXXXcccccccccccXXXXXXXXXXXcccccccccXXXXXXXXXXXXXXX
XwwwwwwwwwwwcccccccccwwwwwwwwwwwwwcccccccwwwwwwwwwwwwwwwX
XwwwwwwwwwwwcccccccccwwwwwwwwwwwwwwcccccwwwwwwwwwwwwwwwwX
XwwwwwwwwwwwwccccccccwwwwwwwwwwwwwwccccwwwwwwwwwwwwwwwwwX
Xwwww...wwwwwccccccccwwwwwww.[.wwwwwcccwwwwwww...wwwwwwwX
Xwwww....wwwwwccccccwwwwww.......wwwcccwwwwww......wwwwwX
Xwww...4..wwwwccccccwwwwww.{...(.wwwcccwwww.........wwwwX
Xwww.......wwwccccccwwwww....w....wwcccwww.....4.....wwwX
Xww..cc+cc..wwwcccccwwww..4.wwww.4wwcccww.............wwX
Xw..cc*.%cc..wwcccccwwww....wwwww..wcccw.....cc+cc.....wX
Xw..c**.%%c..wwcccccwwww.4.wwwwww.4wwccw....cc|.$cc....wX
Xw.3cf*5%gc3.wwcccccwwww...wwwwww..wwccw....c||.$$c....wX
Xw..c*f.g%c..wwcccccwwww...wwcwwwwwwwccw..3.cd|1$ec.3..wX
Xw..cc*!%cc..wwcccccwwwww.wwwcwwwwwwwccw....c|d.e$c....wX
Xww.4ccccc4.wwwccccwwwwwwwwwwcwwww.wwccw....cc|O$cc...wwX
Xwww...3....wwwccccwwwwwwwwwwcwwww.wwccww....ccccc....wwX
Xwwww......wwwcccccwwwwwwwwwwcwwww..wwccww.4.......4.wwwX
Xwwwww....wwwwccccwwwwww..wwwcwww...wwccww.....3.....wwwX
Xwwwww....wwwwccccwwwwww.4.wwcwww....wcccww.........wwwwX
Xwwwww....wwwwccccwwwww....wwcwww....wccccwwww.....wwwwwX
Xwwwww...4.wwwccccwwwww.3..wwcwww.4..wwcccwwwwwwwwwwwwwwX
Xwwwww.....wwwccccwwwww...wwwcwww....wwccccwwwwwwwwwwwwwX
Xwwwwww3...wwwccccwwwww4..wwwcwww..3..wccccwwwwww...wwwwX
Xwwwwww....wwwcccccwww...wwwwcwww.....wwccccwwwww4...wwwX
Xwwwwwww.4.wwwcccccwwww..wwwwcwww...4.wwcccccwwww..3.wwwX
Xwwwwwwww.wwwwcccccwwww.wwwwwcwwww....wwcccccwwww....wwwX
Xwwwwwwwwwwwwwcccccwwwwwwwwwwccwww.....wcccccwwwww.4.wwwX
Xwwwwwwwwwwwwwcccccwwwwwwwwwwccwwww....wcccccwwwwww..wwwX
Xwwwwwwwwwwwwwccccwwwwwwwwwwwcccwwww..wwcccccwwwwww..wwwX
Xwwwwwwwww.wwwccccwwwww..wwwwcccwwww..wwwccccwwwwwwwwwwwX
Xwwwwwwwww.wwwccccwwwww...wwwcccwwww..wwwcccccwwwwwwwwwwX
Xwwwwwwww...wwwcccwwww....wwwcccwwww..wwwcccccwwwwwwwwwwX
Xwwwwwwww...wwwcccwwww....wwwcccwwww.wwwwcccccwwwww..wwwX
Xwwwwwwww....wwccwwwww....wwwcccwwwwwwwwwcccccwwwww..wwwX
Xwwwwwwww....wwccwwwww....wwwcccwwwwwwwwwcccccwwwww..wwwX
Xwwwwwwww....wwccwwwww....wwwcccwwww.wwwwcccccwwww....wwX
Xwwwwwww...4.wwccwwww....wwwwcccwwww.wwwwcccccwwww....wwX
Xwwwwwww.....wwwcwwww....wwwwcccwww...wwwwcccwwwww..4.wwX
Xwwwwwww..3..wwwcwwww..4.wwwwcccwww....wwwcccwwwww....wwX
Xwwwwwww.....wwwcwwww....wwwccccww.....wwwcccwwwww.3.wwwX
Xwwwwwww.4...wwwcwwww.3..wwwccccww.....wwwcccwwww....wwwX
Xwwwwwww.....wwwcwwww....wwwccccww..4..wwwcccwwww.4..wwwX
Xwwwwwww....wwwwcwww.4...wwwccccww....wwwwcccwwww...wwwwX
Xwwwwwwww...wwwwcwww....wwwwccccww.3..wwwwcccwwww...wwwwX
Xwwwwwwww...wwwwcwww....wwwwccccww....wwwwcccwwww..wwwwwX
Xwwwwwwww...wwwwcwww....wwwwcccwww.4.wwwwwcccwwww..wwwwwX
Xwwwwwwwww.wwwwwcwww....wwwwcccwww...wwwwwcccwwww...wwwwX
Xwwwwwwwwwwwwwwccwww....wwwwcccwww...wwwwwcccwwww...wwwwX
Xwwwwwwwwwwwwwwccwww...wwwwwcccwww...wwwwwcccwwww...wwwwX
Xwwwwwwwwwww.wwccwwww..wwwwwcccwwww..wwwwwcccwwww..wwwwwX
Xwwwwwwwwwww.wwccwwww.wwwwwwcccwwww..wwwwwcccwwwwwwwwwwwX
Xwwwwwwwwww..wwccwwww.wwwwwwcccwwww...wwwwwccwwwwwwwwwwwX
Xwwwwwwwwww..wwccwwwwwwwwwwwcccwwwww..wwwwwccwwwwwwwwwwwX
Xwwwwwwwwww..wwcwwwwwwwwwwwwcccwwwwwwwwwwwwccwwwwwwwwwwwX
Xwwwwwwwwww..wwwwwwwww.wwwwwcccwwwwwwwwwwwwccww.wwwwwwwwX
Xwwwwwwwwww...wwwwwww..wwwwwccccwwwwwwwwwwwccww..wwwwwwwX
Xwwwwwwwwww...wwwwwww..wwwwcccccww.wwwwwwwwwwww..wwwwwwwX
Xwwwwwwwwwww.4.wwww....wwwwcccccww..wwwwwwwwww..wwwwwwwwX
cwwwwwwwwwww....ww..4..wwwwcccccww....wwwwwww...wwwwwwwwX
cwwwwwwwwwwww..2......WWwwwWcccWwwW............wwwwwwwwwX
cccwwwwwwwwwww...H...wWWWWWWWWWWWWWW.4.....4..wwwwwwwwwwX
ccccwwwwwwwwwww.....wwwwWWWWWWWWWWww...2.H...wwwwwwwwwwwX
cccccwwwwwwwwwwwwwwwwwwwwcWWWWWcwwwww.......wwwwwwwwwwwwc
cccccccwwwwwwwwwwwwwwwwwwcc3..3cwwwwwww....wwwwwwwwwwwwcc
ccccccccwwwwwwwwwwwwwwwwwccchhccwwwwwwwwwwwwwwwwwwwwwwwcc
cccccccccwwwwwwwwwwwwwwwwccchhccwwwwwwwwwwwwwwwwwwwwwcccc
cccccccccccwwwwwwwwwwwwwwcccccccwwwwwwwwwwwwwwwwwwwcccccc
ccccccccccccwwwwwwwwwwwwwcccccccwwwwwwwwwwwwwwwwwwccccccc
cccccccccccccwwwwwwwwwwwwcccccccwwwwwwwwwwwwwwwwwcccccccc
ccccccccccccccXXXXXXXXXXcccccccccXXXXXXXXXXXXXXcccccccccc
ENDMAP

##############################################################################
# Antaeus (Cocytus) by HangedMan
##############################################################################

NAME:    coc_hangedman
TAGS:    no_pool_fixup
ORIENT:  encompass
MONS:    patrolling Antaeus, ice fiend, blizzard demon
MONS:    ice devil w:12 / blue devil / bog body
MONS:    anaconda simulacrum / titan simulacrum / \
         hydra simulacrum / storm dragon simulacrum
KMONS:   6 = ice statue
KMONS:   7 = patrolling kraken simulacrum w:15 / \
             patrolling elemental wellspring band
KMONS:   89 = ice dragon / frost giant w:4
KMONS:   & = patrolling ice fiend
: serpent_of_hell_setup(_G)
KITEM:   d = $ no_pickup
KFEAT:   d67& = W
KFEAT:   8 = w
SHUFFLE: O1AB / eEFG, {([, !?^~
SUBST:   AE = 2, B = 2333, F = 339, G = 995, !? = &,  ^~ = 6
SUBST:   e = |, y : yY, y = xxw, Y = xww
NSUBST:  = = 1:+ / c, - = 1:+ / c, 6 = 3:6 / 2:677 / *:7
: coc_setup(_G)
MAP
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
cccccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxcccccccccccccccccccccccccccccc
cccccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxcccccccccccccccccccccccccccccc
cccccccxxwyyyyyyyyyyyyyyyyyyyyyyyyyyywxxcccccccccccccccccccccccccccccc
cccccccxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxcccccccccccccccccccccccccccccc
cccccccxxW8wwwwwwwwwwwwwwwwwwwwwwwwwwwxxcccccccccccccccccccccccccccccc
cccccccxxdwwwwwwwww..w..wwwwwwwwwwwwwWxxcccccccccccccccccccccccccccccc
cxxxxxxxxdwwwwwwww.cc.cc.wwwwwwwwwwwwWxxxxxxxxcccccccccccccccccccccccc
cxxxxxxxxd!wwwwww.c..c..c.wwwwwwwwwwww6xxxxxxxcccccccccccccccccccccccc
cxxwwWddddwwwwww.c.......c.wwwwwwwwwwwwWWwwwxxcccccccccccccccccccccccc
cxxyw8ww?wwwwww.c....5....c.wwwwwwwwwwwwwwwyxxcccccccccccccccccccccccc
cxxywwwwwwwwwww.c...535...c.wwwwwwwwwwwwwwwyxxcccccccccccccccccccccccc
cxxywwwwwwwwwww.c...x5x...c.wwwwwwwwwwwwwwwyxxcccccccccccccccccccccccc
cxxywwwwwwwwwww.c.........c.wwwwwwwwwwwwwwwyxxcccccccccccccccccccccccc
cxxywwwwwwwwwwww.c.......c.wwwwwwwwwwwwwwwwyxxcccccccccccccccccccccccc
cxxywwwwww....wWw.c.....c.wWw....wwwwwwwwwwyxxcccccccccccccccccccccccc
cxxywwwww.cccc.w.cccc-cccc.w.cccc.wwwwwwwwwyxxcccccccccccccccccccccccc
cxxywwww.c....c.cccc...cccc.c....c.wwwwwwwwyxxcccccccccccccccccccccccc
cxxywww.c......cccB.....Bccc......c.wwwwwwwyxxcccccccccccccccccccccccc
cxxyww.c........cc....x|.cc........c.wwwwwwyxxcccccccccccccccccccccccc
cxxyww.c...5x...c...1xAx.cc...x4...c.wwwwwwyxxcccccccccccccccccccccccc
cxxywww.c.535...-...xxx|$cc....4..c.wwwwwwwyxxcccccccccccccccccccccccc
cxxyww.c...5x...c..xAxO$|cc...x4...c.wwwwwwwxxcccccccccccccccccccccccc
cxxyww.c........cc.|x|$B|cc........c.wwwwww^xxcccccccccccccccccccccccc
cxxywww.c......cccB..$||cccc......c.wwwwwwwWxxxxxxxxxxxxxxxxxxcccccccc
cxxywwww.c....c.ccccccccccc.c....c.wwwwwwwwWxxxxxxxxxxxxxxxxxxcccccccc
cxxywwwww.cccc.w.ccccccccc.w.cccc.8wwwwwwwwWWW^wyyyyyyyyyyywxxcccccccc
cxxywwwwww....wWw.c.....c.www....wwwwwwwwwwwwwwwwwwwwwwwwwwwxxcccccccc
cxxywwwwwwwwwwww.c.......c.w.Wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxcccccccc
cxxywwwwwwwwwww.c.........c.W.Wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxcccccccc
cxxywwwwwwwwwww.c...x.x...c.wW.WwwwwwwwwwwwwwwwwwwwwwwwwwwwWxxxccccccc
cxxywwwwwwwwwww.c...444...c.wwW.WxxwxxwwwwwwwwwwwwwwwwwwwwwWxxxxxxxxxc
cxxywwwwwwwwwww.c.........c.wwwWx..x.Dxwwwwwwwwwwwwwwwwwwwww6xxxxxxxxc
cxxywwwwwwwwwwww.c.......c.wwwwx......xwwwwwww..w..wwwwwwwwwwWWwwwwxxc
cxxywwwwwwwwwwwww.c..c..c.8wwwwx...W.xwwwwwww.cc.cc.wwwwwwwwwwwwwwyxxc
cxxywwwwwwwwwwwwww.cc.cc.wwwwwwwx.W...xwwww8.c..c..c.wwwwwwwwwwwwwyxxc
cxxwwwwWWwwwwwwwwww..w..wwwwwwwx......xwwww.c.......c.wwwwwwwwwwwwyxxc
cxxxxxxxx6wwwwwwwwwwwwwwwwwwwcwx..x..xWwww.c.........c.wwwwwwwwwwwyxxc
cxxxxxxxxxWwwwwwwwwwwwwwwwwwccW.xxwxxW.Www.c...444...c.wwwwwwwwwwwyxxc
cccccccxxxWwwwwwwwwwwwwwwwwccW.WwwwwwwW.Ww.c....x....c.wwwwwwwwwwwyxxc
ccccccccxxwwwwwwwwwwwwwwwwccW.WccwwwwwwW.W.c.....x...c.wwwwwwwwwwwyxxc
ccccccccxxwwwwwwwwwwwwwWWccW.WccwwwwwwwwW.w.c.......c.wwwwwwwwwwwwyxxc
ccccccccxxwwwwwwwwwwwwWcccW.Wccwwwwww....www.c.....c.wWw....wwwwwwyxxc
ccccccccxxwyyyyyyyyywW~cWW.Wccwwwww8.cccc.w.ccccccccc.w.cccc.wwwwwyxxc
cccccccccccccccccccccccc.2Wccwwwwww.c....c.ccccccccccc.c....c.wwwwyxxc
ccccccccccccccccccccccc...WcWwwwww.c......cccc||$..Gccc......c.wwwyxxc
ccxx8w.......xx44........cccWwwww.c........cc||$Gx|.cc........c.wwyxxc
ccxx8w.......xxx.x......cc~Wwwwww.c...4....cc|$exFx..c....5x..c.wwyxxc
ccxxww.wxx....xxxwx.....ccWwwwwwww.c..4x...cc$Gxxx...=...535.c.wwwyxxc
ccxxx...wxx....xxwwx....ccwwwwwww.c...4.x..cc.xFxE...c...x5...c.wwyxxc
cccxxx...wxx..xwwww.x...ccywwwwww.c........cc.|x....cc........c.wwyxxc
ccccxxx...wxx..xww.wwx..ccywwwwwww.c......cccG.....|ccc......c.wwwyxxc
cccccxxx...wxx..x.wwwwx.ccywwwwwwww.c....c.cccc...cccc.c....c.wwwwyxxc
ccccccxxx...wxx..xwwxx.4ccywwwwwwwww.cccc.w.cccc=cccc.w.cccc.wwwwwyxxc
cccccccxxx...wxx..xwxxx4ccywwwwwwwwww....wWw.c.....c.wWw....wwwwwwyxxc
ccccccccxxx[..wxx..x.xxxccywwwwwwwwwwwwwwww.c.......c.wwwwwwwwwwwwyxxc
cccccccccxxx.{.wxx....xxccywwwwwwwwwwwwwww.c.........c.wwwwwwwwwwwyxxc
ccccccccccxxx...wxx.....ccywwwwwwwwwwwwwww.c....5x...c.wwwwwwwwwwwyxxc
cccccccccccxxx(..wxx....ccywwwwwwwwwwwwwww.c...535...c.wwwwwwwwwwwyxxc
ccccccccccccxxx...wxx...ccwwwwWWwwwwwwwwww.c...x5....c.wwwwwWd!wwwyxxc
cccccccccccccxxx...wxx..ccxxxxxx6wwwwwwwwww.c.......c.wwwwwWddd8wwwxxc
ccccccccccccccxxx...wx..ccxxxxxxxWwwwwwwwwww.c..c..c.wwwwwwddxxxxxxxxc
cccccccccccccccxxx...w..ccccccxxxWwwwwwwwwwww.cc.cc.wwwwwww?dxxxxxxxxc
ccccccccccccccccxxx.....cccccccxxwwwwwwwwwwwww..w..wwwwwwwww8xxccccccc
cccccccccccccccccxxx.wwwcccccccxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxccccccc
ccccccccccccccccccxxxw88cccccccxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxccccccc
cccccccccccccccccccxxxxxcccccccxxwyyyyyyyyyyyyyyyyyyyyyyyyyywxxccccccc
ccccccccccccccccccccxxxxcccccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccc
cccccccccccccccccccccccccccccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccc
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

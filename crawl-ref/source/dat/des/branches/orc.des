##############################################################################
# orc.des: Entries to the Orcish Mines, and (mini)vaults which appear _only_
#          in the Mines. General (mini)vaults, which can also appear in the
#          Mines, should go to vaults.des.
##############################################################################

##############################################################################
# Orcish Mines entries
##############################################################################
# Dummy Orc entry

NAME:   orc_0_dummy
TAGS:   orc_entry
WEIGHT: 50
ORIENT: float
KFEAT:  O = enter_orcish_mines
MAP
O
ENDMAP

##############################################################################
# Freedom of Choice
#
NAME: david_orc_1_choice
TAGS: orc_entry
MONS: orc, warg / orc priest / orc warrior, orc warrior / orc
ITEM: % / scroll of teleportation / scroll of remove curse / scroll of identify
ITEM: % / w:2 * / scroll of blinking / scroll of recharging / scroll of fear
SHUFFLE: %1
ORIENT: float
KFEAT: O = enter_orcish_mines
MAP
           xxx
        xxxxOxxxx
     xxxx.......xxxx
   xxx.............xxx
  xx....x.......x....xx
 xx....xxx.....xxx....xx
xx.....xxx.I.I.xxx..I..x
xx......x.......x......xx
x..x.x..x..x+x..x..x+x..x
x.xx.xx.x.xx.xx.x.xx.xx.x
x.x%1.x.x.x2e2x.x.x3d3x.x
x.x.%1x.x.x2e2x.x.xd3dx.x
x.xx.xx.x.xx.xx.x.xx.xx.x
x..x.x..x..x+x..x..x+x..x
x.......+.......+.......x
xx.....xxx.I.I.xxx..I..xx
 xx...xx xx...xx xx...xx
  x.@.x   xxxxx   x.@.x
ENDMAP

##############################################################################
# Generic Orc entry
#
NAME: david_orc_2
TAGS: orc_entry no_rotate
MONS: weight:50 orc / orc warrior / nothing
ORIENT: float
KFEAT: O = enter_orcish_mines
MAP
xxxx..@..xxxx
x.....I.....x
x...........x
.....111.....
@I...1O1...I@
.....111.....
x...........x
x.....I.....x
xxxx..@..xxxx
ENDMAP

##############################################################################
# Orcish monsters near stair
#
NAME: david_orc_3
TAGS: orc_entry
MONS: weight:80 nothing / weight:50 orc / orc priest / orc wizard / orc warrior
MONS: nothing / orc warrior / warg / orc priest
ORIENT: float
KFEAT: O = enter_orcish_mines
MAP
1.....@....11
1............
.............
......21.....
@.....O.....@
.....12......
.............
.............
11..@......11
ENDMAP

#############################################################################
# The Orc Outfitter
#
NAME: orc_david_3
TAGS: orc_entry
ORIENT: float
#
: if you.absdepth() >= 10 then
MONS: weight:20 orc / orc warrior / weight:1 orc knight
: else
MONS: weight:30 orc / orc warrior
: end
#
KFEAT: X = armour shop / .
KFEAT: Y = weapon shop / .
SUBST: ? = 1 $
KFEAT: O = enter_orcish_mines
MAP
@.........@
...??..??..
.?.........
.?.X.O.Y.?.
.........?.
...??...?..
@.........@
ENDMAP

#############################################################################
# Wary orcs
#
NAME: david_orc_4
TAGS: orc_entry no_rotate
ORIENT: float
SUBST:  ? = I G .
MONS:   orc warrior band
NSUBST: a = 1:1 / *:.
NSUBST: b = 1:1 / *:.
NSUBST: c = 1:1 / *:.
NSUBST: d = 1:1 / *:.
KFEAT:  O = enter_orcish_mines
MAP
xxxxxxxxxxxxxxxxxxxxxx
xx?xxx?xxx?xxx?xxxxxxx
xaaaxbbbxcccxdddx.lllx
.aaaabbbbccccdddx.$llx
@aaaabbbbccccddd+.$Olx
.aaaabbbbccccdddx.$llx
xaaaxbbbxcccxdddx.lllx
xx?xxx?xxx?xxx?xxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
ENDMAP

#############################################################################
# The Mess
#
NAME:    david_orc_5
TAGS:    orc_entry
ORIENT:  float
KFEAT:   C = altar_beogh
SHUFFLE: EFGH
SUBST:   E=3
SUBST:   F=1, G=1, H=1
MONS:    orc priest / nothing
: if you.absdepth() >= 10 then
MONS:    orc w:32 / w:7 orc warrior / w:1 orc knight
: elseif you.absdepth() >= 8 then
MONS:    orc w:40 / orc warrior
: else
MONS:    orc w:80 / orc warrior
: end
MONS:    orc priest
: if you.absdepth() >= 10 then
MONS:    orc w:32 / w:7 orc warrior / w:1 orc knight
: else
MONS:    orc w:32 / w:7 orc warrior
: end
SUBST:   x : cxv
KFEAT:   O = enter_orcish_mines
MAP
xxxxxxxxxxxxxxx
x......O......x
x......F......x
x2..T.ECG.T..2x
x2222..H..2222x
x42222.I.22224x
xxI+IxxxxxI+Ixx
...@.......@...
ENDMAP

##############################################################################
# Caught partying
#
NAME:    erik_orc_1
TAGS:    orc_entry no_rotate
ORIENT:  float
MONS:    orc, orc priest / orc wizard
MONS:    w:50 orc warrior / ogre, swamp drake
# The drake's name is Bobby. Since you did ask.
KITEM:   1 = sausage / nothing / animal skin / potion of confusion /\
             potion of paralysis /\
             potion of degeneration / w:2 potion of speed / w:2 potion of might
KFEAT:   1 = 1
ITEM:    club, potion of confusion, slice of pizza, sausage / weight:40 nothing
ITEM:    potion of confusion
SHUFFLE: defO
KFEAT:   O = enter_orcish_mines
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.........g..........2x...w4xxx
x.....1...32...1......x1...wxxx
x.........xx.....e..........xxx
@.......f...1g............3.x$x
x..x1.................O...x.xhx
x..x1...1........2..1....gx.xhx
x......2....g.........d.1.x1+hx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# C'mon, you *gotta* have a *fort*!
#
NAME:  erik_orc_2
TAGS:  orc_entry no_pool_fixup no_rotate
ORIENT: float
MONS:  orc warrior, orc priest/orc wizard, weight:100 orc / orc warrior
: if you.absdepth() >= 10 and crawl.one_chance_in(10) then
MONS: orc knight
SUBST: G = .
: else
MONS:  orc warrior
SUBST: G = 4
: end
SUBST: c = c:1000 x
KFEAT: O = enter_orcish_mines
MAP
.............................
.Iwwwwwwwwwwwwwwwwwwwwwwwwww.
.wIwwwwwwwwwwwwwwwwwwwwwwwww.
.ww2+cccccccccccccccccccccww.
.ww.c.......3.3.3......w2cww.
.wwxx........3.3.......cccww.
.122........3.I.3.....G4Ocww.
.wwxx........3.3.......cccww.
.ww.c.......3.3.3......w2cww.
.ww2+cccccccccccccccccccccww.
.wIwwwwwwwwwwwwwwwwwwwwwwwww.
.Iwwwwwwwwwwwwwwwwwwwwwwwwww.
.............................
ENDMAP


##############################################################################
# Just like home
#
NAME:   erik_orc_3
TAGS:   orc_entry no_item_gen
MONS:   orc / w:2 goblin / w:2 hobgoblin / nothing
MONS:   orc warrior / orc priest / orc wizard / nothing
MONS:   warg / ogre / nothing
ORIENT: float
SUBST:  ? = x . $:1
KFEAT: O = enter_orcish_mines
MAP
xxx.xxxxxxxxxxxxxxxxxxx
xxx?.x?x?xx?xxx??xx?xxx
xx..xxx..1.xxx....13xxx
xx.x??x.x1xx?x.21xx?..x
xx...x..xxx.2..1.x.x??.
xx?x.....?x.x..x.x1?.xx
xx..??12x?x.1.1?x.x??xx
xxx..x..x??O.x..x.xx.xx
x?..?x......x?.x.??x.xx
xx.x??.x.x..?x..xx.x?xx
x?xx?xx.x??...x.1x.1??x
xx..xxx...x.x.x.1?1.xxx
x?.2....xx2.x...xx.x??x
xxx??xx..xx.xx.x?..xxxx
xx..1.x.x.3...x2.x1..xx
..x??x.xxx..xxxx??.??xx
xx?.??.11.x?xxx..x..xxx
x??xxxx???xx?xxx??x.?xx
xxxxxxxxxxxxxxxxxxxx.xx
ENDMAP

##############################################################################
# Orc-henge
#
NAME: erik_orc_4
TAGS: orc_entry no_rotate
MONS: human / goblin / hobgoblin / w:2 orc
MONS: orc priest
MONS: orc
MONS: orc priest / orc wizard / w:2 goblin / nothing
MONS: orc warrior / warg
ORIENT: float
SUBST: ? : c:60 .
SUBST: Z : c:60 .
SUBST: X : c:60 .
SUBST: v : c:60 .
SUBST: b : c:60 .
KITEM: 2 = dagger
KFEAT: 2 = 2
KFEAT: O = enter_orcish_mines
#
MAP
..............................
...............ZZ.............
.........??....ZZ....vv.......
.........??..........vv.......
....XX.......33533............
....XX........1.2........bb...
.........4....xxx...4....bb...
.............33533............
..bb..........................
..bb...........O..........??..
........4.............4...??..
..................4...........
...ZZ.........................
...ZZ......4............vv....
........................vv....
.......cc..........vv.........
.......cc....??....vv.........
.............??...............
..............................
ENDMAP

#########################
# Orcs are a clue, episode I
#
NAME:   hiddenorc1_lemuel
TAGS:   orc_entry
ORIENT: float
MONS:   orc w:40 / orc wizard w:2 / orc priest w:2, orc warrior
SUBST:  ? = .x
SUBST:  ! : .$
KFEAT: O = enter_orcish_mines
MAP
x??@....@....@??x
xx??.........??xx
 xx??1111111??xx
  xx??11111??xx
   xx??111??xx
    xx??1??xx
     xxI2Ixx
      xv+vx
      x!!!x
      x!O!x
      x!!!x
      xxxxx
ENDMAP

#########################
# Orcs are a clue, episode II
#
NAME:   hiddenorc2_lemuel
TAGS:   orc_entry
ORIENT: float
MONS:   orc w:20 / orc wizard w:2 / orc priest w:2 / orc warrior w:1
SUBST:  a : x. , b : x. , c : x. , d : x.
COLOUR: 1 = none / brown w:5
FTILE:  1 = floor_normal / floor_orc w:5
COLOUR: . = none / brown
FTILE:  . = floor_normal / floor_orc
SUBST:  ? = @ x:20 +
COLOUR: ! = brown
FTILE:  ! = floor_orc
SUBST:  ! : .$
KFEAT:  O = enter_orcish_mines
MAP
xx?xxxxxxx?xx
x111xxxxx111x
?1I1aaaaa1I1?
x111xxxxx111x
xxbx+xxx+xcxx
xxbxx!!!xxcxx
xxbxx!O!xxcxx
xxbxx!!!xxcxx
xxbx+xxx+xcxx
x111xxxxx111x
?1I1ddddd1I1x
x111xxxxx111?
xx?xxxxxxx@xx
ENDMAP

#######################
# Haunted orc entrance
#
NAME:   zombieorc1_lemuel
TAGS:   orc_entry
ORIENT: float
MONS:   orc zombie / orc skeleton w:2 / goblin zombie w:2 /\
        hobgoblin zombie w:2 / goblin skeleton w:1 / hobgoblin skeleton w:1
MONS:   ogre zombie / troll zombie w:3 / hill giant zombie w:1
MONS:   hungry ghost / phantom / w:1 wraith
: if you.absdepth() >= 10 then
MONS:   w:1 orc sorcerer / orc priest / nothing
: else
MONS:   orc priest / nothing
: end
SUBST:  ? = x . $:1
KFEAT:  O = enter_orcish_mines
MAP
xxx.xxxxxxxxxxxxxxxxxxx
xxx?.x?x?xx?xxx??xx?xxx
xx..xxx..1.xxx....11xxx
xx.x??x.x1xx?x.21xx?..x
xx...x..xxx.2..1.x.x??.
xx?x.....?x.x..x.x1?.xx
xx..??12x?x31.1?x.x??xx
xxx..x..x??O3x..x.xx.xx
x?..?x....34x?.x.??x.xx
xx.x??.x.x..?x..xx.x?xx
x?xx?xx.x??...x.1x.1??x
xx..xxx...x.x.x.1?1.xxx
x?.2....xx2.x...xx.x??x
xxx??xx..xx.xx.x?..xxxx
xx..1.x.x.1...x2.x1..xx
..x??x.xxx..xxxx??.??xx
xx?.??.11.x?xxx..x..xxx
x??xxxx???xx?xxx??x.?xx
xxxxxxxxxxxxxxxxxxxx.xx
ENDMAP

#######################
# Haunted orc entrance II
#
NAME:   zombieorc2_lemuel
TAGS:   orc_entry
ORIENT: float
MONS:   orc zombie / orc skeleton w:2 / goblin zombie w:2 /\
        hobgoblin zombie w:2 / goblin skeleton w:1 / hobgoblin skeleton w:1
MONS:   ogre zombie / troll zombie w:3 / hill giant zombie w:1
MONS:   hungry ghost / phantom / w:1 wraith
: if you.absdepth() >= 10 then
MONS:   w:1 orc sorcerer / orc priest
: else
MONS:   orc priest
: end
SUBST:  4 = 34
SUBST:  ? = x . $:1
KFEAT:  O = enter_orcish_mines
MAP
xxxxx..@..xxxxx
x21....I....12x
x11...111...11x
x.............x
..1...343...1..
@I1...2O2...1I@
..1...212...1..
x.............x
x11...111...11x
x21....I....12x
xxxxx..@..xxxxx
ENDMAP

#######################
# Guarded by dogs
#
NAME:   minmay_orc_entry_guard_dogs
TAGS:   orc_entry
WEIGHT: 5
ORIENT: float
MONS:   patrolling warg / nothing w:2
KFEAT:  O = enter_orcish_mines
MAP
1.1
.O.
1.1
ENDMAP

#######################
# Small temple
#
NAME:   minmay_orc_entry_temple_small
TAGS:   orc_entry
ORIENT: float
MONS:   orc w:40 / orc warrior / orc wizard, patrolling orc priest
SUBST:  " = 1...
KFEAT:  _ = altar_beogh
KFEAT:  O = enter_orcish_mines
MAP
ccccccc
c"""""c
c"""""c
c"2_2"c
c"""""c
c"""""c
ccc+ccc
cI"""Ic
 .""".
 ..O..
 .....
ENDMAP

#######################
# Large temple
NAME:   minmay_orc_entry_temple_large
TAGS:   orc_entry
WEIGHT: 1
ORIENT: float
MONS:   orc w:40 / orc warrior / orc wizard, patrolling orc priest
SUBST:  " = 1.......
SUBST:  ' = $.
KFEAT:  _ = altar_beogh
KFEAT:  O = enter_orcish_mines
MAP
ccccc   ccccc
c'''c   c'''c
c'''c   c'''c
c'''c   c'''c
cc+ccccccc+cc
c"""""""""""c
c"""""""""""c
c"""""""""""c
c"""I"""I"""c
c"""""2"""""c
c""""2_2""""c
c"""""2"""""c
c"""I"""I"""c
c"""""""""""c
ccc"""""""ccc
ccc"""""""ccc
cccccc+cccccc
cccccI.Iccccc
ccccI...Icccc
cccI.....Iccc
   .......
   ...O...
   .......
ENDMAP


############################################################################
# (Mini)vaults specific to the Orcish Mines
############################################################################

default-depth: Orc

##################################
# Dummy Orc minivault balancer.
#
NAME: mines_dummy
TAGS: dummy
WEIGHT: 60
MAP
x
ENDMAP

###################################
# mage tower -- either orc with assistants, or ogre with guards
NAME:    mines4_lemuel
TAGS:    no_rotate
SUBST:   d = $d
SUBST:   I = IG
SHUFFLE: 112/334
MONS:    orc sorcerer, orc / orc wizard, ogre mage, ogre / nothing
ITEM:    any book
MAP
  xxxx?xxxx
 xx2.....2xx
xx.2xxxxx2.xx
x..xxd1dxx..x
x.Ix$$G$$xI.x
x..xx222xx..x
xx.2xx+xx2.xx
 xx..2.2..xx
  xxxxxxxxx
ENDMAP

###################################
# warg stables, with goblin stableboy
NAME:  mines5_lemuel
SUBST: ?: wx
MONS:  warg / wolf,goblin
MAP
x???????x
xwwwwwwwx
xwwwwwwwx
x1.1.1.1x
x.1.1.1.x
x1.2...1x
xxxx+xxxx
ENDMAP

###################################
# Beogh 1
NAME:   mines1_david
TAGS:   allow_dup
KFEAT:  C = altar_beogh
WEIGHT: 20
MAP
...........
.xxxx.xxxx.
.xx.....xx.
.x.......x.
.x.......x.
.....C.....
.x.......x.
.x.......x.
.xx.....xx.
.xxxx.xxxx.
...........
ENDMAP

###################################
# Beogh 2
NAME:   mines2_david
TAGS:   allow_dup
KFEAT:  C = altar_beogh
MONS:   orc / orc warrior w:1
SUBST:  . = . 1:2
WEIGHT: 20
MAP
.......
.......
...C...
.......
.......
ENDMAP

###################################
# treasure chamber
#
NAME: mines2_lemuel
SUBST: ? = x@
MAP
  ?x+x?
 ?xx.xx?
?xx$$$xx?
?xx$$$xx?
 ?xxxxx?
   ???
ENDMAP

###################################
# treasure chamber with oklob plant
#
NAME:  mines3_lemuel
SUBST: ? = +.
SUBST: ! = x@
SUBST: $ = $:30 *
MONS:  oklob plant
MAP
    !@!
   !x?x!
   !x.xx!
  !xx..xx!
  !xxx..xx!
   !xxx.1x!
   !xxxx.x!
  !xxxxx.xx!
 !xx$$x..xxx
!!xx$$?.xxxx
!xxx$$xxxx!
 !xxxxxxx!
ENDMAP

##################################################
# Some deep elves on a diplomatic mission (Shiori)
# They have brought some of their enchanted armaments as gifts
NAME:    elf_legates
DEPTH:   Orc:3-
SHUFFLE: 23
SUBST:   6 : 6 .
NSUBST:  0 = 3=9. / *=0.
SUBST:   X = xx. b:2 I:1 $:1 *:1
MONS:    patrolling orc warlord
MONS:    patrolling orc sorcerer
MONS:    patrolling orc high priest
MONS:    deep elf fighter / deep elf summoner / deep elf conjurer / deep elf priest
MONS:    deep elf fighter / deep elf mage / deep elf priest
MONS:    deep elf fighter / nothing
ITEM:    good_item scimitar race:elven / good_item chain mail race:elven
SUBST:   = : +x
MAP
 xxxxx=xxx
xxXXXI1IXXx
xX..02.3XXx
xXX000.00Xx
xXX000.00Xx
xX.000.00Xx
xXX00...0Xx
xX.X.d4d.Xx
xXX.65556Xx
xxXX.6.6XXx
xxxXX..XXxx
 xxxx@@@xx
ENDMAP

##################################################
# Orcish barracks (minmay)
#
NAME:   minmay_orc_barracks
DEPTH:  Orc:2-
MONS:   orc / orc warrior w:2, orc knight / orc warlord
SUBST:  c : cx
MAP
ccccccccccccccccccccc
c1.1.1.1.1.1.1.1c...c
+...............+.2.c
c1.1.1.1.1.1.1.1c...c
ccccccccccccccccccccc
ENDMAP

##################################################
# A temple (minmay)
#
NAME:   minmay_orc_temple_a
TAGS:   mini_float
DEPTH:  Orc
MONS:   orc, patrolling orc priest
KFEAT:  _ = altar_beogh
MAP
ccccccc
c..2..c
c2._.2c
c.....c
ccc+ccc
 I...I
 .111.
  .1.
ENDMAP

##################################################
# Another temple (minmay)
#
NAME:   minmay_orc_temple_b
TAGS:   mini_float
DEPTH:  Orc
MONS:   orc / nothing w:40, patrolling orc priest / nothing w:5
KFEAT:  _ = altar_beogh
MAP
  ccccccc
  cI222Ic
  c.....c
  c2._.2c
  c.....c
  cI...Ic
ccccc+ccccc
c.........c
c.c11111c.c
c..11111..c
c.c11111c.c
c..11111..c
c.c11111c.c
c..11111..c
c.c11111c.c
c..11111..c
c.c11111c.c
c.........c
cccc+++cccc
 ccI...Icc
  ..0....
   ...0.
    .0.
ENDMAP

##################################################
# Still another temple (minmay)
#
NAME:   minmay_orc_temple_c
TAGS:   mini_float
DEPTH:  Orc:2-
MONS:   orc, orc priest, orc high priest
SUBST:  . = 1 .:200
SUBST:  . = 0 .:200
SUBST:  " = .
KFEAT:  _ = altar_beogh
MAP
ccccccccccccccccccc
c.................c
c.................c
c..I..c..c..c..I..c
c.................c
c.................c
c..c..cc+++cc..c..c
c.....c""2""c.....c
c.....c2"""2c.....c
c..c..+""_""+..c..c
c.....c2"""2c.....c
c.....cI"3"Ic.....c
c..c..ccccccc..c..c
c.................c
c.................c
c..I..c..c..c..I..c
c.................c
c.................c
cccccccc+++cccccccc
     .........
     .I.2.2.I.
     .........
      .......
        ...
ENDMAP

#######################
# Yet Another Temple
NAME:   bh_open_orc_temple
TAGS:   mini_float
DEPTH:  Orc:3-
SUBST:  " = 1.......
MONS:   orc w:8 / orc priest w:2 / orc warrior
MONS:   patrolling orc high priest
KFEAT:  _ = altar_beogh
SUBST:  ' = $.
MAP
     cccccccccc
 .............c
...I..I..I..I.c
.........""...cc
.........""...cc
..I..I..I""I'''c
........""""'''c
.I......"""2'_'c
........""""'''c
..I..I..I""I'''c
.........""...cc
.........""...cc
..I...I..I..I.cc
 .............c
     cccccccccc
ENDMAP

##################################################
# Treasure chamber (minmay)
#
NAME:   minmay_orc_treasury
TAGS:   mini_float
DEPTH:  Orc:3-
MONS:   orc warlord band, orc knight band
SUBST:  $ = $:100 *|
MAP
vvvvvvvvvvvvvvvvvvvvvvv
v$$$$$$$$v......9v||||v
v$$$$$$$$+.......vvvv+v
v$$$$$$$$v...2........v
v$$$vvvvvv............v
v$$vv.......9....v..1.v
v$vv............vv....v
vvv...9.........vv....v
v........1......vv....v
v..2.....v......vv...9v
v.......vvv.....vv....v
v........v.9.....v....v
v.....................v
vv...................vv
 vv......vvvvv......vv
  vv....vvvvvvv....vv
   vv++vvvvvvvvv++vv
    .........0.....
    ....v.v.v.v.0..
    .0.............
    ...............
     ......0....0.
     ...0.........
      ...........
       ......0..
         .....
ENDMAP

NAME:    guppyfry_orc_lava_fort
TAGS:    no_item_gen no_monster_gen mini_float
DEPTH:   Orc:3-
KMONS:   1 = orc high priest / orc sorcerer
KMONS:   2 = orc knight
KMONS:   3 = orc warrior / orc ; crossbow . bolt . spear | hand axe | mace
KMONS:   4 = orc priest / orc / nothing
KMONS:   5 = orc wizard / nothing
KMONS:   6 = ogre / two-headed ogre / troll / deep troll / ogre mage / nothing
KMONS:   7 = warg
KFEAT:   S = any shop
SUBST:   ? = 3467
SUBST:   - = S34.
SHUFFLE: 352?
MAP
llllx..xllll
llllx..xllll
ll5lx..xl5ll
lll5x..x5lll
xxxx-??-xxxx
....?12?....
....?21?....
xxxx-??-xxxx
lll5x..x5lll
ll5lx..xl5ll
llllx..xllll
llllx..xllll
ENDMAP

##################################################
# The orcs dug into a natural cave and used the bats as a shooting gallery.
NAME:   mines_bat_river
TAGS:   no_item_gen no_monster_gen no_pool_fixup
DEPTH:  Orc
MONS:   bat
MONS:   generate_awake orc ; bow ego:flame | bow ego:frost | bow ego:vorpal . arrow q:10
NSUBST: . = 2:2 / 2:c
NSUBST: c = *: 2 .
KITEM:  d = arrow q:1
NSUBST: b = 3:1 / 10:d / 4:e / *: .:20 1:1 d:2 e:2
: dgn.delayed_decay(_G, 'e', 'bat corpse')
SHUFFLE: ABC
SUBST:  A = @
SUBST:  B = x
SUBST:  C = x
MAP
     xxx
   xxxxxxx
   xxxww.xx
   xxbww..AA
  xxbbww...Ax
 xxbbbbwwW..x
 xbbbbbbwwW.x
xxbbbbbbbwW.xx
xbbbbbbbbww..x
xxbxbbbbWWww.B
 xxbbbbbW.WW.B
 xxxbbbwwWW..x
  xxxxwwWW..xx
    xxxwW..CC
    xxxxxxxC
     xxxxxxx
ENDMAP

##################################################
# The orcs piled up some loose rubble.
NAME:   rock_pile
TAGS:   no_item_gen no_monster_gen mini_float
DEPTH:  Orc
MONS:   orc ; sling . stone q:30, boulder beetle
KITEM:  d = w:3 stone / w:1 large rock q:1
SUBST:  a = .:4 b
NSUBST: b = 1:2 / 5:1 / *:d
SUBST:  1 = 1 .
MAP
     . .
   ....
 ..axaxa..
 .xabbbax..
..aabbaxa...
 .xaaaax..
  .aaxa....
   . . ..
    ...
ENDMAP

######################################################################
# The other side of the Spider's Nest orc mining accident vaults.
# Although if those vaults don't generate in the same game as
# these, or if the Spider's Nest branch doesn't generate at all,
# you can imagine anything you like behind those walls.
#
# Memorial with eternal flame.
NAME:   nicolae_orc_accident_1
DEPTH:  Orc
ORIENT: southeast
TAGS:   uniq_orcaccident
RTILE:  X = wall_stone_brick
COLOUR: X = lightgrey
KITEM:  W = gold q:1 / gold q:2 / gold q:3 / nothing w:100
KFEAT:  W = shallow_water
KPROP:  e = no_rtele_into
MONS:   patrolling orc warrior / patrolling orc knight w:2
SUBST:  e = xx...
MARKER: P = lua:fog_machine { cloud_type = "flame", \
            pow_min = 10000, pow_max = 10000, delay = 1, \
            size = 1, walk_dist = 0, start_clouds = 1 }
MAP
 e@@@@@@@exxxxxxxxxxxxxxxxx
ee......exxxxeeeeeeexxxxxxx
x......exxeeeee...eeeeeeexx
x.....eeee..............eex
xe....eee.......1.....1..ex
xe....eee....ee..WWWWW...ex
xe....eee....ee..WI.IW...ex
xe.....e.....ee..W.P.W...ex
xe..........eee..WI.IW...ex
xee.........eee..WWWWW...ex
xeee........exe.1.....1.eex
xxeee......eexe.........exx
xxxeeeeeeeeexxxx......xxxxx
xxxxxxeeeexxxxXXXXXXXXXXxxx
xxxxxxxxxxxxxxXXXXXXXXXXxxx
ENDMAP

# Memorial altar.
NAME:   nicolae_orc_accident_2
DEPTH:  Orc
ORIENT: southeast
TAGS:   uniq_orcaccident
RTILE:  X = wall_stone_brick
COLOUR: X = lightgrey
MONS:   patrolling orc warrior / patrolling orc knight w:2
MONS:   patrolling orc priest
KPROP:  e = no_rtele_into
SUBST:  e = xx...
SHUFFLE: AB
SUBST:  A = @
SUBST:  B = .@
KFEAT:  _ = altar_beogh
MAP
          xxxxxxxxx
          A.eeeexxx
          A...eeexx
          A....eexx
          A..0.eeex
         xx.....eex
       xxxxe.....ex
       xxxxee....ex
xBBBBBxxxeeeee..eex
x.....eeeeeee....ex
xe.......eee.....ex
xee.............eex
xee..1.....1....eex
xe.............eeex
xe.I.........I.eexx
xe...I.....I...eexx
xe......_......eexx
xee...........eexxx
xeee..2...2..eeexxx
xxxxxx......xxxxxxx
xxxxXXXXXXXXXXxxxxx
xxxxXXXXXXXXXXxxxxx
ENDMAP

# Quarantine.
NAME:   nicolae_orc_accident_3
DEPTH:  Orc
ORIENT: southwest
TAGS:   uniq_orcaccident
RTILE:  X = wall_stone_brick
COLOUR: X = lightgrey
KPROP:  e2q = no_rtele_into
SUBST:  e = xx...
SUBST:  g = xm
KFEAT:  m = iron_grate
KFEAT:  q = web / floor
MONS:   patrolling orc w:20 / patrolling orc warrior / patrolling orc knight w:2
MONS:   tarantella / wolf spider / spider / redback
MAP
x@@@@@@@eeee
x.......eeexxxx
x.......eeeeexxxx
xe........eeeeexxx
xee...1.....eeeexxx
xee...........eeexx
xxeeeeeee..1...eexx
xxxxeexxxe......eex
xxxxxxxxxe......eex
xxxxeeeee...1...eex
xxxeee.1.......eexx
xxee..........eeexx
xxe..1...1..eeeexx
xxgmmmmmmmmgxxeexx
xxeqqqqqqq2exxxxx
xxeqq2qqqqqexxxxx
xxeqqqqq2qeqxxxx
xxxxq2qqqqxxxxxx
xxXXXXXXXXXXxxxx
xxXXXXXXXXXXxxxx
ENDMAP

# luniq_orc_cave, a series of small vaults with things the orcs have dug into.
# feel free to add your own - nicolae
NAME: nicolae_orc_rich_vein
TAGS: no_item_gen no_trap_gen luniq_orc_cave
DEPTH: Orc
WEIGHT: 3
SUBST: X = $xxx, $ = $$$..
KITEM: S = nothing / gold q:1 / gold q:2 / gold q:3
MAP
    xxxx
 xxxxXXxx
xxXXX$$Xx
xX$$X$$Xxx
xX$$$$$Xxx
xXxx$$$Xxx
xxxSSxXxx
 @SSxxxx
  .xxx
ENDMAP

NAME: nicolae_orc_tapped_out
TAGS: no_item_gen no_trap_gen luniq_orc_cave
DEPTH: Orc
SUBST: X = 'xx, ' = .:5 d:4 ^:1
: if you.depth() == 4 then
SUBST: ^ = .:5 d:4
: end
KITEM: d = stone q:1 / large rock q:1 w:3 / gold q:1 w:3
KFEAT: ^ = shaft trap
MAP
   xxx.
 xxxX...@
xxXX''Xxx
xXX''''Xxx
xX''''''Xx
xX'''''Xxx
xxX'''Xxx
 xxXXXxx
  xxxxx
ENDMAP

NAME: nicolae_orc_crystal_geode
TAGS: no_item_gen no_trap_gen luniq_orc_cave
DEPTH: Orc
SUBST: B = b'
NSUBST: ' = d / '
KITEM: d = nothing w:48 / crystal ball of energy w:1 / crystal plate armour w:1
MAP
 xxxxxxxx
xx....xxxx
...x...bbxx
@xxxB...bbxx
 xbbB'''Bbxx
 xbB'''BBbxx
 xbbBBBBbbxx
 xxbbbbbbxx
  xxxxxxxx
ENDMAP

NAME: nicolae_orc_lava_room
TAGS: no_item_gen no_trap_gen luniq_orc_cave
DEPTH: Orc
SUBST: L = l., X = lx
MAP
   x.@xx
 xxx..xxx
xxx....Xxx
xX.....Llxx
xXLLLLLLlXx
xxllllllXxx
 xxXXXXXxx
  xxxxxxx
ENDMAP

NAME: nicolae_orc_lost_temple
TAGS: no_item_gen no_trap_gen luniq_orc_cave
DEPTH: Orc
NSUBST: D = + / c
SUBST: C = c.
KFEAT: _ = altar_beogh
MAP
 xxxxxx.
xxcccDCC.@
xxcI...I..
xxc.....Cx
xxD.._..Dxx
xxc.....cxx
xxcI...Icxx
xxcccDcccxx
 xxxxxxxxx
   xxxxx
ENDMAP

############################################################################
# Orc endings.
#
# These generally place four random stores; particularly difficult endings
# might consider placing four good quality stores.
############################################################################

default-depth:

NAME:    st_orc_town
TAGS:    no_rotate no_item_gen
PLACE:   Orc:$
MONS:    orc / nothing
#
: local brnd = crawl.random2(8)
: if brnd > 5 then
MONS:    orc wizard, orc sorcerer
: elseif brnd > 2 then
MONS:    orc priest, orc high priest
: else
MONS:    orc warrior, orc knight
: end
#
MONS:    warg / nothing
KFEAT:   S = any shop
KFEAT:   _ = altar_beogh
SUBST:   5 = 32, A:+, C:+, B:c, D:c
SUBST:   T : TIII
SHUFFLE: AC/BD
MAP
...................................
...................................
..cccccc..ccc.ccc.ccccccc..cccccc..
..cS521c..ccc.ccc.ccccccc..c125Sc..
..c5211D..ccc.ccc4ccccccc..B.125c..
..c2111c4....1......121..4.c..12c..
..cccCcc...11211.4ccc1ccc..ccAccc..
@......4..1T232T1.ccc.ccc.4.......@
..cccCcc...112114.ccc.ccc.cccAccc..
..c11..c4.ccc+ccc.ccc1ccc4c...11c..
..c211.D..c1...1c...131...B..112c..
..c5211c..c2.3.2c4ccc1ccc.c.1125c..
..cS521c..c1._.1c.ccc.ccc.c1125Sc..
..cccccc..ccccccc.ccc.ccc.ccccccc..
...................................
...................................
ENDMAP

##################################################
NAME:    st_orc_ogre_mass
TAGS:    no_item_gen no_monster_gen
PLACE:   Orc:$
MONS:    ogre / nothing w:5, two-headed ogre / nothing w:5, ogre mage
KMONS:   S = ogre / nothing w:5
KFEAT:   S = any shop
MAP
  ..........@.........
 ......................
...xxxxxxxx..xxxxxxxx...
..xxxx............xxxx..
..xx.......11.......xx..
..x..212..1221..212..x..
....2111213SS3121112....
....2111213SS3121112....
..x..212..1221..212..x..
..xx.......11.......xx..
..xxxx............xxxx..
...xxxxxxxx..xxxxxxxx...
 ......................
  ....................
ENDMAP

##################################################
NAME:    st_orc_rangers
TAGS:    no_rotate no_item_gen no_monster_gen no_pool_fixup
PLACE:   Orc:$
MONS:    orc warrior w:1 ; crossbow . bolt . halberd | war axe | morningstar /\
         orc w:10 ; crossbow . bolt . trident | hand axe | mace
MONS:    orc warrior ; crossbow . bolt . glaive | broad axe | battleaxe | great mace
MONS:    orc knight ; crossbow . bolt . glaive | broad axe | great mace
MONS:    orc warrior
MONS:    centaur w:20 / centaur warrior w:1 / nothing w:21
SUBST:   3 = 23
KFEAT:   S = any shop
MAP
  ...............@...............
 .................................
...cc..cc..cc...ccc...cc..cc..cc...
..cc..........wwcccww..........cc..
..c..5.......wwwcccwww.......5..c..
....5...5..wwwwcccccwwww..5...5....
..c....5.wwwwccccSccccwwww.5....c..
..c...5.wwwwcccc4.4ccccwwww.5...c..
........1221...34S43...1221........
........1221...34S43...1221........
..c...5.wwwwcccc4.4ccccwwww.5...c..
..c....5.wwwwccccSccccwwww.5....c..
....5...5..wwwwcccccwwww..5...5....
..c..5.......wwwcccwww.......5..c..
..cc..........wwcccww..........cc..
...cc..cc..cc...ccc...cc..cc..cc...
 .................................
  ...............................
ENDMAP

##################################################
NAME:    st_orc_mages
TAGS:    no_item_gen no_monster_gen
PLACE:   Orc:$
MONS:    orc / nothing, orc wizard / nothing, orc sorcerer
MONS:    orc priest / nothing, orc high priest, orc warrior
KFEAT:   S = any shop
SHUFFLE: 23/45
MAP
  ...................
 ...ccccccccccccccc...
...cc4.4...6...4.4cc...
..cc4.2.4..6..4.2.4cc..
..c..4.2.ccccc.2.4..c..
..c.2.2.2.c.c.2.2.2.c..
..c..2.2c.....c2.2..c..
..c...2ccc.3.ccc2...c..
..c..c..c.....c..c..c..
..11.cc...cSc...cc.11..
..61.c..3.S5S.3..c.16.@
..11.cc...cSc...cc.11..
..c..c..c.....c..c..c..
..c...2ccc.3.ccc2...c..
..c..2.2c.....c2.2..c..
..c.2.2.2.c.c.2.2.2.c..
..c..4.2.ccccc.2.4..c..
..cc4.2.4..6..4.2.4cc..
...cc4.4...6...4.4cc...
 ...ccccccccccccccc...
  ...................
ENDMAP

##################################################
NAME:    st_orc_pillars
TAGS:    no_item_gen no_monster_gen
PLACE:   Orc:$
MONS:    orc / nothing, orc warrior / nothing, orc knight / nothing
MONS:    orc priest / nothing, orc high priest
KFEAT:   S = any shop
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
  ccccccccccccccccccccccccccccccccccccccx
  cccccc.....ccccccccccccc..5..cccccccccx
  cccc.44.44.44ccccccccc.........cccccccx
  cc1..44.44.44..ccccc22..33.33..22cccccx
  c1111...........ccc.22.........22.ccccx
 .c.c1c.c.c.c1c.c.c.c.c1c4c4c4c4c4c.cSccx
.....111.222.11.3.....11..2...2...2.5.Scx
@........222.11.33c5c.112.2.2.2.2.2.c.ccx
.....111.222.11.3.....11..2...2...2.5.Scx
 .c.c1c.c.c.c1c.c.c.c.c1c4c4c4c4c4c.cSccx
  c1111...........ccc.22.........22.ccccx
  cc1..44.44.44..ccccc22..33.33..22cccccx
  cccc.44.44.44ccccccccc.........cccccccx
  cccccc.....ccccccccccccc..5..cccccccccx
  ccccccccccccccccccccccccccccccccccccccx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##################################################
NAME:    st_orc_congregation
TAGS:    no_item_gen no_monster_gen
PLACE:   Orc:$
MONS:    orc / nothing, orc warrior / nothing, orc priest / nothing
MONS:    orc wizard / nothing, orc knight, orc high priest, orc sorcerer
KMONS:   V = orc warlord
KFEAT:   V = T
# This one has a lot more high level orcs than the rest,
# so instead of adding more shops, guarantees good shops.
KFEAT:   S = general shop
KFEAT:   A = antique armour shop
KFEAT:   B = antique weapon shop
KFEAT:   C = book shop / jewellery shop
SHUFFLE: 74/63
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xcccccccccccccccccccccccccccx
xc1111.ccC..ccccc..Bcc.1111cx
xc122...ccS..ccc..Acc...221cx
xc122....cc...c...cc....221cx
xcc...7...ccc...ccc...7...ccx
xccc..4.33.........33.4..cccx
xcccc.4..33..222..33..4.ccccx
xcccc.4ccc33.222.33ccc4.ccccx
xcccc.4ccc.6..5..6.ccc4.ccccx
xcccc..ccc.........ccc..ccccx
xcccc..ccc..5.V.5..ccc..ccccx
xcccc..ccc.........ccc..ccccx
xcccc.4ccc.7..6..7.ccc4.ccccx
xcccc.4ccc...222...ccc4.ccccx
xcccc.4......222......4.ccccx
xc1...4...............4...1cx
xc122.4..333.....333..4.221cx
xc122..ccc333222333ccc..221cx
xc1111.ccc.1122211.ccc.1111cx
xccccccccc.11...11.cccccccccx
xccccccccc.11...11.cccccccccx
xccccccccc.1.....1.cccccccccx
xccccccccccc.....cccccccccccx
          .........
            ..@..
ENDMAP

##################################################
NAME:    st_orc_open
TAGS:    no_item_gen no_monster_gen
PLACE:   Orc:$
MONS:    ogre, two-headed ogre, troll, deep troll w:19 / iron troll w:1
MONS:    ogre mage, hill giant w:14 / ettin w:1, cyclops w:29 / stone giant w:1
KFEAT:   S = any shop
MAP
 .....
..111..  .... ....
..151....4....3S3..
..111.....321..3..11
 .......2.252......1
  ....6...123..7..@
   ..S.3...........
  ..4........6.1S.
  ...3.7..S2.. 11.
   .... ....
ENDMAP

##################################################
NAME:    st_orc_elflike
TAGS:    no_item_gen no_monster_gen
PLACE:   Orc:$
MONS:    orc high priest, orc sorcerer, orc knight
MONS:    orc warrior, orc priest, orc wizard
KFEAT:   S = any shop
MAP
xxxxxxxxxxxxxxxxxxxxxxx
xcccccccccccccccccccccx
xccccccS.......Sccccccx
xccccS...cc.cc...Sccccx
xccc...cc3...3cc...cccx
xcc5.cc3...1...3cc.5ccx
xcc5...............5ccx
xcc..c2..1...1..2c..ccx
xcc..cc2.......2cc..ccx
xc....cc.......cc....cx
xc.c6.....c.c.....6c.cx
xc.cc6....c.c....6cc.cx
xc..ccc444c.c444ccc..cx
xcc..cccccc.cccccc..ccx
xccc...cccc.cccc...cccx
xccccc...........cccccx
xcccccccccc.ccccccccccx
xcccccccccc.ccccccccccx
          .@.
ENDMAP

##################################################
NAME:   grunt_orc_gardens
PLACE:  Orc:$
{{
  -- There is functionality here for making the enemy sets consist of
  -- small numbers of cyclopses, ettins, and a stone giant.
  -- I have yet to decide how fair this will be, so it's a fairly low
  -- chance until I decide one way or the other if it works.
  dgn.persist.grunt_orc_gardens_giant_problem = crawl.one_chance_in(8)
  hook("post_place", function()
    local stores = { "antique weapon shop", "antique armour shop",
                     "general shop", "book shop / jewellery shop" }
    local which = crawl.random2(#stores) + 1
    dgn.persist.grunt_orc_gardens_store = stores[which]
    table.remove(stores, which)
    dgn.persist.grunt_orc_gardens_boss = true
    dgn.place_maps{tag="grunt_orc_garden", count=1}
    dgn.persist.grunt_orc_gardens_boss = false
    for i = 1, 3 do
      which = crawl.random2(#stores) + 1
      dgn.persist.grunt_orc_gardens_store = stores[which]
      table.remove(stores, which)
      dgn.place_maps{tag="grunt_orc_garden", count=1}
    end
  end)
}}

NAME: grunt_orc_garden
TAGS: grunt_orc_garden allow_dup transparent no_monster_gen no_item_gen
{{
  if dgn.persist.grunt_orc_gardens_giant_problem then
    if dgn.persist.grunt_orc_gardens_boss then
      mons("stone giant")
    else
      mons("ettin")
    end
    mons("cyclops")
    mons("nothing")
    nsubst("0 : 1:1 / 2:2 / *:.")
  else
    if dgn.persist.grunt_orc_gardens_boss then
       mons("orc warlord")
    end
    mons("orc knight / orc sorcerer w:5 / orc high priest w:5")
    if not dgn.persist.grunt_orc_gardens_boss then
      mons("nothing")
    end
    mons("orc warrior / orc sorcerer w:1 / orc high priest w:1 /"
         .. "ogre w:1 / troll w:1 / warg w:1")
    nsubst("0 : 1:1 / *:2")
  end
}}
MONS: nothing w:20 / fungus
SUBST: . = ..4
NSUBST: 3 = 5:3 / 1:I / 4:P / *:.
SUBST: P = .4
: if is_validating() then
KFEAT: A = any shop
: else
: kfeat("A = " .. dgn.persist.grunt_orc_gardens_store)
: end
MAP
   .....
 ...PPP...
 .PPP3PPP.
..P33333P..
.PP33033PP.
.P330A033P.
.PP33033PP.
..P33333P..
 .PPP3PPP.
 ...PPP...
   .....
ENDMAP

##################################################
NAME:   grunt_orc_community_town_hall
TAGS:   transparent no_monster_gen no_item_gen
PLACE:  Orc:$
KMONS:  O = Saint Roka band / orc warlord band, orc warlord band
MONS:   orc knight / orc warrior w:30, orc sorcerer / orc wizard w:30
MONS:   orc high priest / orc priest w:30
MONS:   orc warrior / orc w:30, orc / nothing
KFEAT:  _ = altar_beogh
KFEAT:  A = antique armour shop
KFEAT:  B = antique weapon shop
NSUBST: 0 = 2:1 / 3:2 / *:3
{{
  local small_house_count = 4 + crawl.random2(3)
  hook("post_place", function()
    dgn.place_maps{tag="grunt_orc_community_big_house", count=2}
    dgn.place_maps{tag="grunt_orc_community_house", count=small_house_count}
  end)
}}
MAP
   .........
  ...ccccc...
 ..ccc._.ccc..
 .ccA0.O.0Bcc.
..cccI0.0Iccc..
.ccccc...ccccc.
.c..4c.4.c4..c.
.c.0.+...+.0.c.
.c4..c.4.c..4c.
.ccccc.I.ccccc.
.c4..c.4.c..4c.
.c.0.+...+.0.c.
.c..4c...c4..c.
.ccccc.4.ccccc.
.ccccI...Icccc.
..ccc.4.4.ccc..
 ...cc+++cc...
   .........
     .I.I.
      .@.
ENDMAP

NAME:    grunt_orc_community_big_house_1
TAGS:    grunt_orc_community_big_house allow_dup transparent no_monster_gen
TAGS:    no_item_gen
KMONS:   A = orc knight / orc sorcerer / orc high priest / \
             orc warrior / orc wizard / orc priest
MONS:    orc warrior / orc wizard / orc priest
MONS:    orc warrior / orc w:30
KFEAT:   A = any shop
KFEAT:   _ = altar_beogh
SHUFFLE: def
SUBST:   d = +, ef = c
MAP
  .......
 ..ccccc..
..cc.A.cc..
.cc.....cc.
.c.2._.2.c.
.c1.2I2.1c.
.c.2...2.c.
.cc.....cc.
..cc...cc.
 ..cdefc..
  ...@...
ENDMAP

NAME:    grunt_orc_community_big_house_2
TAGS:    grunt_orc_community_big_house allow_dup transparent no_monster_gen
TAGS:    no_item_gen
KMONS:   A = orc knight / orc sorcerer / orc high priest / \
             orc warrior / orc wizard / orc priest
MONS:    orc warrior / orc wizard / orc priest
MONS:    orc warrior / orc w:30
KFEAT:   A = any shop
KFEAT:   _ = altar_beogh
SHUFFLE: def, ghi
SUBST:   dg = +, efhi = c
MAP
...........
.ccccccccc.
.cc2._..cc.
.c.....1.c.
.c..2...2c.
.cghic...c.
.c.2.c.1.c.
.c2I2cI.Ic.
.c...c.A.c.
.cdefccccc.
...@.......
ENDMAP

NAME:    grunt_orc_community_house_1
TAGS:    grunt_orc_community_house allow_dup transparent no_monster_gen
TAGS:    no_item_gen
MONS:    orc warrior / orc priest / orc wizard
MONS:    orc / nothing
SHUFFLE: def, I_ / I- / -_ / _- / -I
SUBST:   d = +, ef = c
KFEAT:   - = floor
KFEAT:   _ = altar_beogh
MAP
.......
.ccccc.
.c1_1c.
.c2I2c.
.c.2.c.
.cdefc.
...@...
ENDMAP

NAME:    grunt_orc_community_house_2
TAGS:    grunt_orc_community_house allow_dup transparent no_monster_gen
TAGS:    no_item_gen
MONS:    orc warrior / orc priest / orc wizard
MONS:    orc / nothing
SHUFFLE: de, I_ / I- / -_ / _- / -I
SUBST:   d = +, e = c
KFEAT:   - = floor
KFEAT:   _ = altar_beogh
MAP
......
.cccc.
.c_Ic.
.c11c.
.c22c.
.c.2c.
.cdec.
..@....
ENDMAP

NAME:    grunt_orc_community_house_3
TAGS:    grunt_orc_community_house allow_dup transparent no_monster_gen
TAGS:    no_item_gen
MONS:    orc warrior / orc priest / orc wizard
MONS:    orc / nothing
SHUFFLE: def, I_ / I- / -_ / _- / -I
SUBST:   d = +, ef = c
KFEAT:   - = floor
KFEAT:   _ = altar_beogh
MAP
.........
..ccccc..
.cc.2.cc.
.c.._..c.
.c1.I.1c.
.c.2.2.c.
.cc...cc.
..cdefc..
....@....
ENDMAP

##################################################
# The orcs roasted some dungeon creatures, and they're sleeping off the meal.
NAME:   grunt_orc_tribal_feast
TAGS:   patrolling no_item_gen no_monster_gen
PLACE:  Orc:$
MONS:   orc knight, orc high priest, orc sorcerer
MONS:   orc warrior, orc priest, orc wizard, orc
MARKER: O = lua:fog_machine { cloud_type='flame', pow_min = 10, pow_max = 10, \
                              delay = 10, size = 1, excl_rad = 0 }
: dgn.delayed_decay(_G, 'd', 'reindeer skeleton')
: dgn.delayed_decay(_G, 'e', 'rat skeleton')
KFEAT:  A = any shop
MAP
      ccccccc
     cce.6.5ccc
   xxc4.1.1..Acc
   x...e.2....2cc
  xx.4.d...e...Ac
 xx.e.e3.O.3.1.Ac
 x7..4.e..e...3cc
xx.e.c.d.2.e.Acc
x..7xc4e.e.6cccc
x...xcc.5cccc
x.7..xcccc
xx....7xxxxx
 xx.7....6.6.
  xx....5.1.5
   xxx.7.....
     xx.4.@.4
ENDMAP

##################################################
# A lot of the opposition here comes from the band of orcs following the
# leader; if it seems kind of sparse at first glance, think again.
NAME:   grunt_orc_cross
TAGS:   patrolling no_item_gen no_monster_gen
PLACE:  Orc:$
MONS:   orc knight, orc high priest, orc sorcerer
MONS:   orc warrior, orc wizard, orc priest
SHUFFLE: ABCD, EFGH
KMONS:  A = Saint Roka band / orc warlord band, orc warlord band
KMONS:  B = Nergalle / orc sorcerer, orc sorcerer
KMONS:  C = Urug / orc knight, orc knight
KMONS:  D = Blork the orc / orc wizard, orc wizard
KFEAT:  E = general shop
KFEAT:  F = antique armour shop
KFEAT:  G = antique weapon shop
KFEAT:  H = book shop / jewellery shop
KFEAT:  _ = altar_beogh
MAP
cccc  c..@..c  cccc
cE6ccccI.4.Icccc5Fc
c5..ccc.....ccc..6c
cc...cc.....cc...cc
 cc...c..2..c...cc
  cc.4.......4.cc
ccccc.........ccccc
..I....A...B....I..
...................
@4..1...._....1..4@
...................
..I....C...D....I..
ccccc.........ccccc
  cc.4.......4.cc
 cc...c..3..c...cc
cc...cc.....cc...cc
c6..cccI...Iccc..5c
cG5cccc..4..cccc6Hc
cccc  c..@..c  cccc
ENDMAP

##############################################################################
# zot.des: The ultimate goal. The Orb must be placed (Z). These maps can and
#          should be serious challenges! Players dying on Zot:5 are okay :)
#
# Content: Zot entry vaults (these are on Depths:$ and contain the portal)
#          Zot vaults, mini and not so mini
#          Zot hall (on Zot:5)
#          Vaults that define the floor/wall colours for Zot:1-5
#
##############################################################################

{{
-- Stuff for st's various vaults.
function st_zot_vaults_setup(e)
    e.tags("no_item_gen")
    e.depth("Zot:1-4")
    e.kmons("D = mottled dragon / fire dragon w:25 / ice dragon w:25 /\
               storm dragon w:20 / shadow dragon w:20 / golden dragon /\
               iron dragon w:5 / quicksilver dragon w:5 / bone dragon w:5 /\
               lindwurm w:2 / fire drake w:2 / death drake w:2 /\
               swamp dragon w:5")
    e.kmons("d = base draconian / nonbase draconian")
    e.kmons("Y = patrolling golden dragon")
    e.kitem("Y = |")
end
}}

{{
function callback.grunt_zot_stairs_overgrown_start(data, triggerable,
                                                   triggerer, marker, ev)
  if data.triggered == true then
    return
  end

  crawl.mpr("You have a sense of imminent foreboding.", "warning")
  dgn.persist.grunt_zot_stairs_overgrown_started = true
end

function callback.grunt_zot_stairs_overgrown_oklobify(data, triggerable,
                                                      triggerer, marker, ev)
  if triggerer.type ~= "turn" or triggerer.sub_type ~= "countdown" then
    return
  end

  if dgn.persist.grunt_zot_stairs_overgrown_started == nil then
    return
  end

  if data.count >= 10 then
    dgn.persist.grunt_zot_stairs_overgrown_started = nil
    return
  end

  local possible = {}
  local targets = dgn.find_marker_positions_by_prop("oklobify", 1)
  for _, pos in ipairs(targets) do
    local mons = dgn.mons_at(pos.x, pos.y)
    if mons ~= nil and mons.name ~= "oklob plant" then
      possible[#possible + 1] = pos
    end
  end

  if #possible == 0 then
    dgn.persist.grunt_zot_stairs_overgrown_started = nil
    return
  end

  data.count = data.count + 1

  local pos = possible[crawl.random2(#possible) + 1]
  local mons = dgn.mons_at(pos.x, pos.y)
  mons.dismiss()

  dgn.create_monster(pos.x, pos.y, "oklob plant")

  if not you.see_cell(pos.x, pos.y) then return end

  if not data.warned then
    data.warned = true
    crawl.mpr("One of the plants suddenly grows acid sacs!", "warning")
  else
    crawl.mpr("Another plant grows acid sacs!", "warning")
  end
end

function callback.grunt_zot_stairs_roulette_spawn_doors(data, triggerable,
                                                        triggerer, marker, ev)
  if data.triggered then
    return
  end

  data.triggered = true

  dgn.persist.grunt_zot_stairs_roulette_doors_spawned = true

  for p in iter.slave_iterator("hidden_door", 1) do
    dgn.terrain_changed(p.x, p.y, "clear_stone_wall", false, false, false)
  end
end

function callback.grunt_zot_stairs_roulette_door_open(data, triggerable,
                                                      triggerer, marker, ev)
  if data.triggered then
    return
  end

  if dgn.persist.grunt_zot_stairs_roulette_doors_spawned == false then
    return
  end

  data.triggered = true

  crawl.mpr("Several panes of glass retract into the floor.")
  for p in iter.slave_iterator("hidden_door", 1) do
    dgn.terrain_changed(p.x, p.y, "floor", false, false, false)
  end
end

function callback.grunt_zot_stairs_roulette_teleport(data, triggerable,
                                                     triggerer, marker, ev)
  slave = dgn.find_marker_positions_by_prop("dest", data.dest)
  if you.teleport_to(slave[1].x, slave[1].y) then
      crawl.mpr("Your surroundings suddenly seem different.")
  else
      crawl.mpr("There is a strange hissing noise.")
  end
end
}}

##############################################################################
##############################################################################
#
# Zot entries (D side)
#
##############################################################################
##############################################################################

###########################
# Something to give the builder a chance if massive vaults keep failing.
# NOTE: Not currently used since the large entries are placing as
#       primary vaults instead
#
NAME:   zot_entry_small
TAGS:   zot_entry
KFEAT:  O = enter_zot
COLOUR: c = lightmagenta
COLOUR: .+ = magenta
TILE:   c = wall_zot_magenta
FTILE:  .O+ = floor_hall
MAP
xxxxxxxxx
xxxcccxxx
xxcc.ccxx
xcc...ccx
xc..O..+@
xcc...ccx
xxcc.ccxx
xxxcccxxx
xxxxxxxxx
ENDMAP

NAME:   evilmike_zot_entry_round
TAGS:   no_monster_gen zot_entry
PLACE:  Depths:$
ORIENT: float
SUBST:  = = nnn+
SUBST:  c: c:70 v:20 b:10
SUBST:  v: vcxlmb, n: nnnnvbcx
FTILE:  })]O-4G: floor_marble
SUBST:  - = ^ .:90
KFEAT:  O = enter_zot
SUBST:  4 = 122223
MONS:   patrolling nonbase draconian
MONS:   fire dragon / ice dragon / storm dragon / shadow dragon / bone dragon / golden dragon
MONS:   patrolling base draconian
MAP
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxcccccccxxxxxxx
xxxcccccc..3..ccccccxx
xxcc......vvv......ccx
xxc.v......3......v.cx
xxc.2v...nn=nn...v..cx
xxc...nnnn---nnnn...cx
xxc...n----G----n..3cx
xcc...n-G--4--G-n...cc
xc.1.nn----v----nn...c
xc.v.n----v-v----n.v.+
xc.v.+-G-v-O-v-G-=.v.+
xc.v.n----v-v----n.v.+
xc.1.nn----v----nn...c
xcc...n-G--4--G-n...cc
xxc...n----G----n..3cx
xxc...nnnn---nnnn...cx
xxc.2v...nn=nn...v..cx
xxc.v.....3.......v.cx
xxcc......vvv......ccx
xxxcccccc.3...ccccccxx
xxxxxxxxcccccccxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# Sorry, no wand of wishing in this one
NAME:   evilmike_zot_entry_castle
TAGS:   no_monster_gen zot_entry layout_city
PLACE:  Depths:$
ORIENT: float
NSUBST: O = 1:O / *:n
SUBST:  ^ = ^.
SUBST:  s = ccc=
KFEAT:  ^ = alarm trap / blade trap / bolt trap
KFEAT:  O = enter_zot
KFEAT:  n = stone_arch
FTILE:  "OnDx = floor_marble
COLOUR: " = magenta
TILE:   x = wall_zot_magenta
COLOUR: x = magenta
FTILE:  .+cm^1234$ = floor_hall
SUBST:  D = +, "' = .
MONS:   patrolling nonbase draconian
MONS:   fire dragon / ice dragon / storm dragon / shadow dragon / bone dragon / golden dragon
MONS:   w:20 patrolling base draconian / nothing
MONS:   base draconian ; glaive | halberd
MAP
'''''''''''''''''''''''''''''''''''''''''
'wwwwwwwww'''''''''''''''''''''wwwwwwwww'
'wcccccccwwwwwwwwwwwwwwwwwwwwwwwcccccccw'
'wc1....cccccmccccccmccccccmccccc....1cw'
'wc.""".+....2.............2....+.""".cw'
'wc."O".cccccccccccc+cccccccccccc."O".cw'
'wc.""".s..3.....=4w.w4=......3.s.""".cw'
'wc.....c.3...cccccc.cccccc.""".c.....cw'
'wccccccc3....+...3"""3...+."x"3ccc+cccw'
'wwwwwwccccccccc.""""""".ccc""".c$$$.ccw'
''''''wm.......c.""""""".c4=..3.s$$$2mww'
''''''wc.x3x3x2c"""xxx"""cwcccccccccccw''
''''''WD"""""""D"""xxx"""+.+^^^^^^^^^+w''
''''''wc.x3x3x2c"""xxx"""cwcccccccccccw''
''''''wm.......c.""""""".c4=..3.s$$$2mww'
'wwwwwwccccccccc.""""""".ccc""".c$$$.ccw'
'wccccccc3....+...3"""3...+."x"3ccc+cccw'
'wc.....c.3...cccccc.cccccc.""".c.....cw'
'wc.""".s..3.....=4w.w4=......3.s.""".cw'
'wc."O".cccccccccccc+cccccccccccc."O".cw'
'wc.""".+....2.............2....+.""".cw'
'wc1....cccccmccccccmccccccmccccc....1cw'
'wcccccccwwwwwwwwwwwwwwwwwwwwwwwcccccccw'
'wwwwwwwww'''''''''''''''''''''wwwwwwwww'
'''''''''''''''''''''''''''''''''''''''''
ENDMAP

NAME:   evilmike_zot_entry_gauntlet
TAGS:   no_monster_gen zot_entry
PLACE:  Depths:$
ORIENT: float
SUBST:  b: v.., v: c.
TILE:   X = wall_zot_magenta
FTILE:  -OD = floor_hall
COLOUR: X-D = magenta
KPROP:  O-X = no_tele_into
SUBST:  X = c
SUBST:  - = ., D = +
KFEAT:  O = enter_zot
# easy: 1, 2
: if crawl.coinflip() then
MONS:   pale draconian, steam dragon
: else
MONS:   grey draconian, wyvern
: end
# medium: 3, 4
: if crawl.coinflip() then
MONS:   mottled draconian, mottled dragon
: else
MONS:   green draconian, swamp dragon
: end
# hard: 5, 6
: if crawl.coinflip() then
MONS:   red draconian, fire dragon
: else
MONS:   white draconian, ice dragon
: end
# harder: 7, 8
: if crawl.coinflip() then
MONS:   black draconian
KMONS:  8 = storm dragon
: else
MONS:   purple draconian
KMONS:  8 = quicksilver dragon
: end
# boss: 9, 0
KMONS:  9 = nonbase draconian
KMONS:  0 = golden dragon
MAP
ccc+ccccccccccccccccccc
cvb.bvcccvb.bvccccccccc
cb...bcccb.9.bccccccccc
c.1.1.ccc.0...+.ccccccc
cb222bcccb.9.bc..cccccc
cvb.bvcccvb.bvcc..ccccc
ccc+ccccccc+ccccc..cccc
ccc.ccccccc.cccccc..ccc
ccc+ccccccXDXcccccc+ccc
cvb.bvcccXX-XXcccvb.bvc
cb...bccXX---XXccb.8.bc
c.3.3.ccX--O--Xcc.7.7.c
cb444bccXX---XXccb...bc
cvb.bvcccXX-XXcccvb.bvc
ccc+ccccccXXXcccccc+ccc
ccc..ccccccccccccc..ccc
cccc..ccccccccccc..cccc
ccccc..ccvb.bvcc..ccccc
cccccc..cb.56bc..cccccc
ccccccc.+.....+.ccccccc
cccccccccb.56bccccccccc
cccccccccvb.bvccccccccc
ccccccccccccccccccccccc
ENDMAP

NAME:   evilmike_zot_entry_argh
TAGS:   no_monster_gen patrolling zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
# Grey dracs can escape from the islands if they have water. This is intentional.
SUBST:  G: GXm., m: mmlw
KFEAT:  m = iron_grate
SUBST:  x: xcvX
NSUBST: 2 = 2:2 / *:-
TILE:   X = wall_zot_magenta
COLOUR: X = magenta
SUBST:  X = c
COLOUR: . = white
FTILE:  -23O = floor_hall
SUBST:  - = .
MONS:   draconian zealot / draconian caller
MONS:   fire dragon / ice dragon / storm dragon / shadow dragon / bone dragon / golden dragon
MONS:   base draconian
KMONS:  O = nonbase draconian
MAP
xxxxxxxxxxxx@xxxxxxxxxxxx
xxx........---........xxx
xxmmm.....G.-.G.....mmmxx
x.m1m.......-.......m1m.x
x.mmm...G...-...G...mmm.x
x..........---..........x
x.....G....---....G.....x
x..........---..........x
x...G.....-----.....G...x
x........XX---XX........x
x.G.....-X3---3X-.....G.x
x-...------2-2------...-x
@-----------O-----------@
x-...------2-2------...-x
x.G.....-X3---3X-.....G.x
x........XX---XX........x
x...G.....-----.....G...x
x..........---..........x
x.....G....---....G.....x
x..........---..........x
x.mmm...G...-...G...mmm.x
x.m1m.......-.......m1m.x
xxmmm.....G.-.G.....mmmxx
xxx........---........xxx
xxxxxxxxxxxx@xxxxxxxxxxxx
ENDMAP

NAME:   evilmike_zot_entry_hall
TAGS:   no_monster_gen patrolling zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
TILE:   X = wall_zot_magenta
COLOUR: X+- = magenta
SUBST:  X = c
FTILE:  -O+ = floor_hall
KPROP:  O- = no_tele_into
SUBST:  - = .
SUBST:  v: ....cnXblw, z:..cnXblw
NSUBST: d = 5:3 / 2:2 / *:.
SUBST:  D = +
MONS:   nonbase draconian
MONS:   fire dragon / ice dragon / storm dragon / shadow dragon / bone dragon / golden dragon
MONS:   base draconian
MAP
                   cccccccccccc
                   cc..1....ccc
cccccccccccccccccccc.........cc
ccccccdccdccdccdccc..2XXXXX...c
ccc..............dc..XX---XX..c
D...vv.zz.vv.zz..cc..X-----X..c
D...vv.zz.vv.zz..+...X--O--+..c
D...vv.zz.vv.zz..cc..X-----X..c
ccc..............dc..XX---XX..c
ccccccdccdccdccdccc..2XXXXX...c
cccccccccccccccccccc.........cc
                  ccc..1....ccc
                  ccccccccccccc
ENDMAP

NAME:   evilmike_zot_entry_basic
TAGS:   no_monster_gen zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
TILE:   c = wall_zot_magenta
COLOUR: c+- = magenta
FTILE:  O-+c = floor_hall
KPROP:  O- = no_tele_into
SUBST:  - = .
NSUBST: d = 5:3 / 2:2 / 1:1
MONS:   patrolling nonbase draconian
MONS:   fire dragon / ice dragon / storm dragon / shadow dragon / bone dragon / golden dragon
MONS:   patrolling base draconian
MAP
....d....
.dcc+ccd.
.cc---cc.
.c-----c.
dc--O--cd
.c-----c.
.cc---cc.
.dcccccd.
....d....
ENDMAP

NAME:   evilmike_zot_entry_diamond
TAGS:   no_monster_gen zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
KFEAT:  A = stone_arch
TILE:   c = wall_zot_magenta
COLOUR: c+- = magenta
FTILE:  -O+Ac = floor_hall
KPROP:  -OcA = no_tele_into
SUBST:  - = .
NSUBST: 2 = 2:2 / *:.
NSUBST: 1 = 2:1 / *:2
MONS:   patrolling nonbase draconian
MONS:   fire dragon / ice dragon / storm dragon / shadow dragon / bone dragon / golden dragon
MONS:   patrolling base draconian
MAP
      ..1..
     .2c+c2.
    ..cc-cc..
   3.cc---cc.3
  ..cc--O--cc..
 ..cccc---cccc..
.2cc-ccc-ccc-cc2.
.cc---ccccc---cc.
1+--A--ccc--A--+1
.cc---ccccc---cc.
.2cc-ccc-ccc-cc2.
 ..cccc---cccc..
  ..cc--A--cc..
   3.cc---cc.3
    ..cc-cc..
     .2c+c2.
      ..1..
ENDMAP

NAME:   grunt_zot_entry_hairpin
TAGS:   no_monster_gen patrolling zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
COLOUR: ABC-_ = magenta
FTILE:  ABC-_O = floor_hall
SUBST:  _ = ., A = 1, B = 2, C = c, - = +
MONS:   golden dragon / storm dragon / shadow dragon / bone dragon / \
        fire dragon / ice dragon
MONS:   nonbase draconian, base draconian
MAP
      cccccccccccccccccccc
    ccc..................cc
   cc...........3.........cc
  cc.....cccccccccccccc....c
  c.....cc...........ccc...c
 cc..3.cc3.CCCCCCCCC3.c.3..c
@+....cc..CC_____A_CC.c...cc
@+..2.c.1.-____B__O_C.+1.cc
@+....cc..CC_____A_CC.c...cc
 cc..3.cc3.CCCCCCCCC3.c.3..c
  c.....cc...........ccc...c
  cc.....cccccccccccccc....c
   cc...........3.........cc
    ccc..................cc
      cccccccccccccccccccc
ENDMAP

NAME:   grunt_zot_entry_funnels
TAGS:   no_monster_gen patrolling zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
COLOUR: 123c+. = magenta
FTILE:  123c+.O = floor_hall
MONS:   golden dragon / storm dragon / shadow dragon / bone dragon / \
        fire dragon / ice dragon
MONS:   nonbase draconian, base draconian
MAP
      @       @
    cc+cc   cc+cc
    c...ccccc...c
    cc...ccc...cc
 cccxcc.3.c.3.ccxccc
 c.ccxcc.....ccxcc.c
@+..ccxcc.2.ccxcc..+@
 c...ccxcc+ccxcc...c
 cc.3.ccc...ccc.3.cc
  cc...c..1..c...cc
  ccc.2+.1O1.+2.ccc
  cc...c..1..c...cc
 cc.3.ccc...ccc.3.cc
 c...ccxcc+ccxcc...c
@+..ccxcc.2.ccxcc..+@
 c.ccxcc.....ccxcc.c
 cccxcc.3.c.3.ccxccc
    cc...ccc...cc
    c...ccccc...c
    cc+cc   cc+cc
      @       @
ENDMAP

NAME:   grunt_zot_entry_pinwheel
TAGS:   no_monster_gen patrolling zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
KFEAT:  A = stone_arch
COLOUR: 123c+. = magenta
FTILE:  123c+.AO = floor_hall
NSUBST: A = 1:O / *:A
MONS:   golden dragon / storm dragon / shadow dragon / bone dragon / \
        fire dragon / ice dragon
MONS:   nonbase draconian, base draconian
MAP
       @
     cc+cc
    cc...c
   cc...cc ccccc
   c...cc cc.3.cc
   c3.3c cc.2...cc
   c.2.ccc.1.3...c
   cc.1.cc..ccc..+@
    cc..AcAcc cc.c
 ccc ccccccccc ccc
 c.cc ccAcA..cc
@+..ccc..cc.1.cc
 c...3.1.ccc.2.c
 cc...2.cc c3.3c
  cc.3.cc cc...c
   ccccc cc...cc
         c...cc
         cc+cc
           @
ENDMAP

NAME:   grunt_zot_entry_bicorn
TAGS:   transparent no_monster_gen patrolling zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
COLOUR: 123c+_ = magenta
FTILE:  123+_OT = floor_hall
SUBST:  _ = .
MONS:   golden dragon / storm dragon / shadow dragon / bone dragon / \
        fire dragon / ice dragon
MONS:   nonbase draconian, base draconian
MAP
            .....
          ...ccc...
         ..ccc_ccc..
        ..cc_____cc..
       ..cc_1_O_1_cc..
      ..cc___1_1___cc..
     ..cc___________cc..
    ..cc_____________cc..
    .cc___2___T___2___cc.
   ..c_____3_____3_____c..
  ..cc_3_____________3_cc..
 ..cc_2_3_ccccccccc_3_2_cc..
..cc_3__ccc.......ccc__3_cc..
.cc___ccc...     ...ccc___cc.
.c__ccc...         ...ccc__c.
.c+cc...             ...cc+c.
..@...                 ...@..
ENDMAP

NAME:   grunt_zot_entry_tricorne
TAGS:   transparent no_monster_gen patrolling zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
COLOUR: ABC-_ = magenta
FTILE:  ABC-_O = floor_hall
SUBST:  _ = ., A = 1, B = 3, C = c, - = +
MONS:   golden dragon / storm dragon / shadow dragon / bone dragon / \
        fire dragon / ice dragon
MONS:   nonbase draconian, base draconian
MAP
@......               ......@
.+cccc.................cccc+.
.c...ccccccccccccccccccc...c.
.cc...........c...........cc.
..cc...3....CCCCC....3...cc..
 ..c.3T...CCC___CCC...T3.c..
  .cc...2.C___A___C.2...cc.
  ..cc...C-_B___B_-C...cc..
   ..cc..C_________C..cc..
    ..c..C____O____C.cc..
     .cc.C_________C.cc.
     ..ccCC_A___A_CCcc..
      ..ccC___B___Ccc..
       .ccCCC___CCCcc.
       ..c..CC-CC..c..
        .cc.......cc.
        ..c...2...c..
         .cc..T..cc.
         ..c.3.3.c..
          .cc...cc.
          ..c...c..
           .c...c.
           .cc.cc.
           ..c.c..
            .c+c.
            ..@..
ENDMAP

NAME:   grunt_zot_entry_gee_three
TAGS:   transparent no_monster_gen patrolling zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
COLOUR: ABC-_ = magenta
FTILE:  ABC-_O = floor_hall
SUBST:  _ = ., A = 1, B = 3, C = c, - = +
MONS:   golden dragon / storm dragon / shadow dragon / bone dragon / \
        fire dragon / ice dragon
MONS:   nonbase draconian, base draconian
MAP
           .........
         ...ccccccc...
        ..ccc.CCC.ccc..
       ..cc...-_-...cc..
      ..cc...cC_Cc...cc..
     ..cc.3.cCC_CCc.3.cc..
     .cc....cC___Cc....cc.
    ..c..2.ccCB_BCcc.2..c..
    .cc3...ccC___Ccc...3cc.
   ..c.....ccC___Ccc.....c..
.....c..T3.ccC_A_Ccc.3T..c.....
.ccccc.....ccC___Ccc.....ccccc.
@+..c......ccCA_ACcc......c..+@
.ccc....3..ccC___Ccc..3....ccc.
...ccc..T..ccC_O_Ccc..T..ccc...
.cccc.......cCC_CCc.......cccc.
@+......1...ccCCCcc...1......+@
.ccc.......3..c.c..3.......ccc.
...ccccc..T..3.2.3..T..ccccc...
  ....cc.......T.......cc....
  ..ccc..3..cc.3.cc..3..ccc..
 ..cc......ccc...ccc......cc..
 .cc...ccccc.c...c.ccccc...cc.
 @+.cccc.....c...c.....cccc.+@
 .ccc....   .c.3.c.   ....ccc.
 .....      .c...c.      .....
            .c...c.
            .cc.cc.
            ..c.c..
             .c.c.
             .c+c.
             ..@..
ENDMAP

NAME:   grunt_zot_entry_alcove
TAGS:   transparent no_monster_gen patrolling zot_entry
PLACE:  Depths:$
ORIENT: float
KFEAT:  O = enter_zot
COLOUR: 1C-_ = magenta
FTILE:  1C-_O = floor_hall
SUBST:  _ = ., C = c
NSUBST: @ = 1:@ / *:.
MONS:   golden dragon / storm dragon / shadow dragon / bone dragon / \
        fire dragon / ice dragon
MONS:   nonbase draconian, base draconian
MAP
     ccccccc
    ccCCCCCcc
    cCC___CCc
    cC_____Cc
    cC1_O_1Cc
    cC_1_1_Cc
    cCC___CCc
   cc.......cc
  cc..2...2..cc
 cc.3..2.2..3.cc
cc...3.....3...cc
.................
@.......T.......@
 ....3.....3....
   .3.......3.
       .@.
ENDMAP

##############################################################################
##############################################################################
#
# Zot petite vaults
#
##############################################################################
##############################################################################

# There's few vaults on Zot:5, tone down their chances.
NAME: dummy_balancer_zot
DEPTH: Zot:$
TAGS: dummy
ORIENT: float
WEIGHT: 60
MAP
x
ENDMAP

NAME:   lemuel_baited_zot_trap
TAGS:   allow_dup extra luniq
DEPTH:  Zot
WEIGHT: 20
KFEAT:  * = Zot trap
KITEM:  * = any good_item
MAP
*
ENDMAP

NAME:   lemuel_zot_downstairs
TAGS:   allow_dup extra luniq
DEPTH:  Zot:1-4
ORIENT: float
SUBST:  . = .^
KFEAT:  ^ = Zot trap
SUBST:  > = }])
MAP
xxxxxxx
@....>x
xxxxxxx
ENDMAP

NAME:   lemuel_zot_upstairs
TAGS:   allow_dup extra luniq
DEPTH:  Zot:2-
ORIENT: float
SUBST:  . = .^
KFEAT:  ^ = zot trap
SUBST:  < = ([{
MAP
xxxxxxx
@....<x
xxxxxxx
ENDMAP

###########################
# Halls of Wrath
#
NAME:   lemuel_halls_of_wrath
DEPTH:  Zot:1-4
ORIENT: float
NSUBST: 1 = 6:2 / *=1
SUBST:  1 = 1 .:5
SUBST:  1 = 1 3
MONS:   troll / deep troll / iron troll / ogre / two-headed ogre
MONS:   moth of wrath
MONS:   hill giant / stone giant / fire giant / frost giant / ettin / \
        w:5 efreet / w:3 titan
SUBST:  } = }:80 >, ) = ):80 >
MAP
cccccccccccccccccccccccc
c111111111111111111.n..c
c111111111111111111.n.)c
ccnnccccccccccccc++cc..c
c....G....G....G....c..c
+...................+..c
+...................+.}c
+...................+..c
c....G....G....G....c..c
ccnnccccccccccccc++cc..c
c111111111111111111.n.]c
c111111111111111111.n..c
cccccccccccccccccccccccc
ENDMAP

###########################
# Lich library
#
NAME:   lemuel_lich_library
DEPTH:  Zot:1-4
TAGS:   no_monster_gen
MONS:   flying skull, lich, ancient lich, necromancer, vampire mage
ITEM:   any book mimic:10 w:20 / any scroll mimic:10
SUBST:  w:w?
SUBST:  ?=. 1:1
SUBST:  x:xc
SUBST:  4 = 4 5:2 .:4
MAP
wwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwww
wwxxxxxxxxx+xxxxxxxxxww
wwx....x.......x....xww
wwx....+...G...+....xww
wwx.4..x.......x..4.xww
wwx4.4.xxxx+xxxx.4.4xww
wwx.4.4xxx...xxx4.4.xww
wwx4.4.xxx.2.xxx.4.4xww
wwxxxxxxxxx+xxxxxxx+xww
wwxxxxxxxxx.xxxxxx$$xww
wwxdddxxxxx.x...xx$$xww
wwx.2.xxxxx.+.G.xxxxxww
wwx...xxxxx.x...xxxxxww
wwx...xxxxx.xxxxxxxxxww
wwxx+xxxxxx.xxxdddddxww
wwxx.xxxxxx.xxxd232dxww
wwxx.xxxxxx.xxxd...dxww
wwxx......+...+....dxww
wwxxxxxxxxxmxxxd...dxww
wwxxxxxxxxxGxxxdddddxww
wwxxxxxxxxxxxxxxxxxxxww
wwwwwwwwwwwwwwwwwwwwwww
wwwwwwwwwwwwwwwwwwwwwww
ENDMAP

###########################
# Lake of fire
#
NAME:  lemuel_lake_of_fire
DEPTH: Zot
SUBST: * =*l
KMONS: * = orb of fire
KFEAT: * = l
SUBST: .=.l
MAP
 ..ll..
..llll..
.llllll.
.ll**ll.
.ll**ll.
.llllll.
..llll..
 ..ll..
ENDMAP

###########################
# Globe of electric golems
#
NAME:  lemuel_golem_globe
DEPTH: Zot
TAGS:  no_rotate
KFEAT: T = teleport trap
KFEAT: m = iron_grate
SUBST: 1 = 1.
MONS:  electric golem
MAP
  mmmm
 mm..mm
mm.11.mm
m.1TT1.m
mm.11.mm
 mm..mm
  mmmm
ENDMAP

###################################################################
# Draconian Sentry Boxes
# There is an 70% chance all three downstairs will be here.
#
NAME:   lemuel_draconian_sentries
DEPTH:  Zot:1-4
ORIENT: float
TAGS:   no_rotate uniq_draconian_sentries
MONS:   red draconian scorcher / green draconian zealot / yellow draconian caller
SUBST:  } = }:80 >, ) = ):80 > , ] = ]:80 >
SUBST:  w:w., w=Ww
KFEAT:  m = iron_grate
MAP
............................
..mmm..................mmm..
..m1m..................m1m..
..mmm...mmmwwwwwwmmm...mmm..
........m1mwwwwwwm1m........
........mmmwwwwwwmmm........
mmm.....www...}..www.....mmm
m1m.....www..)...www.....m1m
mmm.....www...]..www.....mmm
........mmmwwwwwwmmm........
........m1mwwwwwwm1m........
..mmm...mmmwwwwwwmmm...mmm..
..m1m..................m1m..
..mmm..................mmm..
............................
ENDMAP

NAME:   lemuel_draconian_sentries_small
DEPTH:  Zot:1-4
ORIENT: float
TAGS:   no_rotate uniq_draconian_sentries
MONS:   red draconian scorcher / green draconian zealot / yellow draconian caller
SUBST:  } = }:80 >, ) = ):80 > , ] = ]:80 >
SUBST:  w:w., w=Ww
KFEAT:  m = iron_grate
MAP
................
................
..mmmwwwwwwmmm..
..m1mwwwwwwm1m..
..mmmwwwwwwmmm..
..www...}..www..
..www..)...www..
..www...]..www..
..mmmwwwwwwmmm..
..m1mwwwwwwm1m..
..mmmwwwwwwmmm..
................
................
ENDMAP

###########################
# Acid trip
# 70% chance of having all stairs.
NAME:   lemuel_acid_trip
DEPTH:  Zot:1-4
ORIENT: centre
MONS:   oklob plant, acid blob, jelly, yellow draconian
MONS:   any nonbase yellow draconian
SUBST:  1 = 1:2 .
SUBST:  . = .:20 3:3 2:1
SUBST:  } = }:80 >, ) = ):80 >, ] = ]:80 >
MAP
       xx++xx
     xxx....xxx
   xxx..4444..xxx
  xx....4554....xx
 xx.....cccc.....xx
 x....ccc..ccc....x
xx...cc......cc...xx
x...cc..1111..cc...x
x...c...1}11...c...x
x.5.c...11]1...c.5.x
x...c...1)11...c...x
x...cc..1111..cc...x
xx...cc......cc...xx
 x....cc....cc....x
 xx....c....c....xx
  xx............xx
   xxx..4444..xxx
     xxx4554xxx
       xxxxxx
ENDMAP

###########################
# Firehouse
#
NAME:   lemuel_firehouse
DEPTH:  Zot:1-4
ORIENT: centre
MONS:   orb of fire
MONS:   fire elemental
MONS:   red draconian / weight:2 mottled draconian
MONS:   any nonbase red draconian
MONS:   efreet
SUBST:  1=122, 3=322
SUBST:  4 = 2:20 4:5 3:15 .:20
MAP
          lllll
       lllllllllll
     lllll.....lllll
    llll....x....llll
   llll....xxx....llll
   llll...xx}xx...llll
  llll...xx]2)xx...llll
  llll..xx2...2xx..llll
 llll..xx...5...xx..llll
 llll..x...2.2...x..llll
llll...x.2xx+xx2.x...llll
llll...x.xx444xx.x...llll
llll...x.x44444x.x...llll
 llll..x.x44F44x.x..llll
 llll..x.x44444x.x..llll
  llll.x.xx444xx.x.llll
  llll.x.1x...x1.x.llll
   llllxxxx+++xxxxllll
    llll333...333llll
     lllll.....lllll
       lllll.lllll
          ll@ll
ENDMAP

######################################
# Circus (by mu)
# full of klowns, caged animals outside
NAME:   circus_mu
DEPTH:  Zot:1-4
ORIENT: float
TAGS:   no_rotate no_hmirror no_monster_gen
MONS:   killer klown
MONS:   elephant / w:5 dire elephant / w:2 hellephant
MONS:   human ; club
SUBST:  1 = 1 2:5 3:20
SUBST:  2 = 2 2 .
SUBST:  % = !:20 ?:5 .:20
KITEM:  ! = banana
KITEM:  ? = wand of random effects
COLOUR: " : yellow
MAP
     ................
    .....xxxxx.............
   .....xx...xx.............
  .....xx..%..xx.....mmmmm...
 .....xx..%.%..xx....m222m....
.....xx.%..1..%.xx...m+mmm.....
....xx...%.%.%...xx............
...xx..%1"""""1%..xx....mmmmm..
..xx..%.""..."".%..xx...m222m..
.xx..%%.".....".%%..xx..m+mmm..
.x.%1..1"..1.."1..1%.x.........
.xx..%%.".....".%%..xx..3......
..xx..%.""..."".%..xx..........
...xx..%1"""""1%..xx...mmmmm...
....xx...%.%.%...xx....m222m...
.....xx.%..1..%.xx.....m+mmm..
 .....xx...%...xx............
  .....xx.%.%.xx............
   .....xx+++xx.....
    .....xx+xx.....
     .............
ENDMAP

######################################
# This is weird, but in Zot weirdness fits. (nrook)
NAME: nrook_zot_tub
DEPTH: Zot
KFEAT: X = W
MARKER: X = lua:fog_machine { cloud_type="steam", \
            pow_min = 2, pow_max = 10, delay = 25, \
            size = 2, walk_dist = 3, spread_rate= 10 }
KMONS: M = patrolling pale draconian att:neutral / \
      patrolling pale draconian scorcher att:neutral w:2 / \
      patrolling pale draconian caller att:neutral w:2 / \
      patrolling pale draconian shifter att:neutral w:2 / \
      patrolling pale draconian annihilator att:neutral w:2 / \
      patrolling pale draconian knight att:neutral w:2 / \
      patrolling pale draconian monk att:neutral w:2
KFEAT: M = W
KMONS: F = orb of fire att:neutral
KFEAT: F = W
MAP
     @
xxxxx+xxxxx
xWWWWWWWWWx
xWMWWMWWMWx
xWWXWXWXWWx
xWWWmmmWWWx
xWMXmFmXMWx
xWWWmmmWWWx
xWWXWXWXWWx
xWMWWMWWMWx
xWWWWWWWWWx
xxxxxxxxxxx
ENDMAP

###################################################
# Minmay's Elemental castle
# 50% electricity, 25% fire, 25% ice
NAME:    minmay_elemental_castle
ORIENT:  southwest
DEPTH:   Zot:1-4
TAGS:    no_rotate
: if crawl.coinflip() then
MONS:    titan, storm dragon, electric golem
SUBST:   F = 3
ITEM:    disc of storms, fan of gales, staff of air, book of the sky
: elseif crawl.coinflip() then
MONS:    frost giant, ice dragon
MARKER:  F = lua:fog_machine { cloud_type = "freezing vapour", \
              pow_min = 5, pow_max = 15, delay = 10, size = 2, \
              walk_dist = 0, spread_rate= 0 }
ITEM:    wand of cold, staff of cold, book of frost, book of ice
SUBST:   w = l
SUBST:   W = l..
: else
MONS:    fire giant, fire dragon
MARKER:  F = lua:fog_machine { cloud_type = "flame", pow_min = 5, \
              pow_max = 15, delay = 10, size = 2, walk_dist = 0, \
              spread_rate= 0 }
ITEM:    wand of fire, staff of fire, book of flames, book of fire
: end
SUBST:   1 = 112.
SUBST:   2 = 221.
SHUFFLE: defg
MAP
xx...................@@...................
xx.WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW.
xx.WwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW.
xx.WwcccccccwwwwwwwwwwwwwwwwwwwwcccccccwW.
xx.Wwcd*%.1cwwwwwwwwwwwwwwwwwwwwc1.%*gcwW.
xx.Wwc*%...cwwwwwwwwwwwwwwwwwwwwc...%*cwW.
xx.Wwc%....ccccccccc++++ccccccccc....%cwW.
xx.Wwc................................cwW.
xx.Wwc1........2............2........1cwW.
xx.Wwcccc..........................ccccwW.
xx.Wwwwwc...cccccccccccccccccccc...cwwwwW.
xx.Wwwwwc...cccccccccccccccccccc...cwwwwW.
xx.Wwwwwc...cccccccccccccccccccc...cwwwwW.
xx.Wwwwwc.2.ccc*|*cccccccc*|*ccc.2.cwwwwW.
xx.Wwwwwc...ccc...cccccccc...ccc...cwwwwW.
xx.Wwwwwc...ccc...cccccccc...ccc...cwwwwW.
xx.Wwwwwc...ccc.F.cccccccc.F.ccc...cwwwwW.
xx.Wwwwwc...ccc...cccccccc...ccc...cwwwwW.
xx.Wwwwwc...ccc...cccccccc...ccc...cwwwwW.
xx.Wwwwwc..........................cwwwwW.
xx.Wwwwwc..........................cwwwwW.
xx.Wwwwwc...ccc..............ccc...cwwwwW.
xx.Wwwwwc...ccc....2....2....ccc...cwwwwW.
xx.Wwwwwc...ccc..............ccc...cwwwwW.
xx.Wwwwwc...ccc..............ccc...cwwwwW.
xx.Wwwwwc...ccc..............ccc...cwwwwW.
xx.Wwwwwc.2.ccc1............1ccc.2.cwwwwW.
xx.Wwwwwc...ccccccccc..ccccccccc...cwwwwW.
xx.Wwwwwc...ccccccccc..ccccccccc...cwwwwW.
xx.Wwwwwc...ccccccccc..ccccccccc...cwwwwW.
xx.Wwcccc..........................ccccwW.
xx.Wwc1........2............2........1cwW.
xx.Wwc................................cwW.
xx.Wwc%....cccccccccccccccccccccc....%cwW.
xx.Wwc*%...cwwwwwwwwwwwwwwwwwwwwc...%*cwW.
xx.Wwce*%.1cwwwwwwwwwwwwwwwwwwwwc1.%*fcwW.
xx.WwcccccccwwwwwwwwwwwwwwwwwwwwcccccccwW.
xx.WwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW.
xx.WWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW.
xx........................................
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

###################################################
# Based on zotdef III. Idea from kilobyte.
NAME:   evilmike_crystal_ball_defense
TAGS:   no_rotate no_monster_gen
DEPTH:  Zot:1-4
ORIENT: float
MONS:   w:4 oklob sapling / w:4 oklob plant / w:6 burning bush / \
        w:6 lightning spire / nothing
MONS:   ice statue / silver statue / orange crystal statue / \
        patrolling curse skull / w:5 nothing
SUBST:  3 = 11.
# Not quite the orb, but but close enough
ITEM:   crystal ball of energy
SUBST:  . = .:9 ~:1
KFEAT:  ~ = dart trap / arrow trap / bolt trap / blade trap / \
            needle trap / spear trap / net trap
SUBST:  } = }:80 >, ) = ):80 >, ] = ]:80 >
MAP
        xxxxxxxxx
        xxx.2.xxx
        xxx...xxx
        xx..1..xx
xxxxxxxxx1.....1xxxxxxxxx
xxxxxxx.....).....xxxxxxx
xxx.....1..}d]..1.....xxx
xx...xx.....>.....xx...xx
@x..xxxxx1.....1xxxxx..x@
.x.3xxxxxx..1..xxxxxx3.x.
.x..xxxxxxx...xxxxxxx..x.
.x..xxxxxxx...xxxxxxx..x.
.x..xxxxxxxx.xxxxxxxx..x.
.x..xxxxxxxx2xxxxxxxx..x.
.x..xxxxxxxx.xxxxxxxx..x.
.xx3.................3xx.
...x.................x...
...x......3.3.3......x...
xxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Xom's getting in the "make people in Zot scream" bracket, with tricks
# resembling his overflow altars and effects. Extremely nasty and annoying.
NAME:    hangedman_xom_zot_hilarity
TAGS:    no_monster_gen no_trap_gen no_pool_fixup patrolling transparent
DEPTH:   Zot:1-4
WEIGHT:  3
KMONS:   1357 = guardian mummy ; randart dire flail ego:chaos | \
        randart great mace ego:chaos | randart battleaxe ego:chaos| \
        randart great sword ego:chaos | randart glaive ego:chaos | \
        randart scythe ego:chaos | any weapon randart ego:chaos w:4 | \
        randart dire flail w:5 | randart great mace w:5 | randart battleaxe w:5 | \
        randart great sword w:5 | randart glaive w:5 | randart scythe w:5 | \
        any weapon randart w:2 | dire flail ego:chaos w:5 | great mace ego:chaos w:5 | \
        great sword ego:chaos w:5 | battleaxe ego:chaos w:5 | glaive ego:chaos w:5 | \
        scythe ego:chaos w:5 | any weapon ego:chaos w:2 | dire flail w:30 | \
        great mace w:30 | battleaxe w:30 | great sword w:30 | \
        scythe w:30 | glaive w:30 | any weapon w:12 . \
        randart sling ego:chaos | randart bow ego:chaos | \
        randart crossbow ego:chaos | sling | bow | crossbow | \
        nothing w:15 . sling bullet | arrow | bolt . \
        lightning rod w:6 | rod of demonology w:4 | rod of the swarm w:4 | \
        rod of destruction w:2 | rod of striking w:6 | wand of hasting w:16 | \
        wand of confusion | wand of random effects w:6 | \
        wand of polymorph w:6 | any wand w:28 | any rod w:2 | \
        nothing w:45 . randart ring mail | randart chain mail w:20 | \
        randart scale mail | ring mail | \
        chain mail w:20 | scale mail | nothing w:20
KMONS:   2468 = killer klown
KMONS:   _ = ghost moth
KMONS:   90 = moth of wrath / butterfly w:30
KFEAT:   0 = known zot trap
KFEAT:   ^~ = teleport trap w:45 / zot trap w:4 / . w:1
KFEAT:   _ = altar_xom
KFEAT:   > = } mimic
KPROP:   ^~'"> = no_rtele_into
MARKER:  - = lua:restrict_door()
COLOUR:  ^~ = warp
FTILE:   ^' = floor_rough_magenta
FTILE:   ~" = floor_rough_lightmagenta
NSUBST:  > = 1:} / 1:) / 1:] / *:>, 9 = 1:0 / *:9
NSUBST:  ^ = 1:' / *:^, ~ = 1:" / *:~
SUBST:   d = tGmcc, c = cccccbbvvx, M = m`, ` = `:195 f:3 F:2
SUBST:   } = }:80 >, ) = ):80 >, ] = ]:80 >, l = wl, - = +
SHUFFLE: 56, 78, ^' / ~", cm / xn
MARKER:  f = lua:fog_machine { cloud_type = "seething chaos", pow_max = 17, \
             delay_min = 450, delay_max = 700, size = 1, excl_rad = 0 }
MARKER:  F = lua:fog_machine { cloud_type = "noxious fumes", pow_max = 17, \
             delay_min = 350, delay_max = 400, size_min = 2, size_max = 3}
MAP
        mmmmmmm
     mmmm2.1.2mmmm
  @@cmMMmm+m+mmMMmc@@
  @..clMMl```lMMlc..@
  c.c.```````````.c.c
 mmc.l````.c.````l.cmm
 mMl``c..`.>.`..c``lMm
 mMM``.>.`d..`d>.``MMm
mmmM``.d.^~~^`...``Mmmm
m3ml`````m--mm^````lm6m
m.+``...^m999-~d..``+.m
m4m``c>.~-9_9-~.>c``m5m
m.+``..d~-999m^...``+.m
m3ml````^mm--m`````lm6m
mmmM``...`^~~^.d.``Mmmm
 mMM``.>d`..d`.>.``MMm
 mMl``c..`.>.`..c``lMm
 mmc.l````.c.````l.cmm
  c.c.```````````.c.c
  @..clMMl```lMMlc..@
  @@cmMMmm+m+mmMMmc@@
     mmmm8.7.8mmmm
        mmmmmmm
ENDMAP

################################################################################
# Space-filling minivaults by bh.

NAME:   bh_zot_ikea_curve
DEPTH:  Zot
TAGS:   extra
MAP
xxxxxxxxxxxxx
@.....x.....@
@.....x.....@
xxxx..x..xxxx
x.....x.....x
x.....x.....x
x..xxxxxxx..x
x..x.....x..x
x..x.....x..x
x.....x.....x
x.....x.....x
xxxxxxxxxxxxx
ENDMAP

NAME:   bh_zot_lava_cross
DEPTH:  Zot
TAGS:   extra
MAP
     @@
    x..x
    x..x
    x..x
 xxxxllxxxx
@...l..l...@
@...l..l...@
 xxxxllxxxx
    x..x
    x..x
    x..x
     @@
ENDMAP

NAME:   bh_zot_levy_curve
DEPTH:  Zot
TAGS:   extra mini_float
SUBST:  W = Ww
MAP
          WWW.
          W.W..
          W.WWW
          ....W
         .....W
        ....WWW
       .......W
      ..www...W
     ...w.w.WWW
    ....wwwWW..
WWW.......W...
W........WW..
WWW..W..WW..
..W..W..W..
 .WWWWWWW.
ENDMAP

#################
# Dragon hoards.
#
# These range from small, essentially just flavour vaults,
# to large vaults with many dragons.
NAME:   st_single_dragon_hoard_1
TAGS:   extra
: st_zot_vaults_setup(_G)
MAP
 xxxxx
xx$x$xx
xx**xxx
x$xY*$x
xx$*$xx
 xx@xx
ENDMAP

NAME:   st_single_dragon_hoard_2
TAGS:   extra
: st_zot_vaults_setup(_G)
MAP
xxxxx
xx*Yx
x.$*x
x.xxx
x..xx
xx@xx
ENDMAP

NAME:   st_single_dragon_hoard_3
TAGS:   extra
: st_zot_vaults_setup(_G)
MAP
xxxxxxxx
xxxx$*xx
xx.$$Y*x
x...$*$x
xx.xx$xx
x...xxxx
xxx..xxx
xxxx@xxx
ENDMAP

NAME:   st_single_dragon_hoard_4
TAGS:   extra
: st_zot_vaults_setup(_G)
MAP
xxxxxxxxx
@.xxx**xx
xx..x$Y*x
x.xx.$$xx
xxx.xx$xx
xxxxxxxxx
ENDMAP

NAME:   st_small_dragon_hoard_1
TAGS:   extra
KITEM:  D = $
: st_zot_vaults_setup(_G)
MAP
xxxxxxxx
xxx*$xDx
xx*Y*x$x
xxx*$$xx
xD$$$x.x
xx$...xx
x.x....x
xx....xx
xxx..xxx
xxx@xxxx
ENDMAP

NAME:   st_small_dragon_hoard_2
TAGS:   extra
KITEM:  D = $
: st_zot_vaults_setup(_G)
MAP
xxxxxxxxxx
xxx$$$xxxx
xx$x*$$xxx
x*$*Y*xDxx
xD$x*$$$*x
xxx$$$$xxx
xxxx@xxxxx
ENDMAP

NAME:   st_small_dragon_hoard_3
TAGS:   extra
KITEM:  D = $
: st_zot_vaults_setup(_G)
MAP
xxxxxxxxxxxxxxxx
xxD$$xxD..xxx$xx
xx$x$$xx.x.x$x*x
x$*Y*$...xx.$$Yx
xx$*x$xx...xx$*x
xxxDxxx..xxxD$xx
xxxxxxx@x.xxxxxx
ENDMAP

NAME:   st_small_dragon_hoard_4
TAGS:   extra
KITEM:  D = $
: st_zot_vaults_setup(_G)
MAP
xxx@x.xxxxxxxxxxxx
xx.x.xxxxxxxxx.$xx
xxx..xxxxxxx..x$Dx
xxxx...xxx..xx$**x
xDxxxx....xxxDxYxx
x**x$x.x.x..xxxx*x
xxY$..xxxxx..xxxxx
xD*$xxxxxxxx...xxx
xxxxxxxxxxxx.xx@xx
ENDMAP

NAME:   st_small_dragon_hoard_5
TAGS:   extra
KITEM:  D = $
: st_zot_vaults_setup(_G)
MAP
xx..xxxxxxxxxxxxxx
x.xxxxxxxD*lxD*xxx
@x.xxxxx$xl$$l$**x
xx...xxx*$lllllYxx
..xx..xxx$.llll*Dx
xxxxx.xxxxx.llllxx
xxxxx...xx..lxllxx
xxxxxxx....xxlxxxx
xxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   st_large_dragon_hoard_1
SUBST:  D = DD$
KITEM:  D = $
: st_zot_vaults_setup(_G)
MAP
xxxxxxx@xxxxxxxx
xxxx.x...lxxlxxx
xxx|x.llllxl.xxx
xxD$llxlllllx..x
xDxllxl|DDll....
x.lllD|Y*Dlllx.x
.xlxllD*Dlll$$xx
xx.llxllllxx*Dxx
xxx...xll.$Dxxxx
xxxxxx...xxxxxxx
xxxxxxx@xxxxxxxx
ENDMAP

NAME:   st_large_dragon_hoard_2
SUBST:  D = D$
KITEM:  D = $
: st_zot_vaults_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxx
xxxxD$$Dxxxxxxxxxxxxxxx
xxxY*xx$$D$$xxxx...xxxx
xxx|$$|D$x*$$.x..x...xx
xxD$x|Y|D$Y*$...xxx.$$x
xxx$$D*$x$|$xx..xxx*$xx
xx$D$|$D$*xxxx.xxD|Y*xx
xxx$xY*$*Dxxx..xx$x|xDx
xxxxx|$D$xxx....xxD$Dxx
xxxxxxxxxxx..xx.xxxxxxx
xxxxxxxxxx..xxx...xxxxx
xxxxxxxx..x@xxxx.x..xxx
ENDMAP

NAME:   st_large_dragon_hoard_3
SUBST:  D = DDD$
KITEM:  D = $
: st_zot_vaults_setup(_G)
MAP
x.xx@xxxxxx.xxxxxxxxxxx
.xx...xxxxxx.xxxxxxxxxx
...xx..x......xxx....xx
xxxxxxx...xxxx....xx..x
xD$*xx..xxxx...xxxx$$xx
xxD$xxxx.x..xxxxxx$*$xx
xDx$$.x...x...xxxD*Y*Dx
xxxxxx..xxxxx..xxx$|$xx
xxxlx.x.lxlxxx.xxxx$$*x
xxlxll.lllxlxx..xxxDxDx
xxllxl$$Dlllxxx..xxx*xx
xxxlllxD*Dlxlxxx..xxxxx
xxlllDD|Y*llxxxxxx.xxxx
xxxxlx*D|Dxlllxxx....x.
xlllllD|Dlllxlxxx.xxx..
xxlxllllxllllllxxx.x.xx
xxxllxxlllxxlxxxxx..xxx
xxxxxxxxxxxxxxxx@.xxxxx
ENDMAP

# draconians are a bit more organised with their hoards.
NAME:   st_large_dragon_hoard_4
SUBST:  D = DDD$
KITEM:  D = $
KMONS:  1 = nonbase draconian band
: st_zot_vaults_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x$$$$xxxx.......xxxxxxxxxxxxxxxx
x$$$$xx......1....x*$*xxxx...xxx
xxx+xx..x.........+$|$xxx.....xx
xxx....xxxx......xx*$*xxx.xx...x
xx..xxxxxxxxxx..xxxxxxxx.xxx..xx
xxx..xxxD*Dxxxx...xxxxx..xxxx..x
xDxx.xx$*Y*$Dxxxx..x....xxxx..xx
xD$x.xxD$|D$$*xxxx...xxxxxxx..xx
xxx..xxx*$$$$$xxxxxxx$Dxxxx..xxx
xx.xxxxxxD$.1..x|*|xD|Dxx...xxxx
xxx..xxxxxx....+*|*xxDxx.x.xxxxx
xxxx.....x....xx|*|xxxx.x.xxxxxx
xxxxxx......xxxxxxxxx..x...xxxxx
xxxxxxxxxxxxxxxxxxxx.xxx@xx.xxxx
ENDMAP

# A mighty hoard indeed...
# Intended to be an extreme visual spectacle.
# Obviously lowering the amount of gold in stacks is necessary.
# There's about 800 gold.
NAME:   st_grand_hoard_of_tiamat
TAGS:   no_monster_gen
KMONS:  1 = patrolling Tiamat, patrolling golden dragon
SUBST:  D = DDD$
KITEM:  D = $
KITEM:  $ = gold q:1
: st_zot_vaults_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxx*x$$x$x$$xxxxxxxxx
xxxxx*xxD*$xxD$$$xD$x$xxxxx
xxx$xD$x$$$$$$x$*$$$$x$$xxx
xx$$x$$$*xx$$$$*Yx$$$$$xxxx
xx$x*$x*Y*$D$$$$*$$xD$x$*xx
xxxxD$x$*$$$D|DD$$$x$$$$Dxx
xx$$x$$$$$DD*|*$DD$$$x$$xx.
xxx$$$$D$$$||1||$$$$$$$$$..
xx$x$x$$$$$D*|*$D$D$x$$$x.@
xxxD*$$x$*$D$|DD$$x*$$$xxx.
xx*xx$$$xY*xxD$$$$*Y*xxD$xx
xxx$$$x$$*$$$$Dx$xx*$x$$xxx
xxxxx$$xDxx$$x$$x$$x$$xxxxx
xxxxxxxxx$$x$$xx*Dxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

#########################
# Minivaults based on the old orb chambers.
#
NAME:    zot_minivault_1
TAGS:    transparent
WEIGHT:  20
DEPTH:   Zot:1-4
MAP
 xxxxxxxxxxx
xxx...0...xxx
xx..0...0..xx
x0.........0x
@..0..0..0..@
x0.........0x
xx..0...0..xx
xxx...0...xxx
 xxxxxxxxxxx
ENDMAP

# Based on orb_monky_inset
NAME:    zot_minivault_2
TAGS:    transparent
WEIGHT:  5
DEPTH:   Zot:1-4
SUBST:   > = }]}
MAP
 xxxxxxxxxxx
xxx0.....0xxx
xx....0....xx
x0...xxx...0x
@...xxxxx...@
x0...xxx...0x
xx....0....xx
xxx0.....0xxx
 xxxxx.xxxxx
xx...0.0...xx
x...........x
x.....>.....x
xx.........xx
 xxxxxxxxxxx
ENDMAP

# Based on orb_two_ways
NAME:    zot_minivault_3
TAGS:    transparent
WEIGHT:  5
DEPTH:   Zot:1-4
SUBST:   < = ([{000
MAP
 xxxxxxxxxxx
xxxx.....xxxx
xx....x...xxx
xx.<.xxx..xxx
xx...xx..xxxx
xxxxxx..xxxxx
xxxx.....xxxx
xxx..0x0..xxx
x0...xxx...0x
@..0xxxxx0..@
x0...xxx...0x
xxx..0x0..xxx
xxxx.....xxxx
xxxxx..xxxxxx
xxxx..xx...xx
xxx..xxx.<.xx
xxx...x....xx
xxxx.....xxxx
 xxxxxxxxxxx
ENDMAP

# Based on orb_monky_within_orb
NAME:    zot_minivault_4
TAGS:    transparent
DEPTH:   Zot:1-4
COLOUR:  n = magenta
TILE:    n = dngn_transparent_stone_magenta
MAP
 xxxxxxxxxxx
xxx0.....0xxx
xx..nn0nn..xx
x..n0...0n..x
@..n.0.0.n..@
x..n0...0n..x
xx..nn0nn..xx
xxx0.....0xxx
 xxxxxxxxxxx
ENDMAP

# Based on orb_monky_twist
NAME:    zot_minivault_5
TAGS:    transparent
DEPTH:   Zot:1-4
COLOUR:  n = magenta
TILE:    n = dngn_transparent_stone_magenta
MAP
 xxxxxxxxxxx
xxxnn...0.xxx
xxnn0.0.n..xx
xnnn....nn.0x
@.nn0.0.0nn.@
x0.nn....nnnx
xx..n.0.0nnxx
xxx.0...nnxxx
 xxxxxxxxxxx
ENDMAP

# Based on orb_nicolae_swirls
# Guarantees an upstairs and downstairs, and makes sure the player can see
# them both from one side.
# Stone is bordered by rock to make it stand out less and not look ugly.
NAME:    zot_minivault_6
TAGS:    transparent
DEPTH:   Zot:1-4
SUBST:   < = ([{
SUBST:   > = }]}
COLOUR:  n = magenta
TILE:    n = dngn_transparent_stone_magenta
MAP
 xxxxxxxxxxxxx
xxcccccccccccxx
xccxxxxxxxxxccx
xcxx.......xxcx
xcx0...>...0xcx
xcx.........xcx
xcxx0.....0xxcx
xcxxxxx...xxxxx
xcxxxx..0nnn.
xxxx...0nnn0@
  ...0nnn....
  @0nnn0...xxxx
  .nnn...xxxxcx
xxxxx0..xxxxxcx
xcxx.......xxcx
xcx.........xcx
xcx0...<...0xcx
xcxx.......xxcx
xccxxxxxxxxxccx
xxcccccccccccxx
 xxxxxxxxxxxxx
ENDMAP

# Based on orb_nicolae_fourfold
NAME:    zot_minivault_7
TAGS:    transparent
WEIGHT:  5
DEPTH:   Zot:1-4
SUBST:   > = }]}
MAP
 xxxxxxxxx
xx...0...xx
x0.0...0.0x
x..xx.xx..x
x..x...x..x
x..x.0.x..x
x..x...x..x
x..xx.xx..x
x.xx...xx.x
x.x.0.0.x.x
@.x0.>.0x.@
x.x.0.0.x.x
x.xx...xx.x
x..xx.xx..x
x..x...x..x
x..x.0.x..x
x..x...x..x
x..xx.xx..x
x0.0...0.0x
xx...0...xx
 xxxxxxxxx
ENDMAP

# Based on orb_grunt_hallway
NAME:    zot_minivault_8
TAGS:    transparent
WEIGHT:  5
DEPTH:   Zot:1-4
SUBST:   > = }]}
MAP
 xxxxxxxxx
xxxx...xxxx
xxx0...0xxx
xxx..>..xxx
xxx0...0xxx
xxxx...xxxx
xxxx0.0xxxx
x00x...x00x
x..x...x..x
@..x0.0x..@
x..x...x..x
x..x...x..x
x..x0.0x..x
x.0x...x0.x
x.0x...x0.x
x..x0.0x..x
x..x...x..x
x.........x
xx.......xx
 xxxxxxxxx
ENDMAP

##########################################################################

NAME: nicolae_zot_draconian_monastery
DEPTH: Zot:1-4
ORIENT: float
TAGS: no_trap_gen no_monster_gen no_item_gen
MONS: draconian monk / base draconian w:15, patrolling draconian zealot, patrolling draconian knight
MONS: patrolling draconian scorcher
SHUFFLE: ]}), rybgmq, DZF
MARKER: + = lua:restrict_door()
# The choice of altar might matter more once monster gods are better implemented.
# Till then, it's just a style thing.
KFEAT: _ = altar_xom / altar_lugonu / altar_zin / altar_the_shining_one / \
           altar_makhleb / altar_yredelemnul
SUBST: 1 = 1.
# What kind of holy books would draconians have?
KITEM: D = book of the Dragon
KITEM: Z = randbook spells:summon_demon|fireball|twisted_resurrection|airstrike title:the_Zealot
KITEM: F = randbook spells:bolt_of_fire|fireball|sticky_flame|conjure_flame title:Fire_and_Brimstone
KITEM: _ = manual of invocations
COLOUR: r = red, y = yellow, b = blue, g = green, m = magenta, q = cyan, c = magenta
TILE: r = dngn_transparent_stone_red, y = dngn_transparent_stone_yellow, \
      b = dngn_transparent_stone_blue, m = dngn_transparent_stone_magenta, \
      g = dngn_transparent_stone_green, q = dngn_transparent_stone
KFEAT: rybgmq = n
MAP
           @.......................@
ccccccccccccqqcrrcyycbbcggcmmcqqcrrccccccccccc
cG............G..G..G..G..G..G..G..ccc.ccc.ccc
c...ccccc..T.......................cc...c...ccl
c]2.cccDccccc..1..1..1..1..1..1..1.c..1...1..cl
c...cc...cc........................cc.......ccll
cG..+..2....c.cc.cc.cc.cc.cc.cc.cc.cT........clll
cccccc...cccc.cc.cc.cc.cc.cc.cc.cc.ccc.......clllll
ccccTc...cccc......................ccc..1..1.c....l
@qc......1.........................+.........+..3.@
@mF.4.._......1..1..1..1..1..1..1..+.........+.T..@
@qc......1.........................+.........+..3.@
ccccTc...cccc......................ccc..1..1.c....l
cccccc...cccc.cc.cc.cc.cc.cc.cc.cc.ccc.......clllll
cG..+..2....c.cc.cc.cc.cc.cc.cc.cc.cT........clll
c...cc...cc........................cc.......ccll
c}2.cccZccccc..1..1..1..1..1..1..1.c..1...1..cl
c...ccccc..T.......................cc...c...ccl
cG............G..G..G..G..G..G..G..ccc.ccc.ccc
ccccccccccccqqcrrcyycbbcggcmmcqqcrrccccccccccc
           @.......................@
ENDMAP

##########################################################################

NAME:    hangedman_zot_spectrum
TAGS:    no_monster_gen
DEPTH:   Zot:1-4
KMONS:   A = red very ugly thing
KMONS:   B = red draconian
KMONS:   C = fire crab / nonbase red draconian w:6
KMONS:   D = cyan very ugly thing
KMONS:   E = black draconian
KMONS:   F = electric golem / nonbase black draconian w:6
KMONS:   G = green very ugly thing
KMONS:   H = green draconian
KMONS:   I = green death / nonbase green draconian w:6
KMONS:   J = white very ugly thing
KMONS:   K = white draconian
KMONS:   L = frost giant / nonbase white draconian w:6
KMONS:   M = brown very ugly thing
KMONS:   N = yellow draconian
KMONS:   O = acid blob / nonbase yellow draconian w:6
KMONS:   Z = golden dragon / killer klown / Tiamat, golden dragon / killer klown
KITEM:   BEHKN = $ w:15 / % w:25 / *
KITEM:   z = any good_item
KITEM:   Z = acquire any
SHUFFLE: defg{>Z' / defg{<'' / fgde>{'Z / fgde<{'' / defg'|Z' / fgde|''Z / \
         fgfg''() / fgfg'')( / dede'''' / dede'''' / dede''''
SHUFFLE: ABC / DEF / GHI / JKL / MNO
SUBST:   d = @, e = ., f = x, g = c, c : ccb, Z = z Z:6, {' = .
NSUBST:  < = 1:{ / 1:[, > = 1:} / 1:]
MAP
 ddddxxxx
ddeeeecxxxxxxxx
dee{..cxxxccccx
de{'c+cccccBBcx
de.cc.......Acxxx
xe.+.......Accccx
xccc..Cc+ccccEEcx
xxxc..cc......Dcxxx
xxxc..+......Dccccx
 xxc..c..Fc+cccHHcx
 xcc..c..cc.....Gcxxx
 xcB.Ac..+.....Gccccx
 xcBAcc..c..Ic+ccKKcx
 xccccE.Dc..cc....Jcx
 xxxxcEDcc..+....Jccx
    xccccH.Gc..Lc+cgx
    xxxxcHGcc..cc.>gf
       xccccK.J+.Z.gf
       xxxxcKJcc>.ggf
          xccccggggff
          xxxxxxffff
ENDMAP

##########################################################################

NAME:    hangedman_zot_vaults_quadrants
TAGS:    no_monster_gen no_item_gen transparent
DEPTH:   Zot:1-4
ORIENT:  centre
# Humanoids, guardian beasts, the strongest; first V, then Zot.
MONS:    ettin / stone giant w:5 / fire giant / frost giant
MONS:    base draconian w:18 / death cob w:1 / guardian mummy w:1
MONS:    glowing shapeshifter w:20 / sphinx w:15 / quicksilver dragon w:20 / \
         iron dragon w:20 / shadow dragon / golden dragon
MONS:    fire dragon / ice dragon / storm dragon w:8 / moth of wrath w:12 / \
         ghost moth w:5 / tentacled monstrosity w:5 / bone dragon w:5
MONS:    titan / lich / deep elf annihilator w:3 / deep elf high priest w:4 / \
         deep elf sorcerer w:3 / giant orange brain w:3
MONS:    nonbase draconian w:20 / killer klown / orb of fire w:5 / \
         curse toe w:5 / ancient lich / electric golem
SHUFFLE: ABCDEF, GH / GH / GH / HG
NSUBST:  1 = 3:1 / 3:2 / 1:112 / 1:122, 0 = 3:1 / 3:2 / 1:112 / 1:122
NSUBST:  3 = 3:3 / 3:4 / 1:334 / 1:344, 6 = 3:5 / 3:6 / 1:556 / 1:566
SUBST:   % = $%, * = %:3 *:1, | = %:60 *:25 |:15, 1 = 1:3 .:2
SUBST:   2 = 2:2 .:1, 3 = 3:4 .:1, 4 = 4:6 .:1, 5 = 5:6 .:1, 6 = 6:5 .:1
SUBST:   A : v:4 .:1, B : v:7 .:3, C : v:1 .:4, DEFH = .
SUBST:   G : v:9 .:1, - : +., + : +:5 .:1, { : {:2 }:3 .:2
NSUBST:  { = 1:{ / 1:( / 1:[ / 1:<, } = 1:} / 1:) / 1:] / 1:>
SUBST:   ( = (:14 >:1 <:1 ):2, [ = [:14 >:1 <:1 ]:2, ) = ):70 >(, ] = ]:70 >[
MAP
       xx@@vvvvv@@xx
      xx..vvvvvvv..xx
      x..vvv6v6vvv..x
     xx.vvv.*v*.vvv.xx
    xx..vv3*|-|*3vv..xx
   xx..Gvvvv+v+vvvvG..xx
 xxx..H.vv0$%-%$0vv.H..xxx
xx...G...vv4$v$4vv...G...xx
x..vvvv...vv.v.vv...vvvv..x
@.vvvvvv..1v...v1..vvvvvv.@
@vvv3v0vv1...A...1vv0v3vvv@
vvv.*v$4vv.BC{DB.vv4$v*.vvv
vv6*|+%$...DEFEC...$%+|*6vv
vvvv-v-vv.A{F<F{A.vv-v-vvvv
vv6*|+%$...CEFED...$%+|*6vv
vvv.*v$4vv.BD{CB.vv4$v*.vvv
@vvv3v0vv1...A...1vv0v3vvv@
@.vvvvvv..1v...v1..vvvvvv.@
x..vvvv...vv.v.vv...vvvv..x
xx...G...vv4$v$4vv...G...xx
 xxx..H.vv0$%-%$0vv.H..xxx
   xx..Gvvvv+v+vvvvG..xx
    xx..vv3*|-|*3vv..xx
     xx.vvv.*v*.vvv.xx
      x..vvv6v6vvv..x
      xx..vvvvvvv..xx
       xx@@vvvvv@@xx
ENDMAP

####
# Revenge of the oklobs!!!
NAME:   grunt_zot_stairs_overgrown
DEPTH:  Zot, !Zot:$
ORIENT: float
MONS:   oklob plant, plant, bush, fungus
SUBST:  2 = 23
SUBST:  . = .:30 2 3 4
SUBST:  : = .
{{
local sm = TriggerableFunction:new {
  func="callback.grunt_zot_stairs_overgrown_start", triggered=false }
sm:add_triggerer(DgnTriggerer:new { type = "player_los" })
lua_marker('1', sm)

local tm = TriggerableFunction:new {
  func="callback.grunt_zot_stairs_overgrown_oklobify",
  repeated=true,
  data={warned = false, count = 0} }
tm:add_triggerer(DgnTriggerer:new { type="turn", delay_min=30, delay_max=100 })
lua_marker('}', tm)
}}
MARKER: 2 = lua:props_marker { oklobify=1 }
SHUFFLE: }])
SUBST: ] = ]>, ) = )>
MAP
      xxxxxxxxx
    xxxcccccccxxx
  xxxccc.....cccxxx
  xccc.........cccx
 xxc....:.:......cxx
 xcc...:.:.:.....ccx
xxc...:.222:2.....cx
xcc....:2:::2.....ccx
xc....:.2:222......cx
xc.....:2:}:2......cx
xc....:.2:1:2......cx
xc.....:2]:)2......cx
xc....:.22222......cx
xcc....:..........ccx
xxc.....:.........cxx
 xcc.....:.......ccx
 xxc......:......cxx
  xccc....:....cccx
  xxxccc:::::cccxxx
    xxccc+++cccxx
     xxxx@@@xxxx
ENDMAP

NAME:    hangedman_zot_gyre
TAGS:    extra transparent
DEPTH:   Zot
WEIGHT:  8
KITEM:   % = % no_pickup
KFEAT:   Z = known zot trap
SHUFFLE: dD, eE, fF, gG, hH, dD / eE / fF / gG / hH
SHUFFLE: cCbB / cCbB / bBcC, qQ / qQ / ZZ / wl / '' / ''
SUBST:   defhH = X, DEF = ., g : X.., G : X.., ' = .
SUBST:   B = bbCc, C : bWcc, b : bbc, c : bWcc, X : bcxx, W : wl
NSUBST:  0 = 2 = 0:19 .:1 / *:.
KPROP:   l = no_cloud_gen
KMASK:   lw = no_monster_gen
COLOUR:  qQ = darkgray
FTILE:   qQ = floor_rough_darkgray
MARKER:  q = lua:fog_machine {cloud_type = "freezing vapour", \
             pow_min = 9, pow_max = 10, delay = 20, size_min = 1, size_max = 1, \
             start_clouds = 1, excl_rad = -1 }
MARKER:  Q = lua:fog_machine {cloud_type = "flame", \
             pow_min = 9, pow_max = 10, delay = 20, size_min = 1, size_max = 1, \
             start_clouds = 1, excl_rad = -1 }
:  if you.depth() == dgn.br_depth(you.branch()) then
SUBST:  { = {([<
:  else
SUBST:  { = {([<})]>%
:  end
MAP
           ......
         ..........
        ..xxxxxxx...
        ........hx...
      ....B......hx....
     .............x.....
    ..G.C.G.C.....ffxxx..
    ..x...x.......x......
   ...x...x...B...x...c....
  ....F...E.......x.........
 ...xxFxxxExxD..Q.eexxx.b.x.
 ..xH.......0xD...x.......x..
..xH......q...x..0x...c...x..
..x...........x..xd.......x..
..x.....b..dxx{xxd..b.....x..
..x.......dx..x...........x..
..x...c...x0..x...q......Hx..
..x.......x...Dx0.......Hx..
 .x.b.gxxee.Q..DxxExxxFxx...
 .........x.......E...F....
  ....c...x...B...x...x...
    ......x.......x...x..
    ..gxxff.....C.G.C.G..
     .....x.............
      ....xh......B....
        ...xh........
         ...xxxxxxx..
          ..........
            ......
ENDMAP

####
# Care for a little gambling, adventurer?
NAME: grunt_zot_stairs_roulette
DEPTH: Zot, !Zot:$
TAGS: no_monster_gen
ORIENT: float

MONS: golden dragon / quicksilver dragon / storm dragon / shadow dragon
MONS: killer klown

# Written out in full so we don't get zealots:
MONS: draconian monk w:1 / draconian knight w:1 / \
      draconian scorcher w:1 / draconian annihilator w:1 / \
      draconian shifter w:1 / draconian caller w:1 / \
      base draconian w:12

KFEAT: hijk = teleporter
KFEAT: O = floor
{{
dgn.persist.grunt_zot_stairs_roulette_doors_spawned = false

local spawndoors = TriggerableFunction:new{
    func="callback.grunt_zot_stairs_roulette_spawn_doors",
    repeated=true,
    data={triggered=false} }
spawndoors:add_triggerer(DgnTriggerer:new{type="player_los"})
lua_marker('O', spawndoors)
lua_marker('O', props_marker {hidden_door=1})

local tm = TriggerableFunction:new{
    func="callback.grunt_zot_stairs_roulette_door_open",
    repeated = true, data={triggered=false}
    }
tm:add_triggerer(DgnTriggerer:new{type="player_move"})
lua_marker('-', tm)

local tele1 = TriggerableFunction:new{
    func="callback.grunt_zot_stairs_roulette_teleport",
    repeated=true,
    data={dest=1} }
tele1:add_triggerer(DgnTriggerer:new{type="player_move"})
lua_marker('h', tele1)

local tele2 = TriggerableFunction:new{
    func="callback.grunt_zot_stairs_roulette_teleport",
    repeated=true,
    data={dest=2} }
tele2:add_triggerer(DgnTriggerer:new{type="player_move"})
lua_marker('i', tele2)

local tele3 = TriggerableFunction:new{
    func="callback.grunt_zot_stairs_roulette_teleport",
    repeated=true,
    data={dest=3} }
tele3:add_triggerer(DgnTriggerer:new{type="player_move"})
lua_marker('j', tele3)

local tele4 = TriggerableFunction:new{
    func="callback.grunt_zot_stairs_roulette_teleport",
    repeated=true,
    data={dest=4} }
tele4:add_triggerer(DgnTriggerer:new{type="player_move"})
lua_marker('k', tele4)

lua_marker('d', props_marker {dest=1})
lua_marker('e', props_marker {dest=2})
lua_marker('f', props_marker {dest=3})
lua_marker('g', props_marker {dest=4})
}}
SHUFFLE: defg, }])>
SUBST: ] = ]>, ) = )>
MAP
    ....@....
 nnnnn.n.n.nnnnn
 nd..n.nhn.n1.en
 n.U.n.nnn.n...n
.n...nn...nn..1n.
.nnnn.nnOnn.nnnn.
....nn=---=nn....
.nnn.n-}-]-n.nnn.
@.in.O--U--O.nj.@
.nnn.n-)->-n.nnn.
....nn=---=nn....
.nnnn.nnOnn3nnnn.
.n222nn...nn333n.
 n..2n.nnn.n333n
 nf.2n.nkn.n33gn
 nnnnn.n.n.nnnnn
    ....@....
ENDMAP

####
# Madness, frenzy, and lunacy, eh?
# bh's curves made me think of some weird yet orderly decorum for Zot.
NAME:    hangedman_zot_decor_unnerving_road
TAGS:    extra transparent
DEPTH:   Zot
WEIGHT:  8
SHUFFLE: @&, (), vb / vb / vb / bv / cc, lw / lw / lw / '', JK / LM / NO
SHUFFLE: JK / JK / JK / !!, LM / ??, NO / `` / `` / `` / ``, JKLMNO / JKLMNO / KJMLON
SUBST:   '!?`& = ., X : xxc, JLN = F, KMO = f
SUBST:   ( = (:1 .:14, ) = ):1 .:14
KMASK:   wlfF = no_monster_gen
COLOUR:  fF = darkgray
FTILE:   fF = floor_rough_darkgray
MARKER:  f = lua:fog_machine {cloud_type = "freezing vapour", \
             pow_min = 9, pow_max = 10, delay = 20, size_min = 1, size_max = 1, \
             start_clouds = 1, excl_rad = -1 }
MARKER:  F = lua:fog_machine {cloud_type = "flame", \
             pow_min = 9, pow_max = 10, delay = 20, size_min = 1, size_max = 1, \
             start_clouds = 1, excl_rad = -1 }
MAP
      XXXX...&&.
      Xllvv......
    XXXlv..v....@
   XXlllv..v....@
   Xllvv.vvJ.....
 XXXlv..v.......
XXlllv..v....XXXX
Xllvv(vvL..ObbwwX
Xlv..v.....b..bwXXX
Xlv..v.....b..bwwwXX
Xllvv.vv....bb.bbwwX
XXlllv..v.....b..bwX
 XXXlv..v.....b..bwX
   XllvvN..Mbb)bbwwX
   XXXX....b..bwwwXX
    .......b..bwXXX
   .....Kbb.bbwwX
   @....b..bwwwXX
   @....b..bwXXX
   ......bbwwX
    .&&...XXXX
ENDMAP

NAME:    hangedman_zot_decor_checkerboard
TAGS:    extra transparent
DEPTH:   Zot
WEIGHT:  8
KFEAT:   Z = known zot trap
SHUFFLE: @' / '@ / "" / "", defg / jkhi / HIJK / NOLM
SUBST:   d : wbb, e : wwb, g : lvv, f : llv
SUBST:   h : lvv, i : llv, j : wbb, k : wwb
SUBST:   H : wwwwb, I : wwwwb, J : llllv, K : llllv
SUBST:   L : lvvvv, M : lvvvv, N : wbbbb, O : wbbbb
SHUFFLE: bv / bv / bv / vb / xx / cc
NSUBST:  Z = 1 = (:1 Z:14 / 1 = ):1 Z:14 / *:Z
SUBST:   Z = ZZUG..., '" = .
KPROP:   l = no_cloud_gen
KMASK:   wl = no_monster_gen
MAP
.......     ...'...
.dw.lf.     .Hw.lJ.
.we.gl.     .wI.Kl.
@..Z...........Z.........
.lg.ew.hl.wj.lK.Iw.Ll.wN.
.fl.wd.li.kw.Jl.wH.lM.Ow.
.........Z...........Z..@
      .wk.il.     .wO.Ml.
      .jw.lh.     .Nw.lL.
      ...'...     .......
ENDMAP

NAME:    hangedman_zot_decor_slant
TAGS:    extra transparent
DEPTH:   Zot
WEIGHT:  8
KFEAT:   zZ = known zot trap
KFEAT:   fF = floor
SHUFFLE: BVfF / VBFf, BV / BV / BV / wl, bv / bv / cc, aAuU / aAuU / aAuU / uUaA
SHUFFLE: AU / "", fF / fF / zz / '' / '', @! / !@ / ?? / ??
SUBST:   aAB = b, uUV = v, Z = ()G .:45 Z:25, '"!? = .
KMASK:   wlfF = no_monster_gen
COLOUR:  fF = darkgray
FTILE:   fF = floor_rough_darkgray
MARKER:  f = lua:fog_machine {cloud_type = "freezing vapour", \
             pow_min = 9, pow_max = 10, delay = 2, size_min = 1, size_max = 1, \
             start_clouds = 1, excl_rad = -1 }
MARKER:  F = lua:fog_machine {cloud_type = "flame", \
             pow_min = 9, pow_max = 10, delay = 2, size_min = 1, size_max = 1, \
             start_clouds = 1, excl_rad = -1 }
MAP
            ...!..
           ........
           ..UuuU..
         ....uuuu.!
         @vv.uuuu..
         .vv.uuuU..
      ......F......
      .vv.VV.vv...
      .vv.VV.vv.
   .@....Z....@.
   .bb.BB.bb.
 ...bb.BB.bb.
......f......
..Aaaa.bb.
..aaaa.bb@
!.aaaa....
..AaaA..
........
 ..!...
ENDMAP

NAME:    hangedman_zot_decor_scrapings
TAGS:    transparent extra
DEPTH:   Zot
KFEAT:   Z = known zot trap
SHUFFLE: HIJKLM / HIJKLM / IHKJML
SUBST:   HJL : xxcb, I : x..., K : x..., M : x...
SUBST:   N : xcG.., O : xc..., P : xcG.., Z = ()G .:45 Z:25
MAP
           ..   ...
          .......x..
         ..x......x..
        ....x..O......
    ................x..
   ..J...P...L...N.I.H.
  ..x.x.....x.x.....x..
 ....K...M.Z.M...K....
..x.....x.x.....x.x...
.H.I.N...L...P...J...
..x.................
 ......O...x....
  ..x.......x..
   ..x........
    ...    ..
ENDMAP

NAME:    hangedman_zot_decor_tridents
TAGS:    transparent extra
DEPTH:   Zot
KFEAT:   Z = known zot trap
SHUFFLE: vb / xy / XY
SUBST:   = = ()+Z .:60, - : ++., _ : +.., H : z., I : z., J : z.
SUBST:   K : zzwl...., L : zzzwl.., M : zzwl...
SUBST:   N : zzzwl.., O : zzwl..., P : zzwl.., XyYz = x
KMASK:   wl = no_monster_gen
MAP
         xxx.....
         xxx.......
         x...Iv.vv.
    .....x...v.....
  .....K.-.O...N..@
.........x...v.....
.bb.bJ..Pxv_vHv.vv.
.....b...x...v.....
@..L...M.=.M...L..@
.....b...y...v.....
.bb.bHb_byP..Jv.vv.
.....b...y.........
@..N...O.-.K.....
.....b...y.....
.bb.bI...y
.......yyy
  .....yyy
ENDMAP

####
# A bit of sporting fun...
NAME: grunt_zot_stairs_faceoff
DEPTH: Zot, !Zot:$
ORIENT: float
# The competitors:
{{
  local teams = { "black", "yellow", "pale", "green", "purple",
                  "red", "white", "grey", "mottled" }
  local team1 = crawl.random2(#teams) + 1
  local team2 = 0
  repeat
    team2 = crawl.random2(#teams) + 1
  until team2 ~= team1
  mons(teams[team1] .. " draconian")
  mons(teams[team1] .. " draconian knight")
  mons(teams[team2] .. " draconian")
  mons(teams[team2] .. " draconian knight")
}}
# The refs:
MONS:   Killer Klown
# The ball / puck / whatever:
ITEM:   crystal ball of energy

KFEAT:  K = iron_grate
NSUBST: @ = 2:@ / *:x
SUBST:  } = }:80 >, ] = ]:80 >, ) = ):80 >
MAP
  xxxxxxxxxxxxxxxxxxxxxxx
 xx.....................xx
xx..mmmmmm.mmmmm.mmmmmm..xx
x..mm........5........mm..x
@.mm...................mm.@
x.m.KK.....1...3.....KK.m.x
x.m.K}..1.........3...K.m.x
x.m.K.1....2.d.4....3)K.m.x
x.m.K]..1.........3...K.m.x
x.m.KK.....1...3.....KK.m.x
@.mm...................mm.@
x..mm........5........mm..x
xx..mmmmmm.mmmmm.mmmmmm..xx
 xx.....................xx
  xxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

####
# A microcosm of everything Zot: draconians, dragons, weird enemies,
# and fractal vault patterns.
NAME:   grunt_zot_stairs_mandelzot
TAGS:   no_monster_gen
DEPTH:  Zot, !Zot:$
ORIENT: east
SUBST:  } = }:80 >, ] = ]:80 >, ) = ):80 >
MONS:   nonbase draconian, base draconian w:30 / nothing, moth of wrath
MONS:   fire dragon w:30 / ice dragon w:30 / shadow dragon w:20 / \
        storm dragon w:50 / bone dragon w:30 / golden dragon w:35
MONS:   Killer Klown / electric golem w:5 / curse toe w:5 / nothing
MONS:   orb of fire
MAP
                   xxxx
                   xllxxx
                   xllllx
                  xxllllxx
                 xxll.lllxx
              xxxxll.].lllxxxxx
              xllllll..lllllllx
             xxl3l.......lllllx
             xll...2.1.2...3llx
     xxxxxxxxx.l...........lllx
     xllxllxxll.....2.2.....llx
    xxllllllll..............llx
   xxlll....ll...5...........lx
  xxlll.2.2..l............4.llx
xxxlll......................llx
@......2.1.2.3......5..4.}6lllx
xxxlll......................llx
  xxlll.2.2..l............4.llx
   xxlll....ll...5...........lx
    xxllllllll..............llx
     xllxllxxll.....2.2.....llx
     xxxxxxxxx.l...........lllx
             xll...2.1.2...3llx
             xxl3l.......lllllx
              xllllll..lllllllx
              xxxxll.).lllxxxxx
                 xxll.lllxx
                  xxllllxx
                   xllllx
                   xllxxx
                   xxxx
ENDMAP

####
NAME: nicolae_moth_party
TAGS: no_pool_fixup no_monster_gen
DEPTH: Zot, !Zot:$
ORIENT: north
SUBST: l : lw
SHUFFLE: ]})>, 12
MONS: ghost moth, moth of wrath
MONS: any base draconian
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxx......xxxxxxxxx......xxx
xx.3.]....xxx}xxx....).3.xx
x...llll...x...x...llll...x
x..llllll.1..2..1.llllll..x
x..llllll.........llllll..x
x...llll...........llll...x
xx...........3...........xx
xxx......3.......3......xxx
xxxx..3.............3..xxxx
xxxxx.................xxxxx
xxxx...................xxxx
xxx.....................xxx
xx...lll...........lll...xx
xx..lllll..x...x..lllll..xx
xx...lll...x...x...lll...xx
xxx...3...xx...xx...3...xxx
xxxx.....xxx...xxx.....xxxx
xxxxxxxxxxx@@@@@xxxxxxxxxxx
ENDMAP

NAME: nicolae_zot_crop_circle
DEPTH: Zot, !Zot:1
TAGS: no_monster_gen transparent
SHUFFLE: {[(
NSUBST: p = 8:1 / *:2
KMONS: 1 = plant
KMONS: 2 = death cob
MAP
   ..@..
  .......
 ...ppp...
...p...p...
..p.....p..
@.p..{..p.@
..p.....p..
...p...p...
 ...ppp...
  .......
   ..@..
ENDMAP


NAME: nicolae_zot_stationary_front
DEPTH: Zot
ORIENT: float
TAGS: transparent no_pool_fixup patrolling
SHUFFLE: Gg/Gg/Gg/Hh, Pp/Pp/Pp/Qq
SUBST: GP = x, g = r, p = c, HQ = @, hq = .
COLOUR: r = red, c = blue
KMONS: R = nonbase red draconian
KMONS: C = nonbase white draconian
KMONS: D = fire dragon
KMONS: I = ice dragon
KFEAT: r = c
MAP
    xxx                   xxx
   xxrxx                 xxrxx
  xxrrrxx               xxrrrxx
 xxrrlrrxx             xxrrlrrxx
xxrrlllrrxx           xxrrlllrrxx
xrrllDllrrxxxxxxPxxxxxxrrllDllrrxxxxxxPxxxxx
rrll...llrrcccccpcccccrrll...llrrcccccpccccc
@....R..........C..........R..........C....@
rrrrrgrrrrrccw.....wccrrrrrgrrrrrccw.....wcc
xxxxxGxxxxxxcww...wwcxxxxxxGxxxxxxcww...wwcx
           xccwwIwwccx           xccwwIwwccx
           xxccwwwccxx           xxccwwwccxx
            xxcccccxx             xxcccccxx
             xxxxxxx               xxxxxxx
ENDMAP

##############################################################################
##############################################################################
#
# hall_of_Zot_5
#
##############################################################################
##############################################################################

NAME:       hall_of_Zot
PLACE:      Zot:$
ORIENT:     north
TAGS:       no_dump layout_halls
LFLAGS:     no_tele_control
LFLOORCOL:  magenta
LROCKCOL:   lightmagenta
LFLOORTILE: floor_rough_magenta
LROCKTILE:  wall_zot_lightmagenta

#traps
SUBST:    C = X:900 c:100
KFEAT:    8 = any trap / w:20 floor
SHUFFLE:  ABab / ^^FF / ^^FF / ^^FF
SHUFFLE:  Aa / Bb
KFEAT:    A = any trap / w:30 floor
KFEAT:    B = any trap / w:10 floor
SUBST:    a=F, b=6 G
SUBST:    ^ = ^ .:10
SUBST:    . = .:500 ^
KMASK:    1! = no_item_gen
SUBST:    ! = ^ .:40
SUBST:    F = .

#monsters
MONS:     patrolling Orb Guardian, Killer Klown, electric golem, orb of fire
MONS:     ancient lich, orange crystal statue / silver statue / ice statue
SUBST:    8=8., 8=8., 5=5.
SHUFFLE:  234
SUBST:    ? = .:8 ?
SUBST:    ? : KLM
SUBST:    K = 4:30 5 8 9
SUBST:    L = 4 5:30 8 9
SUBST:    M = 4 5 8:30 9

COLOUR:   c = magenta
TILE:     c = dngn_stone_wall_magenta

KITEM:    Z = Orb of Zot

validate {{ return has_exit_from_glyph('Z') }}

MAP
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
XXXXXXXXXXXXXXX.............XXXXXXXXXXXXXXXXXXXXXXXXX.............XXXXXXXXXXXXXX
XXXXXXXXXXXX...................XXXXXXXXXXXXXXXXXXX...................XXXXXXXXXXX
XXXXXXXXXXX..........3...........XXXXXXXXXXXXXXX...........3..........XXXXXXXXXX
XXXXXXXXXX..8.....................XXXXXXXXXXXXX.....................8..XXXXXXXXX
XXXXXXXXXX.......?..?.....8.8.....XXX...1...XXX.....8.8.....?..?.......XXXXXXXXX
XXXXXXXXXXX.8...........?.........XX..1...1..XX.........?...........8.XXXXXXXXXX
XXXXXXXXXXXX......................X1...!!!...1X......................XXXXXXXXXXX
XXXXXXXXXXXXX.....4....2....8.8......1.!Z!.1......8.8....2....4.....XXXXXXXXXXXX
XXXXXXXXXXXX....?...?.....?...?...X1...!!!...1X...?...?.....?....?...XXXXXXXXXXX
XXXXXXXXXXX.......?....?..........XX..1...1..XX..........?....2.......XXXXXXXXXX
XXXXXXXXXX.....8..........8.8.....XXX...1...XXX.....8.8..........8.....XXXXXXXXX
XXXXXXXXXX...8....................cXXXXXXXXXXXc....................8...XXXXXXXXX
XXXXXXXXXXX.....8.8.8..8.........ccccccccccccccc........8...8.8.8.....XXXXXXXXXX
XXXXXXXXXXXX...............8...XcccccccccccccccccX...8............8..XXXXXXXXXXX
XXXXXXXXXXXXX........8..8...XXXXcccccccccccccccccXXXX...8..8........XXXXXXXXXXXX
XXXXXXXXXXXXXXa111accCCCCCCXXXXXcccccccccccccccccXXXXXCCCCCCccb111bXXXXXXXXXXXXX
XXXXXXXXXXXcc...............AA1.ccccccccccccccccc.1BB...............ccXXXXXXXXXX
XXXXXXXXXXcc...8.8..8.8.....cc1...ccccccccccccc...1cc.......8..8.8.8.ccXXXXXXXXX
XXXXXXXXXcc.8...8......8.8...ccc...ccccccccccc...ccc...8.8.8..8.......ccXXXXXXXX
XXXXXXXXXc.8..8....8.8........ccc...............ccc.....8....8..8.8....cXXXXXXXX
XXXXXXXXXc..8..8.8....8.8.8.5.cXcc.............ccXc.5.8.8.8....8.8...8.cXXXXXXXX
XXXXXXXXXc..8.....8.8.......8.cXXc.............cXXc8........8.....8.8..cXXXXXXXX
XXXXXXXXXc.8...8......8.8.....cXXcc.1...1...1.ccXXc......8.......8.....cXXXXXXXX
XXXXXXXXXc8..8...8.8.8.8...8.8cXXXc...........cXXXc8.8..8..8.8.8...8.8.cXXXXXXXX
XXXXXXXXXc.....8.........8....cXXcc...........ccXXc....8.8.............cXXXXXXXX
XXXXXXXXXcc...5..8..8.8....8.ccXXc.............cXXcc8.....8....8..5.8.ccXXXXXXXX
XXXXXXXXXXcc8......8....8.8.ccXXcc.............ccXXcc.8.8....8....8..ccXXXXXXXXX
XXXXXXXXXXXcc..............ccXXXc...............cXXXcc..............ccXXXXXXXXXX
XXXXXXXXXXXXccccccccccccccccXXXXcccccccc@ccccccccXXXXccccccccccccccccXXXXXXXXXXX
ENDMAP

##############################################################################
##############################################################################
#
# For colouring the walls and floors of the first four levels
#
##############################################################################
##############################################################################

NAME:       hall_of_Zot_1
DEPTH:      Zot:1
CHANCE:     100%
TAGS:       extra transparent allow_dup no_dump
LFLOORCOL:  lightgrey
LROCKCOL:   white
LFLOORTILE: floor_rough_lightgray
LROCKTILE:  wall_zot_white
MAP
.
ENDMAP

NAME:       hall_of_Zot_2
DEPTH:      Zot:2
CHANCE:     100%
TAGS:       extra transparent allow_dup no_dump
LFLOORCOL:  white
LROCKCOL:   blue
LFLOORTILE: floor_rough_white
LROCKTILE:  wall_zot_blue
MAP
.
ENDMAP

NAME:       hall_of_Zot_3
DEPTH:      Zot:3
CHANCE:     100%
TAGS:       extra transparent allow_dup no_dump
LFLOORCOL:  blue
LROCKCOL:   lightblue
LFLOORTILE: floor_rough_blue
LROCKTILE:  wall_zot_lightblue
MAP
.
ENDMAP

NAME:       hall_of_Zot_4
DEPTH:      Zot:4
CHANCE:     100%
TAGS:       extra transparent allow_dup no_dump
LFLOORCOL:  lightblue
LROCKCOL:   magenta
LFLOORTILE: floor_rough_lightblue
LROCKTILE:  wall_zot_magenta
MAP
.
ENDMAP

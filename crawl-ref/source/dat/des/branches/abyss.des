###############################################################################
# abyss.des: abyss random minivaults, exits and abyssal rune vaults.
# Exit and rune vaults are placed by tag.
#
# Abyssal vaults should usually be minivaults and floating vaults, no larger
# than 28x23. Encompass vaults will be rejected at compile time, and
# other orientations will produce weird results.

{{
 -- Since abyssal stairs don't spawn at the bottom of the Abyss,
 -- replace a vault's empty guarded floor with a floor item at the bottom.
function init_abyss_exit(e, glyph)
  if you.depth() == dgn.br_depth(you.branch()) then
    if crawl.random2(10) > 3 then
      e.kfeat(glyph .. " = exit_abyss")
    else
      e.kitem(glyph .. " = any no_pickup")
    end
  else
    e.kfeat(glyph .. " = exit_abyss w:6 / abyssal_stair w:4")
  end
end

function abyss_entry(e, glyph)
  e.tags("extra luniq_enter_abyss chance_enter_abyss")
  e.depth_chance("Depths:1-3", 3333)
  e.depth_chance("Depths:4",   10000)
  e.depth_chance("Depths:5-",  3333)
  e.depth_chance("D:21-24", 3333)
  e.depth_chance("D:25",    10000)
  e.depth_chance("D:26-",   3333)
  if you.where() == 'Depths:4' or you.where() == 'D:25' then
    e.kfeat(glyph .. ' = enter_abyss no_mimic')
  else
    e.kfeat(glyph .. ' = enter_abyss')
  end
end
}}

###############################################################################
# Abyss entries.
# 1/3 chance to appear on D:21-27, and guaranteed on D:25.
###############################################################################

default-depth: Depths, D:21-

# This one is the backup and the one used in encompass vaults.
NAME: abyss_entry
TAGS: allow_dup overwrite_floor_cell no_exits replace_portal transparent
TAGS: fallback_enter_abyss
WEIGHT: 50
: abyss_entry(_G, 'O')
MAP
O
ENDMAP

NAME:    minmay_room_of_horrors
TAGS:    nolayout_encompass luniq_hellpanabyss_threat_entry
MONS:    small abomination, large abomination, unseen horror / chaos spawn
SHUFFLE: 123
: abyss_entry(_G, '_')
MAP
xxxxxx
x3113x
+1221x
x1221x
x311_x
xxxxxx
ENDMAP

NAME:  marbit_kobold_abyss_entry
TAGS:  no_monster_gen nolayout_encompass
WEIGHT: 5
MONS:  big kobold ; tomahawk ego:dispersal, kobold demonologist
: abyss_entry(_G, '_')
MAP
xxxxxxxxxxxxxxx
xxlllxx<.xlll.x
xlllllxx.lll..x
@x.llll..lll2_x
x.xl1lllllll.>x
xlllxlllx1lll.x
xxxxxxxxxxxxxxx
ENDMAP

NAME:    abyss_entry_small_statue_room
TAGS:    nolayout_encompass
: abyss_entry(_G, 'O')
MAP
xxxxx
xGOGx
xx+xx
ENDMAP

NAME:    abyss_entry_crystal
TAGS:    nolayout_encompass
: abyss_entry(_G, 'O')
SHUFFLE: AB
SUBST:   A : .:50 G:50 T U V Y t
SUBST:   B : .:100 G
MAP
  xxxxx
 xxbbbxx
xxbbObbxx
xbbA.Abbx
xbB...Bbx
xb.....bx
ENDMAP

NAME:    abyss_entry_glasseish
TAGS:    nolayout_encompass transparent
WEIGHT:  5
: abyss_entry(_G, 'O')
KPROP:   T = no_rtele_into
SUBST:   T : T l 1 2 A X
MONS:    tentacled starspawn / starcursed mass / wretched star
MONS:    plant col:random name:demonic name_adjective tile:mons_demonic_plant
KFEAT:   X : stone_arch
MAP
nnn nnn
nTn.nTn
nnn.nnn
 ..O..
nnn.nnn
nTn.nTn
nnn nnn
ENDMAP

NAME:    abyss_entry_flame_altar
TAGS:    nolayout_encompass
: abyss_entry(_G, 'O')
SUBST:   A : deG
MARKER:  d = lua:fog_machine { cloud_type = "flame", \
             pow_min = 10, pow_max = 10, delay = 10, \
             size = 1, walk_dist = 0, spread_rate= 0, excl_rad = 0 }
MARKER:  e = lua:fog_machine { cloud_type = "seething chaos", \
             pow_min = 10, pow_max = 10, delay = 10, \
             size = 1, walk_dist = 0, spread_rate= 0, excl_rad = 0 }
KFEAT:   _ = altar_lugonu
MAP
 bbb
bbObb
bA_Ab
  @
ENDMAP

###############################################################################
# abyss random vaults
###############################################################################

default-depth: Abyss

###############################################################################
# Dummy vaults
NAME: abyss_furniture_dummy
TAGS: dummy
CHANCE: 0 : 90% (Abyss:1)
CHANCE: 0 : 80% (Abyss:2)
CHANCE: 0 : 70% (Abyss:3)
CHANCE: 0 : 60% (Abyss:4)
CHANCE: 0 : 50% (Abyss:5)

# FIXME: this is actually a flat chance on placing the rune at all,
# rather than control over the rare vault-less abyssal rune placement.
NAME: abyss_rune_dummy
TAGS: abyss_rune unrand dummy
CHANCE: 0 : 10%

# FIXME: the tag name is inaccurate with abyssal stairs placed by these vaults.
NAME: abyss_exit_dummy
TAGS: abyss_exit unrand dummy
CHANCE: 0 : 95% (Abyss:1)
CHANCE: 0 : 90% (Abyss:2)
CHANCE: 0 : 85% (Abyss:3)
CHANCE: 0 : 80% (Abyss:4)
CHANCE: 0 : 75% (Abyss:5)

NAME:    abyss_fake_exit
KFEAT:   O = exit_abyss mimic w:6 / abyssal_stair mimic w:4
MAP
O
ENDMAP

###############################################################################
NAME: abyss_furniture_001
TAGS: extra allow_dup
MAP
G.G
.V.
G.G
ENDMAP

#################################################################
# Ecumenical Temple... (1KB)
# 'cuz the fun haters removed overflow temples from the Abyss...
NAME:    abyss_greek_temple
TAGS:    no_rotate
NSUBST:  . = 3:1 / 2:2 / *:.
SUBST:   ' = .
COLOUR:  " : white w:5 / none
FTILE:   "ABCDEFGHIJKLMNOPRQS : floor_limestone w:5 / floor_nerves
SUBST:   " = .
MONS:    plant, bush
SHUFFLE: ABCDEFHIJKLMNOPQRS
KFEAT:   A = altar_makhleb
KFEAT:   B = altar_xom
KFEAT:   C = altar_zin
KFEAT:   D = altar_the_shining_one
KFEAT:   E = altar_kikubaaqudgha
KFEAT:   F = altar_yredelemnul
KFEAT:   H = altar_vehumet
KFEAT:   I = altar_okawaru
KFEAT:   J = altar_sif_muna
KFEAT:   K = altar_trog
KFEAT:   L = altar_nemelex_xobeh
KFEAT:   M = altar_elyvilon
KFEAT:   N = altar_fedhas
KFEAT:   O = altar_cheibriados
KFEAT:   P = altar_ashenzari
KFEAT:   Q = altar_dithmenos
KFEAT:   R = altar_gozag
KFEAT:   S = altar_qazlal
MAP
''''''...............
''ccccccccccccccccc..
''G'c"""""""""""""c..
''''c"A"B"C"D"E"F"c..
''G'c"""""""""""""ct.
''''+"H"I"J"K"L"M"c..
''G'c"""""""""""""c..
''''c"N"O"P"Q"R"S"c.t
''G'c"""""""""""""c..
''ccccccccccccccccc..
.......t......ttt....
ENDMAP

# <bh> I got the idea after I introduced a bug where the whole floor of
#        the abyss turned into altars
NAME:   bh_abyss_xom_lugonu_altar
TAGS:   extra allow_dup
COLOUR: " : magenta w:2 / white
KFEAT:   _ = altar_lugonu
KFEAT:   X = altar_xom
SUBST:   a = XXX"
SUBST:   b = X"
SUBST:   c = XX"
SUBST:   d = X""
SUBST:   e = X"""
MAP
   ebe
  dcacd
 ec"""ce
 ba"_"ab
 ec"""ce
  dcacd
   ebe
ENDMAP

# Inspired by a bug which turned Lemuel's castle moat into draconians,
# a little outpost with either easier interior or exterior. ~HangedMan
NAME:   hangedman_abyss_monster_moat
TAGS:   no_monster_gen patrolling
MONS:   fire bat w:12 / bat simulacrum / spectral bat / vampire bat
MONS:   vampire, vampire knight / vampire mage w:7
MONS:   crimson imp w:12 / white imp / shadow imp / iron imp
MONS:   kobold demonologist, deep elf summoner / orc sorcerer w:7
MONS:   worker ant w:40 / goliath beetle w:20 / vampire mosquito
KMONS:  8 = demonic crawler
KMONS:  9 = emperor scorpion
KMONS:  D = place:D:1 perm_ench:shapeshifter w:30 / \
            small abomination w:15 / chaos spawn
KMONS:  E = ugly thing
KMONS:  F = large abomination / glowing shapeshifter w:7
SHUFFLE: 123 / 456 / 789 / DEF
SUBST:  C = xccv, w : w:8 l:1 .:1, W : .:9 W:1
MAP
 CCCC  CCCCCCCCCC  CCCC
CC 1cccc+2WWWW2+cccc1 CC
C 11n|3Wccc++cccW3|n11 C
C 11c3|3c1WwwW1c3|3c11 C
C 11ccncc11ww11ccncc11 C
C  11111111ww11111111  C
C   111111 ww 111111   C
.C                   .C.
 .CC .C. CCCCCC .C. CC.
ENDMAP

NAME:   hangedman_abyss_sting
TAGS:   no_item_gen no_monster_gen
KMONS:  1 = orange demon
KMONS:  2 = red wasp / yellow wasp / killer bee w:2 / nothing w:2
KMONS:  3 = death drake / green death
KFEAT:  123 = W
KITEM:  23 = royal jelly / nothing
MAP
 xxxxxxx
xx22222xx
x2...222x
x2.++xx3x
x2.+1 xxx
x2.x 1 xx
x22xx W x
xx23xx W
 xxxxxx W
ENDMAP

NAME:   hangedman_abyss_overflow_altar
TAGS:   no_item_gen no_monster_gen no_pool_fixup
MONS:   earth elemental
KFEAT:  A = altar_lugonu
KFEAT:  B = dry_fountain
KFEAT:  C = abyssal_stair
SUBST:  x = xcv++, y = ..w, z = Wl
MAP
yyyyyyyy
yxxxxxxy
yxzzzzxy
yxzA1zxy
yxz.Bzxy
yxzzzzxy
yxxxxxxy
yyyyyyyy
ENDMAP

NAME:   hangedman_abyss_mock_shop
TAGS:   no_monster_gen patrolling
KMONS:  1 = water elemental / nothing w:5
KMONS:  2 = earth elemental
KFEAT:  0 = abandoned_shop
KFEAT:  1 = dry_fountain
SUBST:  x = xcv, . = ...W
MAP
     . .
  ..xx.xx..
xxxxx1$1xxxxx
x.2..$0$..2.x
xxxxx1$1xxxxx
  ..xx.xx..
     . .
ENDMAP

NAME:    hangedman_abyss_shoalhut
TAGS:    no_pool_fixup patrolling
MONS:    red devil w:30 / smoke demon / eye of devastation w:5 / \
         unseen horror / ynoxinul / snapping turtle skeleton w:15
NSUBST:  = = 1:+ / 1:xx+ / *:x
SUBST:   1 = 1., x = xxxxcccvb, l = wWll
KMASK:   wWl = no_monster_gen
MAP
   l l
 lxx=xxl
 xx.1.xx
lx.1.1.xl
 =1.*.1=
lx.1.1.xl
 xx.1.xx
 lxx+xxl
   l l
ENDMAP

NAME:   bh_silent_lugonu_exit
TAGS:   allow_dup
MONS:   silent spectre
KFEAT:   _ = altar_lugonu
MAP
...       xxx
._.       x1x
...       xx+
ENDMAP

NAME:    hangedman_abyss_feature_spike
TAGS:    extra no_item_gen no_pool_fixup allow_dup
MONS:    fungus, plant, bush
ITEM:    stone q:1, large rock q:1
KFEAT:   A : stone_arch w:99 / abandoned_shop w:1
KFEAT:   - = altar_lugonu
KMASK:   wWl = no_monster_gen
SHUFFLE: fghij / opqrs, fghijopqrs / JKLMNOPQRS
SHUFFLE: fghij / fffff / fffff / fffff / fffff / fghgf / fgfgf / ffhii / ffhii
SHUFFLE: opqrs / ooooo / ooooo / ooooo / ooooo / opqpo / opopo / ooqrr / ooqrr
SHUFFLE: JKLMN / JJJJJ / JJJJJ / JJJJJ / JJJJJ / JKLKJ / JKJKJ / JJLMM / JJLMM
SHUFFLE: OPQRS / OOOOO / OOOOO / OOOOO / OOOOO / OPQPO / OPOPO / OOQRR / OOQRR
SUBST:   f = A, g = C, h = T, i = U, j = V
SUBST:   o = t, p = G, q = w, r = W, s = l
SUBST:   J = x, K = c, L = v, MN = m, O = 1, P = 2, Q = 3, RS = b
SUBST:   - = -----A
SHUFFLE: AACCTTUUVYYdeefghi, tGGwWlI, xxccvvbbmm+123
: dgn.delayed_decay(_G, 'f', 'human skeleton')
: dgn.delayed_decay(_G, 'g', 'elf skeleton')
: dgn.delayed_decay(_G, 'h', 'spriggan skeleton')
: dgn.delayed_decay(_G, 'i', 'orc skeleton')
MAP
        .....
        .LqL.
        .QhQ.
        .....
          .
     .          .
    ....      ....
    .pK.      .Mr.
    .Pg.      .iR.
    ....      ....
          -
 .                 .
....             ....
.oJ.             .Ns.
.Of.             .jS.
....             ....
ENDMAP

NAME:    hangedman_abyss_theft
TAGS:    no_monster_gen
KMONS:   1 = place:Lair:$
KMONS:   2 : place:Swamp:1 / place:Swamp:$
KMONS:   3 : place:Snake:1 / place:Snake:$
KMONS:   4 : place:Shoals:1 / place:Shoals:$
KMONS:   5 : place:Spider:1 / place:Spider:$
KMONS:   6 = place:Orc:$
KMONS:   7 : place:Elf:1 / place:Elf:$
KMONS:   8 : place:Crypt:1 / place:Crypt:$
KMONS:   9 : place:Hell / place:Slime
KMONS:   A = plant
KMONS:   B = bush
SHUFFLE: Gwm, xcvbwm, wwl, mmttB, 1234567, 1689, $%
KMASK:   B = no_item_gen
MAP
     ..xbb..
 A x .xxbbb. b A
  x  xx1cvbb  b
 x  x...vc..b  b
   x.x.1cv...b
..x...x.vc2.b.b..
.xx..1..cv.b..bb.
xxbvbvb%$*..2.2bb
ccvbvbv$G$xcxcxbb
cc3.3..*$%cxcxcvv
.cc..c.xb..4..vv.
..c.c.3bx.v...v..
   c...xb4.v.v
 c  c..bx...v  v
  c  ccxb4vv  v
 A c .ccvvv. v A
     ..cvv..
ENDMAP

NAME:    hangedman_abyss_lost_library
TAGS:    patrolling no_monster_gen no_item_gen
MONS:    guardian serpent, great orb of eyes, giant orange brain
MONS:    boggart, rakshasa, cacodemon, shadow demon
KMONS:   0 = wizard
KMONS:   8 = orc sorcerer
KMONS:   9 = sphinx
KMONS:   & = lich
KMONS:   @ = deep elf sorcerer
KMONS:   Z = nonbase draconian
ITEM:    randbook numspells:1 w:19 / randbook numspells:2 w:1, any scroll
KFEAT:   _ = altar_sif_muna
SHUFFLE: ABCDEF
NSUBST:  A = 1:0 / 1:d / 1:dee / *:., B = 1:1 / 1:d / 1:dee / *:.
NSUBST:  C = 1:2 / 1:d / 1:dee / *:., D = 1:3 / 1:d / 1:dee / *:.
NSUBST:  E = 1:4 / 1:d / 1:dee / *:., F = 1:& / 2:d / 1:e / *:.
NSUBST:  + = 2:+ / *:x, - = 2:+ / *:x
SHUFFLE: 0123456789', '''''''''@, &@Z
SUBST:   ' = ., = = +wl x:45, _ : _:30 TUVY .:35
SUBST:   x = x:20 c:4 v:1 b:1 .:4, X = x:20 c:5 v:2 b:2 .:1
MAP
 . .. .. .. .. .. .
..X++XXXXXXXXXXXXX..
 XX. xx xxx xxx xXX
.+._ xAAAxBBBxCCCxX.
.+  .xAAA=BBB=CCC X.
 XX..xAAAxBBBxCCCxX
.XX..xx xxx xxx xxX.
.X .............. X.
.X .............. X.
.Xxx xxx xxx xx..XX.
 XxDDDxEEExFFFx..XX
.X DDD=EEE=FFFx.  -.
.XxDDDxEEExFFFx _.-.
 XXx xxx xxx xx .XX
..XXXXXXXXXXXXX--X..
 . .. .. .. .. .. .
ENDMAP

NAME:    hangedman_abyss_batty_box
MONS:    bat / fire bat / vampire bat, butterfly, raven
MONS:    spatial vortex, wind drake, harpy, unseen horror
KMONS:   8 = wind drake w:9 / harpy w:1
KITEM:   8 = acquire any / any good_item / stone q:1 w:5
NSUBST:  A = 2:2 / 1:2 / 2 = 1122., B = 1:3 / 1:4 / 1:1334. / 1:12334.
NSUBST:  C = 1:556 / 1:6 / 1:7 / 1:66777. / 1:1234567.
SUBST:   x = x:25 c:14 v:6 b:4 G:1 n:2, y = c:3 v:1 b:1, z = x:2 c:1 v:1 b:1 .:1
MAP
 xxxxxxxxxx.
xx x x x x.x
x x0x x.x.x
xx0x.x1x.x.x
x x.x1x.x.x.
xx x1++xyyyy
x x.x+xAzCzy
xx1x.xAAABCy
x x.xyzACBzy
xx.x.yCBB8zy
x.x.xyzCzzzy
.x x.yyyyyyy
ENDMAP

NAME:    hangedman_glass_garden
TAGS:    no_monster_gen no_pool_fixup
DEPTH:   D, Depths, Abyss
MONS:    butterfly, plant, unseen horror, shadow wraith, wind drake
SHUFFLE: 34GT, GTt
: if you.branch() == "Abyss" then
TAGS:    no_exits
SHUFFLE: 345", ABCD / ACBD / ADCB
SUBST:   + = M, ' = x:5 c:2 v:1 b:1, " = ., % = %%***|
SUBST:   AB : wwl, CD : wll
NSUBST:  M : 1:+ / 2 = mmm+ / * = m:19 +:1
: else
TAGS:    extra transparent
WEIGHT:  1
SUBST:   M = m, 34'" = ., ABCD = W
: end
MAP
   ''' '''
  'MmM+MmM'
 'MmAA.BBmM'
'MmA..2..BmM'
'mA.2.4.2.Bm'
'MA..1G1..BM'
 +.2T3%3T2.+
'MC..1G1..DM'
'mC.2.4.2.Dm'
'MmC..2..DmM'
 'MmCC.DDmM'
  'MmM+MmM'
   ''' '''
ENDMAP

NAME:    hangedman_shadowy_showcase
TAGS:    transparent no_monster_gen
MONS:    shadow wraith / unseen horror / thrashing horror, lurking horror
MONS:    shapeshifter / ugly thing / large abomination w:2, wretched star
MONS:    large abomination / ghoul / death ooze w:2, ancient zyme
MONS:    large slime creature w:12 / very large slime creature w:8 / rakshasa w:2
KMONS:   8 = starcursed mass
KMONS:   9 = giant eyeball / chaos spawn / anaconda perm_ench:shapeshifter w:2
KMONS:   0 = tentacled starspawn
KMONS:   A = patrolling shadow demon
KMONS:   B = boggart
SHUFFLE: 12 / 34 / 56 / 78 / 90, A' / A' / A' / A' / 'B
SUBST:   x = x:5 c:4 b:2 v:1, X = x:2 c:4 b:3 v:1, ' = ..lw
SUBST:   . = .:199 W:1, * = .$%%***|, d = %$
MAP
   ...xxxxxxx
  @.. xxxxxxx
 @. .xx ..2xx
.. ..xx.x1.xx
......x..x.xxx
. xx.d.... xxxx
xxxxx.X.XXXXXxxxxxx
xx ......X'AXxxxxxx
xx1x..X..**'X 3.3xx
xx..x.XX*X*XX.x..xx
xx1.1 X'**..X..x3xx
xxxxxxXA'X...... xx
xxxxxxXXXXX.X.xxxxx
    xxxx ....d.xx.
     xxx.x..x... ..
      xx.3x.xx.....
      xx4.. xx . @
      xxxxxxx...@
      xxxxxxx ..
ENDMAP

NAME:    hangedman_relentless_pull
TAGS:    no_monster_gen no_item_gen
MONS:    ugly thing / large abomination / ynoxinul / unseen horror / smoke demon
MONS:    tentacled starspawn / starcursed mass
MONS:    very ugly thing, wretched star, lurking horror, ancient zyme
SHUFFLE: 2d, 34eAB / 34eAB / '''CD, 34 / 56
SUBST:   de = %**, AB = x, C = ., D = @, x = xxccb
MAP
 xxxxx         xxxxx
xx%1xx@       Bxx3exx
 x....xx.   .xxA43ex
xx.xxxxxxx xxxxxxx.xx
 x.xd. .2x x3. .dx.x
xx.xx.x.xxxxx.x.xx.xx
 x..xx.x.xxx.x.xx..x
xx..x.. xxxxx ..x..xx
 x..x .x. x .x. x..x
xx .xx..x.x.x..xx. xx
xxx..x....%....x..xxx
 xx .%.xx.1.xx.%. xx
  xx .xxxxxxxxx. xx
   xxxx x x x xxxx
ENDMAP

NAME:    hangedman_steaming_rock
TAGS:    transparent
MONS:    eye of devastation w:9 / nothing w:1
KMONS:   2 = ynoxinul / smoke demon / thrashing horror / unseen horror / \
             ugly thing w:5/ rakshasa w:5 / chaos spawn w:5
KMONS:   3 = smoke demon / green death / catoblepas w:8 / ophan w:7 / \
             fire crab w:5
KMONS:   4 = apocalypse crab / death drake w:5
KITEM:   2 = % / $, % / *
KITEM:   34 = %, * / |
KFEAT:   ABCD = m
SUBST:   c = cccbbv
SHUFFLE: ABCD
MARKER:  A = lua:fog_machine {cloud_type = "flame", \
             pow_min = 2, pow_max = 3, delay = 85, size_min = 2, size_max = 4, \
             walk_dist = 4, start_clouds = 1, excl_rad = 0 }
MARKER:  B = lua:fog_machine {cloud_type = "freezing vapour", \
             pow_min = 2, pow_max = 3, delay = 75, size_min = 2, size_max = 4, \
             walk_dist = 4, start_clouds = 1, excl_rad = 0 }
MARKER:  C = lua:fog_machine {cloud_type = "rain", \
             pow_min = 3, pow_max = 4, delay = 30, size_min = 3, size_max = 4, \
             walk_dist = 4, start_clouds = 1, excl_rad = 0 }
MARKER:  D = lua:fog_machine {cloud_type = "calcifying dust", \
             pow_min = 2, pow_max = 3, delay = 75, size_min = 2, size_max = 3, \
             walk_dist = 4, start_clouds = 1, excl_rad = 0 }
TILE:    ABCD = dngn_transparent_wall_blue
COLOUR:  ABCD = lightblue
MAP
   ....
  .cccc.
 .mmmccc..
.cmmAcmccc.
.cmAcmmmmcc.
.cccmm   mcc.
 .1cm 322 mc.
 ..cm 422 mc.
.cccmm   mcc.
.cmDcmmmmcc.
.cmmDcmccc.
 .mmmccc..
  .cccc..
   ....
ENDMAP

NAME:    hangedman_classic_abyss_death
TAGS:    transparent no_monster_gen
MONS:    large abomination / ynoxinul / unseen horror / \
         skeleton / sixfirhy / smoke demon / neqoxec / \
         efreet / rakshasa / giant orange brain / ugly thing / \
         golden eye / hell hog
MONS:    lich / hellephant / tentacled monstrosity / profane servitor / \
         death drake / daeva / angel / bone dragon / balrug w:4 / \
         shadow demon w:4 / executioner w:4 / hellion w:4
SHUFFLE: lw / ll / ww / ''
NSUBST:  2 : 2:2 / *:1
SUBST:   c = ccvbb, x = xxxc, ' = .
MAP
         c
        cc
       cc.
      ccx.@
     cc2x..x
    cc%xx wxx
   cc*11x% w.@
  cc2x1x%x% ...cc
 ccxxxx%.%xxxxcc
cc... %x%x1x2cc
   @.l %x11*cc
    xxl xx%cc
     x..x2cc
      @.xcc
       .cc
       cc
       c
ENDMAP

NAME:    hangedman_illusive_loot
TAGS:    patrolling
WEIGHT:  5
MONS:    rakshasa, guardian serpent / great orb of eyes
ITEM:    potion of might / potion of haste / potion of heal wounds w:12 / \
         wand of lightning / wand of draining / wand of slowing w:5 / \
         wand of confusion w:5 / wand of fire w:1 / wand of cold w:1 / \
         ring mail good_item / chain mail good_item w:12 / shield / \
         spear good_item / trident good_item / any weapon w:12 / \
         any w:2
SUBST:   x = ccccvbb
MAP
     .x.x
   x.xxx.x
  x.xx1xx.x
 x. x.. x .x
 .xxx.ddxxx.
.xx..dddd xx.
xx1.ddd...1xx
.xx dd..x.xx.
x.xxxd.x+xx..
 x. x ..x2. x
  x.xx1xx.x.
   x.xxx. ..
     .x..x
ENDMAP

NAME:   hangedman_abyss_or_decor_stabs
TAGS:   transparent no_pool_fixup decor
DEPTH:  D, Depths, Abyss
: if you.branch() == "Abyss" then
SUBST:  xc = xxxxcccb, ' = %0, " = wl...
: else
TAGS:   extra
SUBST:  '" = ., c : xxc
: end
MAP
...."
.cxx.
.x'..."
.x.cxx.
"..x'..."
  .x.cxx.
  "..x'..."
    .x.cxx.
    "..x'..
      .x.
      "..
ENDMAP

NAME:   hangedman_abyss_or_decor_jellyfish_corner
TAGS:   transparent decor
DEPTH:  D:4-, Depths, Abyss
WEIGHT: 5 (D), 10
: if you.branch() == "Abyss" then
SUBST:  x : XXCB, c : CB, X = xxxxcb, C = xccccb, B = xcbbbb
SUBST:  HIJ = cbw,  ' = 000., " = %%%.
: else
TAGS:   extra
SUBST:  '" = ., c : xxc, H : xc.., I : xc.., J : xc..
: end
MAP
       .c
      .xc
     .xcc
    @x..c......
   @x..'ccxx...
  .x..H"c".xxx.
 .x..H'"c'.....
.xc.'"""c.I...
ccccccccc...J.
   .c"'..x....
   .x..I..x...
   .xx.....xx.
   ..x..J..xx.
   ..x........
   ....
ENDMAP

NAME:    hangedman_abyss_or_decor_lesser_grid
TAGS:    transparent no_pool_fixup decor
DEPTH:   D, Depths, Abyss
: if you.branch() == "Abyss" then
SHUFFLE: ABCDEF, "'
SUBST:   A : cbx.., B : cbx., C : cbx.., D : cbx., E : cbx.., F : cbx.
SUBST:   " = 0., ' = %*..., X = wl.
: else
TAGS:    extra
SUBST:   '" = ., X : x.
SUBST:   A : xxc., B : xxc., C : xxc., D : xxc., E : xxc., F : xxc.
: end
MAP
.......
.AB....
.BA.X..
.'".......
.CD"EF"xx.
.DC'FE'xx.
.......'"....
   ..X.FE....
   ....EF.X..
   ....'"....
      .DC"BA.
      .CD'AB.
      .......
ENDMAP

NAME:    hangedman_abyss_or_decor_clamps
TAGS:    transparent no_pool_fixup decor
DEPTH:   D:8-, Depths, Abyss
KFEAT:   @ = +
SHUFFLE: ABCDEFGH, '"
: if you.branch() == "Abyss" then
SUBST:   b = wlb., AB = wl., CD = xcb., EFGH = .
SUBST:   x = xxxxcccb, ' = 00., " = %%*.
: else
TAGS:    extra
SUBST:   b : bbwww., A : @, B : @xx, C : @xxxx, DEFGH = x, '" = .
: end
MAP
    xxAxx
    x...x
    B.'.C
    x...x
xxDxx...xxExx
x....b".....x
F.'.."b"..'.G
x......b....x
xxHxxx+xbx+xxxHxx
    x....b......x
    G.'.."b"..'.F
    x....."b....x
    xxExx...xxDxx
        x...x
        C.'.B
        x...x
        xxAxx
ENDMAP

NAME:    hangedman_abyss_or_decor_passed
TAGS:    transparent no_pool_fixup decor
DEPTH:   D:4-, Depths, Abyss
: if you.branch() == "Abyss" then
SUBST:   ' = 00., " = %%*..
SUBST:   x = xxxxcb, XY = ww...
: else
TAGS:    extra
SUBST:   x : xxxc, Y = x, X : x..
NSUBST:  ; = 1:@ / *:., : = 1:@ / *:., ` = 1:@ / *:.
: end
MAP
     x
 ;;;;x::::
 ;YX.x.XY:
 ;X..x..X:
 ;.......:
xxxx.x.xxxx
 `.......'x
 `X..x.X'"x
 `YX.x.'Y"x
 ````x'""xx
     xxxxx
ENDMAP

NAME:    hangedman_abyss_or_decor_stagger
TAGS:    transparent no_pool_fixup decor
DEPTH:   D:4-, Depths, Abyss
: if you.branch() == "Abyss" then
SUBST:   ' = 00., " = %%., X = xxcccb, x = xxxxxcccb
SUBST:   yY = ...xxxcbw, zZ = ...xxxcbl
: else
TAGS:    extra
SHUFFLE: yY, zZ
SUBST:   x : xxxxc, X : xxc, y : x.., z : x.., YZ'" = .
: end
MAP
..........
.xxxXxxx..
...'X"..z...
..."X'..Z...
.xxxXXXxxx..
..Y.."X'..z...
..y..'X"..Z...
 ..xxxXXXxxx..
 ...Y..'X"..z..
 ...y.."X'..Z..
   ..xxxXXXxxx.
   ...Y.."X'...
   ...y..'X"...
     ..xxxXxxx.
     ..........
ENDMAP

NAME:    hangedman_abyss_or_decor_slice
TAGS:    transparent no_pool_fixup decor
DEPTH:   D:4-, Depths, Abyss
WEIGHT:  5 (D), 10
: if you.branch() == "Abyss" then
SUBST:   ' = 00., " = %%., x = xxxxxcccb, cX = xxccb
: else
TAGS:    extra
SUBST:   x : xxxxc, c : xxc., X : xxc, '" = .
: end
MAP
        ..c
       ..X.
      ..c..
.......c'.
x"x'x"c.xxxx
xxxx.c"x'x"x
  .'c.......
 ..c..
 .X..
 c..
ENDMAP

NAME:    hangedman_abyss_town
TAGS:    no_monster_gen patrolling allow_dup
WEIGHT:  5
MONS:    rakshasa / efreet / smoke demon / wizard / base draconian w:5 / \
         guardian serpent w:1 / vampire w:3 / necromancer w:3
MONS:    thrashing horror / blink frog / ugly thing / \
         large abomination / unseen horror / shapeshifter / \
         wretched star w:5 / air elemental / raiju / worldbinder
MONS:    draconian shifter w:5 / shadow demon w:5 / lich / \
         profane servitor w:5 / lorocyproca / deep elf demonologist / \
         deep elf sorcerer / unborn
MONS:    bone dragon / tentacled starspawn / starcursed mass / \
         apocalypse crab / dancing weapon / tentacled monstrosity / \
         glowing shapeshifter
ITEM:    superb_item, royal jelly q:2
ITEM:    bolt good_item q:40 / arrow good_item q:40, any magical staff
ITEM:    scroll of magic mapping q:4 / scroll of random uselessness q:4
ITEM:    ring mail randart / scale mail randart / chain mail randart w:20
ITEM:    demon blade ego:chaos good_item ident:type / \
         demon trident ego:chaos good_item ident:type / \
         demon whip ego:chaos good_item ident:type
KITEM:   k = acquire misc, acquire misc
SHUFFLE: defghijk
NSUBST:  0 : 2:0 / 1 = VY / *:.
SUBST:   c = c:7 b:2 v:1, x = x:8 c:1 b:1
MAP
  ccccccc..
 ccccccc ...ccccccc
 ccd...cc... ccccccc
 cc.2c+cc...cc...ecc
 cc.cc1 cc..cc+c2.cc
 cc.+12.+..cc 1cc.cc
 cccc .cc...+.21+.cc
 c ccc+c....cc. cccc
 ....c.. xx .c+ccc c
........x00x...c....
....c...x00x........
c ccc+c. xx ..c....
cccc..cc....c+ccc c
cc.+12.+...cc..cccc
cc.cc1.cc..+.34+.cc
cc.2c+cc..cc.4cc1cc
ccf...cc...cc+c1%cc
ccccccc ...cc.1%gcc
 ccccccc... ccccccc
         ..ccccccc
ENDMAP

NAME:    hangedman_abyss_stitches_cave
TAGS:    transparent no_monster_gen
MONS:    thrashing horror / unseen horror / raiju / \
         shapeshifter w:5 / large abomination / common demon
MONS:    starcursed mass / apocalypse crab / hellephant / \
         tentacled monstrosity / tentacled starspawn w:5
SHUFFLE: yY / yY / Yy
SUBST:   x : X:9 C:1, y : W:25 L:25 X:35 C:10 M:5, Y = ., z : X:20 CMB
SUBST:   X = x:50 cb, C = c:50 bv, B = b:50 cv, M = m:50 cb, W = w:50 l
SUBST:   L = l:50 w, $ = $%., % = %:7 .:3, * = *:7 %:3
MAP
            xxxxxxxxxxx
            x%1.zxz...x
           xx.y.zzz.y.xxx
         xxx..Y..Y..Y....
         x%..zzz.y.zzz.Z.
         x2Yyzxz$1.zxz...
         x*1.zzxxxxxxx
   xxxxxxxzz.1*x
...zxz.1$zxzYZ2x
.Z.zzz.Y.zzz..%x
....Y..y..y..xxx
xxx.y.zzz.Y.xx
  x.1.zxz.1%x
  xxxxxxxxxxx
ENDMAP

NAME:    hangedman_abyss_river_spirits
TAGS:    no_monster_gen
MONS:    smoke demon, efreet, fire elemental, water elemental
SHUFFLE: ABCDEF / ABCDEF / ABCDEF / ACEBDF / ACEBDF / GGGGGG
SHUFFLE: 1234 / 1234 / 1234 / 1212 / 1212 / 1212 / 3434 / 3434 / 3434
SUBST:   ACE = w, BDF = l, G : wl, x = x:50 cb
MAP
%2xxAAxxxx
1.xx..xx2x
xx..xx.%xxBBxxxx
xxDDxx%.xx..xx3x
    x1xx..xx.%xxCCxx
    xxxxEExx%.xx..xx
          x4xx..xx.3
          xxxxFFxx4%
ENDMAP

NAME:   bh_abyss_negative
TAGS:   allow_dup decor
MAP
....      ....
.....     .....
.....     .....
.....     .....
 .....   .....
    .......
     .....
     .....
     .....
    .......
 .....   .....
.....     .....
.....     .....
.....     .....
 ....      ....
ENDMAP

###############################################################################
# abyss rune vaults
###############################################################################

NAME:  abyss_rune_water_cross
TAGS:  abyss_rune unrand patrolling
MONS:  smoke demon, drowned soul
KMONS: O = octopode crusher
KITEM: O = abyssal rune of Zot
KFEAT: O = shallow_water
MAP
ww.ww
ww2ww
.1O1.
ww2ww
ww.ww
ENDMAP

NAME:   due_abyss_rune
TAGS:   abyss_rune unrand
KMONS:  OL = lich / ancient lich w:1
KITEM:  O = abyssal rune of Zot
NSUBST: C = 1:+ / * : cc+
NSUBST: L = 1:O / *:L
COLOUR: c : blue / lightblue / red / lightred / lightgreen / \
            white / yellow / magenta / lightmagenta
COLOUR: OL. : blue / lightblue / red / lightred / green / \
              lightgreen / white / lightmagenta
TILE:   c : dngn_stone_dark / dngn_stone_wall_blue / dngn_stone_wall_green / \
            dngn_stone_wall_cyan / dngn_stone_wall_red / dngn_stone_wall_white / \
            dngn_stone_wall_brown / dngn_stone_wall_darkgray / \
            dngn_stone_wall_yellow / dngn_stone_wall_magenta
MAP
  ccccc
 ccccccc
ccLcLcLcc
cc+c+c+cc
@C.....C@
cc+c+c+cc
ccLcLcLcc
 ccccccc
  ccccc
ENDMAP

NAME:   evilmike_abyss_rune_tentacle_tunnel
TAGS:   abyss_rune unrand
KMONS:  K = kraken
KMONS:  T = tentacled monstrosity
KITEM:  O = abyssal rune of Zot
KFEAT:  KT = w
KFEAT:  O = W
NSUBST: T = 1:T / 1:w / Tw
SUBST:  D : cW
SUBST:  . = w., w = WWWw, C = c., c = ccv
MAP
      CCcccCC
   CCCCccOccCCCC
 CCCCcccWcWcccCCCC
ccccccwWDWDWwcccccc
cTwwwwwwwwwwwwwwwTc
cTwwwwwwwwwwwwwwwTc
ccccccccwwwcccccccc
 CCCCCccwwwccCCCCC
   CCCcwwwwwcCCCC
    ..wwwwwww..
     .wwwKwww.
     ..wwwww..
      .wwwww.
      .......
       ..@..
ENDMAP

NAME:  evilmike_abyss_rune_siren
TAGS:  abyss_rune unrand patrolling
MONS:  generate_awake merfolk avatar, blink frog
KMONS: O = starcursed mass
KITEM: O = abyssal rune of Zot
SUBST: X = c., c = ccv, N : n.
KPROP: l = no_cloud_gen
MAP
XXXXcccXXXX
XXXccOccXXX
XXcc.n.ccXX
Xcc+nnn+ccX
Xc2..2..2cX
Xc.N...N.cX
Xcc.....ccX
Xcc.lll.ccX
X c.l1l.c X
Xcc.clc.ccX
Xc.......cX
Xc..ccc..cX
@+cccXccc+@
ENDMAP

# Two versions here - one narrow, one wide (wide one is a bit easier).
NAME:   evilmike_abyss_rune_acolytes_1
TAGS:   abyss_rune unrand no_monster_gen
WEIGHT: 6
KMONS:  O = tentacled monstrosity / thrashing horror / tentacled starspawn
KMONS:  L = wizard hd:16 name:mad_acolyte_of_Lugonu n_rpl n_des col:lightgreen \
            tile:mons_hell_wizard \
            spells:smiting.11.priest;\
                   blink_other.11.priest;\
                   invisibility.11.priest;\
                   malign_gateway.11.priest;\
                   blink.11.priest ; \
            robe good_item . quarterstaff ego:distortion ident:type | \
                             dagger ego:distortion ident:type
KITEM:  O = abyssal rune of Zot
KFEAT:  _ = altar_lugonu
NSUBST: A = 2:L / *:.
FTILE:  ._+OAL = floor_crystal_squares
SUBST:  ' = .
MAP
''''' ''''' '''''
'bbb'''bbb'''bbb'
'bAbbbbbAbbbbbAb'
'bblblblblblblbb''
'Gb...........bbb'
@'+.....L._b..+Ob'
'Gb...........bbb'
'bblblblblblblbb''
'bAbbbbbAbbbbbAb'
'bbb'''bbb'''bbb'
''''' ''''' '''''
ENDMAP

NAME:   evilmike_abyss_rune_acolytes_2
TAGS:   abyss_rune unrand no_monster_gen
WEIGHT: 4
KMONS:  O : tentacled monstrosity / thrashing horror / tentacled starspawn
KMONS:  L = wizard hd:16 name:mad_acolyte_of_Lugonu n_rpl n_des col:lightgreen \
            tile:mons_hell_wizard \
            spells:smiting.11.priest;\
                   blink_other.11.priest;\
                   invisibility.11.priest;\
                   malign_gateway.11.priest;\
                   blink.11.priest ; \
            robe good_item . quarterstaff ego:distortion ident:type | \
                             dagger ego:distortion ident:type
KITEM:  O = abyssal rune of Zot
KFEAT:  _ = altar_lugonu
FTILE:  ._+OL = floor_crystal_squares
SUBST:  ' = .
MAP
  '''''''''''
  'b'b'b'b'b'
'''bbbbbbbbb''
'bbblblblblbb'''
'bLl.......bbbb'
'bbb......b.bb''
@''+....._LbObb'
'bbb......b.bb''
'bLl.......bbbb'
'bbblblblblbb'''
'''bbbbbbbbb''
  'b'b'b'b'b'
  '''''''''''
ENDMAP

NAME:   evilmike_abyss_rune_seething_chaos
TAGS:   abyss_rune unrand
MONS:   fire bat, chaos spawn, fire dragon / ice dragon
MONS:   ogre mage, hill giant
KITEM:  O = abyssal rune of Zot
MARKER: ! = lua:fog_machine { cloud_type = "seething chaos", \
            pow_min = 8, pow_max = 12, delay_min = 20, delay_max = 30, \
            size = 1, walk_dist = 1, spread_rate = 33 }
SUBST:  ! = V, X = x., x = xc
MAP
 cccX X X
cc2ccX X X
c2O2cxx X X
cc2+c3xx X X
Xccc..1xx X X
 Xx3.1.4xx X X
X xx1.!.5xx X
 X xx4.1..xx X
X X xx5....xX ..@
 X X xx....xxx++x
  X X xx........x
   X X xxx....XXx
    X X  xxxxxxxx
ENDMAP

# Contains 1 to 3 hellephants. 3 is rare.
NAME:   evilmike_abyss_rune_elephants
TAGS:   abyss_rune unrand
KMONS:  O = hellephant
MONS:   elephant, dire elephant, hellephant
KITEM:  O = abyssal rune of Zot
SUBST:  l = .....llllx, L : llllX
KPROP:  ' = no_rtele_into
NSUBST: ' = 2 = 322 / 9 = 221. / *:., X = 1:l / *:x
SUBST:  x : cccvvx, x = cccvvx
MAP
  l..lllll..l
 l.lllllllll.l
l.llLLLLLLLll.l
.llLLlllllLLll.
.lLLll'''llLLl.
llLll'''''llLll
llLl''xxx''lLll
llLl''xO+''lLll
llLl''xxx''lLll
llLll'''''llLll
.lLLll'''llLLl.
.llLLlllllLLll.
l.llLLLLLLLll.l
 l.lllllllll.l
  l..lllll..l
ENDMAP

NAME:    evilmike_abyss_rune_shrine
TAGS:    abyss_rune unrand
MONS:    generate_awake spatial vortex
MONS:    patrolling spatial maelstrom / draconian shifter w:3
KITEM:   _ = abyssal rune of Zot
KFEAT:   _ = altar_lugonu
SUBST:   c : cbvx
MARKER:  F = lua:fog_machine { cloud_type = "translocational energy", \
             pow_max = 2, delay_min = 10, delay_max = 20, walk_dist = 0, size = 1 }
MAP
F.........F
..ccccccc..
.ccG..1.cc.
..+..2_1cc.
.ccG..1.cc.
..ccccccc..
F.........F
ENDMAP

NAME:   evilmike_abyss_rune_ettins
TAGS:   abyss_rune unrand
KITEM:  O = abyssal rune of Zot
KMONS:  O = giant eyeball
MONS:   ettin ; giant spiked club ego:distortion ident:type | \
                giant club ego:distortion ident:type . \
                giant spiked club ego:chaos ident:type | \
                giant club ego:chaos ident:type
MONS:   ettin skeleton / rakshasa / shadow wraith w:4, place:Abyss:$
SUBST:  x = xxccn, . = ...W
NSUBST: 0 = 4:1 / 2:2 / *:3
MAP
    ........
 ....xxx.xx..
 .xxxx.xx.xx..
 .x0x.x0xx.xx.
..xx.xxx.xx.x.
.xx.xx0x.x0xx.
.x.xx0O.xx.xx.
.xx.xx.xx.xxx.
..xx.xxxxx0xx.
 .x0x.x0x.xx..
 .xxxx.x.xx..
 ....xxxxx..
    .......
ENDMAP

NAME:  evilmike_abyss_rune_curve
TAGS:  abyss_rune unrand allow_dup
KITEM: O = abyssal rune of Zot
SUBST: X = x.., Y : b., x = xc
MONS:  neqoxec / raiju / worldbinder / thrashing horror / shapeshifter
MONS:  tormentor / hellion / bone dragon / shadow demon / iron dragon zombie
MAP
xxxxxXXXX
...1xxxxXXX
.......xxxXX
..Y......xxXX
.......2..xxx
....Y...x.2Ox
.......2..xxx
..Y......xxXX
.......xxxXX
...1xxxxXXX
xxxxxXXXX
ENDMAP

NAME:  evilmike_abyss_rune_tso_outpost
TAGS:  abyss_rune unrand no_monster_gen
KITEM: O = abyssal rune of Zot
KFEAT: _ = altar_the_shining_one
MONS:  angel, cherub, ophan
MAP
.......................
........ccc...cccccc...
.cccccGcc3ccGcc...2cc..
.+...ccc...ccc....cccc.
.+.T1.+.._..+.....+.Oc.
.+...ccc...ccc....cccc.
.cccccGcc3ccGcc...2cc..
........ccc...cccccc...
.......................
ENDMAP

NAME:  kb_abyss_rune_zappy
TAGS:  abyss_rune unrand
MONS:  generate_awake lightning spire
KMONS: O = patrolling raiju band
KITEM: O = abyssal rune of Zot
MAP
 1.1
.....
1.O.1
.....
 1.1
ENDMAP

NAME:  hangedman_abyss_rune_flesh_and_stone
TAGS:  abyss_rune unrand no_monster_gen no_item_gen no_pool_fixup
MONS:  catoblepas, earth elemental, basilisk
MONS:  boulder beetle, catoblepas skeleton
ITEM:  potion of flight q:1 ident:type
KITEM: O = abyssal rune of Zot
KPROP: l = no_cloud_gen
SUBST: 0 = 222234555
MAP
    .d.3.d.
.ccccGclcGcccc.
 .cOccclccccc.
  c+c0.l.0ccc
.cc1ccclcccccc.
 .c5c0.l.0ccc.
  c4c clc ccc
.cc.c0.l.0cccc.
 .c.ccclccccc.
  c.c0.l.0ccc
.cc.ccclcccccc.
 .c+c3c2c5ccc.
  c 2 c1c 5 c
.cc3 .+ +. 5cc.
 .ccccccccccc.
  .c c. .c c.
ENDMAP

NAME:   hangedman_abyss_rune_macabre_mess
TAGS:   abyss_rune unrand no_monster_gen
KMONS:  1 = giant orange brain
KMONS:  2 = golden eye
KMONS:  3O = flying skull
KMONS:  4 = macabre mass hd:2 / macabre mass hd:3 / small abomination w:4
KMONS:  5 = neqoxec
KMONS:  6 = cacodemon
KMONS:  7 = shining eye
KITEM:  O = abyssal rune of Zot
KFEAT:  n = iron_grate
KFEAT:  1234567 = shallow_water w:1 / . w:14
SUBST:  c = ccccvb
MAP
 .cccccc4@4cccccc.
 cc .. ccWcc .. cc
.c 44.. c5c ..44 c.
cc44nnn.c+c.nnn44cc
c 4nn7n.....n7nn4 c
c54=13n..c..n31=45c
c 4nn2n.....n2nn4 c
cc.4nnn.....nnn4.cc
.c ............. c.
 ccc .4nnnnn4. ccc
  .cc44n2O2n44cc.
   .c 4nn1nn4 c.
    cc44n=n44cc
    .c 44444 c.
     ccc 6 ccc
      .ccccc.
ENDMAP

NAME:    hangedman_abyss_rune_treasure_dump
TAGS:    abyss_rune unrand no_monster_gen no_item_gen
WEIGHT:  2
KMONS:   12 = great orb of eyes perm_ench:shapeshifter
KMONS:   34 = guardian serpent perm_ench:shapeshifter
KMONS:   e = crystal guardian
# Note: if anything in these two item sets becomes useful for more then a tiny
# fraction of characters finding the abyssal rune, remove or replace them.
KITEM:   24d = scroll of random uselessness w:15 q:1 / fruit q:1 w:5 / \
         club w:5 / hammer w:5 / stone q:1 / needle q:1 ego:confusion / \
         mundane animal skin / mundane ring mail / gold q:1
KITEM:   13e = wand of slowing / wand of confusion / wand of magic darts / \
         wand of flame w:5 / wand of frost w:5 / plain deck w:5 / \
         whip not_cursed w:2 / hat not_cursed mundane w:5
KITEM:   f = abyssal rune of zot mimic
KITEM:   g = abyssal rune of zot
SHUFFLE: 14 / 23
NSUBST:  f = 1:g / *:f, d = 2:e / 2 = de / *:d
SUBST:   x = xxxxc, c = cccvb
MAP
  ... x ...
 x .xxxxx. x
. .cdcdcdc. .
..cdddddddc..
.xddfd1dfddx.
 xcdddddddcx
xxdd4ded4ddxx
 xcdddddddcx
.xdd1dfd1ddx.
..cdddddddc..
. .cdcdcdc. .
 x .xxxxx. x
  ... x ...
ENDMAP

####
NAME:   grunt_abyss_rune_the_horror
TAGS:   abyss_rune unrand patrolling
MONS:   unseen horror, thrashing horror, lurking horror
NSUBST: + = 1:. / *:+, x = 4:. / *:x, m = 4:. / *:m
KITEM:  O = abyssal rune of Zot
MAP
    ..@..
  .........
 ...xm+mx...
 ..mx.1.xm..
..xx.2.1.xx..
..m.1.3.2.m..
@.+1..O..1+.@
..m.2.3.1.m..
..xx.1.2.xx..
 ..mx.1.xm..
 ...xm+mx...
  .........
    ..@..
ENDMAP

####
NAME:   grunt_abyss_rune_wretched_hive
TAGS:   abyss_rune unrand patrolling
MONS:   killer bee, wretched star, green ugly thing
ITEM:   royal jelly q:1 / nothing w:190
KMONS:  O = queen bee
KITEM:  O = abyssal rune of Zot
NSUBST: 1 = 4:3 / 1:1 / 1:. / * = 1.
COLOUR: a = yellow
TILE:   a = wall_wax
KFEAT:  a = x
MAP
          xxxxxxx
        xxxaaaaaxxxx
      xxxxaaaOaaaaxxx
    xxxaaaad1a1daaaxxx
   xxaa.aa.1d1d1.aaaaxxx
  xxaa.a...a...a...aaaaxx
 xxaa2aaa1aaa2aaa1aaa2aaxx
xxaad.dad.dad.dad.dad.daaxx
xaa.dad.dad.dad.dad.dad.aax
xaa1aaa1aaa1aaa1aaa1aaa1aaxx
xad.dad.dad.dad.dad.dad.d.ax
aadad.dad.dad.dad.dad.dad.aa
..aaa1aaa1aaa1aaa1aaa1aaa...
 ..a...a...a...a...a...a...
   .....................
        ............
            .@..
ENDMAP

####
# Perhaps part of the Elven Halls was devoured in a twisted experiment?
NAME:    grunt_abyss_rune_sword_and_sorcery
TAGS:    abyss_rune unrand patrolling no_monster_gen
MONS:    tengu reaver, deep elf blademaster, deep elf sorcerer, worldbinder
KITEM:   O = abyssal rune of Zot
KFEAT:   - = floor
SHUFFLE: 123
RTILE:   x = wall_hall
COLOUR:  x = elven_brick
FTILE:   -+UO123 = floor_hall
MAP
    .......
 ..4xxxxxxx.
.xxxxcccccx.
.x2--c3--cx.
.+-U-+-O1cx4
.x3--c2--cx.
.xxxxcccccx.
 ..4xxxxxxx.
    .......
ENDMAP

####
NAME:    grunt_abyss_rune_twisted_forest
TAGS:    abyss_rune unrand patrolling no_pool_fixup
MONS:    shambling mangrove, thorn hunter, dire elephant, satyr, rakshasa
KMONS:   6 = water elemental
KMONS:   7 = water nymph
KFEAT:   6 = deep_water
KFEAT:   7 = shallow_water
KITEM:   O = abyssal rune of Zot
SHUFFLE: 1234
SUBST:   T = t.
SUBST:   E = ttWw
NSUBST:  . = 2:5 /*:.
NSUBST:  w = 2:6 / *:w
NSUBST:  W = 2 = 7 / *:W
FTILE:   .t123456789O = floor_grass
MAP
    .......
  ....EEE....
 ..EWwtttwWE..
 .E.Tt.w.tT.E.
..WT..E.E..TW..
..wtWtt.tt.tw..
.Et.Et1.2tE.tE.
.Etw...O...wtE.
.Et.Et5.3tE.tE.
..wtWtt.tt.tw..
..WT..E.E..TW..
 .E.Tt.w.tT.E.
 ..EWwtttwWE..
  ....EEE....
    .......
ENDMAP

####
NAME:    nicolae_abyss_rune_more_like_crabyss
TAGS:    abyss_rune unrand transparent
KMONS:   O = apocalypse crab / nothing
KMONS:   1P = apocalypse crab
KMONS:   2 = fire crab
KMONS:   3 = ghost crab
KITEM:   OP = abyssal rune of Zot
KFEAT:   O1Pw = W
SHUFFLE: O1/O1/Pw
MAP
  ...........
  .cc.....cc.
....c.ccc.c....
.ccccccOcccccc.
...ccWWWWWcc...
.ccc1WWWWW1ccc.
...cWWWWWWWc...
.cccccWWWccccc.
....cc...cc....
 ..cc.....cc..
 ..c.......c..
 .ccc.....ccc.
 .c2.......3c.
 .ccc.....ccc.
 .............
ENDMAP

NAME:    nicolae_abyss_rune_star_stuff
TAGS:    abyss_rune unrand no_monster_gen
KMONS:   1 = starcursed mass
KMONS:   2 = wretched star
KMONS:   3 = tentacled starspawn
KITEM:   O = abyssal rune of zot
SHUFFLE: AaBbDdEeFf / BbDdEeFfAa / DdEeFfAaBb / EeFfAaBbDd / FfAaBbDdEe
NSUBST:  FAB : 1:O / *:.
NSUBST:  s = 2:1 / 2:2 / 2:3
SUBST:   a = +, DE = ., bdef = c
SUBST:   c = cccxbv
MAP
      ccc
    cccBccc
   ec..s..cf
 ccc...c...ccc
cc....c.c....cc
cA...........Dc
c..sc.....cs..c
cc..c..s..c..cc
 d...........a
 c...c...c...c
 cc..cc.cc..cc
  c.s.....s.c
  cF.......Ec
  cccccbccccc
ENDMAP

###############################################################################
# Abyss Exit Vaults
# Note: Don't tag your abyss exit with allow_dup unless it's small and
# restrained in its gimmickry: there are more than enough abyss_exit vaults
# for the longest of given trips without having to repeat vault gimmicks
# and block seeing other vault gimmicks.
###############################################################################

NAME: abyss_exit_lava
TAGS: abyss_exit unrand allow_dup
: init_abyss_exit(_G, "O")
MAP
l.l
.O.
l.l
ENDMAP

NAME:  evilmike_abyss_exit_glass
TAGS:  abyss_exit
: init_abyss_exit(_G, "O")
MAP
 mmm@
mm.mm
m.O.m
mm.mm
 mmm
ENDMAP

NAME:  evilmike_abyss_exit_plants
TAGS:  abyss_exit
SUBST: 1 = 112.
SUBST: . = .:100 1
MONS:  plant col:random name:demonic name_adjective tile:mons_demonic_plant
MONS:  bush
: init_abyss_exit(_G, "O")
MAP
 .......
.........
..11111..
..12221..
..12O21..
..12221..
..11111..
.........
 .......
ENDMAP

NAME:  evilmike_abyss_exit_1
TAGS:  abyss_exit unrand allow_dup
: init_abyss_exit(_G, "O")
MAP
x+x
+O+
x+x
ENDMAP

NAME:  evilmike_abyss_exit_2
TAGS:  abyss_exit unrand allow_dup
SUBST: x = x:30 c:20 v:20 m:3 b:3
: init_abyss_exit(_G, "O")
MAP
  @ xxxx
 x.xxxxxxx
xx.x.....xx
xx.x.xxx.xx
xx.x.xOx.xx
xx.x.x.x.xx
xx.x...x.xx
xx.xxxxx.xx
xx.......xx
 xxxxxxxxx
  xxxxxxx
ENDMAP

NAME:  evilmike_abyss_exit_3
TAGS:  abyss_exit unrand allow_dup
SUBST: . = ..Wwl
: init_abyss_exit(_G, "O")
MAP
     .
    ...
   .....
  .......
 .........@
.....O.....
 .........
  .......
   .....
    ...
     .
ENDMAP

NAME:  evilmike_abyss_exit_4
TAGS:  abyss_exit unrand allow_dup
KFEAT: n = stone_arch
: init_abyss_exit(_G, "O")
MAP
    x
    x
  ..x..
  .nxn.
xxxxxxxxx
  .Oxn.
  ..x..
 .  x
@   x
ENDMAP

NAME:   evilmike_abyss_exit_5
TAGS:   abyss_exit unrand allow_dup
KFEAT:  n = stone_arch
NSUBST: n = 1:O / *:n
: init_abyss_exit(_G, "O")
MAP
    x   x
  x x x x x
 xxxxxxxxxxx
  xnxnxnxnx
xxx+x+x+x+xxx
  x.........@
xxx+x+x+x+xxx
  xnxnxnxnx
 xxxxxxxxxxx
  x x x x x
    x   x
ENDMAP

NAME:  evilmike_abyss_exit_6
TAGS:  abyss_exit unrand allow_dup
SUBST: X: .x, x: xxxccvlw, .: ...W
: init_abyss_exit(_G, "O")
MAP
  x x x
 x x x x x
xxxxxxxxx x
xX.XxX.Xxx x
x.x.x.x.x x
@XxX.XxOxx x
xxxxxxxxx x
 x x x x x
  x x x
ENDMAP

NAME:  evilmike_abyss_exit_7
TAGS:  abyss_exit unrand allow_dup
SUBST: x:xxcv
: init_abyss_exit(_G, "O")
MAP
xx.xx
x.x.x
.xOx.
x.x.x
xx.xx
ENDMAP

NAME:  evilmike_abyss_exit_8
TAGS:  abyss_exit unrand allow_dup
SUBST: G : GGT, T : TV
: init_abyss_exit(_G, "O")
MAP
...G...
.G...G.
.......
G..O..G
.......
.G...G.
...G...
ENDMAP

NAME:  evilmike_abyss_exit_9
TAGS:  abyss_exit unrand allow_dup
KMONS: O = tentacled monstrosity / large abomination / nothing w:5
SUBST: x = xxc
: init_abyss_exit(_G, "O")
MAP
.xxx.
x...x
x.O.x
x...x
.xxx.
ENDMAP

NAME:  evilmike_abyss_exit_10
TAGS:  abyss_exit
SUBST: X = x.., x = xxxcv
MONS:  brain worm / w:5 neqoxec
: init_abyss_exit(_G, "O")
MAP
XXXXXXXXXXX
XXXXXXXXXXX
XXxxxxxxxXX
XXx..1..xXX
XXx.xxx.xXX
XXx1.Ox....
XXx.xxx.xXX
XXx..1..xXX
XXxxxxxxxXX
XXXXXXXXXXX
XXXXXXXXXXX
ENDMAP

NAME:  evilmike_abyss_exit_11
TAGS:  abyss_exit unrand allow_dup
SUBST: b = bbx
: init_abyss_exit(_G, "O")
MAP
   b.b
  bb.bb
 bb...bb
bb.....bb
b.......b
....O....
b.......b
bb.....bb
 bb...bb
  bb.bb
   b.b
ENDMAP

# Technically possible for this one to require dig, but pretty unlikely.
NAME:  evilmike_abyss_exit_12
TAGS:  abyss_exit
SUBST: X = x., x = xxx++1
MONS:  w:2 trapdoor spider / nothing
: init_abyss_exit(_G, "O")
MAP
XXXxxxXXX
XxxxxxxxX
XxxxxxxxX
xxxxxxxxx
xxxxOxxx+
xxxxxxxxx
XxxxxxxxX
XxxxxxxxX
XXXxxxXXX
ENDMAP

# Orange crystal and obsidian statue are very rare here...
# I want most characters to be able to actually use this exit
NAME:  evilmike_abyss_exit_13
TAGS:  abyss_exit unrand allow_dup
KMONS: O = w:1 orange crystal statue / w:2 obsidian statue / w: 20 ice statue / w: 15 oklob plant
: init_abyss_exit(_G, "O")
MAP
O
ENDMAP

NAME:  evilmike_abyss_exit_14
TAGS:  abyss_exit unrand allow_dup
SUBST: x = xcv, 0 = 0.
: init_abyss_exit(_G, "O")
MAP
x..x..x
.0.x.0.
...x...
xxxOxxx
...x...
.0.x.0.
x..x..x
ENDMAP

NAME:  evilmike_abyss_exit_15
TAGS:  abyss_exit
KMONS: O = unseen horror / lorocyproca / nothing
MONS:  small abomination / nothing
SUBST: X = x., x=xxc
: init_abyss_exit(_G, "O")
MAP
 X X X
X XXX X
 XXXXX X
XXXXXXX X
xxxxxxxX X
+1+1+OxXX X
xxxxxxxX X
XXXXXXX X
 XXXXX X
X XXX X
 X X X
ENDMAP

NAME:   evilmike_abyss_exit_16
TAGS:   abyss_exit
NSUBST: . = 1:O / *:.
SUBST:  c = cbx, . = .....12
MONS:   slime creature, ooze / jelly
: init_abyss_exit(_G, "O")
MAP
c+c+c+c+c+c
+.+.+.+.+.+
c+c+c+c+c+c
+.+.+.+.+.+
c+c+c+c+c+c
+.+.+.+.+.+
c+c+c+c+c+c
+.+.+.+.+.+
c+c+c+c+c+c
+.+.+.+.+.+
c+c+c+c+c+c
ENDMAP

NAME:  evilmike_abyss_exit_17
TAGS:  abyss_exit unrand allow_dup
SUBST: x = xxc, . = ...W, 0 = 0.
: init_abyss_exit(_G, "O")
MAP
..xx0xx..
...xxx...
x...x...x
xx.V.V.xx
0xx.O.xx0
xx.V.V.xx
x...x...x
...xxx...
..xx0xx..
ENDMAP

NAME:   evilmike_abyss_exit_kraken
TAGS:   abyss_exit
NSUBST: w = 1:m / *:w
KFEAT:  m = w
KMONS:  m = kraken
SUBST:  Z = wY, Y = wW, W = w., w=WWw
: init_abyss_exit(_G, "O")
MAP
..WWWWWWW..
.WYYYYYYYW.
WYZZZZZZZYW
WYZwwwwwZYW
WYZwwwwwZYW
WYZwwOwwZYW
WYZwwwwwZYW
WYZwwwwwZYW
WYZZZZZZZYW
.WYYYYYYYW.
..WWWWWWW..
ENDMAP

# Fog machine parameters are the same as that swamp entry vault; you pretty
# much can't avoid the fog here, so I hope you have rMut.
NAME:   evilmike_abyss_exit_mutagenic
TAGS:   abyss_exit
MARKER: O = lua:fog_machine {                               \
                pow_min = 8, pow_max = 12, delay = 25,      \
                size = 2, walk_dist = 1, spread_rate = 33,  \
                cloud_type = "mutagenic fog"                \
            }
: init_abyss_exit(_G, "O")
MAP
...
.O.
...
ENDMAP

# Make sure to close the doors if you don't like what you see behind them...
NAME:   evilmike_abyss_exit_vortex
TAGS:   abyss_exit
KFEAT:  n = stone_arch
SUBST:  x:xxxcv, 1=1., g:G.
SUBST:  G:Gb.
MONS:   generate_awake spatial vortex
NSUBST: n = 1:O / *:n
: init_abyss_exit(_G, "O")
MAP
     xxxxx
     x111x
     x1n1x
     x111x
    @x+++x@
xxxxx.....xxxxx
x111+..g..+111x
x1n1+.gGg.+1n1x
x111+..g..+111x
xxxxx.....xxxxx
    @x+++x@
     x111x
     x1n1x
     x111x
     xxxxx
ENDMAP

NAME:  evilmike_abyss_exit_chaos
TAGS:  abyss_exit
MONS:  chaos spawn
SUBST: 1 = 11., n : nc, c = cvb
: init_abyss_exit(_G, "O")
MAP
 ccncc
cc.1.cccccc
c.1.1.c.1.cccc
n1.O..+......+@
c.1.1.c.1.cccc
cc.1.cccccc
 ccncc
ENDMAP

# This one is really, really weird...
NAME:  evilmike_abyss_exit_smiterflies
TAGS:  abyss_exit
KMONS: O = generate_awake statue spells:summon_butterflies.100.magical \
           tile:mons_statue_mage col:lightblue
MONS:  smoke demon, deep elf priest
: init_abyss_exit(_G, "O")
MAP
       ccc
     ccc1ccc
   ccc.....ccc
  cc.........cc
  c....2......c
 cc...........cc
 c.............c
cc..2..........cc
c1......O.......+@
cc..2..........cc
 c.............c
 cc...........cc
  c....2......c
  cc.........cc
   ccc.....ccc
     ccc1ccc
       ccc
ENDMAP

NAME:  evilmike_abyss_exit_disperse
TAGS:  abyss_exit
SUBST: . = .:100 2
MONS:  centaur; shortbow . arrow ego:dispersal, plant
: init_abyss_exit(_G, "O")
MAP
     xxx xxx xxx
   xxx1xxx1xxx1x
 xxx....x...x..xxx
xx...............x
x1.Ox............+@
xx...............x
 xxx....x...x..xxx
   xxx1xxx1xxx1x
     xxx xxx xxx
ENDMAP

NAME:    hangedman_abyss_exit_choices
TAGS:    abyss_exit unrand no_monster_gen
MONS:    hell hound, raiju, great orb of eyes
MONS:    giant orange brain, swamp drake, wind drake, torpor snail
KMONS:   8 = giant leech
KMONS:   9 = mana viper
KMONS:   0 = anaconda
KMONS:   A = ball lightning
KMONS:   B = air elemental
KMONS:   C = unseen horror
KMONS:   D = shadow wraith
KMONS:   E = elephant
KMONS:   F = catoblepas
SHUFFLE: 12, 56, 90, CD, 12 / 34, 56 / 78, 90 / AB, CD / EF
SUBST:   c = ccv, x = xxc
: init_abyss_exit(_G, "N")
MAP
. . . . . . . . . .
 .xx x x x x x x .
.ccccccccccccccccc.
.c +C+ +9+ +5+ +1+.
.cNcnc.cnc.cnc.cncn
.c +D+ +0+ +6+ +2+.
.ccccccccccccccccc.
 .xx x x x x x x .
. . . . . . . . . .
ENDMAP

###################################################################
# Distorted/chaotic kobolds (abyss exit version!) (Jude)
NAME:   due_exit_kobolds
TAGS:   no_rotate abyss_exit
SUBST:  y  = c.
COLOUR: 1. = random
MARKER: P  = lua:fog_machine { cloud_type="blue smoke", walk_dist=1, \
             size=9, pow_max=20, delay=10, buildup_amnt=14, buildup_time=7, \
             spread_rate=3, start_clouds=1, colour="blue" }
KFEAT:  _ = altar_lugonu
KMONS:  1 = big kobold w:80 ; short sword | rapier | quick blade w:3 /\
            big kobold w:10 ; quick blade ego:distortion w:6 | \
                              rapier ego:distortion | short sword ego:distortion
: init_abyss_exit(_G, "P")
MAP
  ccccc
 cc...cc
cc..P..cc
 cc...cc
 ccc.cccc
ccyy.yyyccc
cy..111.yyc
cc..1_1..cc
cyy.111..yc
cccyy..yycc
  ccc..ccc
    c++c
     @@
ENDMAP

NAME:  guppyfry_abyss_exit_glass_guarded
TAGS:  abyss_exit
: init_abyss_exit(_G, "O")
MAP
.............
.xmxmxmxmxmx.
m.m.......m.m
xm.mmm.mmm.mx
xxxx.....xxxx
x00...O...00x
xxxxxxlxxxxxx
lllllllllllll
mxmxmxmxmxmxx
.............
ENDMAP

NAME:  guppyfry_abyss_exit_spiral_guarded
TAGS:  abyss_exit unrand allow_dup
SUBST: - = llx
: init_abyss_exit(_G, "O")
MAP
.0---------0
.-........--
..--------.-
.--.....--.-
.-.-----.-.-
.-.--O--.-.-
.-.-.---.-.-
.-.--...--.-
.-.-------.-
.--.......--
.0---------0
ENDMAP

NAME: guppyfry_abyss_exit_imp_island
TAGS: abyss_exit
SUBST: - = Wwwww
MONS: crimson imp / white imp
: init_abyss_exit(_G, "O")
MAP
.---------.
---..1..---
--..1.1..--
---..1..---
.---------.
.....O.....
.---------.
---..1..---
--..1.1..--
---..1..---
.---------.
ENDMAP

NAME:  bh_abyss_exit_spiral
TAGS:  abyss_exit unrand allow_dup
: init_abyss_exit(_G, "O")
MAP
 .xxxx.
  .xxxx.
    .xxx.
    .xx.
   ..x..
  ...O...
   ..x..
   .xx.
  .xxx.
   .xxxx.
    .xxxx.
ENDMAP

NAME:  bh_abyss_exit_grid
TAGS:  abyss_exit unrand allow_dup
SUBST: a = x.
SUBST: b = x..
SUBST: c = x...
: init_abyss_exit(_G, "O")
MAP
   b b b b b
  a.a.a.a.a.a
 a.x.x.x.x.x.a
b.x.x.x.x.x.x.b
 a.x.x.x.x.x.a
b.x.x.c.c.x.x.b
 a.x.c.0.c.x.a
b.x.x.c.c.x.x.b
 a.x.x.x.x.x.a
b.x.x.x.x.x.x.b
 a.x.x.x.x.x.a
  a.a.a.a.a.a
   b b b b b
ENDMAP

####
# Take a deep breath...
NAME:  grunt_abyss_exit_deep_breaths
TAGS:  abyss_exit unrand transparent
MONS:  ice statue, fire crab, swamp dragon, catoblepas
: init_abyss_exit(_G, "O")
MAP
   xxxxx
  xxlllxx
 xxll3llxx
xxll.O.llxx
xll..1..llx
xl.2...2.lx
xl...4...lx
xl.......lx
ll.......ll
 ..........
  ...@....
ENDMAP

NAME:  bh_abyss_exit_choice
TAGS:  abyss_exit unrand transparent
DEPTH: Abyss:1-4
KFEAT: <: exit_abyss
KFEAT: >: abyssal_stair
MAP
xxxxxxxxxxx
x00........
x<0......>.
x00........
xxxxxxxxxxx
ENDMAP

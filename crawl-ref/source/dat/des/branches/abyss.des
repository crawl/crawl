###############################################################################
# abyss.des: abyss random minivaults, exits and abyssal rune vaults.
# Exit and rune vaults are placed by tag.
#
# Abyssal vaults should usually be minivaults and floating vaults, no larger
# than 28x23. Encompass vaults will be rejected at compile time, and
# other orientations will produce weird results.

{{
 -- Since abyssal stairs don't spawn at the bottom of the Abyss,
 -- replace a vault's empty guarded floor with a floor item at the bottom.
function init_abyss_exit(e, glyph)
  if you.depth() == dgn.br_depth(you.branch()) then
    if crawl.random2(10) > 3 then
      e.kfeat(glyph .. " = exit_abyss")
    else
      e.kitem(glyph .. " = any no_pickup")
    end
  else
    e.kfeat(glyph .. " = exit_abyss w:6 / abyssal_stair w:4")
  end
end

function abyss_entry(e, glyph)
  e.tags("extra luniq_enter_abyss chance_enter_abyss")
  e.depth_chance("Depths:1-3", 3333)
  e.depth_chance("Depths:4",   10000)
  e.depth_chance("Depths:5-",  3333)
-- D:21-27 entries for save compat, remove when TAG_MAJOR_VERSION > 34
  e.depth_chance("D:21-24", 3333)
  e.depth_chance("D:25",    10000)
  e.depth_chance("D:26-",   3333)
  e.kfeat(glyph .. ' = enter_abyss')
end
}}

###############################################################################
# Abyss entries.
# 1/3 chance to appear on Depths floors and guaranteed on Depths:4.
###############################################################################

default-depth: Depths, D:21-

# This one is the backup and the one used in encompass vaults.
NAME: abyss_entry
TAGS: allow_dup overwrite_floor_cell no_exits replace_portal transparent
TAGS: fallback_enter_abyss
WEIGHT: 50
: abyss_entry(_G, 'O')
MAP
O
ENDMAP

NAME:    minmay_room_of_horrors
TAGS:    nolayout_encompass luniq_hellpanabyss_threat_entry
MONS:    small abomination, large abomination, unseen horror / chaos spawn
SHUFFLE: 123
: abyss_entry(_G, '_')
MAP
xxxxxx
x3113x
+1221x
x1221x
x311_x
xxxxxx
ENDMAP

NAME:  marbit_kobold_abyss_entry
TAGS:  no_monster_gen nolayout_encompass
WEIGHT: 5
MONS:  big kobold ; tomahawk ego:dispersal, kobold demonologist
: abyss_entry(_G, '_')
MAP
xxxxxxxxxxxxxxx
xxlllxx<.xlll.x
xlllllxx.lll..x
@x.llll..lll2_x
x.xl1lllllll.>x
xlllxlllx1lll.x
xxxxxxxxxxxxxxx
ENDMAP

NAME:    abyss_entry_small_statue_room
TAGS:    nolayout_encompass
: abyss_entry(_G, 'O')
MAP
xxxxx
xGOGx
xx+xx
ENDMAP

NAME:    abyss_entry_crystal
TAGS:    nolayout_encompass
: abyss_entry(_G, 'O')
SHUFFLE: AB
SUBST:   A : .:50 G:50 T U V Y t
SUBST:   B : .:100 G
MAP
  xxxxx
 xxbbbxx
xxbbObbxx
xbbA.Abbx
xbB...Bbx
xb.....bx
ENDMAP

NAME:    abyss_entry_glasseish
TAGS:    nolayout_encompass transparent
WEIGHT:  5
: abyss_entry(_G, 'O')
KPROP:   T = no_tele_into
SUBST:   T : T l 1 2 A X
MONS:    tentacled starspawn / starcursed mass / wretched star
MONS:    demonic plant
KFEAT:   X : stone_arch
MAP
nnn nnn
nTn.nTn
nnn.nnn
 ..O..
nnn.nnn
nTn.nTn
nnn nnn
ENDMAP

NAME:    abyss_entry_flame_altar
TAGS:    nolayout_encompass
: abyss_entry(_G, 'O')
SUBST:   A : deG
MARKER:  d = lua:fog_machine { cloud_type = "flame", \
             pow_min = 10, pow_max = 10, delay = 10, \
             size = 1, walk_dist = 0, spread_rate= 0, excl_rad = 0 }
MARKER:  e = lua:fog_machine { cloud_type = "seething chaos", \
             pow_min = 10, pow_max = 10, delay = 10, \
             size = 1, walk_dist = 0, spread_rate= 0, excl_rad = 0 }
KFEAT:   _ = altar_lugonu
MAP
 bbb
bbObb
bA_Ab
  @
ENDMAP

NAME:    hangedman_abyss_entry_splay
TAGS:    nolayout_encompass transparent
SHUFFLE: ABCDE
SUBST:   A : cxxx.., B : cxxx.., CDE = x, F : ccxxb
: abyss_entry(_G, 'O')
MAP
        xx..
        x.A.
        xx..
        x.C......
        xF.D.E.B.
      xxx.F.x.x.x
xxxxxxx.O.xxxxxxx
x.x.x.F.xxx
.B.E.D.Fx
......C.x
     ..xx
     .A.x
     ..xx
ENDMAP

NAME: nicolae_abyss_entry_one_way_or_another
TAGS: nolayout_encompass patrolling
: abyss_entry(_G, 'O')
NSUBST: 1 = 1111111112 / .
# All of these are guaranteed to have banishment in their spell sets.
# One in ten chance they get a distortion weapon too.
: local elfweapon = "long sword ego:distortion w:20 | short sword " ..
:  "ego:distortion | rapier ego:distortion | dagger ego:distortion"
: local louiseweapon = "long sword ego:distortion | short sword " ..
:  "ego:distortion | battleaxe ego:distortion | hand axe ego:distortion | " ..
:  "mace ego:distortion | dire flail ego:distortion | glaive ego:distortion w:2"
KMONS: 1 = Louise, deep elf demonologist / deep elf sorcerer w:5
: kmons( "2 = Louise ; " .. louiseweapon .. " , deep elf demonologist ; " ..
: elfweapon .. " / deep elf sorcerer w:5 ; " .. elfweapon)
MAP
   ......
  ..bbbb..
 ..b....b..
..b......b..
.b..bb1..b...
...b.1O1.b...
...b..1bb..b.
 ..b......b..
  ..b....b..
   ..bbbb..
    ......
ENDMAP

###############################################################################
# abyss random vaults
###############################################################################

default-depth: Abyss

###############################################################################
# Dummy vaults
NAME: abyss_furniture_dummy
TAGS: dummy
CHANCE: 90% (Abyss:1)
CHANCE: 80% (Abyss:2)
CHANCE: 70% (Abyss:3)
CHANCE: 60% (Abyss:4)
CHANCE: 50% (Abyss:5)

NAME: abyss_rune_dummy
TAGS: abyss_rune unrand dummy
CHANCE: 10%

# FIXME: the tag name is inaccurate with abyssal stairs placed by these vaults.
NAME: abyss_exit_dummy
TAGS: abyss_exit unrand dummy
CHANCE: 95% (Abyss:1)
CHANCE: 90% (Abyss:2)
CHANCE: 85% (Abyss:3)
CHANCE: 80% (Abyss:4)
CHANCE: 75% (Abyss:5)

NAME:    abyss_fake_exit
TAGS:    allow_dup
WEIGHT:  1
KFEAT:   O = exit_abyss mimic w:6 / abyssal_stair mimic w:4
MAP
O
ENDMAP

###############################################################################
NAME: abyss_furniture_001
TAGS: extra allow_dup
MAP
G.G
.V.
G.G
ENDMAP

#################################################################
# Ecumenical Temple... (1KB)
# 'cuz the fun haters removed overflow temples from the Abyss...
NAME:    abyss_greek_temple
TAGS:    no_rotate
NSUBST:  . = 3:1 / 2:2 / *:.
SUBST:   ' = .
COLOUR:  " : white w:5 / none
FTILE:   "ABCDEFGHIJKLMNOPRQSTU : floor_limestone w:5 / floor_nerves
SUBST:   " = .
MONS:    plant, bush
SHUFFLE: ABCDEFHIJKLMNOPQRSTU
KFEAT:   A = altar_makhleb
KFEAT:   B = altar_xom
KFEAT:   C = altar_zin
KFEAT:   D = altar_the_shining_one
KFEAT:   E = altar_kikubaaqudgha
KFEAT:   F = altar_yredelemnul
KFEAT:   H = altar_vehumet
KFEAT:   I = altar_okawaru
KFEAT:   J = altar_sif_muna
KFEAT:   K = altar_trog
KFEAT:   L = altar_nemelex_xobeh
KFEAT:   M = altar_elyvilon
KFEAT:   N = altar_fedhas
KFEAT:   O = altar_cheibriados
KFEAT:   P = altar_ashenzari
KFEAT:   Q = altar_dithmenos
KFEAT:   R = altar_gozag
KFEAT:   S = altar_qazlal
KFEAT:   T = altar_uskayaw
KFEAT:   U = altar_hepliaklqana
MAP
''''''...............
''ccccccccccccccccc..
''G'c"""D"""""P"""c..
''''c"A"""I"L"""S"c..
''G'c"""E"""""Q"""ct.
''''+"B"""J"N"""T"c..
''G'c"""F"""""R"""c..
''''c"C"""K"O"""U"c.t
''G'c"""H"""""S"""c..
''ccccccccccccccccc..
.......t......ttt....
ENDMAP

# <bh> I got the idea after I introduced a bug where the whole floor of
#        the abyss turned into altars
NAME:   bh_abyss_xom_lugonu_altar
TAGS:   extra allow_dup
COLOUR: " : magenta w:2 / white
KFEAT:   _ = altar_lugonu
KFEAT:   X = altar_xom
SUBST:   a = XXX"
SUBST:   b = X"
SUBST:   c = XX"
SUBST:   d = X""
SUBST:   e = X"""
MAP
   ebe
  dcacd
 ec"""ce
 ba"_"ab
 ec"""ce
  dcacd
   ebe
ENDMAP

# Inspired by a bug which turned Lemuel's castle moat into draconians...
NAME:   hangedman_abyss_monster_moat
TAGS:   no_monster_gen patrolling
MONS:   fire bat w:12 / bat simulacrum / spectral bat / bat
MONS:   vampire, vampire knight / vampire mage w:7
MONS:   crimson imp w:12 / white imp / shadow imp / iron imp
MONS:   kobold demonologist, deep elf demonologist
MONS:   worker ant w:40 / vampire mosquito w:20 / hornet
KMONS:  8 = demonic crawler
KMONS:  9 = emperor scorpion / death scarab
KMONS:  D = place:D:1 perm_ench:shapeshifter w:30 / \
            small abomination w:15 / chaos spawn
KMONS:  E = ugly thing
KMONS:  F = very ugly thing / glowing shapeshifter w:7
SHUFFLE: 123 / 456 / 789 / DEF
SUBST:  C = xccv, w : w:8 l:1 .:1, W : .:9 W:1
MAP
 CCCC  CCCCCCCCCC  CCCC
CC 1cccc+2WWWW5+cccc4 CC
C 11n|32ccc++ccc56|n44 C
C 11c3|3c1WwwW4c6|6c44 C
C 11ccncc11ww44ccncc44 C
C  11111111ww44444444  C
C   111111 ww 444444   C
.C                   .C.
 .CC .C. CCCCCC .C. CC.
ENDMAP

NAME:   hangedman_abyss_sting
TAGS:   no_item_gen no_monster_gen uniq_hornetnest
KMONS:  1 = orange demon
KMONS:  2 = hornet / killer bee w:2 / nothing w:2
KMONS:  3 = death drake / green death
KFEAT:  123 = W
KITEM:  3 = royal jelly
COLOUR: x = yellow
TILE:   x = wall_wax
MAP
 xxxxxxx
xx22222xx
x2...222x
x2.++xx3x
x2.+1 xxx
x2.x 1 xx
x22xx W x
xx23xx W
 xxxxxx W
ENDMAP

NAME:   hangedman_abyss_overflow_altar
TAGS:   no_item_gen no_monster_gen no_pool_fixup
MONS:   earth elemental
KFEAT:  A = altar_lugonu
KFEAT:  B = dry_fountain
KFEAT:  C = abyssal_stair
SUBST:  x = xcv++, y = ..w, z = Wl
MAP
yyyyyyyy
yxxxxxxy
yxzzzzxy
yxzA1zxy
yxz.Bzxy
yxzzzzxy
yxxxxxxy
yyyyyyyy
ENDMAP

NAME:   hangedman_abyss_mock_shop
TAGS:   no_monster_gen patrolling
KMONS:  1 = water elemental / nothing w:5
KMONS:  2 = earth elemental
KFEAT:  0 = abandoned_shop
KFEAT:  1 = dry_fountain
SUBST:  x = xcv, . = ...W
MAP
     . .
  ..xx.xx..
xxxxx1$1xxxxx
x.2..$0$..2.x
xxxxx1$1xxxxx
  ..xx.xx..
     . .
ENDMAP

NAME:    hangedman_abyss_shoalhut
TAGS:    no_pool_fixup patrolling
MONS:    red devil w:30 / smoke demon / eye of devastation w:5 / \
         unseen horror / ynoxinul / snapping turtle skeleton w:15
NSUBST:  = = 1:+ / 1:xx+ / *:x
SUBST:   1 = 1., x = xxxxcccvb, l = wWll
KMASK:   wWl = no_monster_gen
MAP
   l l
 lxx=xxl
 xx.1.xx
lx.1.1.xl
 =1.*.1=
lx.1.1.xl
 xx.1.xx
 lxx+xxl
   l l
ENDMAP

NAME:   bh_silent_lugonu_exit
TAGS:   allow_dup
MONS:   silent spectre
KFEAT:   _ = altar_lugonu
MAP
...       xxx
._.       x1x
...       xx+
ENDMAP

NAME:    hangedman_abyss_feature_spike
TAGS:    extra no_item_gen no_pool_fixup allow_dup
MONS:    fungus, plant, bush
ITEM:    stone q:1, large rock q:1
KFEAT:   - = altar_lugonu
KMASK:   wWl = no_monster_gen
SHUFFLE: fghij / opqrs, fghijopqrs / JKLMNOPQRS
SHUFFLE: fghij / fffff / fffff / fffff / fffff / fghgf / fgfgf / ffhii / ffhii
SHUFFLE: opqrs / ooooo / ooooo / ooooo / ooooo / opqpo / opopo / ooqrr / ooqrr
SHUFFLE: JKLMN / JJJJJ / JJJJJ / JJJJJ / JJJJJ / JKLKJ / JKJKJ / JJLMM / JJLMM
SHUFFLE: OPQRS / OOOOO / OOOOO / OOOOO / OOOOO / OPQPO / OPOPO / OOQRR / OOQRR
SUBST:   f = %, g = C, h = T, i = U, j = V
SUBST:   o = t, p = G, q = w, r = W, s = l
SUBST:   J = x, K = c, L = v, MN = m, O = 1, P = 2, Q = 3, RS = b
CLEAR:   -
SHUFFLE: AACCTTUUVYYdeefghi, tGGwWlI, xxccvvbbmm+123
: dgn.delayed_decay(_G, '%', 'human skeleton')
MAP
        .....
        .LqL.
        .QhQ.
        .....
          .
     .          .
    ....      ....
    .pK.      .Mr.
    .Pg.      .iR.
    ....      ....
          -
 .                 .
....             ....
.oJ.             .Ns.
.Of.             .jS.
....             ....
ENDMAP

NAME:    hangedman_abyss_theft
TAGS:    no_monster_gen
KMONS:   1 = place:Lair:$
KMONS:   2 : place:Swamp:1 / place:Swamp:$
KMONS:   3 : place:Snake:1 / place:Snake:$
KMONS:   4 : place:Shoals:1 / place:Shoals:$
KMONS:   5 : place:Spider:1 / place:Spider:$
KMONS:   6 = place:Orc:$
KMONS:   7 : place:Elf:1 / place:Elf:$
KMONS:   8 : place:Crypt:1 / place:Crypt:$
KMONS:   9 : place:Hell / place:Slime
KMONS:   A = plant
KMONS:   B = bush
SHUFFLE: Gwm, xcvbwm, wwl, mmttB, 1234567, 1689, $%
KMASK:   B = no_item_gen
MAP
     ..xbb..
 A x .xxbbb. b A
  x  xx1cvbb  b
 x  x...vc..b  b
   x.x.1cv...b
..x...x.vc2.b.b..
.xx..1..cv.b..bb.
xxbvbvb%$*..2.2bb
ccvbvbv$G$xcxcxbb
cc3.3..*$%cxcxcvv
.cc..c.xb..4..vv.
..c.c.3bx.v...v..
   c...xb4.v.v
 c  c..bx...v  v
  c  ccxb4vv  v
 A c .ccvvv. v A
     ..cvv..
ENDMAP

NAME:    hangedman_abyss_lost_library
TAGS:    patrolling no_monster_gen no_item_gen
MONS:    guardian serpent, great orb of eyes, glowing orange brain
MONS:    boggart, rakshasa, cacodemon, shadow demon
KMONS:   0 = wizard
KMONS:   8 = orc sorcerer
KMONS:   9 = sphinx
KMONS:   & = lich
KMONS:   @ = deep elf sorcerer
KMONS:   Z = nonbase draconian
ITEM:    randbook numspells:1 w:19 / randbook numspells:2 w:1, any scroll
KFEAT:   _ = altar_sif_muna
SHUFFLE: ABCDEF
NSUBST:  A = 1:0 / 1:d / 1:dee / *:., B = 1:1 / 1:d / 1:dee / *:.
NSUBST:  C = 1:2 / 1:d / 1:dee / *:., D = 1:3 / 1:d / 1:dee / *:.
NSUBST:  E = 1:4 / 1:d / 1:dee / *:., F = 1:& / 2:d / 1:e / *:.
NSUBST:  + = 2:+ / *:x, - = 2:+ / *:x
SHUFFLE: 0123456789', '''''''''@, &@Z
SUBST:   ' = ., = = +wl x:45, _ : _:30 TUVY .:35
SUBST:   x = x:20 c:4 v:1 b:1 .:4, X = x:20 c:5 v:2 b:2 .:1
MAP
 . .. .. .. .. .. .
..X++XXXXXXXXXXXXX..
 XX. xx xxx xxx xXX
.+._ xAAAxBBBxCCCxX.
.+  .xAAA=BBB=CCC X.
 XX..xAAAxBBBxCCCxX
.XX..xx xxx xxx xxX.
.X .............. X.
.X .............. X.
.Xxx xxx xxx xx..XX.
 XxDDDxEEExFFFx..XX
.X DDD=EEE=FFFx.  -.
.XxDDDxEEExFFFx _.-.
 XXx xxx xxx xx .XX
..XXXXXXXXXXXXX--X..
 . .. .. .. .. .. .
ENDMAP

NAME:    hangedman_abyss_batty_box
MONS:    bat / fire bat / vampire bat, butterfly, wyvern
MONS:    spatial vortex, wind drake, harpy, unseen horror
KMONS:   8 = wind drake w:9 / harpy w:1
KITEM:   8 = acquire any / any good_item / stone q:1 w:5
NSUBST:  A = 2:2 / 1:2 / 2 = 1122., B = 1:3 / 1:4 / 1:1334. / 1:12334.
NSUBST:  C = 1:556 / 1:6 / 1:7 / 1:66777. / 1:1234567.
SUBST:   x = x:25 c:14 v:6 b:4 G:1 n:2, y = c:3 v:1 b:1, z = x:2 c:1 v:1 b:1 .:1
MAP
 xxxxxxxxxx.
xx x x x x.x
x x0x x.x.x
xx0x.x1x.x.x
x x.x1x.x.x.
xx x1++xyyyy
x x.x+xAzCzy
xx1x.xAAABCy
x x.xyzACBzy
xx.x.yCBB8zy
x.x.xyzCzzzy
.x x.yyyyyyy
ENDMAP

NAME:    hangedman_glass_garden
TAGS:    no_monster_gen no_pool_fixup
DEPTH:   D, Depths, Abyss
MONS:    butterfly, plant, unseen horror, shadow wraith, wind drake
SHUFFLE: 34GT, GTt
: if you.in_branch("Abyss") then
TAGS:    no_exits
SHUFFLE: 345", ABCD / ACBD / ADCB
SUBST:   + = M, ' = x:5 c:2 v:1 b:1, " = ., % = %%***|
SUBST:   AB : wwl, CD : wll
NSUBST:  M : 1:+ / 2 = mmm+ / * = m:19 +:1
: else
TAGS:    extra transparent
WEIGHT:  1
SUBST:   M = m, 34'" = ., ABCD = W
: end
MAP
   ''' '''
  'MmM+MmM'
 'MmAA.BBmM'
'MmA..2..BmM'
'mA.2.4.2.Bm'
'MA..1G1..BM'
 +.2T3%3T2.+
'MC..1G1..DM'
'mC.2.4.2.Dm'
'MmC..2..DmM'
 'MmCC.DDmM'
  'MmM+MmM'
   ''' '''
ENDMAP

NAME:    hangedman_shadowy_showcase
TAGS:    transparent no_monster_gen
MONS:    shadow wraith / unseen horror / thrashing horror, lurking horror
MONS:    shapeshifter / ugly thing / large abomination w:2, wretched star
MONS:    large abomination / ghoul / death ooze w:2, ancient zyme
MONS:    large slime creature w:12 / very large slime creature w:8 / rakshasa w:2
KMONS:   8 = starcursed mass
KMONS:   9 = floating eye / chaos spawn / anaconda perm_ench:shapeshifter w:2
KMONS:   0 = tentacled starspawn
KMONS:   A = patrolling shadow demon
KMONS:   B = boggart
SHUFFLE: 12 / 34 / 56 / 78 / 90, A' / A' / A' / A' / 'B
SUBST:   x = x:5 c:4 b:2 v:1, X = x:2 c:4 b:3 v:1, ' = ..lw
SUBST:   . = .:199 W:1, * = .$%%***|, d = %$
MAP
   ...xxxxxxx
  @.. xxxxxxx
 @. .xx ..2xx
.. ..xx.x1.xx
......x..x.xxx
. xx.d.... xxxx
xxxxx.X.XXXXXxxxxxx
xx ......X'AXxxxxxx
xx1x..X..**'X 3.3xx
xx..x.XX*X*XX.x..xx
xx1.1 X'**..X..x3xx
xxxxxxXA'X...... xx
xxxxxxXXXXX.X.xxxxx
    xxxx ....d.xx.
     xxx.x..x... ..
      xx.3x.xx.....
      xx4.. xx . @
      xxxxxxx...@
      xxxxxxx ..
ENDMAP

NAME:    hangedman_relentless_pull
TAGS:    no_monster_gen no_item_gen
MONS:    ugly thing / large abomination / ynoxinul / unseen horror / smoke demon
MONS:    tentacled starspawn / starcursed mass
MONS:    very ugly thing, wretched star, lurking horror, ancient zyme
SHUFFLE: 2d, 34eAB / 34eAB / '''CD, 34 / 56
SUBST:   de = %**, AB = x, C = ., D = @, x = xxccb
MAP
 xxxxx         xxxxx
xx%1xx@       Bxx3exx
 x....xx.   .xxA43ex
xx.xxxxxxx xxxxxxx.xx
 x.xd. .2x x3. .dx.x
xx.xx.x.xxxxx.x.xx.xx
 x..xx.x.xxx.x.xx..x
xx..x.. xxxxx ..x..xx
 x..x .x. x .x. x..x
xx .xx..x.x.x..xx. xx
xxx..x....%....x..xxx
 xx .%.xx.1.xx.%. xx
  xx .xxxxxxxxx. xx
   xxxx x x x xxxx
ENDMAP

NAME:    hangedman_steaming_rock
TAGS:    transparent
MONS:    eye of devastation w:9 / nothing w:1
KMONS:   2 = ynoxinul / smoke demon / thrashing horror / unseen horror / \
             ugly thing w:5/ rakshasa w:5 / chaos spawn w:5
KMONS:   3 = smoke demon / green death / catoblepas w:8 / ophan w:7 / \
             fire crab w:5
KMONS:   4 = apocalypse crab / death drake w:5
KITEM:   2 = % / $, % / *
KITEM:   34 = %, * / |
KFEAT:   ABCD = m
SUBST:   c = cccbbv
SHUFFLE: ABCD
MARKER:  A = lua:fog_machine {cloud_type = "flame", \
             pow_min = 2, pow_max = 3, delay = 85, size_min = 2, size_max = 4, \
             walk_dist = 4, start_clouds = 1, excl_rad = 0 }
MARKER:  B = lua:fog_machine {cloud_type = "freezing vapour", \
             pow_min = 2, pow_max = 3, delay = 75, size_min = 2, size_max = 4, \
             walk_dist = 4, start_clouds = 1, excl_rad = 0 }
MARKER:  C = lua:fog_machine {cloud_type = "rain", \
             pow_min = 3, pow_max = 4, delay = 30, size_min = 3, size_max = 4, \
             walk_dist = 4, start_clouds = 1, excl_rad = 0 }
MARKER:  D = lua:fog_machine {cloud_type = "calcifying dust", \
             pow_min = 2, pow_max = 3, delay = 75, size_min = 2, size_max = 3, \
             walk_dist = 4, start_clouds = 1, excl_rad = 0 }
TILE:    ABCD = dngn_transparent_wall_blue
COLOUR:  ABCD = lightblue
MAP
   ....
  .cccc.
 .mmmccc..
.cmmAcmccc.
.cmAcmmmmcc.
.cccmm   mcc.
 .1cm 322 mc.
 ..cm 422 mc.
.cccmm   mcc.
.cmDcmmmmcc.
.cmmDcmccc.
 .mmmccc..
  .cccc..
   ....
ENDMAP

NAME:    hangedman_classic_abyss_death
TAGS:    transparent no_monster_gen
MONS:    large abomination / ynoxinul / unseen horror / \
         skeleton / sixfirhy / smoke demon / neqoxec / \
         efreet / rakshasa / glowing orange brain / ugly thing / \
         golden eye / hell hog
MONS:    lich / hellephant / tentacled monstrosity / profane servitor / \
         death drake / daeva / angel / bone dragon / balrug w:4 / \
         shadow demon w:4 / executioner w:4 / hellion w:4
SHUFFLE: lw / ll / ww / ''
NSUBST:  2 : 2:2 / *:1
SUBST:   c = ccvbb, x = xxxc, ' = .
MAP
         c
        cc
       cc.
      ccx.@
     cc2x..x
    cc%xx wxx
   cc*11x% w.@
  cc2x1x%x% ...cc
 ccxxxx%.%xxxxcc
cc... %x%x1x2cc
   @.l %x11*cc
    xxl xx%cc
     x..x2cc
      @.xcc
       .cc
       cc
       c
ENDMAP

NAME:    hangedman_illusive_loot
TAGS:    patrolling
WEIGHT:  5
MONS:    rakshasa, guardian serpent / great orb of eyes
ITEM:    potion of might / potion of haste / potion of heal wounds w:12 / \
         wand of lightning / wand of acid / wand of confusion / \
         ring mail good_item / chain mail good_item w:12 / shield / \
         spear good_item / trident good_item / any weapon w:12 / any w:2
SUBST:   x = ccccvbb
MAP
     .x.x
   x.xxx.x
  x.xx1xx.x
 x. x.. x .x
 .xxx.ddxxx.
.xx..dddd xx.
xx1.ddd...1xx
.xx dd..x.xx.
x.xxxd.x+xx..
 x. x ..x2. x
  x.xx1xx.x.
   x.xxx. ..
     .x..x
ENDMAP

NAME:   hangedman_abyss_or_decor_stabs
TAGS:   transparent no_pool_fixup decor
DEPTH:  D, Depths, Abyss
: if you.in_branch("Abyss") then
SUBST:  xc = xxxxcccb, ' = %0, " = wl...
: else
TAGS:   extra
SUBST:  '" = ., c : xxc
: end
MAP
...."
.cxx.
.x'..."
.x.cxx.
"..x'..."
  .x.cxx.
  "..x'..."
    .x.cxx.
    "..x'..
      .x.
      "..
ENDMAP

NAME:   hangedman_abyss_or_decor_jellyfish_corner
TAGS:   transparent decor
DEPTH:  D:4-, Depths, Abyss
WEIGHT: 5 (D), 10
: if you.in_branch("Abyss") then
SUBST:  x : XXCB, c : CB, X = xxxxcb, C = xccccb, B = xcbbbb
SUBST:  HIJ = cbw,  ' = 000., " = %%%.
: else
TAGS:   extra
SUBST:  '" = ., c : xxc, H : xc.., I : xc.., J : xc..
: end
MAP
       .c
      .xc
     .xcc
    @x..c......
   @x..'ccxx...
  .x..H"c".xxx.
 .x..H'"c'.....
.xc.'"""c.I...
ccccccccc...J.
   .c"'..x....
   .x..I..x...
   .xx.....xx.
   ..x..J..xx.
   ..x........
   ....
ENDMAP

NAME:    hangedman_abyss_or_decor_lesser_grid
TAGS:    transparent no_pool_fixup decor
DEPTH:   D, Depths, Abyss
: if you.in_branch("Abyss") then
SHUFFLE: ABCDEF, "'
SUBST:   A : cbx.., B : cbx., C : cbx.., D : cbx., E : cbx.., F : cbx.
SUBST:   " = 0., ' = %*..., X = wl.
: else
TAGS:    extra
SUBST:   '" = ., X : x.
SUBST:   A : xxc., B : xxc., C : xxc., D : xxc., E : xxc., F : xxc.
: end
MAP
.......
.AB....
.BA.X..
.'".......
.CD"EF"xx.
.DC'FE'xx.
.......'"....
   ..X.FE....
   ....EF.X..
   ....'"....
      .DC"BA.
      .CD'AB.
      .......
ENDMAP

NAME:    hangedman_abyss_or_decor_clamps
TAGS:    transparent no_pool_fixup decor
DEPTH:   D:8-, Depths, Abyss
KFEAT:   @ = +
SHUFFLE: ABCDEFGH, '"
: if you.in_branch("Abyss") then
SUBST:   b = wlb., AB = wl., CD = xcb., EFGH = .
SUBST:   x = xxxxcccb, ' = 00., " = %%*.
: else
TAGS:    extra
SUBST:   b : bbwww., A : @, B : @xx, C : @xxxx, DEFGH = x, '" = .
: end
MAP
    xxAxx
    x...x
    B.'.C
    x...x
xxDxx...xxExx
x....b".....x
F.'.."b"..'.G
x......b....x
xxHxxx+xbx+xxxHxx
    x....b......x
    G.'.."b"..'.F
    x....."b....x
    xxExx...xxDxx
        x...x
        C.'.B
        x...x
        xxAxx
ENDMAP

NAME:    hangedman_abyss_or_decor_passed
TAGS:    transparent no_pool_fixup decor
DEPTH:   D:4-, Depths, Abyss
: if you.in_branch("Abyss") then
SUBST:   ' = 00., " = %%*..
SUBST:   x = xxxxcb, XY = ww...
: else
TAGS:    extra
SUBST:   x : xxxc, Y = x, X : x..
NSUBST:  ; = 1:@ / *:., : = 1:@ / *:., ` = 1:@ / *:.
: end
MAP
     x
 ;;;;x::::
 ;YX.x.XY:
 ;X..x..X:
 ;.......:
xxxx.x.xxxx
 `.......'x
 `X..x.X'"x
 `YX.x.'Y"x
 ````x'""xx
     xxxxx
ENDMAP

NAME:    hangedman_abyss_or_decor_stagger
TAGS:    transparent no_pool_fixup decor
DEPTH:   D:4-, Depths, Abyss
: if you.in_branch("Abyss") then
SUBST:   ' = 00., " = %%., X = xxcccb, x = xxxxxcccb
SUBST:   yY = ...xxxcbw, zZ = ...xxxcbl
: else
TAGS:    extra
SHUFFLE: yY, zZ
SUBST:   x : xxxxc, X : xxc, y : x.., z : x.., YZ'" = .
: end
MAP
..........
.xxxXxxx..
...'X"..z...
..."X'..Z...
.xxxXXXxxx..
..Y.."X'..z...
..y..'X"..Z...
 ..xxxXXXxxx..
 ...Y..'X"..z..
 ...y.."X'..Z..
   ..xxxXXXxxx.
   ...Y.."X'...
   ...y..'X"...
     ..xxxXxxx.
     ..........
ENDMAP

NAME:    hangedman_abyss_or_decor_slice
TAGS:    transparent no_pool_fixup decor
DEPTH:   D:4-, Depths, Abyss
WEIGHT:  5 (D), 10
: if you.in_branch("Abyss") then
SUBST:   ' = 00., " = %%., x = xxxxxcccb, cX = xxccb
: else
TAGS:    extra
SUBST:   x : xxxxc, c : xxc., X : xxc, '" = .
: end
MAP
        ..c
       ..X.
      ..c..
.......c'.
x"x'x"c.xxxx
xxxx.c"x'x"x
  .'c.......
 ..c..
 .X..
 c..
ENDMAP

NAME:    hangedman_abyss_town
TAGS:    no_monster_gen patrolling allow_dup
WEIGHT:  5
MONS:    rakshasa / efreet / smoke demon / wizard / base draconian w:5 / \
         guardian serpent w:1 / vampire w:3 / necromancer w:3
MONS:    thrashing horror / blink frog / ugly thing / \
         large abomination / unseen horror / shapeshifter / \
         wretched star w:5 / air elemental / raiju / worldbinder
MONS:    draconian shifter w:5 / shadow demon w:5 / lich / \
         profane servitor w:5 / lorocyproca / deep elf demonologist / \
         deep elf sorcerer / death knight
MONS:    bone dragon / tentacled starspawn / starcursed mass / \
         apocalypse crab / dancing weapon / tentacled monstrosity / \
         glowing shapeshifter
ITEM:    superb_item, royal jelly q:2
ITEM:    bolt good_item q:40 / arrow good_item q:40, any magical staff
ITEM:    scroll of magic mapping q:4 / scroll of random uselessness q:4
ITEM:    ring mail randart / scale mail randart / chain mail randart w:20
ITEM:    demon blade ego:chaos good_item ident:type / \
         demon trident ego:chaos good_item ident:type / \
         demon whip ego:chaos good_item ident:type
KITEM:   k = acquire misc, acquire misc
SHUFFLE: defghijk
NSUBST:  0 : 2:0 / 1 = VY / *:.
SUBST:   c = c:7 b:2 v:1, x = x:8 c:1 b:1
MAP
  ccccccc..
 ccccccc ...ccccccc
 ccd...cc... ccccccc
 cc.2c+cc...cc...ecc
 cc.cc1 cc..cc+c2.cc
 cc.+12.+..cc 1cc.cc
 cccc .cc...+.21+.cc
 c ccc+c....cc. cccc
 ....c.. xx .c+ccc c
........x00x...c....
....c...x00x........
c ccc+c. xx ..c....
cccc..cc....c+ccc c
cc.+12.+...cc..cccc
cc.cc1.cc..+.34+.cc
cc.2c+cc..cc.4cc1cc
ccf...cc...cc+c1%cc
ccccccc ...cc.1%gcc
 ccccccc... ccccccc
         ..ccccccc
ENDMAP

NAME:    hangedman_abyss_stitches_cave
TAGS:    transparent no_monster_gen
MONS:    thrashing horror / unseen horror / raiju / \
         shapeshifter w:5 / large abomination / common demon
MONS:    starcursed mass / apocalypse crab / hellephant / \
         tentacled monstrosity / tentacled starspawn w:5
SHUFFLE: yY / yY / Yy
SUBST:   x : X:9 C:1, y : W:25 L:25 X:35 C:10 M:5, Y = ., z : X:20 CMB
SUBST:   X = x:50 cb, C = c:50 bv, B = b:50 cv, M = m:50 cb, W = w:50 l
SUBST:   L = l:50 w, $ = $%., % = %:7 .:3, * = *:7 %:3
MAP
            xxxxxxxxxxx
            x%1.zxz...x
           xx.y.zzz.y.xxx
         xxx..Y..Y..Y....
         x%..zzz.y.zzz.Z.
         x2Yyzxz$1.zxz...
         x*1.zzxxxxxxx
   xxxxxxxzz.1*x
...zxz.1$zxzYZ2x
.Z.zzz.Y.zzz..%x
....Y..y..y..xxx
xxx.y.zzz.Y.xx
  x.1.zxz.1%x
  xxxxxxxxxxx
ENDMAP

NAME:    hangedman_abyss_river_spirits
TAGS:    no_monster_gen
MONS:    smoke demon, efreet, fire elemental, water elemental
SHUFFLE: ABCDEF / ABCDEF / ABCDEF / ACEBDF / ACEBDF / GGGGGG
SHUFFLE: 1234 / 1234 / 1234 / 1212 / 1212 / 1212 / 3434 / 3434 / 3434
SUBST:   ACE = w, BDF = l, G : wl, x = x:50 cb
MAP
%2xxAAxxxx
1.xx..xx2x
xx..xx.%xxBBxxxx
xxDDxx%.xx..xx3x
    x1xx..xx.%xxCCxx
    xxxxEExx%.xx..xx
          x4xx..xx.3
          xxxxFFxx4%
ENDMAP

NAME:   bh_abyss_negative
TAGS:   allow_dup decor
MAP
....      ....
.....     .....
.....     .....
.....     .....
 .....   .....
    .......
     .....
     .....
     .....
    .......
 .....   .....
.....     .....
.....     .....
.....     .....
 ....      ....
ENDMAP

###############################################################################
# Abyss rune vaults
#
# Follow the lead of the other vaults here, and recolour / retile some border
# floor to instantly distinguish the vault as containing the abyssal rune.
# Solitary stone arches are also to be used here and nowhere else in Abyss
# vaults.
###############################################################################
# Non-combined rune vaults (see the combined rune vault header).

NAME:   evilmike_abyss_rune_ettins
TAGS:   abyss_rune unrand
MONS:   ettin ; giant spiked club ego:distortion | giant club ego:distortion . \
                giant spiked club ego:chaos | giant club ego:chaos
MONS:   ettin skeleton w:6 / flayed ghost / shadow wraith w:4
MONS:   place:Abyss:$, silent spectre
KMONS:  O = floating eye
KITEM:  O = abyssal rune of Zot
KFEAT:  A = stone_arch
SUBST:  x = xxccn, . = ...W
NSUBST: 0 = 4:1 / 2:2 / *:3
FTILE:  "AO = floor_crystal_squares
COLOUR: "O = lightmagenta
MAP
    """"""""
 ""..xxxAxx..
 "xxxx.xx1xx."
 .x0x.x0xx.xx"
".xx.xxx.xx.x"
"xx.xx3x.x0xx"
"x.xx1O2xx.xx"
"xx.xx4xx.xxx"
".xx.xxxxx0xx"
 .x0x.x0x.xx."
 "xxxx.x.xx..
 ""..xxxxx..
    """""""
ENDMAP

NAME:   evilmike_abyss_rune_tso_outpost
TAGS:   abyss_rune unrand no_monster_gen
MONS:   angel, ophan, cherub, patrolling juggernaut
MONS:   dancing weapon ; long sword ego:holy_wrath
KITEM:  O = abyssal rune of Zot
KFEAT:  A = stone_arch
KFEAT:  _ = altar_the_shining_one
FTILE:  "A45OG = floor_crystal_squares
COLOUR: "45OG = lightmagenta
MAP
.......................
....."""ccc"5"cccccc...
.cccccGcc2ccGcc...3cc..
.+...ccc...ccc....cccc.
A+.T15+"._."+"....+4Oc.
.+...ccc...ccc....cccc.
.cccccGcc2ccGcc...3cc..
....."""ccc"5"cccccc...
.......................
ENDMAP

NAME:    hangedman_abyss_rune_flesh_and_stone
TAGS:    abyss_rune unrand no_monster_gen no_item_gen no_pool_fixup
WEIGHT:  5
MONS:    catoblepas, earth elemental, basilisk, gargoyle
MONS:    catoblepas skeleton, air elemental, death drake
ITEM:    potion of flight q:1 ident:type
KMONS:   O = patrolling war gargoyle
KITEM:   O = abyssal rune of Zot
KFEAT:   A = stone_arch
SHUFFLE: 23 / 57
SUBST:   ! = 2, 0 = 222234555
KPROP:   l = no_cloud_gen
FTILE:   "A@6O = floor_crystal_squares
COLOUR:  "@6O = lightmagenta
MAP
    @d0A0d@
6ccccGclcGcccc6
 .cOccclccccc.
  c+c0"l"0ccc
"cc1ccclcccccc"
 .c4c0"l"0ccc.
  c5c clc ccc
"cc.c0"l"0cccc"
 .c.ccclccccc.
  c.c0"l"0ccc
"cc.ccclcccdcc"
 .c+c2c!c5c+c.
  c 3 c1c 7 c
6cc2 2+ +5 5cc6
 .ccccccccccc.
  .c c. .c c.
ENDMAP

# This vault would really appreciate "warning door" and "locked door"
# being separated from their forced union as runed doors...
NAME:   hangedman_abyss_rune_macabre_mess
TAGS:   abyss_rune unrand no_monster_gen
KMONS:  1 = glowing orange brain
KMONS:  2 = golden eye / eye of draining w:4 / floating eye w:2
KMONS:  3O = flying skull
KMONS:  4 = large abomination / small abomination w:6 / macabre mass w:2
KMONS:  5 = cacodemon
KMONS:  6 = executioner
KMONS:  7 = shining eye
KMONS:  8 = titanic slime creature
KITEM:  O = abyssal rune of Zot
KFEAT:  A = stone_arch
KFEAT:  n = iron_grate
KFEAT:  1237 = shallow_water w:1 / . w:14
SUBST:  c = ccccvb
FTILE:  "A+@W48O = floor_crystal_squares
COLOUR: "48 = lightmagenta
MAP
 "cccccc4@4cccccc"
 cc .. ccAcc .. cc
"c 44"" x5x ""44 c"
cc44nnn.c+c.nnn44cc
c 4nn7n.....n7nn4 c
c44+13n5.c.5n31+44c
c 4nn2n.....n2nn4 c
cc"4nnn.....nnn4"cc
"c ......6...... c"
 ccc "4nnnnn4" ccc
  "cc84n2O2n48cc"
   "c 4nn1nn4 c"
    cc44n+n44cc
    "c 44444 c"
     ccc 4 ccc
      "ccccc"
ENDMAP

# This theme is even more disparate than the literal vault combinations...
NAME:   grunt_abyss_rune_wretched_hive
TAGS:   abyss_rune unrand patrolling
MONS:   killer bee, meliai, spark wasp
MONS:   wretched star, green very ugly thing
KMONS:  O = queen bee
ITEM:   royal jelly q:1
KITEM:  O = abyssal rune of Zot
KFEAT:  a = x
KFEAT:  A = stone_arch
NSUBST: 1 = 3:5 / 1:15 / 3:2 / 1:1 / 10 = 11. / * = .
NSUBST: ' = 1:d / * = d:1 .:199
TILE:   a = wall_wax
FTILE:  "A4O = floor_crystal_squares
COLOUR: a = yellow
COLOUR: "4O = lightmagenta
MAP
          xxxxxxx
        xxxaaaaaxxxx
      xxxxaaaOaaaaxxx
    xxxaaaad1a1daaaxxx
   xxaa1aa21'1'12aa1axxx
  xxaa'a'."a"."a".'a'aaxx
 xxaa4aaa1aaa4aaa1aaa4aaxx
xxaa..'a'."a"."a".'a'.'aaxx
xaa.'a'.'a'.'a'.'a'.'a'.aax
xaa1aaa1aaa1aaa1aaa1aaa1aax
xa..'a'.'a'.'a'.'a'.'a'..ax
aa.a'."a".'a'.'a'."a".'a.aa
..aaa1aaa1aaa1aaa1aaa1aaa..
 ..a.."a".'a'.'a'."a"..a..
   .....................
        .....A......
           ..@..
ENDMAP

NAME:    nicolae_abyss_rune_star_stuff
TAGS:    abyss_rune unrand no_monster_gen
WEIGHT:  25
KMONS:   1 = starcursed mass
KMONS:   2 = wretched star
KMONS:   3 = tentacled starspawn
KMONS:   4 = place:Abyss:$
KITEM:   O = abyssal rune of zot
KFEAT:   A = stone_arch
# Place an arch before the door, and place the rune on one of
# the three most distant points from the entrance.
SHUFFLE: Aa'Bb"Dd-Ee_Ff~ / Bb"Dd-Ee_Ff~Aa' / Dd-Ee_Ff~Aa'Bb" / \
         Ee_Ff~Aa'Bb"Dd- / Ff~Aa'Bb"Dd-Ee_
NSUBST:  FAB : 1:O / *:"
NSUBST:  s = 2:1 / 2:2 / 2:3
SUBST:   a = +, ' = A, DE = ., bdef = c
SUBST:   c = cccxbv
CLEAR:   "-_~
FTILE:   1234O = floor_crystal_squares
COLOUR:  1234O = lightmagenta
MAP
     4ccc4
    cccBccc
  _ec..s..cf~
4ccc...c...ccc4
cc....c.c....cc
cA...........Dc
c..sc.....cs..c
cc..c..s..c..cc
-d...........a'
 c...c...c...c
 cc..cc.cc..cc
 4c.s.....s.c4
  cF.......Ec
  cccccbccccc
       "
ENDMAP

NAME:    nicolae_abyss_rune_left_handed
TAGS:    abyss_rune unrand no_monster_gen
WEIGHT:  25
MONS:    ancient zyme, wretched star, starcursed mass, lurking horror
MONS:    thrashing horror, tentacled starspawn, apocalypse crab
# Worldbinders summon from other branches....
KMONS:   8 = nagaraja / ghost moth / thorn hunter / merfolk javelineer
KMONS:   9 = guardian serpent / entropy weaver / swamp dragon / satyr
KMONS:   0 = place:Abyss:$
KMONS:   O = worldbinder
KITEM:   O = abyssal rune of zot
KFEAT:   A = stone_arch
NSUBST:  0 = 1:0 / 1:9 / * = 09
NSUBST:  X = 1:1 / 1:2 / 1:3 / * = 123"", Q = 1:6 / 1:7 / * = 67...
SHUFFLE: 12, 45
FTILE:   "A123O = floor_crystal_squares
COLOUR:  "123O = lightmagenta
MAP
    ...
   ..c....
  ..cc0.c.
 ..ccc"......
..cccccccccc..
..0ccc.4cc.cc..
..cc9+cc+...nc.
."c.5.cc..X..c.
.ccc+ccc.XcX.c.
.....Q.c..X8.c.
..Q.A..cn...nc.
.....Q..ccOcc..
.ccccc..8ccc".0
.ccc"ccccc"cc..
.ccc...0....cc.
............c0.
           .cc.
           ....
ENDMAP

########################################
# Combined abyssal rune vaults, because we were collecting a lot
# and standardizing difficulty was easier with consolidation.
NAME:   dshaligram_kb_abyss_rune_zappy_water_cross
TAGS:   abyss_rune unrand patrolling no_pool_fixup
MONS:   generate_awake lightning spire, raiju, smoke demon, blizzard demon
KMONS:  O = merfolk impaler
KITEM:  O = abyssal rune of Zot
KFEAT:  O = shallow_water
KFEAT:  A = stone_arch
FTILE:  A1234OwW = floor_crystal_squares
COLOUR: 1 = lightmagenta
MAP
www1w1www
www2w4www
123WOW321 A
www4w2www
www1w1www
ENDMAP

NAME:   due_evilmike_abyss_rune_seething_liches
TAGS:   abyss_rune unrand no_monster_gen
MONS:   fire bat, chaos spawn, fire dragon / ice dragon, ogre mage
KMONS:  OL = lich / ancient lich w:1
KITEM:  O = abyssal rune of Zot
KFEAT:  A = stone_arch
NSUBST: L = 1:O / *:L
MARKER: ! = lua:fog_machine { cloud_type = "seething chaos", \
            pow_min = 8, pow_max = 12, delay_min = 20, delay_max = 30, \
            size = 1, walk_dist = 1, spread_rate = 16 }
SUBST:  ! = V, X = x', x = xc, C : c.
FTILE:  '@A+V1234LOx = floor_crystal_squares
COLOUR: '@12LO = lightmagenta
MAP
  cccccc X
 ccLccLcc
cccc+c+cccX
cLccc1.ccc X
cc+c2.1+LcX X
ccc1.2.ccc X X
cL+.1.++Cxx X X
cccc+c++.4xx X X
 cccLcC.c.3xx X X
X ccccx4.1.2xx X
  X X xx3.1.2xx X
   X X xx2.!..xxx
    X X xx2....xx
     X X xx....+'
      X X xx...+'
       X X xx++x@
        X X x''@A
ENDMAP

NAME:  evilmike_abyss_rune_siren_acolytes
TAGS:  abyss_rune unrand patrolling no_pool_fixup
# They really should have bend space instead of blink / other.
MONS:   wizard hd:16 name:mad_acolyte_of_Lugonu n_rpl n_des col:lightgreen \
        tile:mons_hell_wizard \
        spells:smiting.20.priest;malign_gateway.20.priest;\
               blink.10.priest;blink_other.10.priest;\
               invisibility.10.priest ; \
        robe good_item . quarterstaff ego:distortion | dagger ego:distortion
MONS:   generate_awake merfolk avatar, blink frog
MONS:   tentacled monstrosity / tentacled starspawn / thrashing horror w:6
KMONS:  O = starcursed mass
KITEM:  O = abyssal rune of Zot
KFEAT:  _ = altar_lugonu
KFEAT:  A = stone_arch
SUBST:  X = c', c = ccv, N : nb.
KPROP:  l = no_cloud_gen
FTILE:  @A1234O+ = floor_crystal_squares
COLOUR: @1234O = lightmagenta
MAP
 XbbbXXXXXXXXXX@
XXb1bbbbbcccccc+
XXbblblblccwc..c
XXbb+......3...c
Xbb4n.N...llb.cc
XbOnn.1_.3l2l3wcA
Xbb4n.N...llb.cc
XXbb+......3...c
XXbblblblccwc..c
XXb1bbbbbcccccc+
 XbbbXXXXXXXXXX@
ENDMAP

NAME:    evilmike_abyss_rune_elephants_shrine
TAGS:    abyss_rune unrand
KMONS:   1 = elephant
KMONS:   23 = dire elephant
KMONS:   45 = hellephant
KMONS:   67 = generate_awake spatial vortex
KMONS:   89 = draconian shifter
KMONS:   _ = patrolling spatial maelstrom
ITEM:    potion of flight q:1 ident:type
KITEM:   _ = abyssal rune of Zot
KFEAT:   _ = altar_lugonu
KFEAT:   A = stone_arch
# Two rings of opposite sides on the inside / outside of the octagon lava moat,
# with one to three (mostly two) pairs of opposite sides made floor or wall.
SHUFFLE: HIJKL, MNOPQ
SUBST:   HIMN = l, J : ll. KL = ., O : llP, PQ : xxxxb, c : ccv
NSUBST:  ' = 1:4 / 1:8 / 1 = 22248 / 10 = 2216 / *:., d = 1:dddd. / *:.
MARKER:  F = lua:fog_machine { cloud_type = "translocational energy", \
             pow_max = 2, delay_min = 10, delay_max = 20, walk_dist = 0, size = 1 }
FTILE:   "AF3579_OG+ = floor_crystal_squares
COLOUR:  "F3579 = lightmagenta
MAP
  l".HIIIIIIIIJ."l
 l".HllllllllllJ."l
l"dHlMNNNNNNNNOlJd"l
".HlMMllllKlllOOlJ."
.HlMMlJ''''''HlOOlJ.
HlMMlJ''''G'''HlOOlJ
KlPlJ'FccccccF'HlPlK
KlPl''ccG"79cc''lPlK
KlPII''+"5_7cc'IIPlKA
KlPl''ccG37"cc''lPlK
KlPlH'FccccccF'JlPlK
JlOOlH''''G'''JlMMlH
.JlOOlH''''''JlMMlH.
".JlOOllllKlllMMlH."
l"dJlONNNNNNNNMlHd"l
 l".JllllllllllH."l
  l".JIIIIIIIIH."l
ENDMAP

NAME:   evilmike_nicolae_abyss_rune_containment_curve
TAGS:   abyss_rune unrand patrolling no_monster_gen no_item_gen
MONS:   neqoxec w:4 / raiju / worldbinder / thrashing horror / shapeshifter
MONS:   tormentor / hellion / shadow demon / \
        bone dragon / iron dragon zombie w:4
KMONS:  3O = spatial maelstrom
KITEM:  O = abyssal rune of Zot
KFEAT:  A = stone_arch
SUBST:  X : b", x = xv, n = nnnnnc, m = n
FTILE:  "A123$Onc = floor_crystal_squares
COLOUR: "123$O = lightmagenta
MAP
""xxxxxxxxxxxxxxxx
.....1xxnnnnnnnnnxxx
........1nnnnnnnnxxxxx
....X......xxnnnnnxxxx
.........2..nnnn33xxxx
A.....X...x.2$mm3Oxxxx
.........2..nnnn33xxxx
....X......xxnnnnnxxxx
........1nnnnnnnnxxxxx
.....1xxnnnnnnnnnxxx
""xxxxxxxxxxxxxxxx
ENDMAP

NAME:    hangedman_grunt_abyss_rune_horror_hoard
TAGS:    abyss_rune unrand no_monster_gen no_item_gen patrolling transparent
WEIGHT:  5
KMONS:   1 = great orb of eyes perm_ench:shapeshifter
KMONS:   2 = guardian serpent perm_ench:shapeshifter
KMONS:   3 = crystal guardian
KMONS:   4 = unseen horror
KMONS:   5 = thrashing horror
KMONS:   6 = lurking horror
# Note: if anything in these two item sets becomes useful for more then a tiny
# fraction of characters finding the abyssal rune, remove or replace them.
KITEM:   4d = scroll of random uselessness w:15 q:1 / fruit q:1 w:4 / \
         club w:6 / stone q:1 / needle q:1 ego:confusion / \
         mundane animal skin / mundane ring mail / gold q:1
KITEM:   5 = wand of confusion / wand of flame / whip not_cursed w:2 / \
         hat not_cursed mundane w:5
KITEM:   6e = abyssal rune of zot mimic
KITEM:   O = abyssal rune of zot
KFEAT:   A = stone_arch
SHUFFLE: 12
SUBST:   X = xxxxc, c = cccvb
FTILE:   "Ade456O = floor_crystal_squares
COLOUR:  "de456O = lightmagenta
MAP
    .....  . X ".@
  ........cXXXXX. X
 ...xmxmxcx c2c.c" .
 ..mxd4dxm1......c..
..xxd5d4dxx.......X"
..md6ddd5d+3.....cX
@.x4d4O4d4+...e..1XXA
..md5ddd6d+3.....cX
..xxd4d5dxx.......X"
 ..mxd4dxm1......c..
 ...xmxmxxx c2c.c" .
  ........XXXXXX. X
    .....  . X ".@
ENDMAP

# Perhaps parts of Elf and Forest were devoured in a twisted experiment?
NAME:    grunt_abyss_rune_forest_and_sorcery
TAGS:    abyss_rune unrand patrolling no_pool_fixup no_monster_gen transparent
MONS:    worldbinder, rakshasa
KMONS:   34 = deep elf sorcerer
KMONS:   56 = deep elf blademaster
KMONS:   78 = tengu reaver
KMONS:   90 = shambling mangrove
KMONS:   AB = thorn hunter
KMONS:   CD = dire elephant
KMONS:   EF = satyr
KMONS:   G = water elemental
KMONS:   H = water nymph
KFEAT:   G = w
KFEAT:   H = W
KFEAT:   a = stone_arch
KITEM:   O = abyssal rune of Zot
# Exclude one of the deep elves / tengu and one of the Forest denizens.
SHUFFLE: 34 / 56 / 78, 90 / AB / CD / EF
SUBST:   T = t., X = ttWw
NSUBST:  . = 1:2 / *:., w = 2:G / *:w, W = 2:H / *:W
RTILE:   x = wall_hall
COLOUR:  x = elven_brick
FTILE:   -+U4680BDFO = floor_hall
FTILE:   .t23579ACE = floor_grass
FTILE:   'a1 = floor_crystal_squares
COLOUR:  '1 = lightmagenta
MAP
    ..'.'.
  ....XXX....
 ..XWwtttwWX..
 .X.Tt.w.tT1X..'..
..WT.WXAXW.xxxxxxx.
1.wt.tt'xxxxcccccx'
.Xt.Xt3'x0--c6--cx.
aXtw...'+-U-+-O4cx1
.Xt.Xt2'x6--c0--cx.
1.wt.tt'xxxxcccccx'
..WT..XCX..xxxxxxx.
 .X.Tt.w.tT1X..'..
 ..XWwtttwWX..
  ....XXX....
    ..'.'.
ENDMAP

NAME:    evilmike_nicolae_abyss_rune_betentacled_crabyss
TAGS:    abyss_rune unrand transparent no_pool_fixup patrolling
KMONS:   1P = apocalypse crab
KMONS:   2 = fire crab
KMONS:   3 = ghost crab
KMONS:   K = kraken
KMONS:   T = tentacled monstrosity
KMONS:   S = shambling mangrove
KITEM:   OP = abyssal rune of Zot
KFEAT:   TS1P = W
KFEAT:   K = w
KFEAT:   A = stone_arch
SHUFFLE: O1 / O1 / P'
SUBST:   D : cw, ' = w
FTILE:   "A23O = floor_crystal_squares
COLOUR:  "23O = lightmagenta
MAP
  .............
  .cc.......cc.
....c"ccccc"c....
.cccccccOccccccc.
.."cc1WWWWW1cc"..
.cccWWDWSWDWWccc.
.."cTWWWWWWWTc"..
.ccccccWWWcccccc.
..."cc"WWW"cc"...
 ..cc""WWW""cc..
 ..c"""WWW"""c..
 ..c"""WwW"""c..
 .cccWWwwwWWccc.
 .c2WWwwKwwWW3c.
 .cccWWwwwWWccc.
 ...""""A""""...
ENDMAP

NAME:   doesnt_abyss_rune_brain_food
TAGS:   abyss_rune unrand patrolling no_pool_fixup
MONS:   neqoxec, glowing orange brain, orange crystal statue, very ugly thing
MONS:   starcursed mass
KITEM:  O = abyssal rune of Zot
COLOUR: n = lightmagenta
NSUBST: - = 4:d / 2=d. / ., . = 3:1 / 1:2 / 1:4 / 1:5 / 2=112 / 2=45..
SUBST:  l : lw
{{
dgn.delayed_decay(_G, 'd', 'ghoul corpse / minotaur corpse / ' ..
                           'troll corpse / ogre corpse')
}}
MAP
  cccnccc
 cc-nOn-cc
 c.--n--.c
cc.-n-n-.cc
c---...---c
nll-...-lln
n3l-...-l3n
nll-...-lln
n---...---n
nn.......nn
 nn.....nn
  nn...nn
ENDMAP

NAME:    gammafunk_abyss_rune_reverse_zot
TAGS:    abyss_rune unrand
MONS:    draconian
MONS:    draconian annihilator / draconian scorcher / draconian shifter \
         / draconian knight / draconian monk / draconian stormcaller w:5
MONS:    wretched star / starcursed mass / apocalypse crab / lurking horror \
         / very ugly thing / quicksilver dragon / shadow dragon
MONS:    balrug / hellion / lorocyproca / tormentor / cacodemon / reaper \
         / blizzard demon / executioner / angel / daeva
MONS:    hellephant / ancient lich / hell sentinel / profane servitor
KITEM:   O = abyssal rune of Zot
MARKER:  P = lua:transp_loc("reverse_zot_entry")
MARKER:  P = lua:transp_dest_loc("reverse_zot_exit")
MARKER:  Q = lua:transp_dest_loc("reverse_zot_entry")
MARKER:  R = lua:transp_loc("reverse_zot_exit")
SHUFFLE: AB, CD
# Make a classed draconian and band in each lung.
NSUBST:  A = 2 / 2=112 / 2=1' / ', C = 2 / 2=112 / 2=1" / "
NSUBST:  B = 3 / 0 / ', D = 3 / 0 / "
SHUFFLE: hi' / jk"
SUBST:   hk = c, i = ', j = -, R = 5
NSUBST:  ' = 003 / 003... / ., " = 003 / 003... / .
NSUBST:  - = 2=4 / 2=34.. / .
COLOUR:  c = lightmagenta
COLOUR:  n@OQ012345. = magenta
FTILE:   nc+@OPQ012345. = floor_rough_magenta
MAP
         .@.
ccccccccc.P.ccccccccc
ccAAAAcccnnncccCCCCcc
cAAAAAAccnnnccCCCCCCc
cAAAAAAc.....cCCCCCCc
ccAAAAA+..Q..+CCCCCcc
cAAAAAAc.....cCCCCCCc
ccAAAAccccnccccCCCCCc
cccBccccccOccccccDccc
ccBBBcc--c+c--ccDDDcc
cBBBBBcc-----ccDDDDDc
cBBBBBcc-----ccDDDDDc
cBBBBBchhh-jjjcDDDDDc
cBBBBB+''cnc""+DDDDDc
ccBBBccc'iRk"cccDDDcc
ccccccccccccccccccccc
ENDMAP

###############################################################################
# Abyss Exit Vaults
# Note: Don't tag your abyss exit with allow_dup unless it's small and
# restrained in its gimmickry: there are more than enough abyss_exit vaults
# for the longest of given trips without having to repeat vault gimmicks
# and block seeing other vault gimmicks.
###############################################################################

NAME: abyss_exit_lava
TAGS: abyss_exit unrand allow_dup
: init_abyss_exit(_G, "O")
MAP
l.l
.O.
l.l
ENDMAP

NAME:  evilmike_abyss_exit_glass
TAGS:  abyss_exit
: init_abyss_exit(_G, "O")
MAP
 mmm@
mm.mm
m.O.m
mm.mm
 mmm
ENDMAP

NAME:  evilmike_abyss_exit_plants
TAGS:  abyss_exit
SUBST: 1 = 112.
SUBST: . = .:100 1
MONS:  demonic plant
MONS:  bush
: init_abyss_exit(_G, "O")
MAP
 .......
.........
..11111..
..12221..
..12O21..
..12221..
..11111..
.........
 .......
ENDMAP

NAME:  evilmike_abyss_exit_1
TAGS:  abyss_exit unrand allow_dup
: init_abyss_exit(_G, "O")
MAP
x+x
+O+
x+x
ENDMAP

NAME:  evilmike_abyss_exit_2
TAGS:  abyss_exit unrand allow_dup
SUBST: x = x:30 c:20 v:20 m:3 b:3
: init_abyss_exit(_G, "O")
MAP
  @ xxxx
 x.xxxxxxx
xx.x.....xx
xx.x.xxx.xx
xx.x.xOx.xx
xx.x.x.x.xx
xx.x...x.xx
xx.xxxxx.xx
xx.......xx
 xxxxxxxxx
  xxxxxxx
ENDMAP

NAME:  evilmike_abyss_exit_3
TAGS:  abyss_exit unrand allow_dup
SUBST: . = ..Wwl
: init_abyss_exit(_G, "O")
MAP
     .
    ...
   .....
  .......
 .........@
.....O.....
 .........
  .......
   .....
    ...
     .
ENDMAP

NAME:  evilmike_abyss_exit_4
TAGS:  abyss_exit unrand allow_dup
ITEM:  stone q:1 no_pickup
: init_abyss_exit(_G, "O")
MAP
    x
    x
  ..x..
  .dxd.
xxxxxxxxx
  .Oxd.
  ..x..
 .  x
@   x
ENDMAP

NAME:   evilmike_abyss_exit_5
TAGS:   abyss_exit unrand allow_dup
ITEM:   stone q:1 no_pickup
NSUBST: d = 1:O / *:d
: init_abyss_exit(_G, "O")
MAP
    x   x
  x x x x x
 xxxxxxxxxxx
  xdxdxdxdx
xxx+x+x+x+xxx
  x.........@
xxx+x+x+x+xxx
  xdxdxdxdx
 xxxxxxxxxxx
  x x x x x
    x   x
ENDMAP

NAME:  evilmike_abyss_exit_6
TAGS:  abyss_exit unrand allow_dup
SUBST: X: .x, x: xxxccvlw, .: ...W
: init_abyss_exit(_G, "O")
MAP
  x x x
 x x x x x
xxxxxxxxx x
xX.XxX.Xxx x
x.x.x.x.x x
@XxX.XxOxx x
xxxxxxxxx x
 x x x x x
  x x x
ENDMAP

NAME:  evilmike_abyss_exit_7
TAGS:  abyss_exit unrand allow_dup
SUBST: x:xxcv
: init_abyss_exit(_G, "O")
MAP
xx.xx
x.x.x
.xOx.
x.x.x
xx.xx
ENDMAP

NAME:  evilmike_abyss_exit_8
TAGS:  abyss_exit unrand allow_dup
SUBST: G : GGT, T : TV
: init_abyss_exit(_G, "O")
MAP
...G...
.G...G.
.......
G..O..G
.......
.G...G.
...G...
ENDMAP

NAME:  evilmike_abyss_exit_9
TAGS:  abyss_exit unrand allow_dup
KMONS: O = tentacled monstrosity / large abomination / nothing w:5
SUBST: x = xxc
: init_abyss_exit(_G, "O")
MAP
.xxx.
x...x
x.O.x
x...x
.xxx.
ENDMAP

NAME:  evilmike_abyss_exit_10
TAGS:  abyss_exit
SUBST: X = x.., x = xxxcv
MONS:  neqoxec
: init_abyss_exit(_G, "O")
MAP
XXXXXXXXXXX
XXXXXXXXXXX
XXxxxxxxxXX
XXx..1..xXX
XXx.xxx.xXX
XXx1.Ox....
XXx.xxx.xXX
XXx..1..xXX
XXxxxxxxxXX
XXXXXXXXXXX
XXXXXXXXXXX
ENDMAP

NAME:  evilmike_abyss_exit_11
TAGS:  abyss_exit unrand allow_dup
SUBST: b = bbx
: init_abyss_exit(_G, "O")
MAP
   b.b
  bb.bb
 bb...bb
bb.....bb
b.......b
....O....
b.......b
bb.....bb
 bb...bb
  bb.bb
   b.b
ENDMAP

# Technically possible for this one to require dig, but pretty unlikely.
NAME:  evilmike_abyss_exit_12
TAGS:  abyss_exit
SUBST: X = x., x = xxx++.
: init_abyss_exit(_G, "O")
MAP
XXXxxxXXX
XxxxxxxxX
XxxxxxxxX
xxxxxxxxx
xxxxOxxx+
xxxxxxxxx
XxxxxxxxX
XxxxxxxxX
XXXxxxXXX
ENDMAP

# Orange crystal and obsidian statue are very rare here...
# I want most characters to be able to actually use this exit
NAME:  evilmike_abyss_exit_13
TAGS:  abyss_exit unrand allow_dup
KMONS: O = w:1 orange crystal statue / w:2 obsidian statue / w: 20 ice statue / w: 15 oklob plant
: init_abyss_exit(_G, "O")
MAP
O
ENDMAP

NAME:  evilmike_abyss_exit_14
TAGS:  abyss_exit unrand allow_dup
SUBST: x = xcv, 0 = 0.
: init_abyss_exit(_G, "O")
MAP
x..x..x
.0.x.0.
...x...
xxxOxxx
...x...
.0.x.0.
x..x..x
ENDMAP

NAME:  evilmike_abyss_exit_15
TAGS:  abyss_exit
KMONS: O = unseen horror / lorocyproca / nothing
MONS:  small abomination / nothing
SUBST: X = x., x=xxc
: init_abyss_exit(_G, "O")
MAP
 X X X
X XXX X
 XXXXX X
XXXXXXX X
xxxxxxxX X
+1+1+OxXX X
xxxxxxxX X
XXXXXXX X
 XXXXX X
X XXX X
 X X X
ENDMAP

NAME:   evilmike_abyss_exit_16
TAGS:   abyss_exit
NSUBST: . = 1:O / *:.
SUBST:  c = cbx, . = .....12
MONS:   slime creature, ooze / jelly
: init_abyss_exit(_G, "O")
MAP
c+c+c+c+c+c
+.+.+.+.+.+
c+c+c+c+c+c
+.+.+.+.+.+
c+c+c+c+c+c
+.+.+.+.+.+
c+c+c+c+c+c
+.+.+.+.+.+
c+c+c+c+c+c
+.+.+.+.+.+
c+c+c+c+c+c
ENDMAP

NAME:  evilmike_abyss_exit_17
TAGS:  abyss_exit unrand allow_dup
SUBST: x = xxc, . = ...W, 0 = 0.
: init_abyss_exit(_G, "O")
MAP
..xx0xx..
...xxx...
x...x...x
xx.V.V.xx
0xx.O.xx0
xx.V.V.xx
x...x...x
...xxx...
..xx0xx..
ENDMAP

NAME:   evilmike_abyss_exit_kraken
TAGS:   abyss_exit
NSUBST: w = 1:m / *:w
KFEAT:  m = w
KMONS:  m = kraken
SUBST:  Z = wY, Y = wW, W = w., w=WWw
: init_abyss_exit(_G, "O")
MAP
..WWWWWWW..
.WYYYYYYYW.
WYZZZZZZZYW
WYZwwwwwZYW
WYZwwwwwZYW
WYZwwOwwZYW
WYZwwwwwZYW
WYZwwwwwZYW
WYZZZZZZZYW
.WYYYYYYYW.
..WWWWWWW..
ENDMAP

# Fog machine parameters are the same as that swamp entry vault; you pretty
# much can't avoid the fog here, so I hope you have rMut.
NAME:   evilmike_abyss_exit_mutagenic
TAGS:   abyss_exit
MARKER: O = lua:fog_machine {                               \
                pow_min = 8, pow_max = 12, delay = 25,      \
                size = 2, walk_dist = 1, spread_rate = 33,  \
                cloud_type = "mutagenic fog"                \
            }
: init_abyss_exit(_G, "O")
MAP
...
.O.
...
ENDMAP

# Make sure to close the doors if you don't like what you see behind them...
NAME:   evilmike_abyss_exit_vortex
TAGS:   abyss_exit
ITEM:   stone q:1 no_pickup
SUBST:  x:xxxcv, 1=1., g:G.
SUBST:  G:Gb.
MONS:   generate_awake spatial vortex
: init_abyss_exit(_G, "O")
MAP
     xxxxx
     x111x
     x1d1x
     x111x
    @x+++x@
xxxxx.....xxxxx
x111+..g..+111x
x1d1+.gGg.+1d1x
x111+..g..+111x
xxxxx.....xxxxx
    @x+++x@
     x111x
     x1O1x
     x111x
     xxxxx
ENDMAP

NAME:  evilmike_abyss_exit_chaos
TAGS:  abyss_exit
MONS:  chaos spawn
SUBST: 1 = 11., n : nc, c = cvb
: init_abyss_exit(_G, "O")
MAP
 ccncc
cc.1.cccccc
c.1.1.c.1.cccc
n1.O..+......+@
c.1.1.c.1.cccc
cc.1.cccccc
 ccncc
ENDMAP

# This one is really, really weird...
NAME:  evilmike_abyss_exit_smiterflies
TAGS:  abyss_exit
KMONS: O = generate_awake statue spells:summon_butterflies.100.magical \
           tile:mons_statue_mage col:lightblue
MONS:  smoke demon, deep elf mage
: init_abyss_exit(_G, "O")
MAP
       ccc
     ccc1ccc
   ccc.....ccc
  cc.........cc
  c....2......c
 cc...........cc
 c.............c
cc..2..........cc
c1......O.......+@
cc..2..........cc
 c.............c
 cc...........cc
  c....2......c
  cc.........cc
   ccc.....ccc
     ccc1ccc
       ccc
ENDMAP

NAME:    hangedman_abyss_exit_choices
TAGS:    abyss_exit unrand no_monster_gen
MONS:    hell hound, raiju, great orb of eyes
MONS:    glowing orange brain, swamp drake, wind drake, torpor snail
KMONS:   8 = tyrant leech
KMONS:   9 = mana viper
KMONS:   0 = anaconda
KMONS:   A = ball lightning
KMONS:   B = air elemental
KMONS:   C = unseen horror
KMONS:   D = shadow wraith
KMONS:   E = elephant
KMONS:   F = catoblepas
SHUFFLE: 12, 56, 90, CD, 12 / 34, 56 / 78, 90 / AB, CD / EF
SUBST:   c = ccv, x = xxc
: init_abyss_exit(_G, "N")
MAP
. . . . . . . . . .
 .xx x x x x x x .
.ccccccccccccccccc.
.c +C+ +9+ +5+ +1+.
.cNcnc.cnc.cnc.cncn
.c +D+ +0+ +6+ +2+.
.ccccccccccccccccc.
 .xx x x x x x x .
. . . . . . . . . .
ENDMAP

###################################################################
# Distorted/chaotic kobolds (abyss exit version!) (Jude)
NAME:   due_exit_kobolds
TAGS:   no_rotate abyss_exit
SUBST:  y  = c.
COLOUR: 1. = random
MARKER: P  = lua:fog_machine { cloud_type="blue smoke", walk_dist=1, \
             size=9, pow_max=20, delay=10, buildup_amnt=14, buildup_time=7, \
             spread_rate=3, start_clouds=1, colour="blue" }
KFEAT:  _ = altar_lugonu
KMONS:  1 = big kobold w:80 ; short sword | rapier | quick blade w:3 /\
            big kobold w:10 ; quick blade ego:distortion w:6 | \
                              rapier ego:distortion | short sword ego:distortion
: init_abyss_exit(_G, "P")
MAP
  ccccc
 cc...cc
cc..P..cc
 cc...cc
 ccc.cccc
ccyy.yyyccc
cy..111.yyc
cc..1_1..cc
cyy.111..yc
cccyy..yycc
  ccc..ccc
    c++c
     @@
ENDMAP

NAME:  guppyfry_abyss_exit_glass_guarded
TAGS:  abyss_exit
: init_abyss_exit(_G, "O")
MAP
.............
.xmxmxmxmxmx.
m.m.......m.m
xm.mmm.mmm.mx
xxxx.....xxxx
x00...O...00x
xxxxxxlxxxxxx
lllllllllllll
mxmxmxmxmxmxx
.............
ENDMAP

NAME:  guppyfry_abyss_exit_spiral_guarded
TAGS:  abyss_exit unrand allow_dup
SUBST: - = llx
: init_abyss_exit(_G, "O")
MAP
.0---------0
.-........--
..--------.-
.--.....--.-
.-.-----.-.-
.-.--O--.-.-
.-.-.---.-.-
.-.--...--.-
.-.-------.-
.--.......--
.0---------0
ENDMAP

NAME: guppyfry_abyss_exit_imp_island
TAGS: abyss_exit
SUBST: - = Wwwww
MONS: crimson imp / white imp
: init_abyss_exit(_G, "O")
MAP
.---------.
---..1..---
--..1.1..--
---..1..---
.---------.
.....O.....
.---------.
---..1..---
--..1.1..--
---..1..---
.---------.
ENDMAP

NAME:  bh_abyss_exit_spiral
TAGS:  abyss_exit unrand allow_dup
: init_abyss_exit(_G, "O")
MAP
 .xxxx.
  .xxxx.
    .xxx.
    .xx.
   ..x..
  ...O...
   ..x..
   .xx.
  .xxx.
   .xxxx.
    .xxxx.
ENDMAP

NAME:  bh_abyss_exit_grid
TAGS:  abyss_exit unrand allow_dup
SUBST: a = x.
SUBST: b = x..
SUBST: c = x...
: init_abyss_exit(_G, "O")
MAP
   b b b b b
  a.a.a.a.a.a
 a.x.x.x.x.x.a
b.x.x.x.x.x.x.b
 a.x.x.x.x.x.a
b.x.x.c.c.x.x.b
 a.x.c.0.c.x.a
b.x.x.c.c.x.x.b
 a.x.x.x.x.x.a
b.x.x.x.x.x.x.b
 a.x.x.x.x.x.a
  a.a.a.a.a.a
   b b b b b
ENDMAP

####
# Take a deep breath...
NAME:  grunt_abyss_exit_deep_breaths
TAGS:  abyss_exit unrand transparent
MONS:  ice statue, fire crab, swamp dragon, catoblepas
: init_abyss_exit(_G, "O")
MAP
   xxxxx
  xxlllxx
 xxll3llxx
xxll.O.llxx
xll..1..llx
xl.2...2.lx
xl...4...lx
xl.......lx
ll.......ll
 ..........
  ...@....
ENDMAP

NAME:  bh_abyss_exit_choice
TAGS:  abyss_exit unrand transparent
DEPTH: Abyss:1-4
KFEAT: <: exit_abyss
KFEAT: >: abyssal_stair
MAP
xxxxxxxxxxx
x00........
x<0......>.
x00........
xxxxxxxxxxx
ENDMAP

NAME:   gammafunk_abyss_exit_x_factor
TAGS:   abyss_exit unrand transparent
MONS:   large abomination / thrashing horror / tentacled starspawn \
        / starcursed mass / tentacled monstrosity
MONS:   orange crystal statue
NSUBST: E = E / -
NSUBST: - = 2=0 / 1 / 2=01.. / .
SUBST:  c : cvb
MARKER: D = lua:transp_loc("x_factor")
MARKER: E = lua:transp_dest_loc("x_factor")
: init_abyss_exit(_G, "O")
MAP
ccccc     ccccc
nn--cc   cc-Ecc
.nn--ccccc--cc
..nn--c-c--cc
@.DnO--2---c
..nn--c-c--cc
.nn--ccccc--cc
nn--cc   cc-Ecc
ccccc     ccccc
ENDMAP

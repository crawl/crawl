################################################################################
# vaults_rooms_ghost.des: Ghost subvaults for the Vaults.
#
# Content: These are the subvaults that place a player ghost. All vaults here
# should call the function ghost_vault_setup(). Until the Vaults layout can be
# adapted to specially place these vaults, we tag them all with vaults_room so
# they they can have a common rate of placement. Set a different WEIGHT to make
# a specific vault more or less common
################################################################################

: require("dlua/ghost.lua")

NAME:   advil_vaults_ghost_guards
TAGS:   vaults_room
KITEM:  12 = superb_item / star_item
KMONS:  1 = vault guard / ironbrand convoker / ironheart preserver / \
            vault warden
KMONS:  2 = vault warden
KMONS:  3 = player ghost
SHUFFLE: 13
: ghost_vault_setup(_G)
MAP
........
........
..nnnn..
..n31n..
..n12n..
..n==n..
........
........
ENDMAP

# based on st_lines
NAME:   advil_vaults_ghost_lines
TAGS:   vaults_room
KITEM:  123 = superb_item / star_item
KMONS:  1 = vault guard / ironbrand convoker / ironheart preserver / \
            vault warden
KMONS:  2 = vault warden
KMONS:  3 = player ghost
SHUFFLE: 1239
SUBST: 9 = 98
: ghost_vault_setup(_G)
MAP
........
nnn==nnn
..=92=..
..=31=..
nnn==nnn
........
ENDMAP

NAME:  gammafunk_vaults_ghost_grave
TAGS:  vaults_room vaults_orient_w
KITEM: O = superb_item / star_item
KMONS: p = withered plant
KMONS: O = player ghost
SUBST: de = |*, t : tp
TILE:  G = dngn_gravestone
TILE:  t = dngn_tree_dead
: set_feature_name("granite_statue", "a gravestone")
FTILE:  -|*%$OGt = floor_pebble_brown / floor_pebble_darkgray
: ghost_vault_setup(_G)
MAP
..x.....
..x.....
..n.dt..
..=.OGt.
..n.et..
..x.....
..x.....
ENDMAP

NAME:  gammafunk_vaults_ghost_split
TAGS:  vaults_room
KMONS: O = player ghost
SUBST:  defg = |*
NSUBST: D = 2=0 / 2=0. / 9 / 2=9. / 8 / 8. / .
SUBST: c : bcv, p : b., q : b., b : bcv
: ghost_vault_setup(_G)
MAP
.............
.............
..xxxnnnxxx..
..nD..D..Dx..
..n..qpq..x..
..=.Dpqpe.n..
..=...OgdDn..
..=.Dpqpf.n..
..n..qpq..x..
..nD..D..Dx..
..xxxnnnxxx..
.............
.............
ENDMAP

NAME:    gammafunk_vaults_ghost_cemetery
TAGS:    vaults_room vaults_orient_s
WEIGHT:  5 (D:3-7)
KMONS:   DEF = player ghost
KMONS:   p = withered plant
# 1/12 chance for 5 ghosts, 1/6 for 4, 1/4 for 3, 1/2 for 2.
SHUFFLE: DdqEerFfs / DdqZyzFfs / DdqZyzFfs / DdqEerZyz / DdqEerZyz / \
         DdqEerZyz / DdqZyzZyz / DdqZyzZyz / DdqZyzZyz / DdqZyzZyz / \
         DdqZyzZyz / DdqZyzZyz
# 1.5 items per ghost, upgrading quality with depth.
NSUBST:  d = 2=|* / |*--, e = |* / |*--, f = 2=|* / |*--
SUBST:   qrs = G, Z = -, y = p, z = V, t : tp
TILE:    G = dngn_gravestone
TILE:    t = dngn_tree_dead
FTILE:   -|*%$DEFpGt = floor_pebble_brown / floor_pebble_darkgray
: set_feature_name("granite_statue", "a gravestone")
: ghost_vault_setup(_G)
MAP
qd--ere--dq
dD---E---Dd
-----------
-----------
f---------f
sF-------Fs
f---------f
xxnn===nnxx
...........
...........
ENDMAP

NAME:   gammafunk_vaults_ghost_necromancy
TAGS:   vaults_room vaults_orient_w
KMONS:  O = player ghost
MONS:   simulacrum place:Vaults:5 / spectre place:Vaults:5, lich
SUBST:  defg = |*, h = *, i = %$, j = -
NSUBST: 1 = 12 / 1, - = 3=1 / 6=1- / -
FTILE:  -|*%$O12G = floor_pebble_brown / floor_pebble_darkgray
: ghost_vault_setup(_G)
MAP
..xxxxxxx
..n1x1x1x
..n---igx
..=---exx
..=---dOx
..=---fxx
..n---jhx
..n1x1x1x
..xxxxxxx
ENDMAP

NAME:   gammafunk_vaults_ghost_spectres
TAGS:   vaults_room vaults_orient_w
KMONS:  O = player ghost
MONS:   wraith / shadow
MONS:   freezing wraith / shadow wraith / phantasmal warrior / flayed ghost w:5
MONS:   eidolon, revenant / curse skull
SUBST:  defg = |*, h = *, i = %$
NSUBST: D = 2 / 3 / 2=3- / 4-, - = 2=1 / 2=1- / 2 / 3=2- / -
FTILE:  -|*%$O1234G = floor_pebble_brown / floor_pebble_darkgray
: ghost_vault_setup(_G)
MAP
..xxxxxxx
..nDxDxDx
..n----gx
..=---exx
..=--idOx
..=---fxx
..n----hx
..nDxDxDx
..xxxxxxx
ENDMAP

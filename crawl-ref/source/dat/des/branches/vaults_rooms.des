################################################################
#  vaults_rooms
#
#  These are the subvaults used as rooms for the
#    layout_vaults_??? layouts.  Each of these has a tag that
#    that the layout generators use to place it appropriately.
#    The tags have the following form:
#
#        vaults_room_[SIZE]_[DOOR TYPE]_[CONTENT]
#
#  [SIZE] is the number of glyphs across the room proper.  As
#    all rooms are square, this is both the width and the
#    height.  This does not include parts of the subvault
#    outside of the room, such as the door or the walls.  There
#    are instructions in the layout_vaults file for adding a
#    new room size.  The valid sizes at the time of writing are
#      -> 1
#      -> 4
#      -> 7
#      -> 10
#      -> 15
#
#  [DOOR TYPE] indicates how the room can be connected to the
#    rest of the map.  It is best to assign a room the "highest"
#    type that it qualifies for.  This allows it to be placed in
#    the most possible places.  However, the "higher" room
#    classifications also place more restrictions on what can be
#    in the room.  The door types, in order from "highest" to
#    "lowest", are as follows:
#      -> spin: The room must be a square and must be able to
#               connect to other rooms on all for sides.  This
#               means that there should be an open area along
#               each side where the door otherwise would be, and
#               that these areas must all connect.
#        -> This door type should be used for rooms that can
#           have any orientation.
#        -> The layout generator may connect other rooms to the
#           side of this room.
#      -> facing: The room must be a square with a door or doors
#                 of the standard size off one side.  The other
#                 three sides must be able to connect to other
#                 doors, and all of these spots must be
#                 connected.
#        -> This door type can be used to give rooms a fixed
#           orientaion.  This could be used, for example, to
#           put archers on the far side of the room from the
#           door.
#        -> The layout generator may connect other rooms to the
#           side of this room.
#        -> If you make rooms with the no_rotate flag, make sure
#           that there is at least one valid room with allow_dup
#           with the door on each side.
#      -> dead_end: The room must be a square with a door or
#                   doors of the standard size off off one side.
#        -> The layout generator will not attempt to connect
#           other rooms to side of this room.
#        -> If you make rooms with the no_rotate flag, make sure
#           that there is at least one valid room with allow_dup
#           with the door on each side.
#      -> city: The room must be a square with an extra row of
#               glyphs around it on all sides.  It will be used
#               only for city (and chaotic city) layouts.  This
#               room will always be placed so that it is
#               entirely surrounded with hallways, and thus will
#               never be connected to another room.  This room
#               must provide its own walls.
#        -> Use this room type for irregularly-shaped rooms and
#           pad the extra space with '.' glyphs.
#
#  Example 7x7 rooms for each door type:
#
#          SPIN                           FACING
#
#      SPOT FOR DOOR                  SPOT FOR DOOR
#           vvv                            vvv
#
#         xx...xx                        xx...xx
#         xx...xx                        x...xxx
# SPOT >  .......  < SPOT        SPOT >  .......+  <
# FOR  >  ...G...  < FOR         FOR  >  .wwwww.+  < DOOR
# DOOR >  .......  < DOOR        DOOR >  .......+  <
#         xx...xx                        x...xxx
#         xx...xx                        xx...xx
#
#           ^^^                            ^^^
#      SPOT FOR DOOR                  SPOT FOR DOOR
#
#
#         DEAD END                         CITY
#
#                                       ...x+x...
#         xxxxxxx                       .xxx.xxx.
#         xx...xx                       .x.....x.
#         x.....xx  <                   xx.....xx
#         x..G...+  < DOOR              +.......+
#         x.....xx  <                   xx.....xx
#         xx...xx                       .x.....x.
#         xxxxxxx                       .xxx.xxx.
#                                       ...x+x...
#
#
#  [CONTENT] is what is in the room.  Note that monsters and
#    loot can still be generated randomly in vaults without
#    "no_monster_gen" and "no_item_gen" set.  To add another
#    content type, see the instructions in layout_vaults.des.
#    At the time of writing, the basic content types are:
#      -> empty: No monsters or loot.
#      -> encounter: A predefined but not especially hard set of
#                    monsters, possibly with some loot
#      -> dangerous: A predefined set of expecially challenging
#                    monsters, probably OOD and probably with
#                    loot.
#      -> loot: A treasure room with few or no monsters.
#
#      -> up1: A room containing a single up stairs.  This room
#              should have (after substitutions) a single '{'
#              glyph and then choose the appropriate stairs by
#              calling the following LUA function:
#                      : vaults_current_stairs_glyph(_G)
#      -> down1: A room containing a single down stairs.  This
#                room should have (after substitutions) a
#                single '{' glyph and call the same LUA function
#                as above.
#      -> secret: A room that is not accessible from the main
#                 part of the level and contains a single down
#                 stairs.  This room should also have a single
#                 '{' glyph and call the same
#                 vaults_current_stairs_glyph LUA function.
#                 This room should contain challenging monsters,
#                 loot, or both.
#      -> up3: A room containing all three up stairs.
#      -> down: A room containing all three up stairs.
#
#  The content types of branches and portal vaults are:
#      -> crypt: A room containing the entry to the Crypt.
#      -> blade: A room containing the entry to the Hall of
#                Blades.
#      -> bazaar: A room containing an entry to a bazaar.
#      -> icecave: A room containing the entry to the ice cave.
#          -> in icecave.des
#      -> lab: A room containing the entry to the labyrinth.
#      -> trove: A room containing the entry to a treasure
#                trove.
#      -> wizlab: A room containing the entry to a wizard lab.
#          -> in wizlab.des
#      -> ziggurat: A room containing the entry to a ziggurat.
#          -> these only place a ziggurat entry if the room is
#             large enough, otheriwse they are just an empty
#             room
#          -> these are never used, because ziggurats only
#             appear in the dungeon
#
#  There are also content types for serial vaults, but these
#    each have their own .des file in the serial folder, each
#    named vaults_[SERIAL VAULT NAME].des.
#
#  Note:  One room may have tags for any two or even all three
#         of the entry1, exit1, and secret content types.
#
#
#  There must be at least one room for each combination of
#    [SIZE], [DOOR TYPE], and [CONTENT].  One exception is
#    [SIZE] 1, which has no rooms with [DOOR TYPE] "facing" or
#    "dead_end" and no rooms with [CONTENT] "entry3" or "exit3".
#    Another execption is that all rooms with [CONTENT]
#    "secret", "crypt", or "blade" have [DOOR TYPE] "spin".
#
#  There must be at least one room with allow_dup of each type.
#    This theoretically isn't important for unique rooms like
#    branch entries, but it turns out to be essential to avoid
#    crashes of the "no such subvault" kind in Wizard-mode
#    testing.
#



########
#  General functions
#
#  These functions are used by many of the rooms.
#
#
#  subvault_place_stair
#
#  This function substitutes in the correct stair type to an
#    ???_up or ???_down subvault.  The correct type will be
#    set by the map generator before the subvault is added.
#
#  One implication of this system is that it is possible to use
#    the same maps for both entry and exit subvaults.
#

{{
-- A default value
dgn.persist.vault_current_stairs_glyph = '{'

function subvault_place_stair(e)
  e.subst("{([})] = {")
  e.subst("{ = " .. dgn.persist.vault_current_stairs_glyph)
end
}}

#
#  vaults_standard_loot
#
#  This function substitutes loot types for greater randomness.
#

{{
function vaults_standard_loot(e)
  e.subst("| = *|||")
  e.subst("* = %%%******|")
  e.subst("% = ....$%%%%*")
end
}}

#
#  vaults_numbered_gold_items
#
#  This function sets the next items (normally d-g) to have a
#    small amount of gold.  The quantities are:
#      -> d:  1- 5 gold
#      -> e:  6-10 gold
#      -> f: 11-15 gold
#      -> g: 16-20 gold
#

{{
function vaults_numbered_gold_items(e)
  e.item("gold q:1  / gold q:2  / gold q:3  / gold q:4  / gold q:5")
  e.item("gold q:6  / gold q:7  / gold q:8  / gold q:9  / gold q:10")
  e.item("gold q:11 / gold q:12 / gold q:13 / gold q:14 / gold q:15")
  e.item("gold q:16 / gold q:17 / gold q:18 / gold q:19 / gold q:20")
end
}}

#
#  vaults_ornamental_pool
#
#  This function randomly replaces water with some obstruction.
#    Using this standard function allows all the rooms to have
#    the same probability of each.  It also would allow other
#    cell values to be added, such as slippery ice if it ever
#    gets added.
#
#  This function does not activate the no_pool_fixup tag, but
#    does mark the water as no_monster_gen.
#
#  This function currently has a chance of the following:
#      -> deep water    (most common)
#      -> shallow water
#      -> lava
#      -> known spiderwebs   (rare)
#      -> unknown spiderwebs (very rare)
#

{{
function vaults_ornamental_pool(e)
  local replace_with = crawl.random2(60)
  if replace_with < 46 then
    e.subst("w : w:30 W:10 l:6")
    e.kmask("wWl = no_monster_gen")
    e.kfeat("w = w")
    e.kfeat("W = W")
    e.kfeat("l = l")
  elseif replace_with < 50 then
    e.subst("w = ww.")
    if crawl.random2(4) < 1 then
      e.kfeat("w : web trap")
    else
      e.kfeat("w : known web trap")
    end
  else
    -- weight 10
    e.subst("w : .")
  end
end
}}



########
#  Room-specific functions
#
#  Each of these functions is used by one rooms, or possibly a
#    small number of similar rooms
#
#
#  teleporter_vault_action_fn
#
#  This function finds the slave of the teleporter and moves the
#    player to it.  It is copied from Vaults.des and is used by
#    the vaults_room_the_teleporter_subvaults vault.
#

{{
function teleporter_vault_action_fn(data, triggerable, triggerer, marker, ev)
  local position = dgn.point(marker:pos())
  my_slaves = dgn.find_marker_positions_by_prop("teleport_spot",
                                                data.teleport_spot)

  if you.teleport_to(my_slaves[1].x, my_slaves[1].y, true) then
    crawl.mpr("Your surroundings suddenly seem different!")
  else
    crawl.mpr("There is a strange hissing noise.")
  end
end
}}

#
#  st_box_setup
#
#  This is the header for what once where st's "greater box"
#    vaults in Vaults.des.  These have become the
#    vaults_room_huge_??? vaults used by the square ring huge
#    center layout.
#
#  The outside ring of hallway glyphs has been clipped from
#    these vaults.  The suare ring huge center layout already
#    guarentees that these vaults will be surrounded by hallway.
#    The transparent tag and the depth and weight specifications
#    were also removed, as they were no longer appropriate.
#

{{
function st_box_setup(e)
  e.tags("no_item_gen")
  e.kfeat("& = closed_door")
  e.kfeat("& = runed_door")
  e.subst("% = %%%**|$")
  e.kmons("7 = patrolling vault guard")
  e.kmons("Z = frost giant / fire giant / titan w:5 / ettin / shadow dragon /\
               golden dragon w:5 / storm dragon / quicksilver dragon w:2 /\
               iron dragon w:2 / swamp dragon / dragon / ice dragon /\
               catoblepas / moth of wrath w:1 / ghost moth w:1 /\
               eye of draining / giant eyeball / eye of devastation /\
               giant orange brain / greater naga / guardian serpent /\
               vampire knight / vampire mage / deep elf sorcerer /\
               deep elf death mage / deep elf demonologist /\
               deep elf annihilator / deep elf high priest / orc warlord /\
               base draconian w:15 / sphinx / shapeshifter /\
               glowing shapeshifter")
  e.subst("0 = 0:9 Z:1")
  e.subst("9 = 9:3 Z:1")
end
}}





################################################################
#  1x1 subvaults (closets)
#
#  There are fewer types of of these than for other size groups.
#    Specifically, there are no doors, no ???_facing_??? or
#    ???_dead_end_??? subvaults, and no subvaults containing 3
#    staircases.
#



########
#  Free-spinning 1x1 subvaults (closets)
#

NAME:     vaults_room_1_spin_empty_default
TAGS:     vaults_room_1_spin_empty allow_dup no_monster_gen no_item_gen
MAP
.
ENDMAP

NAME:     vaults_room_1_spin_encounter_default
TAGS:     vaults_room_1_spin_encounter allow_dup no_monster_gen no_item_gen
MAP
0
ENDMAP

NAME:     vaults_room_1_spin_dangerous_default
TAGS:     vaults_room_1_spin_dangerous allow_dup no_monster_gen no_item_gen
MAP
9
ENDMAP

NAME:     vaults_room_1_spin_loot_default
TAGS:     vaults_room_1_spin_loot allow_dup no_monster_gen no_item_gen
: vaults_standard_loot(_G)
MAP
%
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_1_spin_stairs1_default
TAGS:     vaults_room_1_spin_up1 vaults_room_1_spin_down1
TAGS:     vaults_room_1_spin_secret  allow_dup no_monster_gen no_item_gen
: subvault_place_stair(_G)
MAP
{
ENDMAP

NAME:     vaults_room_1_spin_crypt_default
TAGS:     vaults_room_1_spin_crypt allow_dup no_monster_gen no_item_gen
KFEAT:    O = enter_crypt
MAP
O
ENDMAP

NAME:     vaults_room_1_spin_blade_default
TAGS:     vaults_room_1_spin_blade allow_dup no_monster_gen no_item_gen
KFEAT:    O = enter_hall_of_blades
MAP
O
ENDMAP

# Entries for ???_icecave_??? and ???_wizlab_??? are in .des files

NAME:     vaults_room_1_spin_bazaar_subvault
TAGS:     vaults_room_1_spin_bazaar allow_dup no_monster_gen no_item_gen
SUBVAULT: B : bzr_entry
SUBST:    B = .
MAP
B
ENDMAP

NAME:     vaults_room_1_spin_lab_default
TAGS:     vaults_room_1_spin_lab allow_dup no_monster_gen no_item_gen
SUBVAULT: B : uniq_lab
SUBST:    B = .
MAP
B
ENDMAP

NAME:     vaults_room_1_spin_trove_default
TAGS:     vaults_room_1_spin_trove allow_dup no_monster_gen no_item_gen
SUBVAULT: B : chance_trove
SUBST:    B = .
MAP
B
ENDMAP

# not used because ziggurats are D only
# too small for a ziggurat entry, so empty
NAME:     vaults_room_1_spin_ziggurat_dummy
TAGS:     vaults_room_1_spin_ziggurat allow_dup
MAP
.
ENDMAP



########
#  City-layout 1x1 subvaults (closets)
#

NAME:     vaults_room_1_city_empty_4_doors
TAGS:     vaults_room_1_city_empty allow_dup
MAP
x+x
+.+
x+x
ENDMAP

NAME:     vaults_room_1_city_empty_statues
TAGS:     vaults_room_1_city_empty allow_dup
MAP
G.G
...
G.G
ENDMAP

NAME:     vaults_room_1_city_empty_1_statue
TAGS:     vaults_room_1_city_empty allow_dup
WEIGHT:   20
MAP
...
.G.
...
ENDMAP

NAME:     vaults_room_1_city_empty_1_pool
TAGS:     vaults_room_1_city_empty allow_dup no_pool_fixup
: vaults_ornamental_pool(_G)
MAP
www
www
www
ENDMAP

NAME:     vaults_room_1_city_encounter_4_doors
TAGS:     vaults_room_1_city_encounter allow_dup
MAP
x+x
+0+
x+x
ENDMAP

NAME:     vaults_room_1_city_encounter_statues
TAGS:     vaults_room_1_city_encounter allow_dup
MAP
G.G
.0.
G.G
ENDMAP

NAME:     vaults_room_1_city_dangerous_4_doors
TAGS:     vaults_room_1_city_dangerous allow_dup
MAP
x+x
+9+
x+x
ENDMAP

NAME:     vaults_room_1_city_dangerous_statue_guards
TAGS:     vaults_room_1_city_dangerous allow_dup
NSUBST:   0 = 2:0 / 1:0. / *:.
MAP
000
0G0
000
ENDMAP

NAME:     vaults_room_1_city_dangerous_ocs_ss_is
TAGS:     vaults_room_1_city_dangerous allow_dup
WEIGHT:   5
SHUFFLE:  12
MONS:     orange crystal statue / silver statue / ice statue
MONS:     statue tile:mons_statue_archer name:archer n_adj; \
          longbow . arrow q:30
MAP
...
.1.
...
ENDMAP

NAME:     vaults_room_1_city_loot_4_doors
TAGS:     vaults_room_1_city_loot allow_dup
: vaults_standard_loot(_G)
MAP
x+x
+%+
x+x
ENDMAP

NAME:     vaults_room_1_city_loot_statues
TAGS:     vaults_room_1_city_loot allow_dup
: vaults_standard_loot(_G)
MAP
G.G
.%.
G.G
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_1_city_stairs1_open
TAGS:     vaults_room_1_city_up1
TAGS:     vaults_room_1_city_down1 allow_dup
WEIGHT:   20
NSUBST:   . = 1:0. / *:~
: subvault_place_stair(_G)
MAP
...
.{.
...
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_1_city_stairs1_4_doors
TAGS:     vaults_room_1_city_up1
TAGS:     vaults_room_1_city_down1 allow_dup
: subvault_place_stair(_G)
MAP
x+x
+{+
x+x
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_1_city_stairs1_statues
TAGS:     vaults_room_1_city_up1
TAGS:     vaults_room_1_city_down1 allow_dup
SUBST:    . = .:10 ~:1
: subvault_place_stair(_G)
MAP
G.G
.{.
G.G
ENDMAP





################################################################
#  4x4 subvaults
#

########
#  Doors for 4x4 subvaults
#

NAME:     vaults_door_4
TAGS:     vaults_door_4 allow_dup
SUBST:    - : x+
MAP
+-
ENDMAP



########
#  Free-spinning 4x4 subvaults
#

NAME:     vaults_room_4_spin_empty_default
TAGS:     vaults_room_4_spin_empty allow_dup
SUBST:    G : xG...
WEIGHT:   50
MAP
G..G
....
....
G..G
ENDMAP

NAME:     vaults_room_4_spin_empty_pool
TAGS:     vaults_room_4_spin_empty allow_dup no_pool_fixup
: vaults_ornamental_pool(_G)
MAP
....
.ww.
.ww.
....
ENDMAP

NAME:     vaults_room_4_spin_empty_ring
TAGS:     vaults_room_4_spin_empty allow_dup
MAP
....
.xx.
.xx.
....
ENDMAP

NAME:     vaults_room_4_spin_empty_feature
TAGS:     vaults_room_4_spin_empty allow_dup
SUBST:    G : xG...
SUBST:    ! = SCT^
SUBST:    T = TTTUVV
KFEAT:    S = any shop
KFEAT:    ^ = known zot trap
MAP
G..G
.!..
....
G..G
ENDMAP

NAME:     vaults_room_4_spin_encounter_default
TAGS:     vaults_room_4_spin_encounter allow_dup no_monster_gen
NSUBST:   0 = 1:0 / *:.
MAP
....
.00.
.00.
....
ENDMAP

NAME:     vaults_room_4_spin_encounter_traps
TAGS:     vaults_room_4_spin_encounter allow_dup no_monster_gen
SUBST:    G : xG~~~
SUBST:    ~ = ~.
MAP
G~~G
~~~~
~~~~
G~~G
ENDMAP

NAME:     vaults_room_4_spin_dangerous_default
TAGS:     vaults_room_4_spin_dangerous allow_dup no_monster_gen
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
: vaults_standard_loot(_G)
MAP
q..q
.pp.
.pp.
q..q
ENDMAP

NAME:     vaults_room_4_spin_loot_default
TAGS:     vaults_room_4_spin_loot allow_dup no_monster_gen
NSUBST:   % = 4:% / *:.
: vaults_standard_loot(_G)
MAP
%..%
.%%.
.%%.
%..%
ENDMAP

# 18 + 26 + 40 + 15 = average 99 gold
NAME:     vaults_room_4_spin_loot_gold
TAGS:     vaults_room_4_spin_loot allow_dup no_monster_gen no_item_gen
NSUBST:   $ = 1:g / 2:f / 5:e / 5:d / *:.
: vaults_numbered_gold_items(_G)
MAP
$$$$
$$$$
$$$$
$$$$
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_4_spin_stairs1_statues
TAGS:     vaults_room_4_spin_up1 vaults_room_4_spin_down1 allow_dup
SUBST:    G : xG...
: subvault_place_stair(_G)
MAP
G..G
.{..
....
G..G
ENDMAP

NAME:     vaults_room_4_spin_up3_center
TAGS:     vaults_room_4_spin_up3 allow_dup
SUBST:    G : xG...
SHUFFLE:  {([<
SUBST:    < = <.
MAP
G..G
.{(.
.[<.
G..G
ENDMAP

NAME:     vaults_room_4_spin_down3_center
TAGS:     vaults_room_4_spin_down3 allow_dup
SUBST:    G : xG...
SHUFFLE:  })]>
SUBST:    > = >.
MAP
G..G
.}).
.]>.
G..G
ENDMAP

NAME:     vaults_room_4_spin_secret_statues
TAGS:     vaults_room_4_spin_secret allow_dup no_monster_gen no_item_gen
SUBST:    G : xG%%%
NSUBST:   % = 8:% / *:.
: vaults_standard_loot(_G)
: subvault_place_stair(_G)
MAP
G%%G
%{.%
%.9%
G%%G
ENDMAP

NAME:     vaults_room_4_spin_secret_ring
TAGS:     vaults_room_4_spin_secret allow_dup no_monster_gen no_item_gen
SUBST:    w : xw
NSUBST:   9 = 1:9 / *:*
: subvault_place_stair(_G)
: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
{..9
.ww.
.ww.
9..9
ENDMAP

NAME:     vaults_room_4_spin_crypt_default
TAGS:     vaults_room_4_spin_crypt allow_dup
NSUBST:   O = 1:O / 1:1 / *:1.
MONS:     place:Crypt:1
KFEAT:    O = enter_crypt
FTILE:    O1. = floor_crypt
MAP
....
.OO.
.OO.
....
ENDMAP

NAME:     vaults_room_4_spin_crypt_grunt
TAGS:     vaults_room_4_spin_crypt allow_dup no_item_gen no_monster_gen
SHUFFLE:  Odef
SUBST:    def = .
KFEAT:    O = enter_crypt
NSUBST:   8 = 2:1 / *:2
MONS:     guardian mummy, mummy
MAP
8..8
.Od.
.ef.
8..8
ENDMAP

NAME:     vaults_room_4_spin_blade_corners
TAGS:     vaults_room_4_spin_blade allow_dup
NSUBST:   O = 1:O / 1:1 / *:1.
MONS:     place:Blade:1
KFEAT:    O = enter_hall_of_blades
FTILE:    xO1. = floor_hall
MAP
x..x
.OO.
.OO.
x..x
ENDMAP

NAME:     vaults_room_4_spin_blade_grunt
TAGS:     vaults_room_4_spin_blade allow_dup no_item_gen no_monster_gen
SHUFFLE:  Odef
SUBST:    def = .
KFEAT:    O = enter_hall_of_blades
NSUBST:   8 = 2:1 / *:.
MONS:     dancing weapon
MAP
8..8
.Od.
.ef.
8..8
ENDMAP

# Entries for ???_icecave_??? and ???_wizlab_??? are in .des files

NAME:     vaults_room_4_spin_bazaar_subvault
TAGS:     vaults_room_4_spin_bazaar allow_dup no_monster_gen no_item_gen
NSUBST:   B = 1:B / *:.
SUBVAULT: B : bzr_entry
SUBST:    B = .
MAP
B...
.BB.
....
....
ENDMAP

NAME:     vaults_room_4_spin_lab_default
TAGS:     vaults_room_4_spin_lab allow_dup no_monster_gen no_item_gen
NSUBST:   B = 1:B / *:.
SUBVAULT: B : uniq_lab
SUBST:    B = .
MAP
B...
.BB.
....
....
ENDMAP

NAME:     vaults_room_4_spin_trove_default
TAGS:     vaults_room_4_spin_trove allow_dup no_monster_gen no_item_gen
NSUBST:   B = 1:B / *:.
SUBVAULT: B : chance_trove
SUBST:    B = .
MAP
B...
.BB.
....
....
ENDMAP

# not used because ziggurats are D only
# too small for a ziggurat entry, so empty
NAME:     vaults_room_4_spin_ziggurat_dummy
TAGS:     vaults_room_4_spin_ziggurat allow_dup
MAP
....
....
....
....
ENDMAP



########
#  4x4 subvaults with a facing direction
#

NAME:     vaults_room_4_facing_empty_default
TAGS:     vaults_room_4_facing_empty allow_dup no_monster_gen
SUBST:    w : wWl
SUBVAULT: A : vaults_door_4
MAP
....
....A
....A
....
ENDMAP

NAME:     vaults_room_4_facing_empty_statues_pool
TAGS:     vaults_room_4_facing_empty allow_dup no_pool_fixup
SUBVAULT: A : vaults_door_4
: vaults_ornamental_pool(_G)
MAP
G...
.ww.A
.ww.A
G...
ENDMAP

NAME:     vaults_room_4_facing_encounter_default
TAGS:     vaults_room_4_facing_encounter allow_dup no_monster_gen
SUBST:    y = 0:2 %:1 .:20
NSUBST:   . = 1:0 / *:.
SUBVAULT: A : vaults_door_4
: vaults_standard_loot(_G)
MAP
y...
y...A
y...A
y...
ENDMAP

NAME:     vaults_room_4_facing_dangerous_default
TAGS:     vaults_room_4_facing_dangerous allow_dup no_monster_gen
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
SUBVAULT: A : vaults_door_4
: vaults_standard_loot(_G)
MAP
pq..
pq..A
pq..A
pq..
ENDMAP

NAME:     vaults_room_4_facing_loot_default
TAGS:     vaults_room_4_facing_loot allow_dup no_monster_gen
SUBST:    G : G%
NSUBST:   % = 4:% / *:.
SUBVAULT: A : vaults_door_4
: vaults_standard_loot(_G)
MAP
G%.x
%%%.A
%%%.A
G%.x
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_4_facing_stairs1_open
TAGS:     vaults_room_4_facing_up1 vaults_room_4_facing_down1 allow_dup
SUBVAULT: A : vaults_door_4
: subvault_place_stair(_G)
MAP
{...
....A
....A
....
ENDMAP

NAME:     vaults_room_4_facing_up3_open
TAGS:     vaults_room_4_facing_up3 allow_dup
SHUFFLE:  {([<
SUBST:    < = <.
SUBVAULT: A : vaults_door_4
MAP
{..(
....A
....A
[..<
ENDMAP

NAME:     vaults_room_4_facing_down3_open
TAGS:     vaults_room_4_facing_down3 allow_dup
SHUFFLE:  })]>
SUBST:    > = >.
SUBVAULT: A : vaults_door_4
MAP
}..)
....A
....A
]..>
ENDMAP

NAME:     vaults_room_4_facing_crypt_grunt
TAGS:     vaults_room_4_facing_crypt allow_dup no_item_gen no_monster_gen
SHUFFLE:  Odef
SUBST:    def = .
KFEAT:    O = enter_crypt
NSUBST:   8 = 2:1 / *:2
MONS:     guardian mummy, mummy
SUBVAULT: A : vaults_door_4
MAP
.8..
8Od.A
8ef.A
.8..
ENDMAP

NAME:     vaults_room_4_facing_blade_grunt
TAGS:     vaults_room_4_facing_blade allow_dup no_item_gen no_monster_gen
SHUFFLE:  Odef
SUBST:    def = .
KFEAT:    O = enter_hall_of_blades
NSUBST:   8 = 2:1 / *:.
MONS:     dancing weapon
SUBVAULT: A : vaults_door_4
MAP
.8..
8Od.A
8ef.A
.8..
ENDMAP



########
#  Dead-end 4x4 subvaults
#

NAME:     vaults_room_4_dead_end_empty_narrow
TAGS:     vaults_room_4_dead_end_empty allow_dup
SUBVAULT: A : vaults_door_4
MAP
xxxx
....A
....A
xxxx
ENDMAP

NAME:     vaults_room_4_dead_end_empty_statues_ring
TAGS:     vaults_room_4_dead_end_empty allow_dup
SUBST:    I : G.
SUBVAULT: A : vaults_door_4
MAP
xG.I
G...A
G...A
xG.I
ENDMAP

NAME:     vaults_room_4_dead_end_empty_flooded
TAGS:     vaults_room_4_dead_end_empty allow_dup
SUBST:    W = w.
KMASK:    w = no_monster_gen
SUBVAULT: A : vaults_door_4
MAP
wwwW
wwW.A
wwW.A
wwwW
ENDMAP

NAME:     vaults_room_4_dead_end_empty_statues_checkerboard
TAGS:     vaults_room_4_dead_end_empty allow_dup
WEIGHT:   5
NSUBST:   % = 3:% / *:.
: vaults_standard_loot(_G)
MAP
.G.G
G.G.+
.G.Gx
G.G.
ENDMAP

NAME:     vaults_room_4_dead_end_encounter_statues
TAGS:     vaults_room_4_dead_end_encounter allow_dup
SUBST:    I : G.
NSUBST:   0 = 1:0 / *:.
SUBVAULT: A : vaults_door_4
MAP
xG0I
G00.A
G00.A
xG0I
ENDMAP

NAME:     vaults_room_4_dead_end_dangerous_flooded
TAGS:     vaults_room_4_dead_end_dangerous allow_dup no_pool_fixup
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
SUBVAULT: A : vaults_door_4
: vaults_ornamental_pool(_G)
: vaults_standard_loot(_G)
MAP
wwqq
wpp.A
wpp.A
wwqq
ENDMAP

NAME:     vaults_room_4_dead_end_loot_flooded
TAGS:     vaults_room_4_dead_end_loot allow_dup no_pool_fixup
NSUBST:   % = 4:% / *:.
SUBVAULT: A : vaults_door_4
: vaults_ornamental_pool(_G)
: vaults_standard_loot(_G)
MAP
ww%%
w%%.A
w%%.A
ww%%
ENDMAP

NAME:     vaults_room_4_dead_end_loot_statues_checkerboard
TAGS:     vaults_room_4_dead_end_loot allow_dup
WEIGHT:   5
NSUBST:   % = 4:% / *:.
: vaults_standard_loot(_G)
MAP
%G%G
G%G.+
%G%Gx
G%G%
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_4_dead_end_stairs1_open
TAGS:     vaults_room_4_dead_end_up1 vaults_room_4_dead_end_down1 allow_dup
WEIGHT:   20
NSUBST:   { = 1:{ / *:.
SUBVAULT: A : vaults_door_4
: subvault_place_stair(_G)
MAP
{{..
{{..A
{{..A
{{..
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_4_dead_end_stairs1_narrow
TAGS:     vaults_room_4_dead_end_up1 vaults_room_4_dead_end_down1 allow_dup
: subvault_place_stair(_G)
MAP
xxxx
xxxxx
{...+
xxxx
ENDMAP

NAME:     vaults_room_4_dead_end_up3_open
TAGS:     vaults_room_4_dead_end_up3 allow_dup
SHUFFLE:  {([<
SUBST:    < = <.
SUBVAULT: A : vaults_door_4
MAP
{...
(...A
[...A
<...
ENDMAP

NAME:     vaults_room_4_dead_end_down3_open
TAGS:     vaults_room_4_dead_end_down3 allow_dup
SHUFFLE:  })]>
SUBST:    > = >.
SUBVAULT: A : vaults_door_4
MAP
}...
)...A
]...A
>...
ENDMAP



########
#  City-layout 4x4 subvaults
#

NAME:     vaults_room_4_city_empty_cross
TAGS:     vaults_room_4_city_empty allow_dup
WEIGHT:   20
MAP
xx++xx
xx..xx
+....+
+....+
xx..xx
xx++xx
ENDMAP

NAME:     vaults_room_4_city_empty_water
TAGS:     vaults_room_4_city_empty allow_dup no_pool_fixup
SHUFFLE:  GHI/pqr
SHUFFLE:  GH
SUBST:    G : G.
SUBST:    HI = .
SUBST:    pqr = w
: vaults_ornamental_pool(_G)
MAP
GHIIHG
HwwwwH
IwwwwI
IwwwwI
HwwwwH
GHIIHG
ENDMAP

NAME:     vaults_room_4_city_encounter_corners
TAGS:     vaults_room_4_city_encounter allow_dup
NSUBST:   p = 1:+ / *:-
NSUBST:   q = 1:+ / *:-
NSUBST:   r = 1:+ / *:-
NSUBST:   s = 1:+ / *:-
SUBST:    - = x:20 +:1
NSUBST:   0 = 1:% / *:0
: vaults_standard_loot(_G)
MAP
xpxxqx
p0xx0q
xxxxxx
xxxxxx
r0xx0s
xrxxsx
ENDMAP

NAME:     vaults_room_4_city_dangerous_corners
TAGS:     vaults_room_4_city_dangerous allow_dup
NSUBST:   A = 1:+ / *:-
NSUBST:   B = 1:+ / *:-
NSUBST:   C = 1:+ / *:-
NSUBST:   D = 1:+ / *:-
SUBST:    - = x:20 +:1
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
: vaults_standard_loot(_G)
MAP
xAxxBx
A.qq.B
xqppqx
xqppqx
C.qq.D
xCxxDx
ENDMAP

NAME:     vaults_room_4_city_loot_corners
TAGS:     vaults_room_4_city_loot allow_dup
NSUBST:   p = 1:+ / *:-
NSUBST:   q = 1:+ / *:-
NSUBST:   r = 1:+ / *:-
NSUBST:   s = 1:+ / *:-
SUBST:    - = x:20 +:1
NSUBST:   % = 3:% / *:.
: vaults_standard_loot(_G)
MAP
xpxxqx
p%GG%q
xG%%Gx
xG%%Gx
r%GG%s
xrxxsx
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_4_city_stairs1_corner
TAGS:     vaults_room_4_city_up1 vaults_room_4_city_down1 allow_dup
NSUBST:   p = 1:+ / *:-
NSUBST:   q = 1:+ / *:-
NSUBST:   r = 1:+ / *:-
NSUBST:   s = 1:+ / *:-
SUBST:    - = x:20 +:1
NSUBST:   { = 1:{ / *0
SUBST:    0 = .:6 0:3 %:1
: vaults_standard_loot(_G)
: subvault_place_stair(_G)
MAP
xpxxqx
p{xx{q
xxxxxx
xxxxxx
r{xx{s
xrxxsx
ENDMAP

NAME:     vaults_room_4_city_stairs1_C
TAGS:     vaults_room_4_city_up1 vaults_room_4_city_down1 allow_dup
: subvault_place_stair(_G)
MAP
xxxxxx
x{...x
xxxx.x
xxxx.x
+....x
xxxxxx
ENDMAP

NAME:     vaults_room_4_city_up3_open
TAGS:     vaults_room_4_city_up3 allow_dup
NSUBST:   p = 1:+ / *:-
NSUBST:   q = 1:+ / *:-
NSUBST:   r = 1:+ / *:-
NSUBST:   s = 1:+ / *:-
SUBST:    - = x:20 +:1
SHUFFLE:  {([<
SUBST:    < = <.0
MAP
xpxxqx
p{xx(q
xxxxxx
xxxxxx
r[xx<s
xrxxsx
ENDMAP

NAME:     vaults_room_4_city_down3_open
TAGS:     vaults_room_4_city_down3 allow_dup
NSUBST:   p = 1:+ / *:-
NSUBST:   q = 1:+ / *:-
NSUBST:   r = 1:+ / *:-
NSUBST:   s = 1:+ / *:-
SUBST:    - = x:20 +:1
SHUFFLE:  })]>
SUBST:    > = >.0
MAP
xpxxqx
p}xx)q
xxxxxx
xxxxxx
r]xx>s
xrxxsx
ENDMAP

#
#  A set of rooms that use subvaults for walls.
#
#  The total wall weight is 100.
#

NAME:     vaults_room_4_city_empty_subring
TAGS:     vaults_room_4_city_empty allow_dup
SUBVAULT: A : vaults_room_4_city_subring
MAP
AAAAAA
AA..AA
A....A
A....A
AA..AA
AAAAAA
ENDMAP

NAME:     vaults_room_4_city_empty_subring_fungus
TAGS:     vaults_room_4_city_empty allow_dup
WEIGHT:   5
MONS:     fungus / nothing w:30,  fungus / nothing
SUBVAULT: A : vaults_room_4_city_subring
MAP
AAAAAA
AA11AA
A1221A
A1221A
AA11AA
AAAAAA
ENDMAP

NAME:     vaults_room_4_city_empty_subring_water
TAGS:     vaults_room_4_city_empty allow_dup no_pool_fixup
WEIGHT:   5
SUBVAULT: A : vaults_room_4_city_subring
: vaults_ornamental_pool(_G)
MAP
AAAAAA
AAwwAA
AwwwwA
AwwwwA
AAwwAA
AAAAAA
ENDMAP

NAME:     vaults_room_4_city_encounter_subring
TAGS:     vaults_room_4_city_encounter allow_dup
NSUBST:   0 = 1:0 / *:.
SUBVAULT: A : vaults_room_4_city_subring
MAP
AAAAAA
AA..AA
A.00.A
A.00.A
AA..AA
AAAAAA
ENDMAP

NAME:     vaults_room_4_city_dangerous_subring
TAGS:     vaults_room_4_city_dangerous allow_dup
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
SUBVAULT: A : vaults_room_4_city_subring
: vaults_standard_loot(_G)
MAP
AAAAAA
AAqqAA
AqppqA
AqppqA
AAqqAA
AAAAAA
ENDMAP

NAME:     vaults_room_4_city_subring_wall
TAGS:     vaults_room_4_city_subring allow_dup
WEIGHT:   60
SHUFFLE:  ABCD
SUBVAULT: A : vaults_door_4
: if crawl.random2(6) < 1 then
:   subvault("B : vaults_door_4")
: else
:   subst("B = x")
: end
: if crawl.random2(6) < 1 then
:   subvault("C : vaults_door_4")
: else
:   subst("C = x")
: end
: if crawl.random2(6) < 1 then
:   subvault("D : vaults_door_4")
: else
:   subst("D = x")
: end
MAP
.xAAx.
xx  xx
B    C
B    C
xx  xx
.xDDx.
ENDMAP

NAME:     vaults_room_4_city_subring_statue_ring
TAGS:     vaults_room_4_city_subring allow_dup
WEIGHT:   20
MAP
..GG..
.G  G.
G    G
G    G
.G  G.
..GG..
ENDMAP

NAME:     vaults_room_4_city_subring_8_statues
TAGS:     vaults_room_4_city_subring allow_dup
WEIGHT:   10
MAP
.G..G.
G.  .G
.    .
.    .
G.  .G
.G..G.
ENDMAP

NAME:     vaults_room_4_city_subring_statue_corners
TAGS:     vaults_room_4_city_subring allow_dup
WEIGHT:   10
MAP
Gx++xG
xx  xx
+    +
+    +
xx  xx
Gx++xG
ENDMAP





################################################################
#  7x7 subvaults
#

########
#  Doors for 7x7 subvaults
#

NAME:     vaults_door_7
TAGS:     vaults_door_7 allow_dup
SUBST:    - = xx+
MAP
-+-
ENDMAP

NAME:     vaults_door_7_double
TAGS:     vaults_door_7 allow_dup
WEIGHT:   1
MAP
+x+
ENDMAP



########
#  Free-spinning 7x7 subvaults
#

NAME:     vaults_room_7_spin_empty_default
TAGS:     vaults_room_7_spin_empty allow_dup
MAP
.......
.......
.......
.......
.......
.......
.......
ENDMAP

NAME:     vaults_room_7_spin_empty_subvaulted
TAGS:     vaults_room_7_spin_empty allow_dup
SUBVAULT: B : vaults_room_1_city_empty
MAP
.......
.......
..BBB..
..BBB..
..BBB..
.......
.......
ENDMAP

NAME:     vaults_room_7_spin_empty_cross
TAGS:     vaults_room_7_spin_empty allow_dup
SUBST:    ~ = ~:1 .:20
MAP
xx...xx
xxx~xxx
.xx~xx.
.~~.~~.
.xx~xx.
xxx~xxx
xx...xx
ENDMAP

NAME:     vaults_room_7_spin_empty_door_ring
TAGS:     vaults_room_7_spin_empty allow_dup
SUBST:    + : +.
MAP
.+...+.
+xxxxx+
.xxxxx.
.xxxxx.
.xxxxx.
+xxxxx+
.+...+.
ENDMAP

NAME:     vaults_room_7_spin_empty_pool
TAGS:     vaults_room_7_spin_empty allow_dup no_pool_fixup
: vaults_ornamental_pool(_G)
MAP
.......
.......
..www..
..www..
..www..
.......
.......
ENDMAP

NAME:     vaults_room_7_spin_empty_4_pools
TAGS:     vaults_room_7_spin_empty allow_dup no_pool_fixup
: vaults_ornamental_pool(_G)
MAP
ww...ww
ww...ww
.......
.......
.......
ww...ww
ww...ww
ENDMAP

NAME:     vaults_room_7_spin_empty_statues
TAGS:     vaults_room_7_spin_empty allow_dup
SHUFFLE:  GI
SUBST:    I : G..
MAP
xx...xx
xxG.Gxx
.G...G.
...I...
.G...G.
xxG.Gxx
xx...xx
ENDMAP

NAME:     vaults_room_7_spin_empty_feature
TAGS:     vaults_room_7_spin_empty allow_dup no_pool_fixup
SUBST:    G : G..
SUBST:    x : xxw...
SUBST:    ! = SCT^
SUBST:    T = TTTUVV
KFEAT:    S = any shop
KFEAT:    ^ = known zot trap
: vaults_ornamental_pool(_G)
MAP
xx...xx
xxG.Gxx
.G...G.
...!...
.G...G.
xxG.Gxx
xx...xx
ENDMAP

NAME:     vaults_room_7_spin_empty_mumra_statues
TAGS:     vaults_room_7_spin_empty allow_dup luniq
SUBST:    A : G..
SUBST:    B : G..
SUBST:    C : G..
SUBST:    D : G..
SUBST:    1 = G . 1:5
KMONS:    1 = statue
MAP
A.....A
.B.D.B.
..C.C..
.D.1.D.
..C.C..
.B.D.B.
A.....A
ENDMAP

NAME:     vaults_room_7_spin_empty_mumra_fountains
TAGS:     vaults_room_7_spin_empty allow_dup luniq
SUBST:    A : T..
SUBST:    B : T..
SUBST:    C : T..
SUBST:    D : T..
SUBST:    T = T .:8 V:3 Y:1
MAP
A.....A
.B.D.B.
..C.C..
.D.T.D.
..C.C..
.B.D.B.
A.....A
ENDMAP

NAME:     vaults_room_7_spin_empty_mumra_puddle
TAGS:     vaults_room_7_spin_empty allow_dup luniq
SUBST:    1 : W.
SUBST:    2 : W.
SUBST:    3 : W.
SUBST:    4 : W.
SUBST:    5 : W.
SUBST:    6 : W.
SUBST:    W = W:20 w:3
SUBST:    . = .:20 W:3
MAP
6664455
6633445
6333345
3322334
3212214
1112221
1111122
ENDMAP

NAME:     vaults_room_7_spin_empty_mumra_cave_out
TAGS:     vaults_room_7_spin_empty allow_dup luniq
WEIGHT:   5
SUBST:    B = .:10 ^:4
SUBST:    A = .:5 ^:5
KFEAT:    ^ = shaft trap
MAP
..BBB..
.BBBBB.
BBAAABB
BBAAABB
BBAAABB
.BBBBB.
..BBB..
ENDMAP

# Some old junk nobody could quite bring themselves to throw out
NAME:     vaults_room_7_spin_empty_mumra_broom_cupboard
TAGS:     vaults_room_7_spin_empty_mumra
SUBST:    w = ww.
KFEAT:    w = web
SUBST:    w = .
SUBST:    . = s:3 .:10
KITEM:    s = club mundane w:20 / short sword mundane / dagger mundane /\
          trident mundane / spear mundane
MAP
ww...ww
w.....w
.......
.......
.......
w.....w
ww...ww
ENDMAP

NAME:     vaults_room_7_spin_encounter_corners
TAGS:     vaults_room_7_spin_encounter allow_dup no_monster_gen
SUBST:    G : G..
MAP
xx...xx
xxG.Gxx
.G...G.
...0...
.G...G.
xxG.Gxx
xx...xx
ENDMAP

NAME:     vaults_room_7_spin_encounter_subvaulted
TAGS:     vaults_room_7_spin_encounter allow_dup
SUBVAULT: B : vaults_room_1_city_encounter
MAP
.......
.......
..BBB..
..BBB..
..BBB..
.......
.......
ENDMAP

NAME:     vaults_room_7_spin_encounter_yaktaur
TAGS:     vaults_room_7_spin_encounter allow_dup
TAGS:     no_monster_gen no_pool_fixup
MONS:     yaktaur / yaktaur band
: vaults_ornamental_pool(_G)
MAP
.......
.wwwww.
.w...w.
.w.1.w.
.w...w.
.wwwww.
.......
ENDMAP

NAME:     vaults_room_7_spin_encounter_grunt_1
TAGS:     vaults_room_7_spin_encounter allow_dup no_item_gen no_monster_gen
WEIGHT:   5
ITEM:     any / nothing w:30
NSUBST:   0 = 4:0 / *:.
MAP
.0...0.
0.....0
..ddd..
..ddd..
..ddd..
0.....0
.0...0.
ENDMAP

NAME:     vaults_room_7_spin_encounter_grunt_2
TAGS:     vaults_room_7_spin_encounter allow_dup no_item_gen no_monster_gen
WEIGHT:   5
ITEM:     any / nothing w:70
NSUBST:   0 = 4:0 / *:.
MAP
0.....0
.0ddd0.
.d...d.
.d...d.
.d...d.
.0ddd0.
0.....0
ENDMAP

NAME:     vaults_room_7_spin_encounter_mumra_cobwebs
TAGS:     vaults_room_7_spin_encounter allow_dup
SUBST:    ' = ':10 .:5
SUBST:    ; = ':10 .:15
SUBST:    . = ':10 .:40
KFEAT:    ' = web
SUBST:    . = 1:1 .:10
NSUBST:   . = 2:$ / *:.
MONS:     trapdoor spider
MAP
';';;''
';;;;;'
;;;..;;
;;..;;'
';;.;;'
'';;;;'
''''';'
ENDMAP

NAME:     vaults_room_7_spin_dangerous_boxed
TAGS:     vaults_room_7_spin_dangerous allow_dup
SUBST:    - : xxm++...
KPROP:    _ = no_rtele_into
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
SUBVAULT: D : vaults_door_7
SUBVAULT: E : vaults_door_7
MAP
_-...-_
-xEEEx-
.D...B.
.D.9.B.
.D...B.
-xCCCx-
_-...-_
ENDMAP

NAME:     vaults_room_7_spin_dangerous_default
TAGS:     vaults_room_7_spin_dangerous allow_dup no_monster_gen
SHUFFLE:  GI
SUBST:    I : G.
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
: vaults_standard_loot(_G)
MAP
G.....G
...q...
..qpq..
.qpIpq.
..qpq..
...q...
G.....G
ENDMAP

NAME:     vaults_room_7_spin_dangerous_grunt_1
TAGS:     vaults_room_7_spin_dangerous allow_dup no_item_gen no_monster_gen
WEIGHT:   5
ITEM:     any good_item / any
NSUBST:   8 = 2:8 / 2:0 / *:.
MAP
.8...8.
8d...d8
..ddd..
..ddd..
..ddd..
8d...d8
.8...8.
ENDMAP

NAME:     vaults_room_7_spin_dangerous_grunt_2
TAGS:     vaults_room_7_spin_dangerous allow_dup no_item_gen no_monster_gen
WEIGHT:   5
ITEM:     any good_item / any
NSUBST:   9 = 2:9 / 2:8 / *:.
MAP
9.....9
.9ddd9.
.d...d.
.d...d.
.d...d.
.9ddd9.
9.....9
ENDMAP

NAME:     vaults_room_7_spin_dangerous_mumra_exhibit_lair
TAGS:     vaults_room_7_spin_dangerous no_monster_gen
SHUFFLE:  t4
NSUBST:   1 = 1:1 / *:2
SUBST:    2 = 2:10 3:5 .:20
SUBST:    3 = 3:10 .:50
KMONS:    1 = hydra
KMONS:    2 = elephant / spiny worm
KMONS:    3 = catoblepas / basilisk
KMONS:    4 = plant
FTILE:    t.1234 = floor_lair
MAP
tt333tt
t32223t
3221223
3211123
3221223
t32223t
tt333tt
ENDMAP

NAME:     vaults_room_7_spin_loot_cross
TAGS:     vaults_room_7_spin_loot allow_dup no_monster_gen
: vaults_standard_loot(_G)
SUBST:    ~ = ~:1 .:10
MAP
xx...xx
xxx~xxx
.xx~xx.
.~~*~~.
.xx~xx.
xxx~xxx
xx...xx
ENDMAP

NAME:     vaults_room_7_spin_loot_boxed
TAGS:     vaults_room_7_spin_loot allow_dup
SUBST:    - : xxm++...
KPROP:    _ = no_rtele_into
NSUBST:   % = 4:% /.
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
SUBVAULT: D : vaults_door_7
SUBVAULT: E : vaults_door_7
: vaults_standard_loot(_G)
MAP
_-...-_
-xEEEx-
.D%%%B.
.D%%%B.
.D%%%B.
-xCCCx-
_-...-_
ENDMAP

NAME:     vaults_room_7_spin_loot_water_ring
TAGS:     vaults_room_7_spin_loot allow_dup no_pool_fixup no_monster_gen
NSUBST:   % = 6:% / *:_
KPROP:    %_ = no_rtele_into
: vaults_ornamental_pool(_G)
: vaults_standard_loot(_G)
MAP
.......
.wwwww.
.w%%%w.
.w%%%w.
.w%%%w.
.wwwww.
.......
ENDMAP

# average 13 + 24 + 114 = 151 gold
NAME:     vaults_room_7_spin_loot_gold
TAGS:     vaults_room_7_spin_loot allow_dup no_monster_gen no_item_gen
NSUBST:   $ = 1:f / 3:e / 38:d / *:.
: vaults_numbered_gold_items(_G)
MAP
$$$$$$$
$$$$$$$
$$$$$$$
$$$$$$$
$$$$$$$
$$$$$$$
$$$$$$$
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_7_spin_stairs1_boxed
TAGS:     vaults_room_7_spin_up1 vaults_room_7_spin_down1 allow_dup
SUBST:    - : xxm++...
KPROP:    _ = no_rtele_into
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
SUBVAULT: D : vaults_door_7
SUBVAULT: E : vaults_door_7
: subvault_place_stair(_G)
MAP
_-...-_
-xEEEx-
.D...B.
.D.{.B.
.D...B.
-xCCCx-
_-...-_
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_7_spin_stairs1_statues
TAGS:     vaults_room_7_spin_up1 vaults_room_7_spin_down1 allow_dup
SHUFFLE:  GI
SUBST:    I = .
: subvault_place_stair(_G)
MAP
.......
.GI.IG.
.I...I.
...{...
.I...I.
.GI.IG.
.......
ENDMAP

NAME:     vaults_room_7_spin_up1_mumra_kobold_pokers
TAGS:     vaults_room_7_spin_up1 allow_dup patrolling
SUBST:    1 = 1 .
SUBST:    2 = 2 ..
MONS:     kobold ; dagger / big kobold ; short sword
MONS:     kobold ; spear / big kobold ; bardiche
: subvault_place_stair(_G)
MAP
2222222
2111112
21...12
21.{.12
21...12
2111112
2222222
ENDMAP

NAME:     vaults_room_7_spin_down1_mumra_elliptics_bane
TAGS:     vaults_room_7_spin_down1 allow_dup patrolling luniq
SUBST:    1 = 1 ..
SUBST:    2 = 2 ...
MONS:     iron troll / iron golem
MONS:     crystal golem / vault guard
KFEAT:    .12 = floor
: subvault_place_stair(_G)
MAP
.......
.11111.
.12221.
.12{21.
.12221.
.11111.
.......
ENDMAP

NAME:     vaults_room_7_spin_up3_water
TAGS:     vaults_room_7_spin_up3 allow_dup no_pool_fixup
SHUFFLE:  wW/IG/yz
SUBST:    W = w
SUBST:    Iyz = .
SHUFFLE:  {([<
SUBST:    < = <.
: vaults_ornamental_pool(_G)
MAP
ww...ww
wW.{.Ww
..III..
.<IGI(.
..III..
wW.[.Ww
ww...ww
ENDMAP

NAME:    vaults_room_7_spin_up3_hangedman_random_cross
TAGS:    vaults_room_7_spin_up3 allow_dup
SHUFFLE: ABCDEF
SUBST:   A : x:4 .:1, B : x:7 .:3, C : x:1 .:4, DEF = .
NSUBST:  { = 1:{ / 1:[ / 1:( / 1:<
MAP
...A...
.BC{DB.
.DEFEC.
A{F<F{A
.CEFED.
.BD{CB.
...A...
ENDMAP

NAME:     vaults_room_7_spin_up3_grunt_open
TAGS:     vaults_room_7_spin_up3 allow_dup no_monster_gen no_item_gen
SHUFFLE:  {([<
SUBST:    < = .
MAP
.......
.{...[.
.......
.......
.......
.(...<.
.......
ENDMAP

NAME:     vaults_room_7_spin_down3_cross
TAGS:     vaults_room_7_spin_down3 allow_dup
SHUFFLE:  })]>
SUBST:    > = >.
MAP
xx...xx
xxx.xxx
.x}.)x.
.......
.x>.]x.
xxx.xxx
xx...xx
ENDMAP

NAME:     vaults_room_7_spin_down3_grunt_open
TAGS:     vaults_room_7_spin_down3 allow_dup no_monster_gen no_item_gen
SHUFFLE: }])>
SUBST: > = .
MAP
.......
.}...].
.......
.......
.......
.)...>.
.......
ENDMAP

NAME:     vaults_room_7_spin_secret_subvaulted
TAGS:     vaults_room_7_spin_secret allow_dup no_monster_gen no_item_gen
SUBST:    c : x.
SUBST:    x : xx.
NSUBST:   1 = 1:0 / *:%
NSUBST:   2 = 1:0 / *:%
NSUBST:   3 = 1:0 / *:%
NSUBST:   4 = 1:0 / *:%
NSUBST:   0 = 1:0 / 1:% / *=%0
SUBVAULT: B : vaults_room_1_city_down1
: vaults_standard_loot(_G)
MAP
xc111cx
c.....c
4.BBB.2
4.BBB.2
4.BBB.2
c.....c
xc333cx
ENDMAP

NAME:     vaults_room_7_spin_secret_grid
TAGS:     vaults_room_7_spin_secret allow_dup no_monster_gen no_item_gen
SUBST:    w : x:5 m:1 w:8 .:2
: subvault_place_stair(_G)
: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
}.....*
.ww.ww.
.ww.ww.
...9...
.ww.ww.
.ww.ww.
*.....*
ENDMAP

# This is just "minmay_crypt_up_simple_box" with new tags
NAME:     vaults_room_7_spin_crypt_default
TAGS:     vaults_room_7_spin_crypt allow_dup no_monster_gen
MONS:     place:Crypt:1
KFEAT:    O = enter_crypt
MAP
.......
.cc.cc.
.c1c1c.
..cOc..
.c1c1c.
.cc.cc.
.......
ENDMAP

NAME:     vaults_room_7_spin_crypt_grunt
TAGS:     vaults_room_7_spin_crypt allow_dup no_item_gen no_monster_gen
NSUBST:   1 = 2:1 / *:2
MONS:     guardian mummy, mummy
KFEAT:    O = enter_crypt
MAP
.......
.1...1.
.......
...O...
.......
.1...1.
.......
ENDMAP

NAME:     vaults_room_7_spin_blade_pillars
TAGS:     vaults_room_7_spin_blade allow_dup no_monster_gen
SUBST:    y : x...
SUBST:    1 = 1.
MONS:     place:Blade:1
KFEAT:    O = enter_hall_of_blades
FTILE:    cxyO1. = floor_hall
MAP
xy...yx
y..c..y
..1.1..
.c.O.c.
..1.1..
y..c..y
xy...yx
ENDMAP

NAME:     vaults_room_7_spin_blade_grunt
TAGS:     vaults_room_7_spin_blade allow_dup no_item_gen no_monster_gen
NSUBST:   1 = 2:1 / *:.
MONS:     dancing weapon
KFEAT:    O = enter_hall_of_blades
MAP
.......
.1...1.
.......
...O...
.......
.1...1.
.......
ENDMAP

# Entries for ???_icecave_??? and ???_wizlab_??? are in .des files

NAME:     vaults_room_7_spin_bazaar_subvault
TAGS:     vaults_room_7_spin_bazaar allow_dup no_monster_gen no_item_gen
SUBVAULT: B : bzr_entry
SUBST:    B = .
MAP
.......
.BBBBB.
.BBBBB.
.BBBBB.
.BBBBB.
.BBBBB.
.......
ENDMAP

NAME:     vaults_room_7_spin_lab_hunger
TAGS:     vaults_room_7_spin_lab allow_dup no_monster_gen no_item_gen
NSUBST:   B = 1:B / 2:1.. / *:.
MONS:     hungry ghost
FTILE:    xB1. = floor_labyrinth
RTILE:    x = wall_lab_rock
SUBVAULT: B : uniq_lab
SUBST:    B = .
MAP
Bx.....
.x.xxx.
....Bx.
.xxxxx.
.xB....
.xxx.xx
Bx....B
ENDMAP

NAME:     vaults_room_7_spin_trove_subvault
TAGS:     vaults_room_7_spin_trove allow_dup no_monster_gen no_item_gen
SUBVAULT: B : chance_trove
SUBST:    B = .
MAP
.......
.BBBBB.
.BBBBB.
.BBBBB.
.BBBBB.
.BBBBB.
.......
ENDMAP

# not used because ziggurats are D only
NAME:     vaults_room_7_spin_ziggurat_subvault
TAGS:     vaults_room_7_spin_ziggurat allow_dup no_monster_gen no_item_gen
SUBVAULT: B : luniq_zig
SUBST:    B = .
MAP
BBBBBBB
BBBBBBB
BBBBBBB
BBBBBBB
BBBBBBB
BBBBBBB
BBBBBBB
ENDMAP



########
#  7x7 subvaults with a facing direction
#

NAME:     vaults_room_7_facing_empty_pool
TAGS:     vaults_room_7_facing_empty allow_dup no_pool_fixup
SUBVAULT: A : vaults_door_7
: vaults_ornamental_pool(_G)
MAP
.....xx
.www..x
.www...A
.www.G.A
.www...A
.www..x
.....xx
ENDMAP

NAME:     vaults_room_7_facing_empty_narrow
TAGS:     vaults_room_7_facing_empty allow_dup
SHUFFLE:  GI/wW
SUBST:    I = .
SUBST:    W = w
SUBVAULT: A : vaults_door_7
: vaults_ornamental_pool(_G)
MAP
GI...xx
II..xxx
...xx..A
.......A
...xx..A
II..xxx
GI...xx
ENDMAP

NAME:     vaults_room_7_facing_encounter_pool
TAGS:     vaults_room_7_facing_encounter allow_dup
TAGS:     no_pool_fixup no_monster_gen
NSUBST:   0 = 1:0 / *:.
NSUBST:   % = 1:%. / *:.
NSUBST:   . = 1:0. / *:.
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
%....xx
.www.0x
.www.00A
.www%G0A
.www.00A
.www.0x
%....xx
ENDMAP

NAME:     vaults_room_7_facing_encounter_yaktaur
TAGS:     vaults_room_7_facing_encounter allow_dup no_monster_gen
MONS:     yaktaur band
SUBVAULT: A : vaults_door_7
MAP
xx...xx
xx...xx
.......A
.2.....A
.......A
xx...xx
xx...xx
ENDMAP

NAME:     vaults_room_7_facing_dangerous_4_pools
TAGS:     vaults_room_7_facing_dangerous allow_dup
TAGS:     no_pool_fixup no_monster_gen
SHUFFLE:  pq/rs
NSUBST:   p = 1:0 / 1:.0 / 1:% / *:.
NSUBST:   q = 1:0 / *:.
NSUBST:   r = 1:9 / *:s
NSUBST:   s = 1:.0 / 1:% / *:.
SUBVAULT: A : vaults_door_7
: vaults_ornamental_pool(_G)
: vaults_standard_loot(_G)
MAP
ww...ww
ww.q.ww
..qpq..A
.qpppq.A
..qpq..A
ww.q.ww
ww...ww
ENDMAP

NAME:     vaults_room_7_facing_dangerous_pool
TAGS:     vaults_room_7_facing_dangerous allow_dup no_pool_fixup
WEIGHT:   1
MONS:     orange crystal statue / silver statue / ice statue
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
%....xx
.www..x
.www...A
.www.1.A
.www...A
.www..x
%....xx
ENDMAP

NAME:     vaults_room_7_facing_loot_narrow
TAGS:     vaults_room_7_facing_loot allow_dup
SHUFFLE:  GI/wW
SUBST:    I = .
SUBST:    W = w
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
GI...xx
II.%xxx
...xx..A
.%.....A
...xx..A
II.%xxx
GI...xx
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_7_facing_stairs1_narrow
TAGS:     vaults_room_7_facing_up1 vaults_room_7_facing_down1 allow_dup
SHUFFLE:  GI/wW
SUBST:    I = .
SUBST:    W = w
SUBVAULT: A : vaults_door_7
: vaults_ornamental_pool(_G)
: subvault_place_stair(_G)
MAP
GI...xx
II..xxx
...xx..A
.{.....A
...xx..A
II..xxx
GI...xx
ENDMAP

NAME:     vaults_room_7_facing_stairs1_statues
TAGS:     vaults_room_7_facing_down1 allow_dup no_monster_gen
WEIGHT:   4
SUBST:    1 = 12....
MONS:     orange crystal statue / silver statue / ice statue
MONS:     statue tile:mons_statue_archer name:archer n_adj; \
          longbow . arrow q:30
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
: subvault_place_stair(_G)
MAP
.......
.G.%.G.
.......A
.{.1...A
.......A
.G.%.G.
.......
ENDMAP

NAME:     vaults_room_7_facing_up3_pool
TAGS:     vaults_room_7_facing_up3 allow_dup no_pool_fixup
SHUFFLE:  {([
SUBVAULT: A : vaults_door_7
: vaults_ornamental_pool(_G)
MAP
.....xx
.www..x
.www{..A
.www(G.A
.www[..A
.www..x
.....xx
ENDMAP

NAME:     vaults_room_7_facing_down3_glass_wall
TAGS:     vaults_room_7_facing_down3 allow_dup no_monster_gen no_item_gen
SHUFFLE:  {([ / })]
SUBST:    {([ = .
SHUFFLE:  })]
NSUBST:   = = 1:+ / *:m
SUBVAULT: A : vaults_door_7
MAP
.....m.
.}.{.=.
.....=.A
.).(.=.A
.....=.A
.].[.=.
.....m.
ENDMAP

NAME:     vaults_room_7_facing_crypt_grunt
TAGS:     vaults_room_7_facing_crypt allow_dup no_item_gen no_monster_gen
NSUBST:   1 = 2:1 / *:2
MONS:     guardian mummy, mummy
KFEAT:    O = enter_crypt
SUBVAULT: A : vaults_door_7
MAP
.1.....
1......
.......A
...O...A
.......A
1......
.1.....
ENDMAP

NAME:     vaults_room_7_facing_blade_grunt
TAGS:     vaults_room_7_facing_blade allow_dup no_item_gen no_monster_gen
NSUBST:   1 = 2:1 / *:.
MONS:     dancing weapon
KFEAT:    O = enter_hall_of_blades
SUBVAULT: A : vaults_door_7
MAP
.1.....
1......
.......A
...O...A
.......A
1......
.1.....
ENDMAP



########
#  Dead-end 7x7 subvaults
#

NAME:     vaults_room_7_dead_end_empty_door_ring_water
TAGS:     vaults_room_7_dead_end_empty no_monster_gen no_item_gen
KPROP:    _ = no_rtele_into
SUBVAULT: A : vaults_door_7
MAP
_W___W_
WxxxxxW
_xxxxx.A
_xxxxx.A
_xxxxx.A
WxxxxxW
_W___W_
ENDMAP

NAME:     vaults_room_7_dead_end_empty_flooded
TAGS:     vaults_room_7_dead_end_empty allow_dup
SUBST:    W = w.
SUBVAULT: A : vaults_door_7
: vaults_ornamental_pool(_G)
MAP
xxwwWxx
xxwWWxx
wwwW...A
wwwW...A
wwwW...A
xxwWWxx
xxwwWxx
ENDMAP

# This is always water, never e.g. lava
NAME:     vaults_room_7_dead_end_empty_cross_dud
TAGS:     vaults_room_7_dead_end_empty allow_dup
NSUBST:   ~ = 1:~ / *:^
SUBST:    ^ = ~....
KMASK:    w = no_monster_gen
SUBVAULT: A : vaults_door_7
MAP
xxwwwxx
xxxwxxx
.xx^xx.A
.^^.~~.A
.xx^xx.A
xxxwxxx
xxwwwxx
ENDMAP

NAME:     vaults_room_7_dead_end_empty_glass_wall
TAGS:     vaults_room_7_dead_end_empty allow_dup
TAGS:     no_monster_gen no_item_gen
WEIGHT:   5
SHUFFLE:  %*
SUBST:    * = .
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
MAP
.....m.
.%.*.m.
.....m.A
9%.*.m.A
.....m.A
.%.*.m.
.....m.
ENDMAP

# 50% chance of door to loot
NAME:     vaults_room_7_dead_end_encounter_glass_wall
TAGS:     vaults_room_7_dead_end_encounter allow_dup
TAGS:     no_monster_gen no_item_gen
SHUFFLE:  %*
SUBST:    * = .
NSUBST:   = = 1:+m / *:m
KFEAT:    + = runed_door
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
MAP
.....m0
.%.*.=.
.....=.A
9%.*.=.A
.....=.A
.%.*.=.
.....m0
ENDMAP

NAME:     vaults_room_7_dead_end_encounter_doors
TAGS:     vaults_room_7_dead_end_encounter allow_dup
SUBST:    + = +x
NSUBST:   _ = 2:% / 6:0 / *:_
KPROP:    =+_%$0 = no_rtele_into
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
MAP
_+_+_+_
+x+x+x+
_+_+_+.A
+x+x+x.A
_+_+_+.A
+x+x+x+
_+_+_+_
ENDMAP

# average 54 + 91 + 40 + 18 = 203 gold
NAME:     vaults_room_7_dead_end_encounter_rich_statue
TAGS:     vaults_room_7_dead_end_encounter allow_dup no_monster_gen
WEIGHT:   5
SUBST:    g = gf
SUBST:    f = fe
SUBST:    e = ed
SUBST:    d = d.
MONS:     orange crystal statue / silver statue / ice statue
SUBVAULT: A : vaults_door_7
: vaults_numbered_gold_items(_G)
MAP
xxfedxx
xxfd.xx
gfed...A
gfe1...A
gfed...A
xxfd.xx
xxfedxx
ENDMAP

NAME:     vaults_room_7_dead_end_encounter_statue_hallway
TAGS:     vaults_room_7_dead_end_encounter allow_dup
NSUBST:   1 = 1:2 / *:1
MONS:     generate_awake statue name:warrior n_adj \
          tile:mons_statue_scythe ; scythe
MONS:     generate_awake statue name:warrior n_adj \
          tile:mons_statue_scythe ; good_item scythe
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
MAP
xxxxxxx
xxxxxxx
G.G.G.1x
.*.....+
G.G.G.1x
xxxxxxx
xxxxxxx
ENDMAP

NAME:     vaults_room_7_dead_end_dangerous_quarters
TAGS:     vaults_room_7_dead_end_dangerous allow_dup no_monster_gen
SHUFFLE:  CD
NSUBST:   % = 1:* / 1:. / *:%
SUBVAULT: A : vaults_door_7
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
: if crawl.random2(4) < 1 then
:   subvault("D : vaults_door_7")
: else
:   subst("D = x")
: end
: vaults_standard_loot(_G)
MAP
%%%C...
%%%C.9.
%%%C...+
DDDxxxxx
...B...+
.0.B.0.
...B...
ENDMAP

NAME:     vaults_room_7_dead_end_dangerous_shadowmancer
TAGS:     vaults_room_7_dead_end_dangerous no_monster_gen
SUBST:    G : G.
MONS:     generate_awake statue name:shadowy n_adj \
          tile:mons_statue_mage \
          spells:shadow_creatures;shadow_creatures;shadow_creatures;\
                 shadow_creatures;shadow_creatures;shadow_creatures
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
MAP
xx%*%xx
xxG%Gxx
%G...G.A
*%.1...A
%G...G.A
xxG%Gxx
xx%*%xx
ENDMAP

NAME:     vaults_room_7_dead_end_dangerous_hangedman_quadrants
TAGS:     vaults_room_7_dead_end_dangerous no_monster_gen allow_dup luniq
SUBST:    $ : $., % : $%., = : -., - : +:4 $:1
MAP
xx9x0xx
x.$x$0x
9$%-%$.+
xx=x=xx+
9$%-%$.+
x.$x$0x
xx9x0xx
ENDMAP

NAME:     vaults_room_7_dead_end_loot_glass_wall
TAGS:     vaults_room_7_dead_end_loot allow_dup
TAGS:      no_monster_gen no_item_gen
SHUFFLE:  %*
SUBST:    * = .
NSUBST:   = = 1:+ / *:m
KFEAT:    + = runed_door
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
MAP
.....m.
.%.*.=.
.....=.A
9%.*.=.A
.....=.A
.%.*.=.
.....m.
ENDMAP

NAME:     vaults_room_7_dead_end_loot_statue_hallway
TAGS:     vaults_room_7_dead_end_loot allow_dup
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
MAP
xxxxxxx
xxxxxxx
G.G.G.Gx
.*.....+
G.G.G.Gx
xxxxxxx
xxxxxxx
ENDMAP

# This is always water, never e.g. lava
NAME:     vaults_room_7_dead_end_loot_cross_dud
TAGS:     vaults_room_7_dead_end_loot allow_dup
NSUBST:   ~ = 1:~ / *:^
SUBST:    ^ = ~....
SUBST:    W = w.
KMASK:    w = no_monster_gen
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
MAP
Wwwwwxx
%Wxwxxx
.xx^xx.A
.^^.~~.A
.xx^xx.A
%Wxwxxx
Wwwwwxx
ENDMAP

NAME:     vaults_room_7_dead_end_loot_doors
TAGS:     vaults_room_7_dead_end_loot allow_dup
SUBST:    + = +x
NSUBST:   _ = 1:* / 4:% / 2:0 / *:_
KPROP:    =+_%$0 = no_rtele_into
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
MAP
_+_+_+_
+x+x+x+
_+_+_+.A
+x+x+x.A
_+_+_+.A
+x+x+x+
_+_+_+_
ENDMAP


NAME:     vaults_room_7_dead_end_stairs1_statues
TAGS:     vaults_room_7_dead_end_up1 allow_dup no_pool_fixup
SUBST:    + = +++xx
NSUBST:   _ = 1:{ / 2:0 / *:_
KPROP:    +_%$0 = no_rtele_into
SUBVAULT: A : vaults_door_7
: vaults_ornamental_pool(_G)
: subvault_place_stair(_G)
MAP
.......
.wwwww.
.wwwww.A
...{ww.A
.wwwww.A
.wwwww.
.......
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_7_dead_end_stairs1_door_maze
TAGS:     vaults_room_7_dead_end_up1 vaults_room_7_facing_down1 allow_dup
SHUFFLE:  pqr
SUBST:    p = +
SUBST:    qr = -
NSUBST:   s = 1:+ / *:-
NSUBST:   u = 3:+ / *:x
SUBST:    - = +x
NSUBST:   { = 1:{ / *:_
NSUBST:   _ = 1:% / 2:0 / *:_
KPROP:    +_%$0{ = no_rtele_into
SUBVAULT: A : vaults_door_7
: vaults_standard_loot(_G)
: subvault_place_stair(_G)
MAP
{u{p_-_
uxuxpx-
{u{q_s.A
-xrx+x.A
_-_r_s.A
-x-x-x-
_-_-_-_
ENDMAP

NAME:     vaults_room_7_dead_end_stairs1_statue_hallway
TAGS:     vaults_room_7_dead_end_down1 allow_dup
: subvault_place_stair(_G)
MAP
xxxxxxx
xxxxxxx
G.G.G.Gx
.{.....+
G.G.G.Gx
xxxxxxx
xxxxxxx
ENDMAP

NAME:     vaults_room_7_dead_end_up3_branches
TAGS:     vaults_room_7_dead_end_up3 allow_dup
SHUFFLE:  {([0
SUBVAULT: A : vaults_door_7
MAP
{....+.
xxxxxx.
(....+.A
xxxxxx.A
[....+.A
xxxxxx.
0....+.
ENDMAP

# This is basically grunt_vaults_arrival_ludios from Vaults.des
#
# With apologies to NetHack.
# You'd expect the second room to be completely full of monsters,
# but that would be just a little unfair.
NAME:     vaults_room_grunt_arrival_ludios
WEIGHT:   100 (Vaults:1), 3
TAGS:     vaults_room_7_dead_end_up3 generate_awake
TAGS:     no_monster_gen no_item_gen no_trap_gen
SHUFFLE:  {([
SUBST:    0 = 00.
MAP
.0.0.x.
0.0.0+.
.0.0.x.+
0.0.0x.+
..x+xxxx
%%x...(
%%x..{[
ENDMAP

NAME:     vaults_room_7_dead_end_down3_branches
TAGS:     vaults_room_7_dead_end_down3 allow_dup
SHUFFLE:  })]0
SUBVAULT: A : vaults_door_7
MAP
}....+.
xxxxxx.
)....+.A
xxxxxx.A
]....+.A
xxxxxx.
0....+.
ENDMAP



########
#  City-layout 7x7 subvaults
#

NAME:     vaults_room_7_city_empty_subvaults
TAGS:     vaults_room_7_city_empty allow_dup
SHUFFLE:  GHIJ
SUBST:    HIJ = .
SUBVAULT: A : vaults_room_1_city_empty
SUBVAULT: B : vaults_room_1_city_empty
SUBVAULT: C : vaults_room_1_city_empty
SUBVAULT: D : vaults_room_1_city_encounter
MAP
DDD...AAA
DDD.G.AAA
DDD...AAA
.........
.G..I..G.
.........
CCC...BBB
CCC.G.BBB
CCC...BBB
ENDMAP

NAME:     vaults_room_7_city_empty_quartered_pool
TAGS:     vaults_room_7_city_empty allow_dup no_pool_fixup
SHUFFLE:  GHIJ
SUBST:    HIJ = .
: vaults_ornamental_pool(_G)
MAP
..ww.ww..
.www.www.
wwww.wwww
wwwG.Gwww
....I....
wwwG.Gwww
wwww.wwww
.www.www.
..ww.ww..
ENDMAP

NAME:     vaults_room_7_city_empty_boxed_pool
TAGS:     vaults_room_7_city_empty allow_dup no_pool_fixup
SHUFFLE:  GHI
SUBST:    HI = .
SHUFFLE:  +-
SUBST:    - : x++++
: vaults_ornamental_pool(_G)
MAP
.IG...GI.
Ixx+-+xxI
GxwwwwwxG
.+wwwww+.
.-wwwww-.
.+wwwww+.
GxwwwwwxG
Ixx+-+xxI
.IG...GI.
ENDMAP

NAME:     vaults_room_7_city_empty_cross_statues
TAGS:     vaults_room_7_city_empty allow_dup
WEIGHT:   20
SHUFFLE:  GHIJK
SUBST:    HIJK = .
SUBVAULT: A : vaults_room_7_city_cross_wall
MAP
AAAAAAAAA
AAA...AAA
AAAGIGAAA
A.G...G.A
A.I.H.I.A
A.G...G.A
AAAGIGAAA
AAA...AAA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_empty_cross_water
TAGS:     vaults_room_7_city_empty allow_dup
SUBST:    W = w.
SUBVAULT: A : vaults_room_7_city_cross_wall
MAP
AAAAAAAAA
AAAWWWAAA
AAAwwwAAA
AWwwwwwWA
AWwwwwwWA
AWwwwwwWA
AAAwwwAAA
AAAWWWAAA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_empty_statue_rings
TAGS:     vaults_room_7_city_empty allow_dup
WEIGHT:   20
SHUFFLE:  GI
SUBST:    I : G.
SUBST:    H = G.
SUBVAULT: A : vaults_room_7_city_statue_rings
MAP
AAAAAAAAA
AA.....AA
A..AAA..A
A.A...A.A
A.A.G.A.A
A.A...A.A
A..AAA..A
AA.....AA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_encounter_cross
TAGS:     vaults_room_7_city_encounter allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
SUBVAULT: A : vaults_room_7_city_cross_wall
MAP
AAAAAAAAA
AAA...AAA
AAAGIGAAA
A.G...G.A
A.I.0.I.A
A.G...G.A
AAAGIGAAA
AAA...AAA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_encounter_statue_rings
TAGS:     vaults_room_7_city_encounter allow_dup
SUBST:    G = G0
NSUBST:   0 = 1:0 / *:.
SUBVAULT: A : vaults_room_7_city_statue_rings
MAP
AAAAAAAAA
AA.....AA
A..AAA..A
A.A000A.A
A.A0G0A.A
A.A000A.A
A..AAA..A
AA.....AA
AAAAAAAAA
ENDMAP

# Some kobolds to poke you
NAME:     vaults_room_7_city_encounter_mumra_kobold_gallery
TAGS:     vaults_room_7_city_encounter allow_dup no_pool_fixup luniq
SUBST:    AB = x
KFEAT:    H : w
KMONS:    1 = kobold ; trident / kobold ; spear
KPROP:    1% = no_rtele_into
MAP
xxxx.xxxx
xx1H.H1xx
xx1A.B1xx
x%1H.H1%x
xx1A*B1xx
x%1H.H1%x
xx1A.B1xx
xx1H.H1xx
xxxx.xxxx
ENDMAP

NAME:     vaults_room_7_city_dangerous_cross
TAGS:     vaults_room_7_city_dangerous allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
SHUFFLE:  pq
NSUBST:   p = 1:9 / 1:%. / *:.
NSUBST:   q = 3:0 / 1:% / *:.
SUBVAULT: A : vaults_room_7_city_cross_wall
: vaults_standard_loot(_G)
MAP
AAAAAAAAA
AAA...AAA
AAAGIGAAA
A.GpppG.A
A.IpppI.A
A.GpppG.A
AAAGIGAAA
AAA...AAA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_dangerous_ringed_statue
TAGS:     vaults_room_7_city_dangerous allow_dup
WEIGHT:   3
NSUBST:   % = 1:* / *:%
MONS:     orange crystal statue / silver statue / ice statue
SUBVAULT: A : vaults_room_7_city_statue_rings
: vaults_standard_loot(_G)
MAP
AAAAAAAAA
AA.....AA
A..AAA..A
A.A%%%A.A
A.A%1%A.A
A.A%%%A.A
A..AAA..A
AA.....AA
AAAAAAAAA
ENDMAP

# average 36 + 39 + 16 + 6 = 99 gold
NAME:     vaults_room_7_city_loot_glass_walls
TAGS:     vaults_room_7_city_loot allow_dup
SUBST:    G : G..
NSUBST:   = = 1:+ / *:m
NSUBST:   $ = 2:g / 3:f / 2:e / *:d
SUBVAULT: A : vaults_door_7
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
SUBVAULT: D : vaults_door_7
: vaults_numbered_gold_items(_G)
MAP
xxxBBBxxx
xGm...mGx
xmm===mmx
C.=$$$=.A
C.=$$$=.A
C.=$$$=.A
xmm===mmx
xGm...mGx
xxxDDDxxx
ENDMAP

NAME:     vaults_room_7_city_loot_statue_rings
TAGS:     vaults_room_7_city_loot allow_dup no_pool_fixup
WEIGHT:   5
SUBVAULT: A : vaults_room_7_city_statue_rings
: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
AAAAAAAAA
AAwwwwwAA
AwwAAAwwA
AwA...AwA
AwA.*.AwA
AwA...AwA
AwwAAAwwA
AAwwwwwAA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_loot_mumra_shop_pool
TAGS:     vaults_room_7_city_loot luniq
SUBST:    w = w:10 2:1
MONS:     plant
KMONS:    2 = electric eel
KFEAT:    2 = w
KFEAT:    s = any shop
FTILE:    _1s = floor_swamp
MAP
.........
.G.www.G.
..wwwww..
.wwwswww.
.ww1_1ww.
.www_www.
..ww_ww..
.G.w_w.G.
.........
ENDMAP

NAME:     vaults_room_7_city_loot_mumra_shop_minimaze
TAGS:     vaults_room_7_city_loot no_wall_fixup luniq
NSUBST:   s = 1:s / *:.
KFEAT:    s = any shop
RTILE:    x = wall_lab_rock
MAP
.xsx...x.
.x.x.x.x.
.x.xsx...
.x.xxx.xx
...x...x.
.xxx.xxx.
...x.x...
.xxx.xxx.
.........
ENDMAP

NAME:     vaults_room_7_city_up1_statue_rings
TAGS:     vaults_room_7_city_up1 allow_dup
WEIGHT:   5
SUBVAULT: A : vaults_room_7_city_statue_rings
: subvault_place_stair(_G)
MAP
AAAAAAAAA
AA.....AA
A..AAA..A
A.A...A.A
A.A.{.A.A
A.A...A.A
A..AAA..A
AA.....AA
AAAAAAAAA
ENDMAP

NAME:     vaults_room_7_city_down1_subvaults
TAGS:     vaults_room_7_city_down1 allow_dup
SHUFFLE:  GH
SUBST:    H = .
SUBVAULT: A : vaults_room_1_city_empty
SUBVAULT: B : vaults_room_1_city_empty
SUBVAULT: C : vaults_room_1_city_empty
SUBVAULT: D : vaults_room_1_city_encounter
: subvault_place_stair(_G)
MAP
DDD...AAA
DDD.G.AAA
DDD...AAA
.........
.G..{..G.
.........
CCC...BBB
CCC.G.BBB
CCC...BBB
ENDMAP

NAME:     vaults_room_7_city_up3_glass_box
TAGS:     vaults_room_7_city_up3 allow_dup
SUBST:    G : G__
KPROP:    _ = no_rtele_into
SHUFFLE:  pqrs
SUBST:    p = +
SUBST:    qrs = m
SHUFFLE:  {}
SUBST:    } = .
NSUBST:   { = 1:{ / 1:( / 1:[ / *:<
SUBVAULT: A : vaults_door_7
SUBVAULT: B : vaults_door_7
SUBVAULT: C : vaults_door_7
SUBVAULT: D : vaults_door_7
MAP
xxxBBBxxx
xGm...mGx
xmxqqqxmx
C.r{}{p.A
C.r}.}p.A
C.r{}{p.A
xmxsssxmx
xGm...mGx
xxxDDDxxx
ENDMAP

NAME:     vaults_room_7_city_down3_cross
TAGS:     vaults_room_7_city_down3 allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
SHUFFLE:  {}
SUBST:    { = .
NSUBST:   } = 1:} / 1:) / 1:] / *:>
SUBVAULT: A : vaults_room_7_city_cross_wall
: vaults_standard_loot(_G)
MAP
AAAAAAAAA
AAA...AAA
AAAGIGAAA
A.G}{}G.A
A.I{.{I.A
A.G}{}G.A
AAAGIGAAA
AAA...AAA
AAAAAAAAA
ENDMAP

# a subvault used for the walls of cross-shaped 7x7 city rooms
NAME:     vaults_room_7_city_cross_wall
TAGS:     vaults_room_7_city_cross_wall allow_dup
SUBST:    G : G....
SHUFFLE:  ABCD
SUBVAULT: A : vaults_door_7
: if crawl.random2(6) < 1 then
:   subvault("B : vaults_door_7")
: else
:   subst("B = x")
: end
: if crawl.random2(6) < 1 then
:   subvault("C : vaults_door_7")
: else
:   subst("C = x")
: end
: if crawl.random2(6) < 1 then
:   subvault("D : vaults_door_7")
: else
:   subst("D = x")
: end
MAP
G.xBBBx.G
..x   x..
xxx   xxx
C       A
C       A
C       A
xxx   xxx
..x   x..
G.xDDDx.G
ENDMAP

# a subvault used for the double statue rings
NAME:     vaults_room_7_city_statue_rings
TAGS:     vaults_room_7_city_statue_rings allow_dup
SHUFFLE:  GH
SHUFFLE:  IJ
SHUFFLE:  GI
SUBST:    I : G.
SUBST:    HJ = .
MAP
..HG.GH..
.G     G.
H  IJI  H
G I   I G
. J   J .
G I   I G
H  IJI  H
.G     G.
..HG.GH..
ENDMAP




################################################################
#  10x10 subvaults
#

########
#  Doors for 10x10 subvaults
#

NAME:     vaults_door_10
TAGS:     vaults_door_10 allow_dup
SUBST:    - = xx+
MAP
-++-
ENDMAP

NAME:     vaults_door_10_double
TAGS:     vaults_door_10 allow_dup
WEIGHT:   1
MAP
+xx+
ENDMAP

NAME:     vaults_door_10_side
TAGS:     vaults_door_10 allow_dup
WEIGHT:   2
SHUFFLE:  +-
SUBST:    - = x+
MAP
-+xx
ENDMAP



########
#  Free-spinning 10x10 subvaults
#

NAME:     vaults_room_10_spin_empty_octagon
TAGS:     vaults_room_10_spin_empty allow_dup
SUBST:    G : Gc..
MAP
xxx....xxx
xx......xx
x........x
...G..G...
..........
..........
...G..G...
x........x
xx......xx
xxx....xxx
ENDMAP

NAME:     vaults_room_10_spin_empty_grid
TAGS:     vaults_room_10_spin_empty allow_dup
SHUFFLE:  xyz
SUBST:    x : xxxxw
SUBST:    y : xxxw.
SUBST:    z : xxw..
: vaults_ornamental_pool(_G)
MAP
..........
.xx.yy.xx.
.xx.yy.xx.
..........
.yy.zz.yy.
.yy.zz.yy.
..........
.xx.yy.xx.
.xx.yy.xx.
..........
ENDMAP

NAME:     vaults_room_10_spin_empty_pool
TAGS:     vaults_room_10_spin_empty allow_dup no_pool_fixup
SHUFFLE:  GHIJK
SUBST:    G : G.
SUBST:    HIJK = .
: vaults_ornamental_pool(_G)
MAP
J.K....K.J
.GHI..IHG.
KHwwwwwwHK
.IwwwwwwI.
..wwwwww..
..wwwwww..
.IwwwwwwI.
KHwwwwwwHK
.GHI..IHG.
J.K....K.J
ENDMAP

NAME:     vaults_room_10_spin_empty_grid_feature
TAGS:     vaults_room_10_spin_empty allow_dup
SHUFFLE:  xyz
SUBST:    x : xxxxw
SUBST:    y : xxxw.
SUBST:    z : xxw..
SUBST:    ! = SC
SUBST:    T : TT^...
SUBST:    T = TTTUVV
KFEAT:    S = any shop
KFEAT:    ^ = known zot trap
: vaults_ornamental_pool(_G)
MAP
..........
.xx.yy.xx.
.xx.yy.xx.
...!..T...
.yy.zz.yy.
.yy.zz.yy.
...T..T...
.xx.yy.xx.
.xx.yy.xx.
..........
ENDMAP

NAME:     vaults_room_10_spin_encounter_octagon
TAGS:     vaults_room_10_spin_encounter allow_dup no_monster_gen
SUBST:    G : G..
NSUBST:   0 = 1:0 / *:.
: vaults_standard_loot(_G)
MAP
xxx....xxx
xx......xx
x........x
...G..G...
....00....
....00....
...G..G...
x........x
xx......xx
xxx....xxx
ENDMAP

NAME:     vaults_room_10_spin_encounter_pool_traps
TAGS:     vaults_room_10_spin_encounter allow_dup no_pool_fixup
WEIGHT:   2
SHUFFLE:  GHIJK
SUBST:    G : G.
SUBST:    HIJK = .
SUBST:    . = ~.
: vaults_ornamental_pool(_G)
MAP
J.K....K.J
.GHI..IHG.
KHwwwwwwHK
.IwwwwwwI.
..wwwwww..
..wwwwww..
.IwwwwwwI.
KHwwwwwwHK
.GHI..IHG.
J.K....K.J
ENDMAP

NAME:     vaults_room_10_spin_encounter_4_subvaults
TAGS:     vaults_room_10_spin_encounter allow_dup
SHUFFLE:  ABCD
SUBVAULT: A : vaults_room_1_city_empty
SUBVAULT: B : vaults_room_1_city_encounter
SUBVAULT: C : vaults_room_1_city_encounter
SUBVAULT: D : vaults_room_1_city_loot
MAP
..........
.DDD..AAA.
.DDD..AAA.
.DDD..AAA.
..........
..........
.CCC..BBB.
.CCC..BBB.
.CCC..BBB.
..........
ENDMAP

NAME:     vaults_room_10_spin_dangerous_grid
TAGS:     vaults_room_10_spin_dangerous allow_dup no_monster_gen
SHUFFLE:  xyz
SUBST:    x : xxxxw
SUBST:    y : xxxw.
SUBST:    z : xxw..
NSUBST:   0 = 1:% / *:0
: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
..........
.xx.yy.xx.
.xx.yy.xx.
...0..0...
.yy.zz.yy.
.yy.zz.yy.
...0..0...
.xx.yy.xx.
.xx.yy.xx.
..........
ENDMAP

# average 36 + 26 + 32 + 6 = 100 gold
NAME:     vaults_room_10_spin_dangerous_glass_cross
TAGS:     vaults_room_10_spin_dangerous allow_dup no_monster_gen
SUBST:    x : mxx
SUBST:    w : xw.
NSUBST:   9 = 1:9 / *:.
NSUBST:   $ = 2:g / 2:f / 4:e / 2:d / *:.
: vaults_ornamental_pool(_G)
: vaults_numbered_gold_items(_G)
MAP
ww......ww
ww......ww
..9xmmx9..
..xx$$xx..
..m$$$$m..
..m$$$$m..
..xx$$xx..
..9xmmx9..
ww......ww
ww......ww
ENDMAP

NAME:     vaults_room_10_spin_loot_octagon
TAGS:     vaults_room_10_spin_loot allow_dup no_monster_gen
SUBST:    G : Gc.%
SUBST:    % = %.
: vaults_standard_loot(_G)
MAP
xxx....xxx
xx......xx
x........x
...G%%G...
...%%%%...
...%%%%...
...G%%G...
x........x
xx......xx
xxx....xxx
ENDMAP

NAME:     vaults_room_10_spin_loot_glass_cross
TAGS:     vaults_room_10_spin_loot allow_dup
SUBST:    x : mxx
SUBST:    w : xw.
: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
ww......ww
ww......ww
...xmmx...
..xx%%xx..
..m%%%%m..
..m%%%%m..
..xx%%xx..
...xmmx...
ww......ww
ww......ww
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_10_spin_stairs1_spiral
TAGS:     vaults_room_10_spin_up1
TAGS:     vaults_room_10_spin_down1 allow_dup
NSUBST:   { = 1:{ / 1:0 / *=0%..
: subvault_place_stair(_G)
MAP
..........
.x+xxxxxx.
.x.x{...+.
.x.xxxxxx.
.x.xxxx{x.
.x{xxxx.x.
.xxxxxx.x.
.+...{x.x.
.xxxxxx+x.
..........
ENDMAP

NAME:     vaults_room_10_spin_up1_statues
TAGS:     vaults_room_10_spin_up1 allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
SUBVAULT: B : vaults_room_4_city_up1
MAP
I.G....G.I
..........
G.BBBBBB.G
..BBBBBB..
..BBBBBB..
..BBBBBB..
..BBBBBB..
G.BBBBBB.G
..........
I.G....G.I
ENDMAP

NAME:     vaults_room_10_spin_down1_statues
TAGS:     vaults_room_10_spin_down1 allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
SUBVAULT: B : vaults_room_4_city_down1
MAP
I.G....G.I
..........
G.BBBBBB.G
..BBBBBB..
..BBBBBB..
..BBBBBB..
..BBBBBB..
G.BBBBBB.G
..........
I.G....G.I
ENDMAP

NAME:     vaults_room_10_spin_up3_grid
TAGS:     vaults_room_10_spin_up3 allow_dup no_pool_fixup
SHUFFLE:  xyz
SUBST:    x : xxxxw
SUBST:    y : xxxw.
SUBST:    z : xxw..
SHUFFLE:  {([<
SUBST:    < = <.
: vaults_ornamental_pool(_G)
MAP
..........
.xx.yy.xx.
.xx.yy.xx.
...{..(...
.yy.zz.yy.
.yy.zz.yy.
...<..[...
.xx.yy.xx.
.xx.yy.xx.
..........
ENDMAP

NAME:     vaults_room_10_spin_down3_grid
TAGS:     vaults_room_10_spin_down3 allow_dup no_pool_fixup
SHUFFLE:  xyz
SUBST:    x : xxxxw
SUBST:    y : xxxw.
SUBST:    z : xxw..
SHUFFLE:  })]>
SUBST:    > = >.
: vaults_ornamental_pool(_G)
MAP
..........
.xx.yy.xx.
.xx.yy.xx.
...}..)...
.yy.zz.yy.
.yy.zz.yy.
...>..]...
.xx.yy.xx.
.xx.yy.xx.
..........
ENDMAP

NAME:     vaults_room_10_spin_secret_4_subvaults
TAGS:     vaults_room_10_spin_secret allow_dup no_monster_gen no_item_gen
SHUFFLE:  ABCD
SUBVAULT: A : vaults_room_1_city_down1
SUBVAULT: B : vaults_room_1_city_dangerous
SUBVAULT: C : vaults_room_1_city_dangerous /\
              vaults_room_1_city_loot
SUBST:    D = %
: vaults_standard_loot(_G)
MAP
..........
.DDD..AAA.
.DDD..AAA.
.DDD..AAA.
..........
..........
.CCC..BBB.
.CCC..BBB.
.CCC..BBB.
..........
ENDMAP

NAME:     vaults_room_10_spin_secret_pinwheel
TAGS:     vaults_room_10_spin_secret allow_dup no_monster_gen no_item_gen
NSUBST:   * = 1:* / *=***1
MONS:     vault guard
: subvault_place_stair(_G)
: vaults_standard_loot(_G)
MAP
x{+....xxx
xxxxxx..x*
x....xx.x+
..xx..x.x.
.xx.11..x.
.x..11.xx.
.x.x..xx..
+x.xx....x
*x..xxxxxx
xxx....+*x
ENDMAP

NAME:     vaults_room_10_spin_crypt_double
TAGS:     vaults_room_10_spin_crypt no_wall_fixup allow_dup
SUBST:    m : xxm
MONS:     place:Crypt:1
KFEAT:    m = iron_grate
KFEAT:    O = enter_crypt
FTILE:    xmO1+_ = floor_crypt
RTILE:    x = wall_crypt
MAP
..........
......xxx.
.x+x..xOx.
.m_m..x+x.
.m1m..m_m.
.m_m..m1m.
.x+x..m_m.
.x1x..x+x.
.xxx......
..........
ENDMAP

NAME:     vaults_room_10_spin_blade_pillars
TAGS:     vaults_room_10_spin_blade allow_dup
SUBST:    y : x.
SHUFFLE:  PQRS/pqrs
SUBST:    PQRS = .
SUBST:    p = O
SUBST:    qs = 1
SUBST:    r = .
MONS:     place:Blade:1
KFEAT:    O = enter_hall_of_blades
FTILE:    xO1. = floor_hall
MAP
y........y
..x....x..
.xxx..xxx.
..xR..Qx..
....rq....
....sp....
..xS..Px..
.xxx..xxx.
..x....x..
y........y
ENDMAP

# Entries for ???_icecave_??? and ???_wizlab_??? are in .des files

NAME:     vaults_room_10_spin_bazaar_subvault
TAGS:     vaults_room_10_spin_bazaar allow_dup no_monster_gen no_item_gen
SHUFFLE:  GHI
SUBST:    HI = .
SUBVAULT: B : bzr_entry
SUBST:    B = .
MAP
I.G....G.I
..........
G.BBBBBB.G
..BBBBBB..
..BBBBBB..
..BBBBBB..
..BBBBBB..
G.BBBBBB.G
..........
I.G....G.I
ENDMAP

NAME:     vaults_room_10_spin_lab_harpies
TAGS:     vaults_room_10_spin_lab allow_dup no_monster_gen no_item_gen
SUBST:    G : G.
SUBST:    z : y.
SUBST:    y : xx.
MONS:     harpy band
SUBVAULT: B : uniq_lab
SUBST:    B = .
MAP
xxx....xxx
xxxG..Gxxx
xxxy..yxxx
.Gyz..zyG.
....B.....
.....1....
.Gyz..zyG.
xxxy..yxxx
xxxG..Gxxx
xxx....xxx
ENDMAP

NAME:     vaults_room_10_spin_trove_subvault
TAGS:     vaults_room_10_spin_trove allow_dup no_monster_gen no_item_gen
SUBVAULT: B : chance_trove
SUBST:    B = .
MAP
..........
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
..........
ENDMAP

# not used because ziggurats are D only
NAME:     vaults_room_10_spin_ziggurat_subvault
TAGS:     vaults_room_10_spin_ziggurat allow_dup no_monster_gen no_item_gen
SUBVAULT: B : luniq_zig
SUBST:    B = .
MAP
..........
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
.BBBBBBBB.
..........
ENDMAP



#
#  A set of rooms that place subvaults in the center
#

NAME:     vaults_room_10_spin_empty_subvault_center
TAGS:     vaults_room_10_spin_empty allow_dup
SUBVAULT: A : vaults_room_10_spin_subvault_wall
SUBVAULT: B : vaults_room_4_spin_empty

MAP
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
ENDMAP

NAME:     vaults_room_10_spin_encounter_subvault_center
TAGS:     vaults_room_10_spin_encounter allow_dup
SUBVAULT: A : vaults_room_10_spin_subvault_wall
SUBVAULT: B : vaults_room_4_spin_encounter

MAP
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
ENDMAP

NAME:     vaults_room_10_spin_dangerous_subvault_center
TAGS:     vaults_room_10_spin_dangerous allow_dup
SUBVAULT: A : vaults_room_10_spin_subvault_wall
SUBVAULT: B : vaults_room_4_spin_dangerous

MAP
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
ENDMAP

NAME:     vaults_room_10_spin_loot_subvault_center
TAGS:     vaults_room_10_spin_loot allow_dup
SUBVAULT: A : vaults_room_10_spin_subvault_wall
SUBVAULT: B : vaults_room_4_spin_loot
MAP
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
ENDMAP

NAME:     vaults_room_10_spin_crypt_subvault_center
TAGS:     vaults_room_10_spin_crypt allow_dup
SUBVAULT: A : vaults_room_10_spin_subvault_wall
SUBVAULT: B : vaults_room_4_spin_crypt
MAP
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
ENDMAP

NAME:     vaults_room_10_spin_blade_subvault_center
TAGS:     vaults_room_10_spin_blade allow_dup
SUBVAULT: A : vaults_room_10_spin_subvault_wall
SUBVAULT: B : vaults_room_4_spin_blade
MAP
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAABBBBAAA
AAAAAAAAAA
AAAAAAAAAA
AAAAAAAAAA
ENDMAP

NAME:     vaults_room_10_spin_subvault_wall
TAGS:     vaults_room_10_spin_subvault_wall allow_dup
SUBST:    G : xG..
SUBVAULT: C : vaults_door_4
# doors to central room
: if crawl.random2(6) < 1 then
:   subvault("D : vaults_door_4")
: else
:   subst("D = x")
: end
: if crawl.random2(6) < 1 then
:   subvault("E : vaults_door_4")
: else
:   subst("E = x")
: end
: if crawl.random2(6) < 1 then
:   subvault("F : vaults_door_4")
: else
:   subst("F = x")
: end
# doors bewteen areas
: if crawl.random2(4) < 1 then
:   subvault("H : vaults_door_4")
:   subvault("I : vaults_door_4")
:   subvault("J : vaults_door_4")
:   subvault("K : vaults_door_4")
:   subvault("L : vaults_door_4")
:   subvault("M : vaults_door_4")
:   subvault("N : vaults_door_4")
:   subvault("O : vaults_door_4")
: else
:   subst("GHIJKLMN = .")
: end
MAP
G.K....J.G
..K....J..
LLxxDDxxII
..x    x..
..E    C..
..E    C..
..x    x..
MMxxFFxxHH
..N....O..
G.N....O.G
ENDMAP



########
#  10x10 subvaults with a facing direction
#

NAME:     vaults_room_10_facing_empty_door_hall
TAGS:     vaults_room_10_facing_empty allow_dup
SHUFFLE:  GHJ
SUBST:    HJ = .
SUBST:    I : xG
SUBST:    J : xG
SUBVAULT: A : vaults_door_10
MAP
H.G....xxx
.......Jxx
G..xxxxxxx
...xIxIx..A
...+.+.+..A
...+.+.+..A
...xIxIx..A
G..xxxxxxx
.......Jxx
H.G....xxx
ENDMAP

NAME:     vaults_room_10_facing_empty_2_pools
TAGS:     vaults_room_10_facing_empty allow_dup no_pool_fixup
SUBVAULT: A : vaults_door_10
: vaults_ornamental_pool(_G)
MAP
..........
.wwwwwwww.
.wwwwwwww.
.wwwwwwww.A
..........A
..........A
.wwwwwwww.A
.wwwwwwww.
.wwwwwwww.
..........
ENDMAP

NAME:     vaults_room_10_facing_encounter_octagon_archers
TAGS:     vaults_room_10_facing_encounter allow_dup no_monster_gen
SHUFFLE:  123456
SUBST:    23456 = .
SHUFFLE:  12
MONS:     human ; bow . arrow q:20
MONS:     human ; crossbow . bolt q:20
SUBVAULT: A : vaults_door_10
MAP
xxx5.23xxx
xx4.1..6xx
x4.2.....x
3.15..6...A
..2.......A
..2.......A
3.15..6...A
x4.2.....x
xx4.1..6xx
xxx5.23xxx
ENDMAP

NAME:     vaults_room_10_facing_encounter_2_pools
TAGS:     vaults_room_10_facing_encounter allow_dup
TAGS:     no_pool_fixup no_monster_gen
NSUBST:   0 = 1:0 / *:.
NSUBST:   % = 1:%. / *:.
NSUBST:   . = 1:0. / *:.
SUBVAULT: A : vaults_door_10
: vaults_standard_loot(_G)
: vaults_ornamental_pool(_G)
MAP
..........
.wwwwwwww.
.wwwwwwww.
.wwwwwwww.A
.%%000....A
.%%000....A
.wwwwwwww.A
.wwwwwwww.
.wwwwwwww.
..........
ENDMAP

NAME:     vaults_room_10_facing_dangerous_door_hall
TAGS:     vaults_room_10_facing_dangerous allow_dup no_monster_gen
SHUFFLE:  GHJ
SUBST:    HJ = .
SUBST:    I : xG
SUBST:    J : xG
SHUFFLE:  12
SUBST:    2 = .
SHUFFLE:  12
MONS:     rock troll
MONS:     iron troll
SUBVAULT: A : vaults_door_10
MAP
H.G....xxx
.......Jxx
G..xxxxxxx
...xIxIx..A
...+2+1+..A
...+2+1+..A
...xIxIx..A
G..xxxxxxx
.......Jxx
H.G....xxx
ENDMAP

NAME:     vaults_room_10_facing_loot_door_hall
TAGS:     vaults_room_10_facing_loot allow_dup no_monster_gen
SHUFFLE:  GHJ
SUBST:    HJ = $
SUBST:    I : xG
SUBST:    J : xG
SUBVAULT: A : vaults_door_10
MAP
H$G....xxx
$$.....Jxx
G..xxxxxxx
...xIxIx..A
...+.+.+..A
...+.+.+..A
...xIxIx..A
G..xxxxxxx
$$.....Jxx
H$G....xxx
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_10_facing_stairs1_narrow
TAGS:     vaults_room_10_facing_up1
TAGS:     vaults_room_10_facing_down1 allow_dup
SHUFFLE:  GHJ
SUBST:    HJ = .
SUBST:    I : xG
NSUBST:   { : 1:{ / *:x
SUBVAULT: A : vaults_door_10
: subvault_place_stair(_G)
MAP
H.G....xxx
.......{xx
G..xxxxxxx
...xIxIx..A
...+.+.+..A
...+.+.+..A
...xIxIx..A
G..xxxxxxx
.......{xx
H.G....xxx
ENDMAP

NAME:     vaults_room_10_facing_up3_boxes
TAGS:     vaults_room_10_facing_up3 allow_dup no_pool_fixup
SUBST:    v : c.
SUBST:    c : xx.
SUBST:    G : G.
SUBST:    I : G.
SHUFFLE:  {([
SUBVAULT: A : vaults_door_10
MAP
........+(
..xxx..Ixx
..x[x.....
..x.x.vv..A
..x.x.cc..A
..x.x.cc..A
..x.x.vv..A
..x+x.....
..G.G..Ixx
........+{
ENDMAP

NAME:     vaults_room_10_facing_down3_boxes
TAGS:     vaults_room_10_facing_down3 allow_dup no_pool_fixup
SUBST:    v : c.
SUBST:    c : xx.
SUBST:    G : G.
SUBST:    I : G.
SHUFFLE:  })]
SUBVAULT: A : vaults_door_10
MAP
........+)
..xxx..Ixx
..x]x.....
..x.x.vv..A
..x.x.cc..A
..x.x.cc..A
..x.x.vv..A
..x+x.....
..G.G..Ixx
........+}
ENDMAP



########
#  Dead-end 10x10 subvaults
#

NAME:     vaults_room_10_dead_end_empty_semicircle
TAGS:     vaults_room_10_dead_end_empty allow_dup
WEIGHT:   20
SUBST:    - = x+
MAP
xxxxxxxxxx
xxxxxx....
xxxx......
xxx.......x
xx........-
xx...G....+
xx........-
xxx.......
xxxx......
xxxxxx....
ENDMAP

# The name is from the old letter
NAME:     vaults_room_10_dead_end_empty_thorn_pools
TAGS:     vaults_room_10_dead_end_empty allow_dup no_pool_fixup
SUBVAULT: A : vaults_door_10
: vaults_ornamental_pool(_G)
MAP
ww..wwwwww
ww..wwwwww
ww........
ww..wwww..A
ww..wwww..A
ww..wwww..A
ww..wwww..A
ww........
ww..wwwwww
ww..wwwwww
ENDMAP

NAME:     vaults_room_10_dead_end_empty_double_glass
TAGS:     vaults_room_10_dead_end_empty allow_dup
TAGS:     no_monster_gen no_item_gen
KPROP:  _ = no_rtele_into
MAP
xxxxxxxxxx
xx_______x
xx_______x
xxmmmmmmmxx
xG........+
xxxxxxxxxxx
xG........+
xxmmmmmmmx
xx_______x
xx_______x
ENDMAP

# Loosely "loot" due to having some large rocks
NAME:     vaults_room_10_dead_end_empty_mumra_cave_in
TAGS:     vaults_room_10_dead_end_empty allow_dupe luniq
SUBST:    B = _:10 B:4 x:2
SUBST:    C = _:12 C:2 B:3 x:2
SUBST:    x = _:10 x:3
KITEM:    C = large rock
KITEM:    B = stone
KPROP:  _ = no_rtele_into
SUBVAULT: A : vaults_door_10
MAP
xxx____xxx
xxxBBBBxxx
xxBBBBBBxx
_BBCCCCBB.A
_BBCCCCBB.A
_BBCCCCBB.A
_BBCCCCBB.A
xxBBBBBBxx
xxxBBBBxxx
xxx____xxx
ENDMAP

NAME:     vaults_room_10_dead_end_empty_mumra_latrine_1
TAGS:     vaults_room_10_dead_end_empty allow_dup luniq_mumra_latrine
KMONS:    p = plant
# Might have a single fish
KMONS:    f = big fish
KFEAT:    f = W
SUBST:    w = W:8 f
# Maybe a little flooding
SUBST:    1 = .:3 W
SUBST:    2 = .:3 W
SUBST:    3 = .:3 W
# Some empty faucets
SUBST:    T = T:4 V:1
# Rare chance of blood fountain
SUBST:    T = T:10 Y:1
SUBVAULT: A : vaults_door_10
MAP
xxxxxxxxxx
xxWxWxWxWx
xx+x+x+x+x
xp1133....A
xT1T3T.T..A
x1133322..A
xT3T3T2T..A
xp33.2222x
xx+x+x+x+x
xxWxWxWxWx
ENDMAP

NAME:     vaults_room_10_dead_end_empty_mumra_latrine_2
TAGS:     vaults_room_10_dead_end_empty allow_dup luniq_mumra_latrine
KMONS:    p = plant
# Maybe a little flooding
SUBST:    1 = .:3 W
SUBST:    2 = .:3 W
SUBST:    3 = .:3 W
# Might have the odd fish
KMONS:    f = big fish
KFEAT:    f = W
SUBST:    w = W:8 f
# Some empty faucets
SUBST:    T = T:4 V:1
# Rare chance of blood fountain
SUBST:    T = T:10 Y:1
SUBVAULT: A : vaults_door_10
MAP
xxxxxxxxxx
xT.p3T3p.T
x..333333.
xp.xxxxx.pA
x..+WcW+.2A
xT1xxxxx2TA
x11+WcW+22A
xp1xxxxx2p
x111..222.
xT.1.T.2.T
ENDMAP

NAME:     vaults_room_10_dead_end_encounter_semicircle
TAGS:     vaults_room_10_dead_end_encounter allow_dup
TAGS:     no_pool_fixup no_monster_gen
SUBST:    - = x+
NSUBST:   0 = 1:0 / 1:0. / *:.
NSUBST:   % = 1:% / *:.
: vaults_standard_loot(_G)
MAP
xxxxxxxxxx
xxxxxx%...
xxxx%.....
xxx%..00..x
xx%..000..-
xx%..G00..+
xx%..000..-
xxx%..00..
xxxx%.....
xxxxxx%...
ENDMAP

NAME:     vaults_room_10_dead_end_encounter_thorn_pools
TAGS:     vaults_room_10_dead_end_encounter allow_dup no_pool_fixup
SUBVAULT: A : vaults_door_10
: vaults_ornamental_pool(_G)
MAP
ww..wwwwww
ww..wwwwww
ww.0......
ww..wwww..A
ww..wwww..A
ww..wwww..A
ww..wwww..A
ww.0......
ww..wwwwww
ww..wwwwww
ENDMAP

NAME:     vaults_room_10_dead_end_encounter_many_doors
TAGS:     vaults_room_10_dead_end_encounter allow_dup no_monster_gen
SHUFFLE:  GHIJ
SUBST:    HIJ = .
NSUBST:   _ = 1:% / *= 0
: vaults_standard_loot(_G)
MAP
xxxxxxxxxx
xxxxxx_x_x
xxxxxx+x+x
xxxxxG.G.Gx
xxx_+.H.H.+
xxxxxG.I.Gx
xxx_+.H.H.+
xxxxxG.G.G
xxxxxx+x+x
xxxxxx_x_x
ENDMAP

#  I actually changed this one quite a bit.  The original had
#    wax walls and no monsters.  ~infinplex
NAME:     vaults_room_10_dead_end_encounter_mumra_exhibit_hive
TAGS:     vaults_room_10_dead_end_encounter no_monster_gen no_item_gen
NSUBST:   1 = 1:1 / 1:d / 1:e / *:.
NSUBST:   . = 7:2 / 2:3 / 2:e / *:.
MONS:     queen bee
MONS:     killer bee
MONS:     yellow wasp / red wasp
ITEM:     royal jelly
ITEM:     honeycomb
SUBVAULT: A : vaults_door_10
MAP
xxxxxxxxxx
xxxxx..xxx
xxxx....xx
xx11......A
x111......A
x111......A
xx11......A
xxxx....xx
xxxxx..xxx
xxxxxxxxxx
ENDMAP

NAME:     vaults_room_10_dead_end_dangerous_semicircle
TAGS:     vaults_room_10_dead_end_dangerous allow_dup no_monster_gen
WEIGHT:   5
SUBST:    - = x+
NSUBST:   % = 1:* / *=%.
MONS:     orange crystal statue / silver statue / ice statue
: vaults_standard_loot(_G)
MAP
xxxxxxxxxx
xxxxxx%...
xxxx%.....
xxx%......x
xx%.......-
xx%..1....+
xx%.......-
xxx%......
xxxx%.....
xxxxxx%...
ENDMAP

NAME:     vaults_room_10_dead_end_dangerous_many_doors
TAGS:     vaults_room_10_dead_end_dangerous allow_dup no_monster_gen
SHUFFLE:  GHIJ
SUBST:    HIJ = .
SUBST:    _ = %.
: vaults_standard_loot(_G)
MAP
xxxxxxxxxx
xxxxxx_x_x
xxxxxx+x+x
xxxxxG.G.Gx
xxx_+.H.H.+
xxxxxG.I9Gx
xxx_+.H.H.+
xxxxxG.G.G
xxxxxx+x+x
xxxxxx_x_x
ENDMAP

#  This is basically lemuel_jelly_drop_b from Vaults.des
NAME:     vaults_room_10_dead_end_dangerous_lemuel_jelly_drop
TAGS:     vaults_room_10_dead_end_dangerous uniq_jelly_drop
TAGS:     no_monster_gen no_trap_gen no_item_gen
WEIGHT:   5
MONS:     jelly generate_awake
KFEAT:    ^ = known teleport trap
KPROP:    1^. = no_rtele_into
DEPTH:    Vaults
MAP
xx..^x1xxx
x.xxxxx.xx
.xx.1x.x.x
.x.x1x.x.xx
.x.x1x.x.xx
.x.x1x.x.xx
.x.x1x.x.xx
.x.x1.xx.x
x.xxxxx.xx
xx1x^..xxx
ENDMAP

# Flooded vault
# TODO: On opening door, spill water out into the corridor
# Monsters:
# - A kraken and some minor fish
# - Nest of crocodiles
# - Eels
NAME:     vaults_room_10_dead_end_dangerous_mumra_flooded
TAGS:     vaults_room_10_dead_end_dangerous
WEIGHT:   5
SUBST:    w = w:10 W:20
NSUBST:   1 = 1:1 / *:w
SUBST:    2 = 2:2 w:12 W:4
KFEAT:    12 = deep_water
MONS:     kraken
MONS:     shark / snapping turtle / harpy
SUBVAULT: A : vaults_door_10
MAP
WWWwwwwWWW
Wwww22wwwW
Ww222222wW
ww221122wwA
w22111122wA
w22111122wA
ww221122wwA
Ww222222wW
Wwww222wwW
WWWwwwwWWW
ENDMAP

NAME:     vaults_room_10_dead_end_dangerous_mumra_dragons_den
TAGS:     vaults_room_10_dead_end_dangerous
WEIGHT:   5
MONS:     dragon
SUBVAULT: A : vaults_door_10
: vaults_standard_loot(_G)
MAP
xx%xxxxxxx
%%%%.xxxxx
x%%.1xxxxx
%xxx..xxx.A
xxxxx..x..A
xxxxxx....A
xxxxxxx...A
xxxxxxxxxx
xxxxxxxxxx
xxxxxxxxxx
ENDMAP

NAME:     vaults_room_10_dead_end_loot_double_glass
TAGS:     vaults_room_10_dead_end_loot allow_dup
TAGS:     no_monster_gen no_item_gen
: if crawl.random2(3) < 1 then
:   subst('G = 1')
:   subst('_ = %')
:   subst(': = .')
:   subst('- = +')
: else
:   subst('- = m')
:   subst(': = _')
: end
SUBST:    % = %%.
MONS:     statue tile:mons_statue_archer name:archer n_adj; \
          longbow . arrow q:30
KPROP:  _ = no_rtele_into
: vaults_standard_loot(_G)
MAP
xxxxxxxxxx
xx..%%%%%x
xx..%%%%%x
xx+xmmmmmxx
x1........+
xxxxxxxxxxx
xG........+
xx-xmmmmmx
xx::_____x
xx::_____x
ENDMAP

NAME:     vaults_room_10_dead_end_loot_many_doors
TAGS:     vaults_room_10_dead_end_loot allow_dup no_monster_gen
SHUFFLE:  GHIJ
SUBST:    HIJ = .
NSUBST:   _ = 1:0 / *= %
: vaults_standard_loot(_G)
MAP
xxxxxxxxxx
xxxxxx_x_x
xxxxxx+x+x
xxxxxG.G.Gx
xxx_+.H.H.+
xxxxxG.I.Gx
xxx_+.H.H.+
xxxxxG.G.G
xxxxxx+x+x
xxxxxx_x_x
ENDMAP

NAME:   vaults_room_10_dead_end_loot_mumra_cupboards_2
TAGS:   vaults_room_10_dead_end_loot allow_dup  luniq_mumra_cupboards
SUBST:  % = %:20 .:4 0:4 *:2
SUBVAULT: A : vaults_door_10
MAP
%x%x..x%x%
+x+x..x+x+
..........
..xxxxx...A
..+%xxx...A
..xxx%+...A
..xxxxx...A
..........
+x+x..x+x+
%x%x..x%x%
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_10_dead_end_stairs1_semicircle
TAGS:     vaults_room_10_dead_end_up1
TAGS:     vaults_room_10_dead_end_down1 allow_dup
SUBST:    - = x+
NSUBST:   { = 1:{ / *:.
: subvault_place_stair(_G)
MAP
xxxxxxxxxx
xxxxxx...{
xxxx......
xxx.......x
xx........-
xx...G....+
xx........-
xxx.......
xxxx......
xxxxxx...{
ENDMAP

#  This is basically grunt_vaults_arrival_checkpoint from Vaults.des
#  Sorry that it is no longer (normally) on Vaults:1
NAME:     vaults_room_grunt_arrival_checkpoint
TAGS:     vaults_room_10_dead_end_up3 no_monster_gen no_item_gen no_trap_gen
WEIGHT:   100 (Vaults:1), 3
MONS:     vault guard
MAP
xxxxxxxxxx
xxxxxxxxxx
xxxx.T.xxx
xxxx...xxxx
x{.n..1n..x
x(.+...+..+
x[.n..1n..x
xxxx...xxx
xxxx.T.xxx
xxxxxxxxxx
ENDMAP

NAME:     vaults_room_10_dead_end_up3_thorn_pools
TAGS:     vaults_room_10_dead_end_up3 allow_dup no_pool_fixup
SHUFFLE:  {([<
SUBST:    < = <.
SUBVAULT: A : vaults_door_10
: vaults_ornamental_pool(_G)
MAP
ww{(wwwwww
ww..wwwwww
ww........
ww..wwww..A
ww..wwww..A
ww..wwww..A
ww..wwww..A
ww........
ww..wwwwww
ww[<wwwwww
ENDMAP

NAME:     vaults_room_10_dead_end_up3_many_doors
TAGS:     vaults_room_10_dead_end_up3 allow_dup no_monster_gen
SHUFFLE:  GHIJ
SUBST:    HIJ = .
NSUBST:   _ = 1:{ / 1:( / 1:[ / *= ...00%
: vaults_standard_loot(_G)
MAP
xxxxxxxxxx
xxxxxx_x_x
xxxxxx+x+x
xxxxxG.G.Gx
xxx_+.H.H.+
xxxxxG.I.Gx
xxx_+.H.H.+
xxxxxG.G.G
xxxxxx+x+x
xxxxxx_x_x
ENDMAP

NAME:     vaults_room_10_dead_end_down3_thorn_pools
TAGS:     vaults_room_10_dead_end_down3 allow_dup no_pool_fixup
SHUFFLE:  })]>
SUBST:    > = >.
SUBVAULT: A : vaults_door_10
: vaults_ornamental_pool(_G)
MAP
ww})wwwwww
ww..wwwwww
ww........
ww..wwww..A
ww..wwww..A
ww..wwww..A
ww..wwww..A
ww........
ww..wwwwww
ww]>wwwwww
ENDMAP

NAME:     vaults_room_10_dead_end_down3_many_doors
TAGS:     vaults_room_10_dead_end_down3 allow_dup no_monster_gen
SHUFFLE:  GHIJ
SUBST:    HIJ = .
NSUBST:   _ = 1:} / 1:) / 1:] / *= ...00%
: vaults_standard_loot(_G)
MAP
xxxxxxxxxx
xxxxxx_x_x
xxxxxx+x+x
xxxxxG.G.Gx
xxx_+.H.H.+
xxxxxG.I.Gx
xxx_+.H.H.+
xxxxxG.G.G
xxxxxx+x+x
xxxxxx_x_x
ENDMAP



########
#  City-layout 10x10 subvaults
#

NAME:     vaults_room_10_city_empty_subvault_glass
TAGS:     vaults_room_10_city_empty allow_dup
SHUFFLE:  Pp/Qq/Rr/Ss
SUBST:    P = 0
NSUBST:   p = 1:% / *:.
SUBST:    QqRrSs = .
SUBVAULT: A : vaults_room_4_spin_empty
MAP
xxxxxmmxxxxx
xsss+..+rrrx
xsSs+..+rRrx
xssxx++xxrrx
xmmxAAAAxmmx
m..+AAAAm..+
m..+AAAAm..+
x++xAAAAx++x
xppxx++xxqqx
xpPpm..mqQqx
xpppm..mqqqx
xxxxx++xxxxx
ENDMAP

NAME:     vaults_room_10_city_empty_fortress
TAGS:     vaults_room_10_city_empty allow_dup no_pool_fixup
: if crawl.random2(3) < 1 then
:   subst('+ = m')
:   subst('- = +')
: else
:   subst('- = x')
: end
SHUFFLE:  pqrs
NSUBST:   p = 1:+ / 1:x+ / *:x
NSUBST:   q = 1:+ / *:x
NSUBST:   r = 1:+ / *:x
NSUBST:   s = 1:+ / *:x
: vaults_ornamental_pool(_G)
MAP
xppx....xqqx
p..xxxxxx..q
p..m....m..q
xxmx....x+xx
.x........x.
.x...ww...-.
.x...ww...-.
.x........x.
xxmx....xmxx
s..m....m..r
s..xxxxxx..r
xssx....xrrx
ENDMAP

NAME:     vaults_room_10_city_empty_fancy_cross
TAGS:     vaults_room_10_city_empty allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
NSUBST:   p = 1:+ / *:x
NSUBST:   q = 1:+ / *:x
NSUBST:   r = 1:+ / *:x
NSUBST:   s = 1:+ / *:x
MAP
...Gx+xG....
..I.x.x.I...
.Ixsx.xpxI..
G.s.s.p.p.G.
xxxsx.xpxxx.
+.........+.
xxxrx.xqxxx.
G.r.r.q.q.G.
.Ixrx.xqxI..
..I.x.x.I...
...Gx+xG....
............
ENDMAP

NAME:     vaults_room_10_city_empty_ringed_pool
TAGS:     vaults_room_10_city_empty allow_dup no_pool_fixup
SHUFFLE:  xc
SUBST:    c : x.
SUBST:    G : G.
: vaults_ornamental_pool(_G)
MAP
..xxx..xxx..
............
x.G.cccc.G.x
x..........x
x.c.wwww.c.x
..c.wwww.c..
..c.wwww.c..
x.c.wwww.c.x
x..........x
x.G.cccc.G.x
............
..xxx..xxx..
ENDMAP

NAME:     vaults_room_10_city_empty_subvault_7
TAGS:     vaults_room_10_city_empty allow_dup
WEIGHT:   5
SHUFFLE:  GHIJ
SUBST:    HIJ = x
SUBVAULT: A : vaults_room_7_dead_end_empty
MAP
xxGHxIxHGxx.
xxxxxxxxxxx.
GxAAAAAAAxG.
HxAAAAAAAxH.
xxAAAAAAAA..
IxAAAAAAAA..
xxAAAAAAAA..
HxAAAAAAAxH.
GxAAAAAAAxG.
xxxxxxxxxxx.
xxGHxIxHGxx.
............
ENDMAP

NAME:     vaults_room_10_city_empty_subvault_4
TAGS:     vaults_room_10_city_empty allow_dup
WEIGHT:   5
SUBVAULT: A : vaults_room_4_dead_end_empty
: vaults_ornamental_pool(_G)
MAP
..w......w..
.www....www.
wwwww..wwwww
.wwxxxxxxww.
..wxAAAAxw..
...xAAAAA...
...xAAAAA...
..wxAAAAxw..
.wwxxxxxxww.
wwwww..wwwww
.www....www.
..w......w..
ENDMAP

NAME:     vaults_room_10_city_encounter_fancy_cross
TAGS:     vaults_room_10_city_encounter allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
NSUBST:   p = 1:+ / *:x
NSUBST:   q = 1:+ / *:x
NSUBST:   r = 1:+ / *:x
NSUBST:   s = 1:+ / *:x
NSUBST:   _ = 1:% / 2:0 / *:.
MAP
...Gx+xG....
..I.x.x.I...
.Ixsx.xpxI..
G.s_s.p_p.G.
xxxsx.xpxxx.
+...._....+.
xxxrx.xqxxx.
G.r_r.q_q.G.
.Ixrx.xqxI..
..I.x.x.I...
...Gx+xG....
............
ENDMAP

NAME:     vaults_room_10_city_encounter_ringed_pool
TAGS:     vaults_room_10_city_encounter allow_dup no_pool_fixup
SHUFFLE:  xc
SUBST:    c : x.
SUBST:    G : G.
NSUBST:   0 = 1:0 / 1:0% / *:.
: vaults_ornamental_pool(_G)
MAP
..xxx..xxx..
............
x.G.cccc.G.x
x..0....0..x
x.c.wwww.c.x
..c.wwww.c..
..c.wwww.c..
x.c.wwww.c.x
x..0....0..x
x.G.cccc.G.x
............
..xxx..xxx..
ENDMAP

NAME:     vaults_room_10_city_encounter_subvault_7
TAGS:     vaults_room_10_city_encounter allow_dup
WEIGHT:   5
SHUFFLE:  GHIJ
SUBST:    HIJ = x
SUBVAULT: A : vaults_room_7_dead_end_encounter
MAP
xxGHxIxHGxx.
xxxxxxxxxxx.
GxAAAAAAAxG.
HxAAAAAAAxH.
xxAAAAAAAA..
IxAAAAAAAA..
xxAAAAAAAA..
HxAAAAAAAxH.
GxAAAAAAAxG.
xxxxxxxxxxx.
xxGHxIxHGxx.
............
ENDMAP

NAME:     vaults_room_10_city_encounter_subvault_4
TAGS:     vaults_room_10_city_encounter allow_dup
WEIGHT:   5
SUBVAULT: A : vaults_room_4_dead_end_encounter
: vaults_ornamental_pool(_G)
MAP
..w......w..
.www....www.
wwwww..wwwww
.wwxxxxxxww.
..wxAAAAxw..
...xAAAAA...
...xAAAAA...
..wxAAAAxw..
.wwxxxxxxww.
wwwww..wwwww
.www....www.
..w......w..
ENDMAP

NAME:     vaults_room_10_city_dangerous_subvault_glass
TAGS:     vaults_room_10_city_dangerous allow_dup
SHUFFLE:  Pp/Qq/Rr/Ss
SUBST:    PQ = 0
SUBST:    pq = .
SUBST:    R = *
SUBST:    r = %
SUBST:    S = 9
SUBST:    s = $.
SUBVAULT: A : vaults_room_4_spin_dangerous
MAP
xxxxxmmxxxxx
xsss+..+rrrx
xsSs+..+rRrx
xssxx++xxrrx
xmmxAAAAxmmx
m..+AAAAm..+
m..+AAAAm..+
x++xAAAAx++x
xppxx++xxqqx
xpPpm..mqQqx
xpppm..mqqqx
xxxxx++xxxxx
ENDMAP

NAME:     vaults_room_10_city_dangerous_fortress
TAGS:     vaults_room_10_city_dangerous allow_dup no_pool_fixup
: if crawl.random2(3) < 1 then
:   subst('+ = m')
:   subst('- = +')
: else
:   subst('- = x')
: end
NSUBST:   p = 1:+ / *:x
NSUBST:   q = 1:+ / *:x
NSUBST:   r = 1:+ / *:x
NSUBST:   s = 1:+ / *:x
NSUBST:   1 = 1:0 / 1:%. / *:.
NSUBST:   2 = 1:0 / 1:%. / *:.
NSUBST:   3 = 1:0 / 1:%. / *:.
NSUBST:   4 = 1:0 / 1:%. / *:.
: vaults_ornamental_pool(_G)
MAP
xppx....xqqx
p11xxxxxx22q
p11m....m22q
xxmx....x+xx
.x%%..0...x.
.x%%.ww...-.
.x%%.ww...-.
.x%%..0...x.
xxmx....xmxx
s33m....m44r
s33xxxxxx44r
xssx....xrrx
ENDMAP

NAME:     vaults_room_10_city_dangerous_subvault_7
TAGS:     vaults_room_10_city_dangerous allow_dup
WEIGHT:   5
SHUFFLE:  GHIJ
SUBST:    HIJ = x
SUBVAULT: A : vaults_room_7_dead_end_dangerous
MAP
xxGHxIxHGxx.
xxxxxxxxxxx.
GxAAAAAAAxG.
HxAAAAAAAxH.
xxAAAAAAAA..
IxAAAAAAAA..
xxAAAAAAAA..
HxAAAAAAAxH.
GxAAAAAAAxG.
xxxxxxxxxxx.
xxGHxIxHGxx.
............
ENDMAP

NAME:     vaults_room_10_city_dangerous_subvault_4
TAGS:     vaults_room_10_city_dangerous allow_dup
WEIGHT:   5
SUBVAULT: A : vaults_room_4_dead_end_dangerous
: vaults_ornamental_pool(_G)
MAP
..w......w..
.www....www.
wwwww..wwwww
.wwxxxxxxww.
..wxAAAAxw..
...xAAAAA...
...xAAAAA...
..wxAAAAxw..
.wwxxxxxxww.
wwwww..wwwww
.www....www.
..w......w..
ENDMAP

NAME:     vaults_room_10_city_dangerous_mumra_exhibit_shoals
TAGS:     vaults_room_10_city_dangerous no_wall_fixup no_pool_fixup
TAGS:     no_monster_gen no_item_gen
SUBST:    W = WWWw
SUBST:    w = wwwW
TILE:     x = wall_vines
SUBST:    W = 4:1 w:2 W:20
SUBST:    1 = 1:20 .:5 2:5
SUBST:    2 = 2:20 .:8 3:5
SUBST:    3 = 3:20 .:10
MONS:     merfolk aquamancer / siren
MONS:     merfolk impaler
MONS:     merfolk javelineer
MONS:     snapping turtle
MAP
WWWWWWWWWWW.
WWWwwwwwWWW.
WWwxxxxxwWW.
Wwxx323xxwW.
Wwx32123xwW.
Wwx21*12+.W.
Wwx32123xwW.
Wwxx323xxwW.
WWwxxxxxwWW.
WWWwwwwwWWW.
WWWWWWWWWWW.
............
ENDMAP

NAME:     vaults_room_10_city_loot_subvault_glass
TAGS:     vaults_room_10_city_loot allow_dup
SHUFFLE:  Pp/Qq/Rr/Ss
SUBST:    P = *
SUBST:    p = %
SUBST:    Q = *
SUBST:    q = %
SUBST:    R = $
SUBST:    r = $
SUBST:    S = .
SUBST:    s = .
SUBVAULT: A : vaults_room_4_facing_encounter
MAP
xxxxx++xxxxx
xsssm..mrrrx
xsSsm..mrRrx
xssxxmmxxrrx
x++xAAAAxm+x
m..+AAAAm..m
m..+AAAAm..m
x++xAAAAx++x
xppxxAAxxqqx
xpPpm..mqQqx
xpppm..mqqqx
xxxxx++xxxxx
ENDMAP

NAME:     vaults_room_10_city_loot_subvault_7
TAGS:     vaults_room_10_city_loot allow_dup
WEIGHT:   5
SHUFFLE:  GHIJ
SUBST:    HIJ = x
SUBVAULT: A : vaults_room_7_dead_end_loot
MAP
xxGHxIxHGxx.
xxxxxxxxxxx.
GxAAAAAAAxG.
HxAAAAAAAxH.
xxAAAAAAAA..
IxAAAAAAAA..
xxAAAAAAAA..
HxAAAAAAAxH.
GxAAAAAAAxG.
xxxxxxxxxxx.
xxGHxIxHGxx.
............
ENDMAP

NAME:     vaults_room_10_city_loot_subvault_4
TAGS:     vaults_room_10_city_loot allow_dup
WEIGHT:   5
SUBVAULT: A : vaults_room_4_dead_end_loot
: vaults_ornamental_pool(_G)
MAP
..w......w..
.www....www.
wwwww..wwwww
.wwxxxxxxww.
..wxAAAAxw..
...xAAAAA...
...xAAAAA...
..wxAAAAxw..
.wwxxxxxxww.
wwwww..wwwww
.www....www.
..w......w..
ENDMAP

# This is for both entry and exit stairs
NAME:     vaults_room_10_city_stairs1_fancy_cross
TAGS:     vaults_room_10_city_up1
TAGS:     vaults_room_10_city_down1 allow_dup
SHUFFLE:  GHI
SUBST:    HI = .
NSUBST:   p = 1:+ / *:x
NSUBST:   q = 1:+ / *:x
NSUBST:   r = 1:+ / *:x
NSUBST:   s = 1:+ / *:x
NSUBST:   _ = 1:0 / 1:0% / *:.
: subvault_place_stair(_G)
MAP
...Gx+xG....
..I.x.x.I...
.Ixsx.xpxI..
G.s_s.p_p.G.
xxxsx.xpxxx.
+....{....+.
xxxrx.xqxxx.
G.r_r.q_q.G.
.Ixrx.xqxI..
..I.x.x.I...
...Gx+xG....
............
ENDMAP

NAME:     vaults_room_10_city_up1_subvault_4
TAGS:     vaults_room_10_city_up1 allow_dup
SUBVAULT: A : vaults_room_4_dead_end_up1
: vaults_ornamental_pool(_G)
MAP
..w......w..
.www....www.
wwwww..wwwww
.wwxxxxxxww.
..wxAAAAxw..
...xAAAAA...
...xAAAAA...
..wxAAAAxw..
.wwxxxxxxww.
wwwww..wwwww
.www....www.
..w......w..
ENDMAP

NAME:     vaults_room_10_city_down1_subvault_4
TAGS:     vaults_room_10_city_down1 allow_dup
SUBVAULT: A : vaults_room_4_dead_end_down1
: vaults_ornamental_pool(_G)
MAP
..w......w..
.www....www.
wwwww..wwwww
.wwxxxxxxww.
..wxAAAAxw..
...xAAAAA...
...xAAAAA...
..wxAAAAxw..
.wwxxxxxxww.
wwwww..wwwww
.www....www.
..w......w..
ENDMAP

NAME:     vaults_room_10_city_up3_fancy_cross
TAGS:     vaults_room_10_city_up3 allow_dup
SHUFFLE:  {([<
SUBST:    < = <.
SHUFFLE:  xc
SUBST:    c : x.
: vaults_ornamental_pool(_G)
MAP
..xxx..xxx..
............
x.{.cccc.(.x
x..........x
x.c.wwww.c.x
..c.wwww.c..
..c.wwww.c..
x.c.wwww.c.x
x..........x
x.<.cccc.[.x
............
..xxx..xxx..
ENDMAP

NAME:     vaults_room_10_city_down3_fancy_cross
TAGS:     vaults_room_10_city_down3 allow_dup
SHUFFLE:  })]>
SUBST:    > = >.
SHUFFLE:  xc
SUBST:    c : x.
: vaults_ornamental_pool(_G)
MAP
..xxx..xxx..
............
x.}.cccc.).x
x..........x
x.c.wwww.c.x
..c.wwww.c..
..c.wwww.c..
x.c.wwww.c.x
x..........x
x.>.cccc.].x
............
..xxx..xxx..
ENDMAP





################################################################
#  15x15 subvaults
#

########
#  Doors for 15x15 subvaults
#

NAME:     vaults_door_15_small
TAGS:     vaults_door_15 allow_dup
SUBST:    - = x+
MAP
xx-+-xx
ENDMAP

NAME:     vaults_door_15_large
TAGS:     vaults_door_15 allow_dup
SUBST:    - = x+
MAP
x-+++-x
ENDMAP

NAME:     vaults_door_15_general
TAGS:     vaults_door_15 allow_dup
WEIGHT:   1
SHUFFLE:  abcd
SUBST:    a : x
SUBST:    b : +
SUBST:    c : x+
SUBST:    d : x+
MAP
abcdcba
ENDMAP

NAME:     vaults_door_15_double
TAGS:     vaults_door_15 allow_dup
WEIGHT:   1
SHUFFLE:  de
SUBST:    d : a
SUBST:    e : c
SHUFFLE:  abc
SUBST:    a : +
SUBST:    b : x+
SUBST:    c : x+
MAP
abcxdbe
ENDMAP

NAME:     vaults_door_15_side
TAGS:     vaults_door_15 allow_dup
WEIGHT:   2
SUBST:    - = x+
MAP
-++-xxx
ENDMAP



########
#  Free-spinning 15x15 subvaults
#

# This one might have any contents
NAME:     vaults_room_15_spin_subvaults_4
TAGS:     vaults_room_15_spin_empty vaults_room_15_spin_encounter
TAGS:     vaults_room_15_spin_dangerous vaults_room_15_spin_loot
TAGS:     allow_dup no_monster_gen
SHUFFLE:  GHI
SUBST:    HI = .
SUBST:    G : GG.
SHUFFLE:  abc
SUBST:    ab = x
SUBST:    c = C
SHUFFLE:  di
SUBST:    d = D
SHUFFLE:  efgh
SUBST:    fgh = x
SUBST:    e = E
SHUFFLE:  ij
SUBST:    i = x
SUBST:    j : xxxx+
SHUFFLE:  BD
SUBVAULT: B : vaults_room_4_facing_empty     w:10 /\
              vaults_room_4_facing_encounter w:10 /\
              vaults_room_4_facing_dangerous w:5
SUBVAULT: C : vaults_room_4_dead_end_empty     w:10 /\
              vaults_room_4_dead_end_encounter w:5  /\
              vaults_room_4_dead_end_loot w:5
SUBVAULT: D : vaults_room_4_facing_empty     w:10 /\
              vaults_room_4_facing_encounter w:10 /\
              vaults_room_4_facing_dangerous w:5
SUBVAULT: E : vaults_room_4_dead_end_empty     w:10 /\
              vaults_room_4_dead_end_encounter w:5  /\
              vaults_room_4_dead_end_loot w:5
MAP
...............
...I..IGI..I...
..xxffxxxjjxx..
.IxEEEExBBBBxI.
..eEEEEgBBBBB..
..eEEEEgBBBBB..
.IxEEEExBBBBxI.
.GxxhhxxxaaxxG.
.IxDDDDxCCCCxI.
..dDDDDxCCCCb..
..dDDDDxCCCCb..
.IxDDDDxCCCCxI.
..xxiixxxccxx..
...I..IGI..I...
...............
ENDMAP

NAME:     vaults_room_15_spin_empty_octagon_1
TAGS:     vaults_room_15_spin_empty allow_dup
SHUFFLE:  pqrs
SUBST:    p = c
SUBST:    qrs = .
SUBST:    c : xxxxxxmww
: vaults_ornamental_pool(_G)
MAP
xxxx.......xxxx
xxx....r....xxx
xx....rrr....xx
x..pcp.r.pcp..x
...ccc...ccc...
...pcp...pcp...
..r....q....r..
.rrr..qqq..rrr.
..r....q....r..
...pcp...pcp...
...ccc...ccc...
x..pcp.r.pcp..x
xx....rrr....xx
xxx....r....xxx
xxxx.......xxxx
ENDMAP

NAME:     vaults_room_15_spin_empty_octagon_2
TAGS:     vaults_room_15_spin_empty allow_dup
SHUFFLE:  pq/rs
SUBST:    q : pp.
SUBST:    s : rr.
SUBST:    r : pp.
SUBST:    p : xxxxxxmww
: vaults_ornamental_pool(_G)
MAP
xxxx.......xxxx
xxx...srs...xxx
xx....rrr....xx
x.....srs.....x
...............
...............
.srs..qpq..srs.
.rrr..ppp..rrr.
.srs..qpq..srs.
...............
...............
x.....srs.....x
xx....rrr....xx
xxx...srs...xxx
xxxx.......xxxx
ENDMAP

NAME:     vaults_room_15_spin_empty_boxed
TAGS:     vaults_room_15_spin_empty allow_dup
SHUFFLE:  GI
SUBST:    I = x
SUBST:    G : Gx
NSUBST:   - = 1:+ / 2:xx+ / *:x
SUBST:    c : x.
SUBVAULT: B : vaults_door_15
SUBVAULT: C : vaults_door_15
SUBVAULT: D : vaults_door_15
SUBVAULT: E : vaults_door_15
MAP
xxxI.......Ixxx
xxxG.......Gxxx
xxxxEEEEEEExxxx
IGx.........xGI
..D.x-----x.B..
..D.-.....-.B..
..D.-.ccc.-.B..
..D.-.ccc.-.B..
..D.-.ccc.-.B..
..D.-.....-.B..
..D.x-----x.B..
IGx.........xGI
xxxxCCCCCCCxxxx
xxxG.......Gxxx
xxxI.......Ixxx
ENDMAP

NAME:     vaults_room_15_spin_empty_star
TAGS:     vaults_room_15_spin_empty allow_dup
SUBST:    z : yy.
SUBST:    y : uu.
SUBST:    u : xx.
SUBST:    r : qq.
SUBST:    q : pp.
SUBST:    p : xx.
SUBST:    ' : ".
SUBST:    " : x..
MAP
uuyz.......yyuu
uuzz...x...zzuu
yz.....x.....zy
zz.x"'.x.'"x.zy
..."x".x."x"...
...'"x.r.x"'...
......rqr......
.xxxxrqpqrxxxx.
......rqr......
...'"x.r.x"'...
..."x".x."x"...
yz.x"'.x.'"x.zy
yz.....x.....zy
uuzz...x...zzuu
uuyy.......yyuu
ENDMAP

NAME:     vaults_room_15_spin_empty_concentric
TAGS:     vaults_room_15_spin_empty allow_dup
WEIGHT:   20
SUBVAULT: A : vaults_room_15_concentric_circles
MAP
...............
.AAAAAAAAAAAAA.
.A...........A.
.A.AAAAAAAAA.A.
.A.A.......A.A.
.A.A.AAAAA.A.A.
.A.A.A...A.A.A.
.A.A.A...A.A.A.
.A.A.A...A.A.A.
.A.A.AAAAA.A.A.
.A.A.......A.A.
.A.AAAAAAAAA.A.
.A...........A.
.AAAAAAAAAAAAA.
...............
ENDMAP

NAME:     vaults_room_15_spin_empty_concentric_feature
TAGS:     vaults_room_15_spin_empty allow_dup
SUBST:    ! = SC
SUBST:    T : TT^...
SUBST:    T = TTTUVV
KFEAT:    S = any shop
KFEAT:    ^ = known zot trap
SUBVAULT: A : vaults_room_15_concentric_circles
MAP
...............
.AAAAAAAAAAAAA.
.A...........A.
.A.AAAAAAAAA.A.
.A.A.......A.A.
.A.A.AAAAA.A.A.
.A.A.AT.TA.A.A.
.A.A.A.!.A.A.A.
.A.A.AT.TA.A.A.
.A.A.AAAAA.A.A.
.A.A.......A.A.
.A.AAAAAAAAA.A.
.A...........A.
.AAAAAAAAAAAAA.
...............
ENDMAP

NAME:     vaults_room_15_spin_empty_subvault_10
TAGS:     vaults_room_15_spin_empty allow_dup
SUBVAULT: A : vaults_room_10_city_empty
MAP
...............
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
...............
...............
ENDMAP

NAME:     vaults_room_15_spin_encounter_octagon_1
TAGS:     vaults_room_15_spin_encounter allow_dup no_monster_gen
SHUFFLE:  12345
SUBST:    2345 = .
NSUBST:   1 = 1:0 / 3=0. / *:.
SHUFFLE:  pqrs
SUBST:    p = c
SUBST:    qrs = .
SUBST:    c : xxxxxxmww
: vaults_ornamental_pool(_G)
MAP
xxxx.......xxxx
xxx..5.r.5..xxx
xx3...rrr...3xx
x..pcp2r2pcp..x
...ccc...ccc...
.5.pcp.4.pcp.5.
..r2..1q1..2r..
.rrr.4qqq4.rrr.
..r2..1q1..2r..
.5.pcp.4.pcp.5.
...ccc...ccc...
x..pcp2r2pcp..x
xx3...rrr...3xx
xxx..5.r.5..xxx
xxxx.......xxxx
ENDMAP

NAME:     vaults_room_15_spin_encounter_octagon_2
TAGS:     vaults_room_15_spin_encounter allow_dup no_monster_gen
SHUFFLE:  1234
NSUBST:   1 = 1:0 / *:.
NSUBST:   2 = 1:0. / *:.
NSUBST:   3 = 1:0. / *:.
NSUBST:   4 = 1:0. / *:.
SHUFFLE:  pq/rs
SUBST:    q : pp.
SUBST:    s : rr.
SUBST:    r : pp.
SUBST:    p : xxxxxxmww
: vaults_ornamental_pool(_G)
MAP
xxxx.......xxxx
xxx...srs...xxx
xx111.rrr.222xx
x.111.srs.222.x
..111.....222..
...............
.srs..qpq..srs.
.rrr..ppp..rrr.
.srs..qpq..srs.
...............
..444.....333..
x.444.srs.333.x
xx444.rrr.333xx
xxx...srs...xxx
xxxx.......xxxx
ENDMAP

NAME:     vaults_room_15_spin_encounter_boxed
TAGS:     vaults_room_15_spin_encounter allow_dup no_monster_gen
SHUFFLE:  GI
SUBST:    I = x
SUBST:    G : Gx
NSUBST:   - = 1:+ / 2=xx+ / *:x
NSUBST:   ' = 1:0 / *:.
NSUBST:   " = 1:0. / *:.
SUBVAULT: B : vaults_door_15
SUBVAULT: C : vaults_door_15
SUBVAULT: D : vaults_door_15
SUBVAULT: E : vaults_door_15
MAP
xxxI.......Ixxx
xxxG.......Gxxx
xxxxEEEEEEExxxx
IGx'''''''''xGI
..D'x-----x'B..
..D'-.....-'B..
..D'-.""".-'B..
..D'-.""".-'B..
..D'-.""".-'B..
..D'-.....-'B..
..D'x-----x'B..
IGx'''''''''xGI
xxxxCCCCCCCxxxx
xxxG.......Gxxx
xxxI.......Ixxx
ENDMAP

NAME:     vaults_room_15_spin_encounter_concentric
TAGS:     vaults_room_15_spin_encounter allow_dup no_monster_gen
SHUFFLE:  123
SUBST:    23 = .
SUBST:    1 = 0
SUBVAULT: A : vaults_room_15_concentric_circles
MAP
...............
.AAAAAAAAAAAAA.
.A1.........1A.
.A.AAAAAAAAA.A.
.A.A.......A.A.
.A.A.AAAAA.A.A.
.A.A.A...A.A.A.
.A.A2A.3.A2A.A.
.A.A.A...A.A.A.
.A.A.AAAAA.A.A.
.A.A.......A.A.
.A.AAAAAAAAA.A.
.A1.........1A.
.AAAAAAAAAAAAA.
...............
ENDMAP

NAME:     vaults_room_15_spin_encounter_subvault_10
TAGS:     vaults_room_15_spin_encounter allow_dup
SUBVAULT: A : vaults_room_10_city_encounter
MAP
...............
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
...............
...............
ENDMAP

NAME:     vaults_room_15_spin_dangerous_boxed
TAGS:     vaults_room_15_spin_dangerous allow_dup no_monster_gen
SHUFFLE:  GI
SUBST:    I = x
SUBST:    G : Gx
NSUBST:   % = 8:% / *:.
SUBVAULT: B : vaults_door_15
SUBVAULT: C : vaults_door_15
SUBVAULT: D : vaults_door_15
SUBVAULT: E : vaults_door_15
MAP
xxxI.......Ixxx
xxxG.......Gxxx
xxxxEEEEEEExxxx
IGx.........xGI
..D.x-----x.B..
..D.-%%%%%-.B..
..D.-%%%%%-.B..
..D.-%%8%%-.B..
..D.-%%%%%-.B..
..D.-%%%%%-.B..
..D.x-----x.B..
IGx.........xGI
xxxxCCCCCCCxxxx
xxxG.......Gxxx
xxxI.......Ixxx
ENDMAP

NAME:     vaults_room_15_spin_dangerous_star
TAGS:     vaults_room_15_spin_dangerous allow_dup
SUBST:    z : yy.
SUBST:    y : uu.
SUBST:    u : xx.
SUBST:    r : qq.
SUBST:    q : pp.
SUBST:    p : xx.
SUBST:    ' : ".
SUBST:    " : x..
SHUFFLE:  12
SUBST:    2 = x
MONS:     statue tile:mons_statue_archer name:archer n_adj; \
          longbow . arrow q:30
MAP
uuyz.......yyuu
uuzz...2...zzuu
yz.....x.....zy
zz.1"'.x.'"1.zy
..."x".x."x"...
...'"x.r.x"'...
......rqr......
.2xxxrqpqrxxx2.
......rqr......
...'"x.r.x"'...
..."x".x."x"...
yz.1"'.x.'"1.zy
yz.....x.....zy
uuzz...2...zzuu
uuyy.......yyuu
ENDMAP

NAME:     vaults_room_15_spin_dangerous_subvault_10
TAGS:     vaults_room_15_spin_dangerous allow_dup
SUBVAULT: A : vaults_room_10_city_encounter
MAP
...............
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
...............
...............
ENDMAP

NAME:     vaults_room_15_spin_loot_concentric
TAGS:     vaults_room_15_spin_loot allow_dup
SUBVAULT: A : vaults_room_15_concentric_circles
MAP
...............
.AAAAAAAAAAAAA.
.A...........A.
.A.AAAAAAAAA.A.
.A.A.......A.A.
.A.A.AAAAA.A.A.
.A.A.A%%%A.A.A.
.A.A.A%%%A.A.A.
.A.A.A%%%A.A.A.
.A.A.AAAAA.A.A.
.A.A.......A.A.
.A.AAAAAAAAA.A.
.A...........A.
.AAAAAAAAAAAAA.
...............
ENDMAP

NAME:     vaults_room_15_spin_loot_octagon_1
TAGS:     vaults_room_15_spin_loot allow_dup
SHUFFLE:  123
SUBST:    23 = .
MONS:     vault guard
SHUFFLE:  pqrs
SUBST:    p = c
SUBST:    qrs = .
SUBST:    c : xxxxxxmww
: vaults_ornamental_pool(_G)
MAP
xxxx.......xxxx
xxx....r....xxx
xx....rrr....xx
x..pcp.r.pcp..x
...ccc.3.ccc...
...pcp.2.pcp...
..r...1%1...r..
.rrr32%*%23rrr.
..r...1%1...r..
...pcp.2.pcp...
...ccc.3.ccc...
x..pcp.r.pcp..x
xx....rrr....xx
xxx....r....xxx
xxxx.......xxxx
ENDMAP

NAME:     vaults_room_15_spin_loot_subvault_10
TAGS:     vaults_room_15_spin_loot allow_dup
SUBVAULT: A : vaults_room_10_city_loot
MAP
...............
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
...............
...............
ENDMAP

NAME:     vaults_room_15_spin_loot_mumra_cupboards_1
TAGS:     vaults_room_15_spin_loot allow_dup luniq_mumra_cupboards
# Each corner cupboard has random contents
NSUBST:   A = 1:+ / *:x
NSUBST:   B = 1:+ / *:x
NSUBST:   C = 1:+ / *:x
NSUBST:   D = 1:+ / *:x
SUBST:    1 = *:5 0:10 .:2
MAP
1D%+.......+%C1
Dxxx.......xxxC
%x%+.......+%x%
+x+x.......x+x+
...............
...............
...............
...............
...............
...............
...............
+x+x.......x+x+
%x%+.......+%x%
Axxx.......xxxB
1A%+.......+%B1
ENDMAP

# This is for for both entry and exit stairs
NAME:     vaults_room_15_spin_stairs1_boxed
TAGS:     vaults_room_15_spin_up1
TAGS:     vaults_room_15_spin_down1 allow_dup
SHUFFLE:  GI
SUBST:    I = x
SUBST:    G : Gx
NSUBST:   - = 1:+ / 2:xx+ / *:x
SUBVAULT: B : vaults_door_15
SUBVAULT: C : vaults_door_15
SUBVAULT: D : vaults_door_15
SUBVAULT: E : vaults_door_15
: subvault_place_stair(_G)
MAP
xxxI.......Ixxx
xxxG.......Gxxx
xxxxEEEEEEExxxx
IGx.........xGI
..D.x-----x.B..
..D.-.....-.B..
..D.-.....-.B..
..D.-..{..-.B..
..D.-.....-.B..
..D.-.....-.B..
..D.x-----x.B..
IGx.........xGI
xxxxCCCCCCCxxxx
xxxG.......Gxxx
xxxI.......Ixxx
ENDMAP

# They get you if you move...
NAME:     vaults_room_15_spin_up1_star
TAGS:     vaults_room_15_spin_up1 allow_dup
SUBST:    z : yy.
SUBST:    y : uu.
SUBST:    u : xx.
SUBST:    ' : ".
SUBST:    " : x..
SHUFFLE:  12
SUBST:    2 = x
SUBST:    1 : 1xxxx
MONS:     statue tile:mons_statue_archer name:archer n_adj; \
          longbow . arrow q:30
: subvault_place_stair(_G)
MAP
uuyz.......yyuu
uuzz...2...zzuu
yz.....x.....zy
zz.1...x...1.zy
....x..x..x....
.....x...x.....
...............
.2xxx..{..xxx2.
...............
.....x...x.....
....x..x..x....
yz.1...x...1.zy
yz.....x.....zy
uuzz...2...zzuu
uuyy.......yyuu
ENDMAP

NAME:     vaults_room_15_spin_up1_subvault_10
TAGS:     vaults_room_15_spin_up1 allow_dup
SUBVAULT: A : vaults_room_10_city_up1
MAP
...............
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
...............
...............
ENDMAP

NAME:     vaults_room_15_spin_down1_subvaults_4
TAGS:     vaults_room_15_spin_down1 allow_dup no_monster_gen
SHUFFLE:  GHI
SUBST:    HI = .
SUBST:    G : GG.
SHUFFLE:  abc
SUBST:    ab = x
SUBST:    c = C
SHUFFLE:  di
SUBST:    d = D
SHUFFLE:  efgh
SUBST:    fgh = x
SUBST:    e = E
SHUFFLE:  ij
SUBST:    i = x
SUBST:    j : xxxx+
SHUFFLE:  BD
SUBVAULT: B : vaults_room_4_facing_encounter
SUBVAULT: C : vaults_room_4_dead_end_down1
SUBVAULT: D : vaults_room_4_facing_empty     w:5  /\
              vaults_room_4_facing_encounter w:10
SUBVAULT: E : vaults_room_4_dead_end_empty     w:10 /\
              vaults_room_4_dead_end_encounter w:5  /\
              vaults_room_4_dead_end_loot w:5
MAP
...............
...I..IGI..I...
..xxffxxxjjxx..
.IxEEEExBBBBxI.
..eEEEEgBBBBB..
..eEEEEgBBBBB..
.IxEEEExBBBBxI.
.GxxhhxxxaaxxG.
.IxDDDDxCCCCxI.
..dDDDDxCCCCb..
..dDDDDxCCCCb..
.IxDDDDxCCCCxI.
..xxiixxxccxx..
...I..IGI..I...
...............
ENDMAP

NAME:     vaults_room_15_spin_down1_subvault_10
TAGS:     vaults_room_15_spin_down1 allow_dup
SUBVAULT: A : vaults_room_10_city_down1
MAP
...............
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
...............
...............
ENDMAP

NAME:     vaults_room_15_spin_up3_subvaults_4
TAGS:     vaults_room_15_spin_up3 allow_dup no_monster_gen
SHUFFLE:  GHI
SUBST:    HI = .
SUBST:    G : GG.
SHUFFLE:  abc
SUBST:    ab = x
SUBST:    c = C
SHUFFLE:  di
SUBST:    d = D
SHUFFLE:  efgh
SUBST:    fgh = x
SUBST:    e = E
SHUFFLE:  ij
SUBST:    i = x
SUBST:    j : xxxx+
SHUFFLE:  BD
SUBVAULT: B : vaults_room_4_facing_encounter
SUBVAULT: C : vaults_room_4_dead_end_up1
SUBVAULT: D : vaults_room_4_facing_up1
SUBVAULT: E : vaults_room_4_dead_end_up1
MAP
...............
...I..IGI..I...
..xxffxxxjjxx..
.IxEEEExBBBBxI.
..eEEEEgBBBBB..
..eEEEEgBBBBB..
.IxEEEExBBBBxI.
.GxxhhxxxaaxxG.
.IxDDDDxCCCCxI.
..dDDDDxCCCCb..
..dDDDDxCCCCb..
.IxDDDDxCCCCxI.
..xxiixxxccxx..
...I..IGI..I...
...............
ENDMAP

NAME:     vaults_room_15_spin_up3_octagon
TAGS:     vaults_room_15_spin_up3 allow_dup
SHUFFLE:  {([<
SHUFFLE:  pq/rs
SUBST:    q : pp.
SUBST:    s : rr.
SUBST:    r : pp.
SUBST:    p : xxxxxxmww
: vaults_ornamental_pool(_G)
MAP
xxxx.......xxxx
xxx...srs...xxx
xx....rrr....xx
x..{..srs..(..x
...............
...............
.srs..qpq..srs.
.rrr..ppp..rrr.
.srs..qpq..srs.
...............
...............
x..<..srs..[..x
xx....rrr....xx
xxx...srs...xxx
xxxx.......xxxx
ENDMAP

NAME:     vaults_room_15_spin_down3_subvaults_4
TAGS:     vaults_room_15_spin_down3 allow_dup no_monster_gen
SHUFFLE:  GHI
SUBST:    HI = .
SUBST:    G : GG.
SHUFFLE:  abc
SUBST:    ab = x
SUBST:    c = C
SHUFFLE:  di
SUBST:    d = D
SHUFFLE:  efgh
SUBST:    fgh = x
SUBST:    e = E
SHUFFLE:  ij
SUBST:    i = x
SUBST:    j : xxxx+
SHUFFLE:  BD
SUBVAULT: B : vaults_room_4_facing_encounter
SUBVAULT: C : vaults_room_4_dead_end_down3
SUBVAULT: D : vaults_room_4_facing_empty     w:5  /\
              vaults_room_4_facing_encounter w:10
SUBVAULT: E : vaults_room_4_dead_end_empty     w:10 /\
              vaults_room_4_dead_end_encounter w:5  /\
              vaults_room_4_dead_end_loot      w:5
MAP
...............
...I..IGI..I...
..xxffxxxjjxx..
.IxEEEExBBBBxI.
..eEEEEgBBBBB..
..eEEEEgBBBBB..
.IxEEEExBBBBxI.
.GxxhhxxxaaxxG.
.IxDDDDxCCCCxI.
..dDDDDxCCCCb..
..dDDDDxCCCCb..
.IxDDDDxCCCCxI.
..xxiixxxccxx..
...I..IGI..I...
...............
ENDMAP

NAME:     vaults_room_15_spin_down3_octagon
TAGS:     vaults_room_15_spin_down3 allow_dup
SHUFFLE:  })]>
SHUFFLE:  pq/rs
SUBST:    q : pp.
SUBST:    s : rr.
SUBST:    r : pp.
SUBST:    p : xxxxxxmww
: vaults_ornamental_pool(_G)
MAP
xxxx.......xxxx
xxx...srs...xxx
xx....rrr....xx
x..}..srs..)..x
...............
...............
.srs..qpq..srs.
.rrr..ppp..rrr.
.srs..qpq..srs.
...............
...............
x..>..srs..]..x
xx....rrr....xx
xxx...srs...xxx
xxxx.......xxxx
ENDMAP

# This room uses validate to ensure that it is possible to reach
#  the edge from the middle (the '_').
#
# average 36 + 78 + 80 + 57 = 251 gold
NAME:     vaults_room_15_spin_secret_concentric
TAGS:     vaults_room_15_spin_secret allow_dup no_monster_gen no_item_gen
NSUBST:   1 = 1:9 / *=9..
NSUBST:   $ = 2:g / 6:f / 10:e / 19:d / *:.
SUBVAULT: A : vaults_room_15_concentric_circles
: vaults_numbered_gold_items(_G)
: subvault_place_stair(_G)
validate {{ return has_exit_from_glyph('_') }}
MAP
$$$$$$$$$$$$$$$
$AAAAAAAAAAAAA$
$A.....1.....A$
$A.AAAAAAAAA.A$
$A.A0.....0A.A$
$A.A.AAAAA.A.A$
$A.A.A._.A.A.A$
$A1A.A.{.A.A1A$
$A.A.A...A.A.A$
$A.A.AAAAA.A.A$
$A.A0.....0A.A$
$A.AAAAAAAAA.A$
$A.....1.....A$
$AAAAAAAAAAAAA$
$$$$$$$$$$$$$$$
ENDMAP

NAME:     vaults_room_15_spin_secret_curves
TAGS:     vaults_room_15_spin_secret allow_dup no_monster_gen no_item_gen
SHUFFLE:  GHI
SUBST:    HI = .
NSUBST:   p = 1:+ / *:x
NSUBST:   q = 1:+ / *:x
NSUBST:   r = 1:+ / *:x
NSUBST:   s = 1:+ / *:x
SUBST:    m = x:15 m:1
SHUFFLE:  1234
NSUBST:   2 = 1:{ / *:.
NSUBST:   3 = 1:* / *=%
NSUBST:   4 = 4:234 / 1:| / *:.
MONS:     vault guard
MONS:     stone golem
MONS:     iron golem
MONS:     crystal golem
: subvault_place_stair(_G)
: vaults_standard_loot(_G)
MAP
xxx.........xxx
xxxmx..G..xmxxx
xx44xx.I.xx11xx
.m444m...m111m.
.xx44xx.xx11xx.
..xmx.s.p.xmx..
....xsx.xpx....
.GI.........IG.
....xrx.xqx....
..xmx.r.q.xmx..
.xx33xx.xx22xx.
.m333m...m222m.
xx33xx.I.xx22xx
xxxmx..G..xmxxx
xxx.........xxx
ENDMAP

NAME:     vaults_room_15_spin_secret_subvault_10
TAGS:     vaults_room_15_spin_secret allow_dup
SUBVAULT: A : vaults_room_10_city_loot
: subvault_place_stair(_G)
MAP
...............
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
.AAAAAAAAAAAA..
...............
..............{
ENDMAP

# This is the wall subvault for the concentric_circles rooms
NAME:     vaults_room_15_concentric_circles
TAGS:     vaults_room_15_concentric_circles allow_dup
NSUBST:   a = 1:a / 3=aAA / *:A
NSUBST:   b = 1:b / 2=bBB / *:B
NSUBST:   c = 1:c / 1=cCC / *:C
SHUFFLE:  aA / bB / cC / dD / eE / fF
SUBST:    abc = +
SUBST:    AB = x
SUBST:    C : xxxxm
SUBST:    dD = w
SUBST:    eE = .
SUBST:    fF : ....~
SUBST:    ~ = ~.
: vaults_ornamental_pool(_G)
MAP
AAAaaaaaaaAAA
A           A
A BBbbbbbBB A
a B       B a
a b CcccC b a
a b c   c b a
a b c   c b a
a b c   c b a
a b CcccC b a
a B       B a
A BBbbbbbBB A
A           A
AAAaaaaaaaAAA
ENDMAP

# This is adapted from minmay_crypt_entry_simple_sparse in Crypt.des
NAME:     vaults_room_15_spin_crypt_simple_walls
TAGS:     vaults_room_15_spin_crypt allow_dup
NSUBST:   + = 1:+ / 3=+xx / *:.
NSUBST:   H : xxG
NSUBST:   I : xGG
MONS:     place:Crypt:1
KFEAT:    O = enter_crypt
MAP
...............
.xxx+++x+++xxx.
.x...........x.
.x.H...H...H.x.
.+...........+.
.+...I.1.I...+.
.+...........+.
.x.H.1.O.1.H.x.
.+...........+.
.+...I.1.I...+.
.+...........+.
.x.H...H...H.x.
.x...........x.
.xxx+++x+++xxx.
...............
ENDMAP

NAME:     vaults_room_15_spin_blade_star
TAGS:     vaults_room_15_spin_blade allow_dup
SUBST:    z : yy.
SUBST:    y : uu.
SUBST:    u : xx.
SUBST:    ' : "-
SUBST:    " : c--
: if crawl.random2(2) < 1 then
:   subst("- = _")
: else
:   subst("c = x")
:   subst("- = .")
: end
SHUFFLE:  12
SUBST:    2 = _
MONS:     place:Blade:1
KFEAT:    O = enter_hall_of_blades
KFEAT:    c = rock_wall
FTILE:    cO12_ = floor_hall
MAP
uuyz.......yyuu
uuzz...x...zzuu
yz...--c--...zy
zz.x"'-c-'"x.zy
..."c"-c-"c"...
..-'"c___c"'-..
..---_212_---..
.xccc_1O1_cccx.
..---_212_---..
..-'"c___c"'-..
..."c"-c-"c"...
yz.x"'-c-'"x.zy
yz...--c--...zy
uuzz...x...zzuu
uuyy.......yyuu
ENDMAP

# Entries for ???_icecave_??? and ???_wizlab_??? are in .des files

NAME:     vaults_room_15_spin_bazaar_subvault
TAGS:     vaults_room_15_spin_bazaar allow_dup no_monster_gen no_item_gen
NSUBST:   x = 1:+ / 3=xx+ / *:x
SUBST:    c = x
SUBVAULT: B : bzr_entry
SUBST:    B = .
MAP
...............
.cccxxxxxxxccc.
.c...........c.
.c.BBBBBBBBB.c.
.x.BBBBBBBBB.x.
.x.BBBBBBBBB.x.
.x.BBBBBBBBB.x.
.x.BBBBBBBBB.x.
.x.BBBBBBBBB.x.
.x.BBBBBBBBB.x.
.x.BBBBBBBBB.x.
.c.BBBBBBBBB.c.
.c...........c.
.cccxxxxxxxccc.
...............
ENDMAP

NAME:     vaults_room_15_spin_lab_maze
TAGS:     vaults_room_15_spin_lab allow_dup no_wall_fixup
TAGS:     no_monster_gen no_item_gen
SUBST:    H : G...
SUBST:    I : G...
SUBST:    J : G...
NSUBST:   B = 1:B / *:_
NSUBST:   n = 1:_ / *:x
NSUBST:   o = 1:_ / *:x
NSUBST:   p = 1:_ / *:x
NSUBST:   q = 1:_ / *:x
NSUBST:   r = 1:_ / *:x
NSUBST:   s = 1:_ / *:x
NSUBST:   t = 1:_ / *:x
NSUBST:   u = 1:_ / *:x
NSUBST:   v = 1:_ / *:x
NSUBST:   w = 1:_ / *:x
NSUBST:   y = 1:_ / *:x
NSUBST:   z = 1:_ / *:x
FTILE:    B_ = floor_labyrinth
RTILE:    x = wall_lab_rock
SUBVAULT: B : uniq_lab
SUBST:    B = _
MAP
__Bx.......____
_xpx.J.I.J.x_xq
BxBv.......x_xB
pxvxnx.H.xwx_xq
..._Bn...wBw...
.J.xxxnxuxwx.J.
.....__uB_.....
.I.H.x_xyx.H.I.
.....x_yBy.....
.J.xtx_xyxzx.J.
...xBt...zBx...
_xsxxx.H.xxx_xo
_sBs.......r_oB
_xsx.J.I.J.x_xo
____.......r__B
ENDMAP

# Entries for ???_icecave_??? and ???_wizlab_??? are in .des files

NAME:     vaults_room_15_spin_trove_subvault
TAGS:     vaults_room_15_spin_trove allow_dup
SUBVAULT: B : chance_trove
SUBST:    B = .
: vaults_ornamental_pool(_G)
MAP
...............
.BBBBBBBBBB.ww.
.BBBBBBBBBB.ww.
.BBBBBBBBBB....
.BBBBBBBBBB.ww.
.BBBBBBBBBB.ww.
.BBBBBBBBBB.ww.
.BBBBBBBBBB.ww.
.BBBBBBBBBB.ww.
.BBBBBBBBBB.ww.
.BBBBBBBBBB.ww.
...............
.ww.wwwwwww.ww.
.ww.wwwwwww.ww.
...............
ENDMAP

NAME:     vaults_room_15_spin_ziggurat_subvault
TAGS:     vaults_room_15_spin_ziggurat allow_dup no_monster_gen no_item_gen
NSUBST:   x = 1:+ / 3=xx+ / *:x
SUBST:    c = x
SUBVAULT: B : luniq_zig
SUBST:    B = .
MAP
...............
.cccxxxxxxxccc.
.cBBBBBBBBBBBc.
.cBBBBBBBBBBBc.
.xBBBBBBBBBBBx.
.xBBBBBBBBBBBx.
.xBBBBBBBBBBBx.
.xBBBBBBBBBBBx.
.xBBBBBBBBBBBx.
.xBBBBBBBBBBBx.
.xBBBBBBBBBBBx.
.cBBBBBBBBBBBc.
.cBBBBBBBBBBBc.
.cccxxxxxxxccc.
...............
ENDMAP



########
#  15x15 subvaults with a facing direction
#

NAME:     vaults_room_15_facing_empty_door_line
TAGS:     vaults_room_15_facing_empty allow_dup
SHUFFLE:  GHIJKL
SUBST:    I : H.
SUBST:    H : G.
SUBST:    IJKL = .
SUBST:    Z : xxxG
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_door_15
SUBVAULT: C : vaults_door_15
SUBVAULT: D : vaults_door_15
MAP
I..........Zxxx
.G.xxxxxxxxxxxx
..Hx...x...x...
J..x.x.x.x.x.x.
...D.x.C.x.B.x.A
.G.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
.K.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
.G.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
J..x.x.x.x.x.x.
..Hx...x...x...
.G.xxxxxxxxxxxx
I..........Zxxx
ENDMAP

NAME:     vaults_room_15_facing_empty_edged_pool
TAGS:     vaults_room_15_facing_empty allow_dup
SUBST:    W : w..
SUBST:    l : ww.
SUBST:    G : xxG
SUBST:    H : xxxG
SUBST:    I : xxG
SUBST:    J : xxxxG
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_4_city_empty
SUBVAULT: C : vaults_room_4_city_empty     /\
              vaults_room_4_city_encounter
: vaults_ornamental_pool(_G)
MAP
...............
.CCCCCC..xxHxx.
.CCCCCC..IxxxG.
.CCCCCC..xxJxx.
.CCCCCC..WwlwW.A
.CCCCCC..WwlwW.A
.CCCCCC..WwlwW.A
.........WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..xxJxx.
.BBBBBB..IxxxG.
.BBBBBB..xxHxx.
...............
ENDMAP

NAME:     vaults_room_15_facing_empty_mumra_abandoned_shopping_mall
TAGS:     vaults_room_15_facing_empty
KFEAT:    S = abandoned_shop
MONS:     plant
MAP
xxxxxxxxxxxxxxx
xxSxSxS..SxSxSx
xx.x.x....x.x.x
xx............x
x..............+
x...1..WW..1...+
x..T..WGGW..T..x
x..T..WGGW..T..x
x...1..WW..1...+
x..............+
xx............x
xx.x.x....x.x.x
xxSxSxS..SxSxSx
xxxxxxx..xxxxxx
xxxxxxxxxxxxxxx
ENDMAP

NAME:     vaults_room_15_facing_encounter_door_line
TAGS:     vaults_room_15_facing_encounter allow_dup
NSUBST:   1 = 1:0 / *:.
NSUBST:   2 = 1:0 / *:.
NSUBST:   3 = 1:0 / *:.
NSUBST:   4 = 1:0 / *:.
SHUFFLE:  GHIJKL
SUBST:    I : H.
SUBST:    H : G.
SUBST:    IJKL = .
SUBST:    Z : xxxG
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_door_15
SUBVAULT: C : vaults_door_15
SUBVAULT: D : vaults_door_15
MAP
I..........Zxxx
.G.xxxxxxxxxxxx
..Hx...x...x...
J..x.x.x.x.x.x.
..4D.x3C.x2B.x1A
.G4D.x3C.x2B.x1A
..4D.x3C.x2B.x1A
.K4D.x3C.x2B.x1A
..4D.x3C.x2B.x1A
.G4D.x3C.x2B.x1A
..4D.x3C.x2B.x1A
J..x.x.x.x.x.x.
..Hx...x...x...
.G.xxxxxxxxxxxx
I..........Zxxx
ENDMAP

NAME:     vaults_room_15_facing_encounter_edged_pool
TAGS:     vaults_room_15_facing_encounter allow_dup
WEIGHT:   5
SUBST:    W : w..
SUBST:    l : ww.
SUBST:    G : xxG
SUBST:    H : xxxG
SUBST:    I : xxG
SUBST:    J : xxxxG
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_4_city_encounter
SUBVAULT: C : vaults_room_4_city_empty     w:15 /\
              vaults_room_4_city_encounter w:10 /\
              vaults_room_4_city_dangerous w:5
: vaults_ornamental_pool(_G)
MAP
...............
.CCCCCC..xxHxx.
.CCCCCC..IxxxG.
.CCCCCC..xxJxx.
.CCCCCC.0WwlwW.A
.CCCCCC..WwlwW.A
.CCCCCC..WwlwW.A
.........WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB.0WwlwW.A
.BBBBBB..xxJxx.
.BBBBBB..IxxxG.
.BBBBBB..xxHxx.
...............
ENDMAP

NAME:     vaults_room_15_facing_dangerous_door_line_statue
TAGS:     vaults_room_15_facing_dangerous allow_dup
SHUFFLE:  GHIJK
SUBST:    H : G...
SUBST:    G : GGG.
SUBST:    IJK = .
SUBST:    Z : xxxG
MONS:     orange crystal statue / silver statue / ice statue
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_door_15
SUBVAULT: C : vaults_door_15
SUBVAULT: D : vaults_door_15
MAP
I..........Zxxx
.G.xxxxxxxxxxxx
..Hx...x...x...
J..x.x.x.x.x.x.
...D.x.C.x.B.x.A
.G.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
.1.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
.G.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
J..x.x.x.x.x.x.
..Hx...x...x...
.G.xxxxxxxxxxxx
I..........Zxxx
ENDMAP

NAME:     vaults_room_15_facing_dangerous_edged_pool_archers
TAGS:     vaults_room_15_facing_dangerous allow_dup
WEIGHT:   5
SUBST:    W : w..
SUBST:    G : xxG
SUBST:    H : xxxG
SUBST:    I : xxG
SUBST:    J : xxxxG
SHUFFLE:  12
MONS:     human ; bow . arrow q:20
MONS:     human ; crossbow . bolt q:20
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_4_city_encounter
SUBVAULT: C : vaults_room_4_city_encounter
: vaults_ornamental_pool(_G)
MAP
...............
.CCCCCC..xxHxx.
.CCCCCC..IxxxG.
.CCCCCC..xxJxx.
.CCCCCC..Ww1wW.A
.CCCCCC..Ww1wW.A
.CCCCCC..Ww1wW.A
.........Ww1wW.A
.BBBBBB..Ww1wW.A
.BBBBBB..Ww1wW.A
.BBBBBB..Ww1wW.A
.BBBBBB..xxJxx.
.BBBBBB..IxxxG.
.BBBBBB..xxHxx.
...............
ENDMAP

NAME:     vaults_room_15_facing_loot_edged_pool
TAGS:     vaults_room_15_facing_loot allow_dup
SUBST:    W : w..
SUBST:    l : ww.
SUBST:    G : xxG
SUBST:    H : xxxG
SUBST:    I : xxG
SUBST:    J : xxxxG
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_4_city_loot
SUBVAULT: C : vaults_room_4_city_loot
: vaults_ornamental_pool(_G)
MAP
...............
.CCCCCC..xxHxx.
.CCCCCC..IxxxG.
.CCCCCC..xxJxx.
.CCCCCC..WwlwW.A
.CCCCCC..WwlwW.A
.CCCCCC..WwlwW.A
.........WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..xxJxx.
.BBBBBB..IxxxG.
.BBBBBB..xxHxx.
...............
ENDMAP

NAME:     vaults_room_15_facing_up1_edged_pool
TAGS:     vaults_room_15_facing_up1 allow_dup
SUBST:    W : w..
SUBST:    l : ww.
SUBST:    G : xxG
SUBST:    H : xxxG
SUBST:    I : xxG
SUBST:    J : xxxxG
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_4_city_empty
SUBVAULT: C : vaults_room_4_city_up1
: vaults_ornamental_pool(_G)
MAP
...............
.CCCCCC..xxHxx.
.CCCCCC..IxxxG.
.CCCCCC..xxJxx.
.CCCCCC..WwlwW.A
.CCCCCC..WwlwW.A
.CCCCCC..WwlwW.A
.........WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..xxJxx.
.BBBBBB..IxxxG.
.BBBBBB..xxHxx.
...............
ENDMAP

NAME:     vaults_room_15_facing_down1_edged_pool
TAGS:     vaults_room_15_facing_down1 allow_dup
SUBST:    W : w..
SUBST:    l : ww.
SUBST:    G : xxG
SUBST:    H : xxxG
SUBST:    I : xxG
SUBST:    J : xxxxG
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_4_city_empty
SUBVAULT: C : vaults_room_4_city_down1
: vaults_ornamental_pool(_G)
MAP
...............
.CCCCCC..xxHxx.
.CCCCCC..IxxxG.
.CCCCCC..xxJxx.
.CCCCCC..WwlwW.A
.CCCCCC..WwlwW.A
.CCCCCC..WwlwW.A
.........WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..WwlwW.A
.BBBBBB..xxJxx.
.BBBBBB..IxxxG.
.BBBBBB..xxHxx.
...............
ENDMAP

NAME:     vaults_room_15_facing_up3_door_line
TAGS:     vaults_room_15_facing_up3 allow_dup
SHUFFLE:  HI{
SHUFFLE:  GHIJ
SUBST:    HIJ = .
SUBST:    Z : xxxG
NSUBST:   { = 1:{ / *:(
SHUFFLE:  {([
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_door_15
SUBVAULT: C : vaults_door_15
SUBVAULT: D : vaults_door_15
MAP
{..........Zxxx
.G.xxxxxxxxxxxx
..Hx...x...x...
I..x.x.x.x.x.x.
...D.x.C.x.B.x.A
.G.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
.[.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
.G.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
I..x.x.x.x.x.x.
..Hx...x...x...
.G.xxxxxxxxxxxx
{..........Zxxx
ENDMAP

NAME:     vaults_room_15_facing_down3_door_line
TAGS:     vaults_room_15_facing_down3 allow_dup
SHUFFLE:  HI}
SHUFFLE:  GHIJ
SUBST:    HIJ = .
SUBST:    Z : xxxG
NSUBST:   } = 1:} / *:)
SHUFFLE:  })]
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_door_15
SUBVAULT: C : vaults_door_15
SUBVAULT: D : vaults_door_15
MAP
}..........Zxxx
.G.xxxxxxxxxxxx
..Hx...x...x...
I..x.x.x.x.x.x.
...D.x.C.x.B.x.A
.G.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
.].D.x.C.x.B.x.A
...D.x.C.x.B.x.A
.G.D.x.C.x.B.x.A
...D.x.C.x.B.x.A
I..x.x.x.x.x.x.
..Hx...x...x...
.G.xxxxxxxxxxxx
}..........Zxxx
ENDMAP

########
#  Dead-end 15x15 subvaults
#

NAME:     vaults_room_15_dead_end_empty_guarded_subvault
TAGS:     vaults_room_15_dead_end_empty allow_dup
SHUFFLE:  GHIJ
SUBST:    H = G
SUBST:    IJ = .
SHUFFLE:  pq/rs/yz
SUBST:    pqrs = x
SUBST:    y = .
SUBST:    z = xm+
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_7_facing_empty     w:15 /\
              vaults_room_7_facing_encounter w:10 /\
              vaults_room_7_facing_dangerous w:5  /\
              vaults_room_7_facing_loot      w:5
MAP
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
xxxppppp..xxxxx
xxxqxxxx..xxxxx
BBBBBBBxI.xxx..A
BBBBBBBx..xx...A
BBBBBBBB.Hx....A
BBBBBBBB..+..G.A
BBBBBBBB.Hx....A
BBBBBBBx..xx...A
BBBBBBBxI.xxx..A
xxxsxxxx..xxxxx
xxxrrrrr..xxxxx
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
ENDMAP

NAME:     vaults_room_15_dead_end_empty_subvaults_7
TAGS:     vaults_room_15_dead_end_empty allow_dup no_monster_gen
: if crawl.random2(4) < 1 then
:   subst("p = x")
:   shuffle("qrs")
:   subvault("q : vaults_door_7")
:   subvault("r : vaults_door_7")
:   subst("s = x")
:   subvault("E : vaults_room_7_spin_empty")
: else
:   subst("p = +")
:   subst("q = x")
:   shuffle("rs")
:   subst("r = E")
:   subst("s = x")
:   subvault("E : vaults_room_7_dead_end_empty")
: end
SHUFFLE:  BD
SUBVAULT: B : vaults_room_7_spin_encounter
SUBVAULT: C : vaults_room_7_facing_empty
SUBVAULT: D : vaults_room_7_spin_empty
MAP
CCCCCCCxBBBBBBB
CCCCCCCxBBBBBBB
CCCCCCCCBBBBBBB
CCCCCCCCBBBBBBB
CCCCCCCCBBBBBBB+
CCCCCCCxBBBBBBBx
CCCCCCCxBBBBBBBx
xxsssxxxxxqqqxxx
EEEEEEExDDDDDDDx
EEEEEEExDDDDDDDx
EEEEEEErDDDDDDDp
EEEEEEErDDDDDDD
EEEEEEErDDDDDDD
EEEEEEExDDDDDDD
EEEEEEExDDDDDDD
ENDMAP

NAME:     vaults_room_15_dead_end_encounter_guarded_subvault
TAGS:     vaults_room_15_dead_end_encounter allow_dup no_monster_gen
SHUFFLE:  GHIJ
SUBST:    H = 2
SUBST:    IJ = .
SHUFFLE:  pq/rs/yz
SUBST:    pqrs = x
SUBST:    y = .
SUBST:    z = xm+
MONS:     vault guard
MONS:     generate_awake statue name:warrior n_adj \
          tile:mons_statue_scythe ; good_item scythe
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_7_facing_empty     w:10 /\
              vaults_room_7_facing_encounter w:10 /\
              vaults_room_7_facing_dangerous w:5  /\
              vaults_room_7_facing_loot      w:15
MAP
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
xxxppppp..xxxxx
xxxqxxxx..xxxxx
BBBBBBBxI.xxx..A
BBBBBBBx1.xx...A
BBBBBBBB.Hx....A
BBBBBBBB..+..G.A
BBBBBBBB.Hx....A
BBBBBBBx1.xx...A
BBBBBBBxI.xxx..A
xxxsxxxx..xxxxx
xxxrrrrr..xxxxx
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
ENDMAP

NAME:     vaults_room_15_dead_end_encounter_subvaults_7
TAGS:     vaults_room_15_dead_end_encounter allow_dup no_monster_gen
: if crawl.random2(4) < 1 then
:   subst("p = x")
:   shuffle("qrs")
:   subvault("q : vaults_door_7")
:   subvault("r : vaults_door_7")
:   subst("s = x")
:   subvault("E : vaults_room_7_spin_loot / " ..
:                "vaults_room_7_spin_empty")
: else
:   subst("p = +")
:   subst("q = x")
:   shuffle("rs")
:   subst("r = E")
:   subst("s = x")
:   subvault("E : vaults_room_7_dead_end_loot / " ..
:                "vaults_room_7_dead_end_empty")
: end
SHUFFLE:  BD
SUBVAULT: B : vaults_room_7_spin_encounter
SUBVAULT: C : vaults_room_7_facing_encounter
SUBVAULT: D : vaults_room_7_spin_dangerous /\
              vaults_room_7_spin_empty
MAP
CCCCCCCxBBBBBBB
CCCCCCCxBBBBBBB
CCCCCCCCBBBBBBB
CCCCCCCCBBBBBBB
CCCCCCCCBBBBBBB+
CCCCCCCxBBBBBBBx
CCCCCCCxBBBBBBBx
xxsssxxxxxqqqxxx
EEEEEEExDDDDDDDx
EEEEEEExDDDDDDDx
EEEEEEErDDDDDDDp
EEEEEEErDDDDDDD
EEEEEEErDDDDDDD
EEEEEEExDDDDDDD
EEEEEEExDDDDDDD
ENDMAP

NAME:     vaults_room_15_dead_end_dangerous_guarded_subvault
TAGS:     vaults_room_15_dead_end_dangerous allow_dup
SHUFFLE:  GHI
SUBST:    H : G.
SUBST:    I = .
SHUFFLE:  pq/rs/yz
SUBST:    pqrs = x
SUBST:    y = .
SUBST:    z = xm+
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_7_facing_empty     w:15 /\
              vaults_room_7_facing_encounter w:10 /\
              vaults_room_7_facing_dangerous w:5  /\
              vaults_room_7_facing_loot      w:5
MAP
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
xxxppppp..xxxxx
xxxqxxxx..xxxxx
BBBBBBBxI.xxx..A
BBBBBBBx..xx...A
BBBBBBBB.Hx....A
BBBBBBBB..+..9.A
BBBBBBBB.Hx....A
BBBBBBBx..xx...A
BBBBBBBxI.xxx..A
xxxsxxxx..xxxxx
xxxrrrrr..xxxxx
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
ENDMAP

NAME:     vaults_room_15_dead_end_dangerous_spiral_pool
TAGS:     vaults_room_15_dead_end_dangerous allow_dup
SUBST:    - = x+
SUBST:    G = G4
MONS:     fungus / nothing
MONS:     fungus / nothing w:30
MONS:     orange crystal statue / silver statue / ice statue
MONS:     generate_awake statue name:warrior n_adj \
          tile:mons_statue_scythe ; good_item scythe
KFEAT:    ^ = known zot trap
MAP
xxxxx..^..xxxxx
xxx..xxxx...xxx
xx.xxxxxxx...xx
x.xxxxxxxxx...x
x.xxxxwwwxx...xx
xxxxx1www%xx..xx
xxx221www%%x...-
xx.221www3%xG..+
xx.x21www%%x...-
x.xxx1www%xx..xx
x.xxxxwwwxx...xx
x..xxxxxxxx...x
xx.xxxxxxx...xx
xxx..xxxx...xxx
xxxxx..^..xxxxx
ENDMAP

# This is adapted from metal_show in Vaults.des
#
# The Hell Knights are playing the Vault tonight. Rock! \m/ (Shiori)
NAME:     vaults_room_metal_show
TAGS:     vaults_room_15_dead_end_dangerous no_wall_fixup
TAGS:     no_secret_doors no_monster_gen no_item_gen
COLOUR:   M = magenta
COLOUR:   K = magenta
FTILE:    M = floor_rough_red, K = floor_rough_red, # = floor_rough_red
MARKER:   # = lua:fog_machine{\
              pow_max = 15, delay_min = 100, delay_max = 150, \
              size = 2, size_buildup_amnt = 10, \
              size_buildup_time = 1000 }
KFEAT:    # = fountain_blood
# the talent:
: if crawl.one_chance_in(10) then
NSUBST:   M = M / K.
: else
SUBST:    M = .
: end
KMONS:    K = patrolling hell knight
KMONS:    M = patrolling Margery
# the staff-- manager and bartender:
KMONS:    S = human; robe
# the bouncers:
KMONS:    B: iron troll / cyclops / stone giant / minotaur / vault guard
# the fans:
SUBST:    F = 011112223334455679.........
MONS:     human; leather armour|ring mail . dagger|club|whip|nothing
MONS:     elf; robe|leather armour . w:24 quarterstaff|\
          w:1 lajatang|w:75 nothing
# these are Sludge Elves.
MONS:     ghoul / necrophage / rotting hulk / vampire
MONS:     wizard / necromancer
MONS:     big kobold / kobold demonologist
MONS:     deep elf fighter / w:5 deep elf knight /\
          w:3 deep elf mage / w:2 deep elf priest
MONS:     Ijyb / Blork the orc / Urug / Erolcha / Snorg /\
          Terence / Jessica / Sigmund / Psyche / Erica / Agnes\
          / Maud / Louise / Rupert / Wiglaf / Norris / goblin
KITEM:    ! = any potion / potion of confusion, \
              any potion / potion of paralysis, \
              any potion / potion of slowing
KITEM:    $ = q:132 gold / q:66 gold / q:33 gold
MAP
vvvvvvvvvvvvvxx
v!+.MMMM.+S$v..
v!v.KKKK.v$$vF.
vvvv#MM#vvvvvFF
v..FFFFFFvFFvFFx
v!mFvFFvFvFF+BFx
v!mFFFFFFvFFvFF+
v!mFFFFFFvFF+BF+
v!mFvFFvFvFFvFF+
v!mFFFFFFvFF+BFx
vSmFFFFFFvFFvFFx
vvvvvBBvvvFFvFF
vFFFFFFFFFFFvF.
vvvvvvvvvvvvv..
xxxxxxxxxxxxxxx
ENDMAP

NAME:     vaults_room_15_dead_end_loot_guarded_subvault
TAGS:     vaults_room_15_dead_end_loot allow_dup
SHUFFLE:  GHIJ
SUBST:    H = G
SUBST:    IJ = .
SHUFFLE:  pq/rs/yz
SUBST:    pqrs = x
SUBST:    y = .
SUBST:    z = xm+
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_7_facing_loot
MAP
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
xxxppppp..xxxxx
xxxqxxxx..xxxxx
BBBBBBBxI.xxx..A
BBBBBBBx..xx...A
BBBBBBBB.Hx....A
BBBBBBBB..+..G.A
BBBBBBBB.Hx....A
BBBBBBBx..xx...A
BBBBBBBxI.xxx..A
xxxsxxxx..xxxxx
xxxrrrrr..xxxxx
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
ENDMAP

# This is for both entry and exit stairs
# average 24 + 13.5 = 37.5 gold
NAME:     vaults_room_15_dead_end_stairs1_spiral_pool
TAGS:     vaults_room_15_dead_end_up1 allow_dup
TAGS:     vaults_room_15_dead_end_down1 allow_dup
SUBST:    - = x+
SUBST:    I = Gd
MONS:     fungus / nothing
MONS:     fungus / nothing w:30
KFEAT:    ^ = known zot trap
: vaults_ornamental_pool(_G)
: vaults_numbered_gold_items(_G)
: subvault_place_stair(_G)
MAP
xxxxx..^..xxxxx
xxx..xxxx...xxx
xx.xxxxxxx...xx
x.xxxxxxxxx...x
x{xxxxwwwxx...xx
xxxxx1wwwdxx..xx
xxx221wwwdex...-
xx.221wwwIexG..+
xx.x21wwwdex...-
x.xxx1wwwdxx..xx
x.xxxxwwwxx...xx
x..xxxxxxxx...x
xx.xxxxxxx...xx
xxx..xxxx...xxx
xxxxx..^..xxxxx
ENDMAP

NAME:     vaults_room_15_dead_end_up1_subvaults_7
TAGS:     vaults_room_15_dead_end_up1 allow_dup no_monster_gen
: if crawl.random2(4) < 1 then
:   subst("p = x")
:   shuffle("qrs")
:   subvault("q : vaults_door_7")
:   subvault("r : vaults_door_7")
:   subst("s = x")
:   subvault("E : vaults_room_7_spin_up1")
: else
:   subst("p = +")
:   subst("q = x")
:   shuffle("rs")
:   subst("r = E")
:   subst("s = x")
:   subvault("E : vaults_room_7_dead_end_up1")
: end
SHUFFLE:  BD
SUBVAULT: B : vaults_room_7_spin_encounter
SUBVAULT: C : vaults_room_7_facing_encounter
SUBVAULT: D : vaults_room_7_spin_dangerous /\
              vaults_room_7_spin_empty
MAP
CCCCCCCxBBBBBBB
CCCCCCCxBBBBBBB
CCCCCCCCBBBBBBB
CCCCCCCCBBBBBBB
CCCCCCCCBBBBBBB+
CCCCCCCxBBBBBBBx
CCCCCCCxBBBBBBBx
xxsssxxxxxqqqxxx
EEEEEEExDDDDDDDx
EEEEEEExDDDDDDDx
EEEEEEErDDDDDDDp
EEEEEEErDDDDDDD
EEEEEEErDDDDDDD
EEEEEEExDDDDDDD
EEEEEEExDDDDDDD
ENDMAP

NAME:     vaults_room_15_dead_end_down1_subvaults_7
TAGS:     vaults_room_15_dead_end_down1 allow_dup no_monster_gen
: if crawl.random2(4) < 1 then
:   subst("p = x")
:   shuffle("qrs")
:   subvault("q : vaults_door_7")
:   subvault("r : vaults_door_7")
:   subst("s = x")
:   subvault("E : vaults_room_7_spin_down1")
: else
:   subst("p = +")
:   subst("q = x")
:   shuffle("rs")
:   subst("r = E")
:   subst("s = x")
:   subvault("E : vaults_room_7_dead_end_down1")
: end
SHUFFLE:  BD
SUBVAULT: B : vaults_room_7_spin_encounter
SUBVAULT: C : vaults_room_7_facing_encounter
SUBVAULT: D : vaults_room_7_spin_dangerous /\
              vaults_room_7_spin_empty
MAP
CCCCCCCxBBBBBBB
CCCCCCCxBBBBBBB
CCCCCCCCBBBBBBB
CCCCCCCCBBBBBBB
CCCCCCCCBBBBBBB+
CCCCCCCxBBBBBBBx
CCCCCCCxBBBBBBBx
xxsssxxxxxqqqxxx
EEEEEEExDDDDDDDx
EEEEEEExDDDDDDDx
EEEEEEErDDDDDDDp
EEEEEEErDDDDDDD
EEEEEEErDDDDDDD
EEEEEEExDDDDDDD
EEEEEEExDDDDDDD
ENDMAP

NAME:     vaults_room_15_dead_end_up3_guarded_subvault
TAGS:     vaults_room_15_dead_end_up3 allow_dup no_monster_gen
SHUFFLE:  GHIJ
SUBST:    H = G2
SUBST:    IJ = .
SHUFFLE:  pq/rs/yz
SUBST:    pqrs = x
SUBST:    y = .
SUBST:    z = xm+
SUBST:    1 : 1.
MONS:     vault guard
MONS:     generate_awake statue name:warrior n_adj \
          tile:mons_statue_scythe ; good_item scythe
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_7_facing_up3
MAP
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
xxxppppp..xxxxx
xxxqxxxx..xxxxx
BBBBBBBxI.xxx..A
BBBBBBBx1.xx...A
BBBBBBBB.Hx....A
BBBBBBBB..+..G.A
BBBBBBBB.Hx....A
BBBBBBBx1.xx...A
BBBBBBBxI.xxx..A
xxxsxxxx..xxxxx
xxxrrrrr..xxxxx
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
ENDMAP

NAME:     vaults_room_15_dead_end_down3_guarded_subvault
TAGS:     vaults_room_15_dead_end_down3 allow_dup no_monster_gen
SHUFFLE:  GHIJ
SUBST:    H = 2
SUBST:    IJ = .
SHUFFLE:  pq/rs/yz
SUBST:    pqrs = x
SUBST:    y = .
SUBST:    z = xm+
MONS:     vault guard
MONS:     generate_awake statue name:warrior n_adj \
          tile:mons_statue_scythe ; good_item scythe
SUBVAULT: A : vaults_door_15
SUBVAULT: B : vaults_room_7_facing_down3
MAP
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
xxxppppp..xxxxx
xxxqxxxx..xxxxx
BBBBBBBxI.xxx..A
BBBBBBBx1.xx...A
BBBBBBBB.Hx....A
BBBBBBBB..+..G.A
BBBBBBBB.Hx....A
BBBBBBBx1.xx...A
BBBBBBBxI.xxx..A
xxxsxxxx..xxxxx
xxxrrrrr..xxxxx
xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx
ENDMAP



########
#  City-layout 15x15 subvaults
#

# This one might have any contents
NAME:     vaults_room_15_city_subvaults_1044444
TAGS:     vaults_room_15_city_empty vaults_room_15_city_encounter
TAGS:     vaults_room_15_city_dangerous vaults_room_15_city_loot
TAGS:     allow_dup no_monster_gen
# big room
SHUFFLE:  -_
SUBST:    - = A
SUBST:    _ = x
SUBVAULT: A : vaults_room_10_facing_empty     w:10 /\
              vaults_room_10_facing_encounter w:10 /\
              vaults_room_10_facing_dangerous w:5
# side small rooms
SUBST:    m = xxxxxxm++
SHUFFLE:  CE
SUBVAULT: C : vaults_room_4_facing_empty     w:10 /\
              vaults_room_4_facing_encounter w:5  /\
              vaults_room_4_facing_loot      w:2
SUBVAULT: E : vaults_room_4_facing_encounter w:10 /\
              vaults_room_4_facing_dangerous w:5
# outside corner small rooms
SHUFFLE:  abc
SUBST:    a = B
SUBST:    bc = x
SHUFFLE:  efg
SUBST:    e = F
SUBST:    fg = x
SHUFFLE:  BF
SUBVAULT: B : vaults_room_4_dead_end_empty     w:10 /\
              vaults_room_4_dead_end_encounter w:10 /\
              vaults_room_4_dead_end_dangerous w:5
SUBVAULT: F : vaults_room_4_dead_end_empty w:5 /\
              vaults_room_4_dead_end_loot  w:5
# middle corner small room
SHUFFLE:  %*
SUBST:    * : x.
SHUFFLE:  '"
SUBVAULT: ' : vaults_door_4
: if crawl.random2(3) < 1 then
:   subvault('" : vaults_door_4')
: else
:   subst('" = x')
: end
SUBVAULT: D : vaults_room_4_spin_empty     w:5  /\
              vaults_room_4_spin_encounter w:10 /\
              vaults_room_4_spin_dangerous w:5
MAP
xxxx----xxxxxggxx
xAAAAAAAAAAxFFFFx
xAAAAAAAAAAxFFFFf
xAAAAAAAAAAxFFFFf
_AAAAAAAAAAxFFFFx
_AAAAAAAAAAxxeexx
_AAAAAAAAAAxEEEEx
_AAAAAAAAAAmEEEEE
xAAAAAAAAAAxEEEEE
xAAAAAAAAAAxEEEEx
xAAAAAAAAAAxxxxxx
xxxxxxxmxxxxx**xx
xBBBBxCCCCxxDDDDx
aBBBBcCCCCx%DDDD"
aBBBBcCCCCx%DDDD"
xBBBBxCCCCxxDDDDx
xxbbxxxCCxxxx''xx
ENDMAP

NAME:     vaults_room_15_city_empty_pool_x
TAGS:     vaults_room_15_city_empty allow_dup
SHUFFLE:  HIJK
SUBST:    H = G
SUBST:    IJK = x
SUBST:    L : xG
KPROP:    _ = no_rtele_into
KMASK:    _ = no_monster_gen
: vaults_ornamental_pool(_G)
MAP
IxJxxJH...HJxxJxI
xxxxxxx+++xxxxxxx
Jx__wxL...Lxw__xJ
xx_wwxx...xxww_xx
xxwwwwx+++xwwwwxx
Jxxxwww...wwwxxxJ
HxLxxwww.wwwxxLxH
.+..+.w...w.+..+.
.+..+...G...+..+.
.+..+.w...w.+..+.
HxLxxwww.wwwxxLxH
Jxxxwww...wwwxxxJ
xxwwwwx+++xwwwwxx
xx_wwxx...xxww_xx
Jx__wxL...Lxw__xJ
xxxxxxx+++xxxxxxx
IxJxxJH...HJxxJxI
ENDMAP

NAME:     vaults_room_15_city_empty_broken_box
TAGS:     vaults_room_15_city_empty allow_dup
SHUFFLE:  Wl / _-
SUBST:    Wl = w
SUBST:    _ = .
SUBST:    - = x
SHUFFLE:  xX/cC/vV
SUBST:    cCvV = .
NSUBST:   X = 2:+ / 4=xx+ / *:x
SHUFFLE:  GHIJKLMN
SUBST:    H = G
SUBST:    IJKLMN = .
: vaults_ornamental_pool(_G)
MAP
....WWWwwwWWW....
.xXXxlwwwwwlxXXx.
.XcCCcwwwwwcCCcX.
.XCvvVvwwwvVvvCX.
WxCvI.J.W.J.IvCxW
WlcV.L..K..L.VclW
WwwvJ..H.H..JvwwW
wwww..H...H..wwww
wwwwWK..G..KWwwww
wwww..H...H..wwww
WwwvJ..H.H..JvwwW
WlcV.L..K..L.VclW
WxCvI.J.W.J.IvCxW
.XCvvVvwwwvVvvCX.
.XcCCcwwwwwcCCcX.
.xXXxlwwwwwlxXXx.
....WWWwwwWWW....
ENDMAP

NAME:   vaults_room_15_city_empty_mumra_abandoned_shopping_mall
TAGS:   vaults_room_15_city_empty no_wall_fixup
KFEAT:  S = abandoned_shop
KMONS:  p = plant
MAP
.................
..xxxxx..........
.xx...xx.ccccc...
.x.....x.cpppc...
.x..S..x...Sxc...
.x..........xc...
.xx....WW...cc...
..xxx.WGGW.......
......WGGWbbbbb..
.v.....WW.b...b..
.v........b.b.b..
.v..S..vb.bSb.b..
.v+v+v+vb...b.b..
.v.v.v.vbbbbb.b..
.vvvvvvv......b..
.................
.................
ENDMAP

# no randomized loot to avoid breaking no_rtele_into status
NAME:     vaults_room_15_city_encounter_pool_x
TAGS:     vaults_room_15_city_encounter allow_dup
SHUFFLE:  HIJK
SUBST:    H = G
SUBST:    IJK = x
SUBST:    L : xG
MONS:     vault guard
KPROP:    _1% = no_rtele_into
KMASK:    _1% = no_monster_gen
: vaults_ornamental_pool(_G)
MAP
IxJxxJH...HJxxJxI
xxxxxxx+++xxxxxxx
Jx%%wxL111Lxw%%xJ
xx%wwxx___xxww%xx
xxwwwwx+++xwwwwxx
Jxxxwww...wwwxxxJ
HxLxxwww.wwwxxLxH
.+1_+.w...w.+_1+.
.+1_+...G...+_1+.
.+1_+.w...w.+_1+.
HxLxxwww.wwwxxLxH
Jxxxwww...wwwxxxJ
xxwwwwx+++xwwwwxx
xx%wwxx___xxww%xx
Jx%%wxL111Lxw%%xJ
xxxxxxx+++xxxxxxx
IxJxxJH...HJxxJxI
ENDMAP

NAME:     vaults_room_15_city_encounter_broken_box
TAGS:     vaults_room_15_city_encounter allow_dup no_monster_gen
SHUFFLE:  Wl / _-
SUBST:    Wl = w
SUBST:    _ = .
SUBST:    - = x
SHUFFLE:  xX/cC/vV
SUBST:    cCvV = .
NSUBST:   X = 2:+ / 4=xx+ / *:x
SHUFFLE:  GHIJKLMN
SUBST:    H = G
SUBST:    IJKLMN = .
SHUFFLE:  12345
NSUBST:   1 = 1:0 / *:.
NSUBST:   2 = 1:0. / *:.
NSUBST:   3 = 1:0. / *:.
NSUBST:   4 = 1:%. / *:.
NSUBST:   5 = 1:%. / *:.
: vaults_ornamental_pool(_G)
: vaults_standard_loot(_G)
MAP
....WWWwwwWWW....
.xXXxlwwwwwlxXXx.
.XcCCcwwwwwcCCcX.
.XCvvVvwwwvVvvCX.
WxCvI1J.W.J2IvCxW
WlcV1L1.K.2L2VclW
WwwvJ1.H.H.2JvwwW
wwww..H.5.H..wwww
wwwwWK.5G5.KWwwww
wwww..H.5.H..wwww
WwwvJ4.H.H.3JvwwW
WlcV4L4.K.3L3VclW
WxCvI4J.W.J3IvCxW
.XCvvVvwwwvVvvCX.
.XcCCcwwwwwcCCcX.
.xXXxlwwwwwlxXXx.
....WWWwwwWWW....
ENDMAP

NAME:     vaults_room_15_city_dangerous_broken_box
TAGS:     vaults_room_15_city_dangerous allow_dup
SHUFFLE:  Wl / _-
SUBST:    Wl = w
SUBST:    _ = .
SUBST:    - = x
SHUFFLE:  xX/cC/vV
SUBST:    cCvV = .
NSUBST:   X = 2:+ / 4=xx+ / *:x
SHUFFLE:  HIJKLMN
SUBST:    H = G
SUBST:    IJKLMN = .
NSUBST:   0 = 1:9 / *:0
: vaults_ornamental_pool(_G)
: vaults_standard_loot(_G)
MAP
....WWWwwwWWW....
.xXXxlwwwwwlxXXx.
.XcCCcwwwwwcCCcX.
.XCvvVvwwwvVvvCX.
WxCvI.J.W.J.IvCxW
WlcV.L..K..L.VclW
WwwvJ..H.H..JvwwW
wwww..H.0.H..wwww
wwwwWK.0|0.KWwwww
wwww..H.0.H..wwww
WwwvJ..H.H..JvwwW
WlcV.L..K..L.VclW
WxCvI.J.W.J.IvCxW
.XCvvVvwwwvVvvCX.
.XcCCcwwwwwcCCcX.
.xXXxlwwwwwlxXXx.
....WWWwwwWWW....
ENDMAP

# no randomized loot to preserve no_rtele_into status
NAME:     vaults_room_15_city_loot_pool_x
TAGS:     vaults_room_15_city_loot allow_dup
SHUFFLE:  HIJK
SUBST:    H = G
SUBST:    IJK = x
SUBST:    L : xG
KPROP:    %* = no_rtele_into
KMASK:    %* = no_monster_gen
: vaults_ornamental_pool(_G)
MAP
IxJxxJH...HJxxJxI
xxxxxxx+++xxxxxxx
Jx*%wxL...Lxw%*xJ
xx%wwxx...xxww%xx
xxwwwwx+++xwwwwxx
Jxxxwww...wwwxxxJ
HxLxxwww.wwwxxLxH
.+..+.w...w.+..+.
.+..+...G...+..+.
.+..+.w...w.+..+.
HxLxxwww.wwwxxLxH
Jxxxwww...wwwxxxJ
xxwwwwx+++xwwwwxx
xx%wwxx...xxww%xx
Jx*%wxL...Lxw%*xJ
xxxxxxx+++xxxxxxx
IxJxxJH...HJxxJxI
ENDMAP

# average 18 + 52 + 80 + 60 = 201 gold
NAME:     vaults_room_15_city_loot_broken_box_archers
TAGS:     vaults_room_15_city_loot allow_dup
SHUFFLE:  Wl / _-
SUBST:    Wl = w
SUBST:    _ = .
SUBST:    - = x
SHUFFLE:  xX/cC/vV
SUBST:    cCvV = .
NSUBST:   X = 2:+ / 4=xx+ / *:x
SHUFFLE:  12
SUBST:    2 = K
NSUBST:   1 = 1:1 / *=GG1
SHUFFLE:  GHIJKLM
SUBST:    HIJKLM = d
NSUBST:   g = 1:g / *:f
NSUBST:   f = 4:f / *:e
NSUBST:   e = 10:e / *:d
NSUBST:   d = 17:d / *:.
MONS:     statue tile:mons_statue_archer name:archer n_adj; \
          longbow . arrow q:30
: vaults_ornamental_pool(_G)
: vaults_numbered_gold_items(_G)
MAP
....WWWwwwWWW....
.xXXxlwwwwwlxXXx.
.XcCCcwwwwwcCCcX.
.XCvvVvwwwvVvvCX.
WxCvI.JdWdJ.IvCxW
WlcV.2ee1ee2.VclW
WwwvJeeHfHeeJvwwW
wwwwdeHgggHedwwww
wwwwW1fgGgf1Wwwww
wwwwdeHgggHedwwww
WwwvJeeHfHeeJvwwW
WlcV.2ee1ee2.VclW
WxCvI.JdWdJ.IvCxW
.XCvvVvwwwvVvvCX.
.XcCCcwwwwwcCCcX.
.xXXxlwwwwwlxXXx.
....WWWwwwWWW....
ENDMAP

NAME:     vaults_room_15_city_up1_subvaults_1044444
TAGS:     vaults_room_15_city_up1 allow_dup no_monster_gen
: local stairs_in_big_room = false
: if crawl.random2(2) < 1 then
:   stairs_in_big_room = true
: end
# big room
SHUFFLE:  -_
SUBST:    - = A
SUBST:    _ = x
: if stairs_in_big_room then
:   subvault("A : vaults_room_10_facing_up1")
: else
:   subvault("A : vaults_room_10_facing_empty     w:10 / " ..
:                "vaults_room_10_facing_encounter w:10 / " ..
:                "vaults_room_10_facing_dangerous w:5")
: end
# side small rooms
SUBST:    m = xxxxxxm++
SHUFFLE:  CE
SUBVAULT: C : vaults_room_4_facing_empty     w:10 /\
              vaults_room_4_facing_encounter w:5  /\
              vaults_room_4_facing_loot      w:2
SUBVAULT: E : vaults_room_4_facing_encounter w:10 /\
              vaults_room_4_facing_dangerous w:5
# outside corner small rooms
SHUFFLE:  abc
SUBST:    a = B
SUBST:    bc = x
SHUFFLE:  efg
SUBST:    e = F
SUBST:    fg = x
SHUFFLE:  BF
: if stairs_in_big_room then
:   subvault("B : vaults_room_4_dead_end_empty     w:10 / " ..
:                "vaults_room_4_dead_end_encounter w:10 / " ..
:                "vaults_room_4_dead_end_dangerous w:5")
: else
:   subvault("B : vaults_room_4_facing_up1")
: end
SUBVAULT: F : vaults_room_4_dead_end_empty w:5 /\
              vaults_room_4_dead_end_loot  w:5
# middle corner small room
SHUFFLE:  %*
SUBST:    * : x.
SHUFFLE:  '"
SUBVAULT: ' : vaults_door_4
: if crawl.random2(3) < 1 then
:   subvault('" : vaults_door_4')
: else
:   subst('" = x')
: end
SUBVAULT: D : vaults_room_4_spin_empty     w:5  /\
              vaults_room_4_spin_encounter w:10 /\
              vaults_room_4_spin_dangerous w:5
MAP
xxxx----xxxxxggxx
xAAAAAAAAAAxFFFFx
xAAAAAAAAAAxFFFFf
xAAAAAAAAAAxFFFFf
_AAAAAAAAAAxFFFFx
_AAAAAAAAAAxxeexx
_AAAAAAAAAAxEEEEx
_AAAAAAAAAAmEEEEE
xAAAAAAAAAAxEEEEE
xAAAAAAAAAAxEEEEx
xAAAAAAAAAAxxxxxx
xxxxxxxmxxxxx**xx
xBBBBxCCCCxxDDDDx
aBBBBcCCCCx%DDDD"
aBBBBcCCCCx%DDDD"
xBBBBxCCCCxxDDDDx
xxbbxxxCCxxxx''xx
ENDMAP


NAME:     vaults_room_15_city_down1_subvaults_1044444
TAGS:     vaults_room_15_city_down1 allow_dup no_monster_gen
: local stairs_in_big_room = false
: if crawl.random2(2) < 1 then
:   stairs_in_big_room = true
: end
# big room
SHUFFLE:  -_
SUBST:    - = A
SUBST:    _ = x
: if stairs_in_big_room then
:   subvault("A : vaults_room_10_facing_down1")
: else
:   subvault("A : vaults_room_10_facing_empty     w:10 / " ..
:                "vaults_room_10_facing_encounter w:10 / " ..
:                "vaults_room_10_facing_dangerous w:5")
: end
# side small rooms
SUBST:    m = xxxxxxm++
SHUFFLE:  CE
SUBVAULT: C : vaults_room_4_facing_empty     w:10 /\
              vaults_room_4_facing_encounter w:5  /\
              vaults_room_4_facing_loot      w:2
SUBVAULT: E : vaults_room_4_facing_encounter w:10 /\
              vaults_room_4_facing_dangerous w:5
# outside corner small rooms
SHUFFLE:  abc
SUBST:    a = B
SUBST:    bc = x
SHUFFLE:  efg
SUBST:    e = F
SUBST:    fg = x
SHUFFLE:  BF
SUBVAULT: B : vaults_room_4_dead_end_empty     w:10 /\
              vaults_room_4_dead_end_encounter w:10 /\
              vaults_room_4_dead_end_dangerous w:5
: if stairs_in_big_room then
:   subvault("F : vaults_room_4_dead_end_empty / " ..
:                "vaults_room_4_dead_end_loot")
: else
:   subvault("F : vaults_room_4_facing_down1")
: end
# middle corner small room
SHUFFLE:  %*
SUBST:    * : x.
SHUFFLE:  '"
SUBVAULT: ' : vaults_door_4
: if crawl.random2(3) < 1 then
:   subvault('" : vaults_door_4')
: else
:   subst('" = x')
: end
SUBVAULT: D : vaults_room_4_spin_empty     w:5  /\
              vaults_room_4_spin_encounter w:10 /\
              vaults_room_4_spin_dangerous w:5
MAP
xxxx----xxxxxggxx
xAAAAAAAAAAxFFFFx
xAAAAAAAAAAxFFFFf
xAAAAAAAAAAxFFFFf
_AAAAAAAAAAxFFFFx
_AAAAAAAAAAxxeexx
_AAAAAAAAAAxEEEEx
_AAAAAAAAAAmEEEEE
xAAAAAAAAAAxEEEEE
xAAAAAAAAAAxEEEEx
xAAAAAAAAAAxxxxxx
xxxxxxxmxxxxx**xx
xBBBBxCCCCxxDDDDx
aBBBBcCCCCx%DDDD"
aBBBBcCCCCx%DDDD"
xBBBBxCCCCxxDDDDx
xxbbxxxCCxxxx''xx
ENDMAP

NAME:     vaults_room_15_city_stairs1_pool_x
TAGS:     vaults_room_15_city_up1
TAGS:     vaults_room_15_city_down1 allow_dup
SHUFFLE:  HIJK
SUBST:    H = G
SUBST:    IJK = x
SUBST:    L : xG
KPROP:    _ = no_rtele_into
KMASK:    _ = no_monster_gen
SUBST:    1 : 1__
MONS:     vault guard
: vaults_ornamental_pool(_G)
: subvault_place_stair(_G)
MAP
IxJxxJH...HJxxJxI
xxxxxxx+++xxxxxxx
Jx__wxL111Lxw__xJ
xx_wwxx___xxww_xx
xxwwwwx+++xwwwwxx
Jxxxwww...wwwxxxJ
HxLxxwww.wwwxxLxH
.+1_+.w...w.+_1+.
.+1_+...{...+_1+.
.+1_+.w...w.+_1+.
HxLxxwww.wwwxxLxH
Jxxxwww...wwwxxxJ
xxwwwwx+++xwwwwxx
xx_wwxx___xxww_xx
Jx__wxL111Lxw__xJ
xxxxxxx+++xxxxxxx
IxJxxJH...HJxxJxI
ENDMAP

NAME:     vaults_room_15_city_up3_broken_box
TAGS:     vaults_room_15_city_up3 allow_dup
SHUFFLE:  Wl / _-
SUBST:    Wl = w
SUBST:    _ = .
SUBST:    - = x
SHUFFLE:  xX/cC/vV
SUBST:    cCvV = .
NSUBST:   X = 2:+ / 4=xx+ / *:x
SHUFFLE:  GHIJKL
SUBST:    HIJKL = .
SHUFFLE:  {([<
SUBST:    < = <.
: vaults_ornamental_pool(_G)
MAP
....WWWwwwWWW....
.xXXxlwwwwwlxXXx.
.XcCCcwwwwwcCCcX.
.XCvvVvwwwvVvvCX.
WxCvI.J.W.J.IvCxW
WlcV.{..K..(.VclW
WwwvJ..H.H..JvwwW
wwww..H...H..wwww
wwwwWK..G..KWwwww
wwww..H...H..wwww
WwwvJ..H.H..JvwwW
WlcV.<..K..[.VclW
WxCvI.J.W.J.IvCxW
.XCvvVvwwwvVvvCX.
.XcCCcwwwwwcCCcX.
.xXXxlwwwwwlxXXx.
....WWWwwwWWW....
ENDMAP

NAME:     vaults_room_15_city_down3_broken_box
TAGS:     vaults_room_15_city_down3 allow_dup
SHUFFLE:  Wl / _-
SUBST:    Wl = w
SUBST:    _ = .
SUBST:    - = x
SHUFFLE:  xX/cC/vV
SUBST:    cCvV = .
NSUBST:   X = 2:+ / 4=xx+ / *:x
SHUFFLE:  GHIJKL
SUBST:    HIJKL = .
SHUFFLE:  })]>
SUBST:    > = >.
: vaults_ornamental_pool(_G)
MAP
....WWWwwwWWW....
.xXXxlwwwwwlxXXx.
.XcCCcwwwwwcCCcX.
.XCvvVvwwwvVvvCX.
WxCvI.J.W.J.IvCxW
WlcV.}..K..).VclW
WwwvJ..H.H..JvwwW
wwww..H...H..wwww
wwwwWK..G..KWwwww
wwww..H...H..wwww
WwwvJ..H.H..JvwwW
WlcV.>..K..].VclW
WxCvI.J.W.J.IvCxW
.XCvvVvwwwvVvvCX.
.XcCCcwwwwwcCCcX.
.xXXxlwwwwwlxXXx.
....WWWwwwWWW....
ENDMAP





################################################################
#  Huge center subvaults
#
#  These are the vaults used for the center of the square ring
#    huge center layout.  They are classified by size in glyphs
#    instead of using the system for other vaults_ subvaults.
#    The legal sizes are determined by the layout_vaults_.des
#    file.  The current legal sizes are:
#      -> 30x20
#

# 32 monsters, 38 %
NAME:     vaults_room_st_greater_box_1
TAGS:     vaults_room_huge_30x20
NSUBST:   ' = 5:9 / 3:0 / *:.
NSUBST:   " = 8:9 / *:.
NSUBST:   - = 8:9 / *:.
NSUBST:   8 = 2:8 / *:.
: st_box_setup(_G)
MAP
vvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
v''''''''''''''''''''''''''''v
v''''''''''''''''''''''''''''v
v''''''''''''''''''''''''''''v
v''''''''''''''''''''''''''''v
vvvv&&vvvv''''''''''vv&&vvvvvv
v""""""""v''''''''''v----v-8-v
v""""""""v''''''''''v----v---v
v""""""""vvvvv&&vvvvv----v---v
v""""""""v%%%v77v---v----v---v
v""""""""v%%%v77v------------v
v""""""""v%%%v77v------------v
v""""""""v"""v&&v------------v
v"""""""""""8v..v------------v
v""""""""""""v..v------------v
v""""""""v"""v..v------------v
v""""""""v%%%v..v-----8------v
v""""""""v%%%v..v$%%%%%%%%%%$v
v"""8""""v%%%v..v$%%%%%%%%%%$v
vvvvvvvvvvvvvv&&vvvvvvvvvvvvvv
ENDMAP

# 32 monsters, 36 %
NAME:     vaults_room_st_greater_box_2
TAGS:     vaults_room_huge_30x20
SHUFFLE:  ab
SUBST:    a = +
SUBST:    b = v
SUBST:    c : v.
SUBST:    d : v.
NSUBST:   ' = 8:9 / *:.
NSUBST:   " = 8:9 / *:.
NSUBST:   - = 8:9 / *:.
NSUBST:   8 = 2:8 / *:.
: st_box_setup(_G)
MAP
vvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
v%%%---v"""""""8""""""v---%%%v
v%%%---+""""""""""""""+---%%%v
v%%%8--v""""""""""""""v--8%%%v
v------vvvvvvvaavvvvvvv------v
v------v''''''''''''''v------v
vvbbvvvb'c''''dd''''c'vvvvbbvv
v""""v'''c''''dd''''c'''v""""v
v""""v'ccc''''''''''ccc'v""""v
v""""a'''''dd''8'dd'''''a"""8v
v8"""a'''''dd'8''dd'''''a""""v
v""""v'ccc''''''''''ccc'v""""v
v""""v'''c''''dd''''c'''v""""v
vv++vvvv'c''''dd''''c'bvvv++vv
v------v''''''''''''''v------v
v------vvvvvvv&&vvvvvvv------v
v%%%8--v..7........7..v--8%%%v
v%%%---v..7........7..v---%%%v
v%%%---v..7........7..v---%%%v
vvvvvvvvvvvvvv&&vvvvvvvvvvvvvv
ENDMAP

# 36 monsters, 42 %
NAME:     vaults_room_st_greater_box_3
TAGS:     vaults_room_huge_30x20
NSUBST:   ' = 12:9 / *:.
NSUBST:   " = 10:9 / *:.
: st_box_setup(_G)
MAP
vvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
vv''''vv''''vv77vv..9..9..vv"v
v''''vv''''vv....vv......vv""v
v...vv''''vv......vv....vv"""v
v...v....vv........vv++vv""""v
v...+....&7...vv...7&..+"""""v
v...+....&7...vv...7&..+"""""v
v...v....vv........vv++vv""""v
v...vv''''vv......vv""""vv"""v
v''''vv''''vv....vv""""""vv""v
vv''''vv''''vv77vv""""""""vv+v
vvv''''vv''''v&&v""""""""""+8v
v%vv''''vv''vv..vv""""""""vv+v
v%%vv''''vvvv....vv""""""vv$$v
v%%%vv''''vv......vv""""vv$%%v
v%%%%vv'''v0......0vv++vv%%%%v
v%%$%$vv++vv......vv$%%$vv%%%v
v%%%$&v....vv....vv$%%%%$vv%%v
v%%$vv.....8vv..vv$%%%%%%$vv%v
vvvvvvvvvvvvvv++vvvvvvvvvvvvvv
ENDMAP

# 31 monsters, 28 %, 8 |
NAME:     vaults_room_st_greater_box_4
TAGS:     vaults_room_huge_30x20
NSUBST:   ' = 5:9 / 3:0 / *:.
NSUBST:   " = 8:9 / *:.
NSUBST:   - = 8:9 / 2:8 / *:.
: st_box_setup(_G)
MAP
vvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
v%$%$v------------------v%$%$v
v$%$%v------------------v$%$%v
v%|%$+------------------+%|%$v
v$%|%+------------------+$%|%v
v%$%$v------------------v%$%$v
v$%$%v------------------v$%$%v
vvvvvvvvvvvvvv&&vvvvvvvvvvvvvv
v......."""""""""""""".......v
v......."""""""""""""".......v
v+vvvv+vvvvvvvvvvvvvvvv+vvvv+v
v''''v""""""v%||%v""""""v''''v
v''''v""""""+%||%+""""""v''''v
vvvv+vvvvvvvvvvvvvvvvvvvv+vvvv
v.......''''''''''''''.......v
v.......''''''''''''''.......v
v++vvvvvvvvvvvvvvvvvvvvvvvv++v
v........77........77........v
v........7..........7........v
vvvvvvvvvvvvvv&&vvvvvvvvvvvvvv
ENDMAP

# 26 monsters, 36 %
NAME:     vaults_room_st_greater_box_5
TAGS:     vaults_room_huge_30x20
NSUBST:   ' = 15:9 / 3:8 / *:.
SHUFFLE:  abc
SUBST:    a = +
SUBST:    b = v
SUBST:    c = v
: st_box_setup(_G)
MAP
vvvvvvvvvvvvvvvvvvvvvvvvvvvvvv
v%$%$v''''''''''''''''''v%$%$v
v$%$%+''''''''''''''''''+$%$%v
v%$%$v''''''''''''''''''v%$%$v
vvvvvv''''''''''''''''''vvvvvv
v%%%%v''''''''''''''''''v%%%%v
v%%%%+''''''''''''''''''+%%%%v
v%%%%v''''''''''''''''''v%%%%v
vvvvvv''''''''98''''''''vvvvvv
v$$$$v''''''''''''''''''v$$$$v
v$$$$+''''''''''''''''''+$$$$v
v$$$$v''''''''''''''''''v$$$$v
vvvvvv''''''''''''''''''vvvvvv
v....v''''''''''''''''''v....v
v....+''''''''''''''''''+....v
v....v''''''''''''''''''v....v
vavvvvvvvcvvvvbbvvvvcvvvvvvvav
v.........77......77.........v
v.........7........7.........v
vvvvvvvvvvvvvv&&vvvvvvvvvvvvvv
ENDMAP

# This is based off the the_teleporter vault in Vaults.des
#
# The Teleporter! There's some nice loot, but you'll have to fight your
# way to it.
NAME:     vaults_room_the_teleporter_subvaults
TAGS:     vaults_room_huge_30x20 uniq_the_teleporter
TAGS:     no_secret_doors no_monster_gen no_item_gen

{{
-- Lua marker magic!
local m_marker = TriggerableFunction:new {
  func="teleporter_vault_action_fn",
  data = {teleport_spot=1},
  repeated=true }
local n_marker = TriggerableFunction:new {
  func="teleporter_vault_action_fn",
  data = {teleport_spot=2},
  repeated=true }
m_marker:add_triggerer(DgnTriggerer:new { type="player_move" })
n_marker:add_triggerer(DgnTriggerer:new { type="player_move" })

lua_marker('M', m_marker)
lua_marker('N', n_marker)

-- And the slaves.
lua_marker('n', portal_desc { teleport_spot=1})
lua_marker('m', portal_desc { teleport_spot=2})
}}

KFEAT:    MN = teleporter
MONS:     patrolling iron golem / patrolling crystal golem / patrolling stone golem
MONS:     vault guard
SUBST:    mn = U

# place subvaults
SHUFFLE:  ab
SUBST:    b = x
SUBST:    a = A
SUBVAULT: A : vaults_room_7_dead_end_empty     w:15 /\
              vaults_room_7_dead_end_encounter w:10/\
              vaults_room_7_dead_end_dangerous w:5 /\
              vaults_room_7_dead_end_loot      w:5
SUBVAULT: B : vaults_room_4_facing_encounter
SUBVAULT: C : vaults_room_7_facing_encounter
SHUFFLE:  DE
SUBVAULT: D : vaults_room_4_spin_empty
SUBVAULT: E : vaults_room_4_spin_dangerous
SUBVAULT: d : vaults_door_4
SUBVAULT: e : vaults_door_4
NSUBST:   V = 1:. / *:v
SUBVAULT: F : vaults_room_4_dead_end_empty     w:15 /\
              vaults_room_4_dead_end_encounter w:10/\
              vaults_room_4_dead_end_dangerous w:5 /\
              vaults_room_4_dead_end_loot      w:5
NSUBST:   ' = 1:2 / *:%
SHUFFLE:  HI
SUBVAULT: H : vaults_room_7_facing_empty
SUBVAULT: I : vaults_room_7_facing_encounter
SHUFFLE:  ij
SUBST:    i = x
SUBST:    j = J
SHUFFLE:  Jk / Ll
SUBST:    k = x
SUBST:    l = K
SUBVAULT: J : vaults_room_4_dead_end_encounter
SUBVAULT: L : vaults_room_4_facing_dangerous
SUBVAULT: K : vaults_room_4_dead_end_loot
MAP
xxxxxxx+xxxHHHxxxxFFxxxxaaaxxx
xU...Ux'xHHHHHHHxFFFFxAAAAAAAx
x.|*|.xxxHHHHHHHxFFFFxAAAAAAAx
x.*n*.+NxHHHHHHHxFFFFxAAAAAAAb
x.|*|.xxxHHHHHHHxFFFFxAAAAAAAb
xU...Ux%+HHHHHHHxxxxxxAAAAAAAb
xxxxxxxxxHHHHHHHxx**xxAAAAAAAx
xIIIIIIIxHHHHHHHxDDDDxAAAAAAAx
xIIIIIIIxxx+xxxxxDDDDxxxxxxxxx
IIIIIIIIxEEEEx..+DDDD+.xxBBBBx
IIIIIIIIeEEEEx.xxDDDDxx.+BBBBB
IIIIIIIIeEEEE+.xxxddxxxxxBBBBB
xIIIIIIIxEEEEx.xCCCCCCCxxBBBBx
xIIIIIIIxx**xx2xCCCCCCCxxxxxxx
xxxxxxxxxxxxxxxxCCCCCCC+%x000x
xJJJJxKKKKxLLLLxCCCCCCCxxx010x
jJJJJkKKKKlLLLLxCCCCCCCxM+1m1+
jJJJJkKKKKlLLLLxCCCCCCCxxx010x
xJJJJxKKKKxLLLLxCCCCCCCx'x000x
xxiixxxxxxxxLLxxxxCCCxxx+xxxxx
ENDMAP

# A couple generic ones, with low weight and the allow_dup tag

NAME:     vaults_room_huge_30x20_subvaults_1
TAGS:     vaults_room_huge_30x20 allow_dup no_monster_gen no_item_gen
WEIGHT:   1
# corner, left and right rooms
SHUFFLE:  ab
SHUFFLE:  cd
SHUFFLE:  gh
SHUFFLE:  ij
SUBST:    bdhj = x
SUBST:    a = A
SUBST:    c = C
SUBST:    g = G
SUBST:    i = I
SHUFFLE:  ABCGHI
SUBVAULT: A : vaults_room_4_dead_end_empty
SUBVAULT: B : vaults_room_4_dead_end_empty
SUBVAULT: C : vaults_room_4_dead_end_encounter
SUBVAULT: G : vaults_room_4_dead_end_encounter
SUBVAULT: H : vaults_room_4_dead_end_dangerous
SUBVAULT: I : vaults_room_4_dead_end_loot
# top and bottom rooms
SHUFFLE:  DFJL
SUBVAULT: D : vaults_room_4_facing_empty
SUBVAULT: F : vaults_room_4_facing_encounter
SUBVAULT: J : vaults_room_4_facing_dangerous
SUBVAULT: L : vaults_room_4_facing_loot
SHUFFLE:  ef
SHUFFLE:  kl
SUBVAULT: e : vaults_door_4
: if crawl.random2(2) < 1 then
:   subvault("f : vaults_door_4")
: else
:   subst("f : mx")
: end
SUBVAULT: k : vaults_door_4
: if crawl.random2(2) < 1 then
:   subvault("l : vaults_door_4")
: else
:   subst("l : mx")
: end
SHUFFLE:  EK
SUBVAULT: E : vaults_room_4_spin_empty
SUBVAULT: K : vaults_room_4_spin_encounter
# windows
SHUFFLE:  mnopqrst
SUBST:    n : m
SUBST:    o : mx
SUBST:    p : mx
SUBST:    qrst = x
# center stuff
SUBST:    W : w.
SUBVAULT: M : vaults_room_4_city_encounter w:10 /\
              vaults_room_4_city_dangerous w:5
SUBVAULT: N : vaults_room_4_city_empty     w:10 /\
              vaults_room_4_city_loot      w:5
: vaults_ornamental_pool(_G)
MAP
xxaaxx+xxLLxxxkkxxxJJxx+xxiixx
xAAAAx.xLLLLxKKKKxJJJJx.xIIIIx
bAAAAx.xLLLLmKKKKnJJJJx.xIIIIj
bAAAAx.xLLLLmKKKKnJJJJx.xIIIIj
xAAAAx.xLLLLxKKKKxJJJJx.xIIIIx
xxxxxx.xxooxxxllxxxppxx.xxxxxx
+............................+
xxxxxx.MMMMMM.WW.NNNNNN.xxxxxx
xBBBBx.MMMMMM.ww.NNNNNN.xHHHHx
BBBBBx.MMMMMM.ww.NNNNNN.xHHHHH
BBBBBx.MMMMMM.ww.NNNNNN.xHHHHH
xBBBBx.MMMMMM.ww.NNNNNN.xHHHHx
xxxxxx.MMMMMM.WW.NNNNNN.xxxxxx
+............................+
xxxxxx.xxqqxxxffxxxrrxx.xxxxxx
xCCCCx.xDDDDxEEEExFFFFx.xGGGGx
cCCCCx.xDDDDsEEEEtFFFFx.xGGGGh
cCCCCx.xDDDDsEEEEtFFFFx.xGGGGh
xCCCCx.xDDDDxEEEExFFFFx.xGGGGx
xxddxx+xxDDxxxeexxxFFxx+xxggxx
ENDMAP

NAME:     vaults_room_huge_30x20_subvaults_2
TAGS:     vaults_room_huge_30x20 allow_dup no_monster_gen no_item_gen
WEIGHT:   1
MONS:     vault guard
# big corner rooms
SHUFFLE:  ak
SHUFFLE:  bl
SUBST:    kl = x
SUBST:    a = A
SUBST:    b = B
SHUFFLE:  AB
SUBVAULT: A : vaults_room_10_dead_end_encounter
SUBVAULT: B : vaults_room_10_dead_end_empty     w:10 /\
              vaults_room_10_dead_end_encounter w:5 /\
              vaults_room_10_dead_end_dangerous w:5 /\
              vaults_room_10_dead_end_loot      w:5
# middle rooms
SHUFFLE:  CD
SUBVAULT: C : vaults_room_7_facing_encounter
SUBVAULT: D : vaults_room_7_facing_dangerous
NSUBST:   m = 1:+ / *=+x
NSUBST:   n = 1:+ / *=+x
SHUFFLE:  1%
SUBVAULT: c : vaults_door_7
SUBVAULT: d : vaults_door_7
SHUFFLE:  12*%
SUBST:    2 = 1
SUBST:    * = .
# other corner rooms
SHUFFLE:  eij
SHUFFLE:  fgh
SUBST:    e = E
SUBST:    f = F
SUBST:    ghij = x
SUBVAULT: E : vaults_room_7_dead_end_loot
SUBVAULT: F : vaults_room_7_dead_end_empty
: vaults_standard_loot(_G)
MAP
xxxxaaaaxxxxxxxxDDDxxxxxeeexxx
xAAAAAAAAAAxxxDDDDDDDxEEEEEEEx
xAAAAAAAAAAxxxDDDDDDDxEEEEEEEx
xAAAAAAAAAAx2dDDDDDDDiEEEEEEEj
kAAAAAAAAAAx2dDDDDDDDiEEEEEEEj
kAAAAAAAAAAx2dDDDDDDDiEEEEEEEj
kAAAAAAAAAAxxxDDDDDDDxEEEEEEEx
kAAAAAAAAAAxxxDDDDDDDxEEEEEEEx
xAAAAAAAAAAxxxxxmmxxxxxxxxxxxx
xAAAAAAAAAAx11xx%%xBBBBBBBBBBx
xAAAAAAAAAAx11xx%%xBBBBBBBBBBx
xxxxxxxxxxxxnnxxxxxBBBBBBBBBBx
xFFFFFFFxCCCCCCCxxxBBBBBBBBBBl
xFFFFFFFxCCCCCCCxxxBBBBBBBBBBl
gFFFFFFFhCCCCCCCc*xBBBBBBBBBBl
gFFFFFFFhCCCCCCCc*xBBBBBBBBBBl
gFFFFFFFhCCCCCCCc*xBBBBBBBBBBx
xFFFFFFFxCCCCCCCxxxBBBBBBBBBBx
xFFFFFFFxCCCCCCCxxxBBBBBBBBBBx
xxxfffxxxxxCCCxxxxxxxxbbbbxxxx
ENDMAP

NAME:     vaults_room_huge_30x20_subvaults_3
TAGS:     vaults_room_huge_30x20 allow_dup no_monster_gen no_item_gen
WEIGHT:   1
SUBST:    J : I.
SUBST:    I : HH.
SUBST:    H : GGG.
SUBST:    G : GG.
SUBVAULT: A : vaults_room_15_city_empty     w:10 /\
              vaults_room_15_city_encounter w:15 /\
              vaults_room_15_city_dangerous w:5  /\
              vaults_room_15_city_loot      w:5
SUBVAULT: B : vaults_room_10_city_empty     w:10 /\
              vaults_room_10_city_encounter w:15 /\
              vaults_room_10_city_dangerous w:5  /\
              vaults_room_10_city_loot      w:5
# small rooms
SHUFFLE:  def
SUBST:    ef = x
SHUFFLE:  ijk
SUBST:    ij = x
SHUFFLE:  dD/kK
SUBST:    d = D
SHUFFLE:  gk
SUBST:    g = x
SUBST:    k = K
SUBVAULT: D : vaults_room_4_facing_empty     w:10 /\
              vaults_room_4_facing_encounter w:15 /\
              vaults_room_4_facing_dangerous w:5
SUBVAULT: K : vaults_room_4_dead_end_empty     w:10 /\
              vaults_room_4_dead_end_encounter w:5  /\
              vaults_room_4_dead_end_loot      w:5
MAP
G.J.I.J.H.J.I.J.G.BBBBBBBBBBBB
..................BBBBBBBBBBBB
..................BBBBBBBBBBBB
AAAAAAAAAAAAAAAAA.BBBBBBBBBBBB
AAAAAAAAAAAAAAAAA.BBBBBBBBBBBB
AAAAAAAAAAAAAAAAA.BBBBBBBBBBBB
AAAAAAAAAAAAAAAAA.BBBBBBBBBBBB
AAAAAAAAAAAAAAAAA.BBBBBBBBBBBB
AAAAAAAAAAAAAAAAA.BBBBBBBBBBBB
AAAAAAAAAAAAAAAAA.BBBBBBBBBBBB
AAAAAAAAAAAAAAAAA.BBBBBBBBBBBB
AAAAAAAAAAAAAAAAA.BBBBBBBBBBBB
AAAAAAAAAAAAAAAAA.............
AAAAAAAAAAAAAAAAA.............
AAAAAAAAAAAAAAAAA..xxjjxxxddxx
AAAAAAAAAAAAAAAAA..xKKKKxDDDDx
AAAAAAAAAAAAAAAAA..iKKKKgDDDDe
AAAAAAAAAAAAAAAAA..iKKKKgDDDDe
AAAAAAAAAAAAAAAAA..xKKKKxDDDDx
AAAAAAAAAAAAAAAAA..xxkkxxxffxx
ENDMAP

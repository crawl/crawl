##############################################################################
# pan.des: Minivaults particular to Pan, and the lairs of the Pan Demon Lords
#
# Contents:
#   Pan inline functions
#   Entry portals to Pan
#   Panlord vaults: Mnoleg, Lom Lobon, Cerebov, Gloorx Vloq.
#   Lesser demon vaults: repeating rune vaults.
#   Lesser demon vaults: standalone vault.
#
##############################################################################


##############################################################################
# Pan lua functions
###############################################################################

# The generic pan lord warning message.

{{
function pan_lord_epilogue(name)
    local msgs = {
        Mnoleg = "The air here is shimmering with an eerie glow.",
        ['Lom Lobon'] = "You sense a powerful magical presence. It is not pleased.",
        Cerebov = "Searing heat pours from the floors and walls of this place.",
        ['Gloorx Vloq'] = "Shadowy figures dance across your vision."
    }

    crawl.mpr(msgs[name], "warning")
    crawl.mpr("The mighty Pandemonium lord " .. name .. " resides here.", "warning")
    crawl.take_note('Entered the realm of ' .. name .. '.')
    crawl.mark_milestone('br.enter', 'entered the realm of ' .. name .. '.', "")
end

function pan_lord_setup(_G, name)
    -- Set level monsters.
    -- needs exactly 10 monsters, weights are not supported yet
    local monsters = {
        Mnoleg = "large abomination w:200 / " ..
                 "neqoxec w:200 / chaos spawn w:200 / " ..
                 "cacodemon w:300 / tormentor / " ..
                 "tentacled monstrosity w:5 / brain worm / " ..
                 "giant eyeball / golden eye band / eye of devastation / " ..
                 "eye of draining / shining eye / very ugly thing band",
        ['Lom Lobon'] = "hellwing w:300 / sixfirhy w:100 / " ..
                 "smoke demon w:300 / blizzard demon w:100 / " ..
                 "green death w:200 / giant orange brain / rakshasa / " ..
                 "wizard / draconian annihilator / deep elf annihilator / " ..
                 "titan w:5 / vapour / lich w:5",
        Cerebov = "efreet / orange demon / sun demon / ynoxinul / balrug / " ..
                  "brimstone fiend w:1",
        ['Gloorx Vloq'] = "skeleton w:400 / " ..
                 "soul eater w:200 / lorocyproca w:100 / " ..
                 "executioner w:300 / demonic crawler / shadow wraith / " ..
                 "unseen horror"
    }

    _G.set_random_mon_list(monsters[name])

    -- Set level colours / tiles, and the rune.
    if name == "Mnoleg" then
        _G.lfloorcol("lightred")
        _G.lrockcol("yellow")
        _G.lfloortile("floor_nerves")
        _G.lrocktile("wall_zot_yellow")
        _G.kitem("O = glowing rune of Zot")
    elseif name == "Lom Lobon" then
        _G.lfloorcol("lightgrey")
        _G.lrockcol("white")
        _G.lfloortile("floor_rough_lightgray")
        _G.lrocktile("wall_hall_white")
        _G.kitem("O = magical rune of Zot")
    elseif name == "Cerebov" then
        _G.lfloorcol("red")
        _G.lrockcol("red")
        _G.lfloortile("floor_volcanic")
        _G.lrocktile("wall_zot_red")
        _G.kitem("O = fiery rune of Zot")
    elseif name == "Gloorx Vloq" then
        _G.lfloorcol("blue")
        _G.lrockcol("blue")
        _G.lfloortile("floor_grey_dirt")
        _G.lrocktile("wall_hall_darkgray")
        _G.kitem("O = dark rune of Zot")
    end
end

colours = {
    "magenta", "blue", "cyan", "green",
    "brown", "red", "lightgrey"
}
brightcolours = {
    "lightmagenta", "lightblue", "lightcyan", "lightgreen",
    "yellow", "lightred", "white"
}

disco_choices = {
    function (x, y, ballx, bally, turn, rand)
        local index = ((x + turn) % #colours) + 1
        if rand > 60 then
            return colours[index]
        else
            return brightcolours[index]
        end
    end,
    function (x, y, ballx, bally, turn, rand)
        local index = ((y + turn) % #colours) + 1
        if rand > 60 then
            return colours[index]
        else
            return brightcolours[index]
        end
    end,
    function (x, y, ballx, bally, turn, rand)
        local angle = math.atan2(y - bally, x - ballx)
        local segment = (math.floor(angle / (2 * math.pi) * (2 * #colours)) + 2 * #colours) % (2 * #colours)
        local index = (math.floor((segment + turn) / 2) % #colours) + 1
        if rand > 60 then
            return colours[index]
        else
            return brightcolours[index]
        end
    end,
    function (x, y, ballx, bally, turn, rand)
        local distance = math.floor(math.sqrt((x - ballx)^2 + (y - bally)^2))
        local index = ((distance + turn) % #colours) + 1
        if rand > 60 then
            return colours[index]
        else
            return brightcolours[index]
        end
    end
}

colour.add_colour("disco", function(rand, x, y)
    local vault = dgn.vault_at(x, y)
    local ballx = 0
    local bally = 0
    if vault then
        local ul = vault:pos()
        local ulx, uly = ul:xy()
        x, y = x - ulx, y - uly

        local map = vault:map()
        local bx, by = dgn.gly_point(map, "D")
        if bx ~= nil then
            ballx = bx
            bally = by
        end
    end

    local cycle = disco_choices[math.floor((you.turns() % (16 * #disco_choices)) / 16) + 1]
    return cycle(x, y, ballx, bally, you.turns() % 16, rand)
end)

function pan_demonic_rune(e, glyph, place_lord)
    e.subst(glyph .. ' = ' .. (place_lord and glyph or '.') .. ':6 O:1 N:2')
    e.kitem('O = demonic rune of Zot')
    e.kfeat('N = exit_pandemonium')
    if place_lord then
        e.kmons(glyph .. 'ON = pandemonium lord')
    end
end

function pan_entry(e, glyph, noboss)
    e.tags("extra luniq_enter_pan chance_enter_pan")
    e.depth("Depths, D:21-")
    e.depth_chance("Depths:3", 10000)
    e.depth_chance("D:24", 10000)
    e.chance(2500)
    if you.where() == 'Depths:3' or you.where() == 'D:24' then
        e.kfeat(glyph .. " = enter_pandemonium no_mimic")
    else
        e.kfeat(glyph .. " = enter_pandemonium")
    end
    if not noboss then
        e.kmons(glyph .. " = green death / blizzard demon / balrug / "
                      .. "   cacodemon / hell beast / hellion / reaper / "
                      .. "   lorocyproca / tormentor / shadow demon")
    end
end

}}


##############################################################################
# Entry portals to Pan
##############################################################################

default-depth: Depths

# The generic entry; this is the one that should place in encompass vaults.
# Has a higher weight than everything else.
NAME: pan_entry
TAGS: allow_dup can_overwrite replace_portal transparent fallback_enter_pan
WEIGHT: 50
: pan_entry(_G, 'O', true)
MAP
O
ENDMAP

# Other entries; these ones appear to be derived from rand_demon Pan vaults.
NAME:   rand_demon_pan_entry_1
TAGS:   patrolling no_monster_gen transparent nolayout_encompass
MONS:   common demon
: pan_entry(_G, 'O')
MAP
xxxxxxxx@@x
x..1...x..x
x..x...x..x
x..x...x..x
x..x.1.x..x
x..x.O.x..x
x..x.1.x..x
x..x...x..x
x..x...1..x
x@@xxxxxxxx
ENDMAP

NAME:   rand_demon_pan_entry_3
TAGS:   patrolling no_monster_gen mini_float transparent nolayout_encompass
MONS:   common demon
: pan_entry(_G, 'O')
MAP
............
.x.x.x.x.x..
..x.x.x.x.x.
.x.x.x.x.x..
..x.x1x1x.x.
.x.x.xOx.x..
..x.x1x1x.x.
.x.x.x.x.x..
..x.x.x.x.x.
.x.x.x.x.x..
..x.x.x.x.x.
............
ENDMAP

NAME:   rand_demon_pan_entry_4
TAGS:   patrolling no_monster_gen mini_float transparent nolayout_encompass
MONS:   common demon
: pan_entry(_G, 'O')
MAP
...........
.xxxxxxxxx.
.x...x1+.x.
.x+x+x+x.x.
.x1x...x.x.
.x+xxx+x.x.
.x...x1xOx.
.x+x.xxxxx.
.x1x.....+.
.xxxxxxxxx.
...........
ENDMAP

NAME:   rand_demon_pan_entry_5
TAGS:   patrolling no_monster_gen mini_float transparent nolayout_encompass
MONS:   common demon
: pan_entry(_G, 'O')
MAP
............
...xx++xx...
..xx....xx..
.xx......xx.
.x........x.
.+...1O...+.
.+...11...+.
.x........x.
.xx......xx.
..xx....xx..
...xx++xx...
............
ENDMAP

NAME:   rand_demon_pan_entry_7
TAGS:   patrolling no_monster_gen mini_float transparent nolayout_encompass
MONS:   common demon
NSUBST: ? = 1:O / 4:1 / *:.
: pan_entry(_G, 'O')
MAP
............
.xxx....xxx.
.x?xx+xxx?x.
.xx+....+xx.
..x.x++x.x..
..x.+??+.+..
..+.+??+.x..
..x.x++x.x..
.xx+....+xx.
.x?xxx+xx?x.
.xxx....xxx.
............
ENDMAP

NAME:   rand_demon_pan_entry_8
TAGS:   patrolling no_monster_gen mini_float transparent nolayout_encompass
MONS:   common demon
KFEAT:  O = enter_pandemonium
: pan_entry(_G, 'O')
MAP
 ...........
....xxxxxxx.
..xxx....1x.
.xx..1..O.x.
.x........x.
.xx.......x.
..xx.1..1.x.
.........xx.
.....x...x..
.....xx.xx..
 @....xxx...
  ........
ENDMAP

NAME:   rand_demon_pan_entry_9
TAGS:   patrolling no_monster_gen mini_float transparent nolayout_encompass
MONS:   common demon
NSUBST: ^ = 4:1 / *:.
: pan_entry(_G, 'O')
MAP
............
.xxxxxxxxxx.
.x.+.....xx.
.x.xxxxxx.x.
.x.xO^^^x.x.
.x.x^^^^x.x.
.x.x^^^^x.x.
.x.x^^^^x.x.
.x.xxxx+x.x.
.xx.....x.x.
.xxxxxxxx+x.
............
ENDMAP

###################################
# More entries based on panlord vaults.

NAME:  grunt_pan_entry_in_and_out
TAGS:  patrolling no_monster_gen mini_float transparent nolayout_encompass
MONS:  common demon, lesser demon
KMONS: d = any demon
: pan_entry(_G, 'e')
MAP
............
.x+xxxxxx+x.
.x.x.de.x.x.
.x2x....x2x.
.x.x.11.x.x.
.x2x....x2x.
.x.xx++xx.x.
.x2xx..xx2x.
.x.xx..xx.x.
.x2..11..2x.
.xxxxxxxxxx.
............
ENDMAP

NAME:  grunt_pan_entry_spiral
TAGS:  patrolling no_monster_gen mini_float transparent nolayout_encompass
MONS:  common demon, lesser demon
KMONS: d = any demon
: pan_entry(_G, 'e')
MAP
............
.x+xxxxxxxx.
.x.xx2.2..+.
.x.x.xxxxxx.
.x2x1x.1.xx.
.x.x.dexx2x.
.x2xxfg.x.x.
.xx.1.x1x2x.
.xxxxxx.x.x.
.+..2.2xx.x.
.xxxxxxxx+x.
............
ENDMAP

NAME:  grunt_pan_entry_zigzag
TAGS:  patrolling no_monster_gen mini_float transparent nolayout_encompass
MONS:  common demon, lesser demon
KMONS: d = any demon
: pan_entry(_G, 'e')
MAP
............
.xxxxxxxxxx.
.x..2..2..+.
.x.2.xxxxxx.
.xx.1.xx....
..xx.d1xx...
...xx1e.xx..
....xx.1.xx.
.xxxxxx.2.x.
.+..2..2..x.
.xxxxxxxxxx.
............
ENDMAP

##############################################################################
# Random unguarded exits
##############################################################################
default-depth:

NAME:   pan_exit
TAGS:   extra allow_dup transparent no_dump
DEPTH:  Pan
CHANCE: 5%
KFEAT:  O = exit_pandemonium
MAP
O
ENDMAP

NAME:   pan_to_abyss
TAGS:   extra allow_dup transparent no_dump
DEPTH:  Pan
CHANCE: 25%
KFEAT:  O = exit_through_abyss
MAP
O
ENDMAP


##############################################################################
# Pan Demon Lairs
#
# NOTE: The pandemonium lord levels are requested by tag; if you change the
# tag, also update dungeon.cc.
#
# You can define alternate levels for a pandemonium lord by using the same
# TAGS: and a different NAME:
##############################################################################

###########################################################################
# Mnoleg
#
NAME:      mnoleg
ORIENT:    northeast
TAGS:      mnoleg uniq_mnoleg
: pan_lord_setup(_G, 'Mnoleg')
TILE:      c = dngn_stone_wall_yellow
MONS:      patrolling Mnoleg, neqoxec
epilogue{{
  pan_lord_epilogue('Mnoleg')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.................2............xxxx
x.....2........................xxxx
x..cccccccc...ccccccc..ccccccc.xxxx
x..ccccccccc.2.ccccccc..cccccc.xxxx
x..cccccccccc...ccccccc..ccccc.xxxx
x..ccccccccccc.1.ccccccc..cccc.xxxx
x2.cccccccccc.2..Occccccc2.ccc.xxxx
x..ccccccccc.....ccccccccc..cc.xxxx
x..cccccccc...c...ccccccccc..c.xxxx
x..ccccccc...ccc...ccccccccc...xxxx
x..cccccc...ccccc...ccccccccc..xxxx
x..ccccc...ccccccc...ccccccccc.xxxx
x..cccc...ccccccccc...ccccccc..xxxx
x..ccc.2.ccccccccccc.2.ccccc...xxxx
x..cc.....ccccccccccc...ccc....xxxx
x..c...c...ccccccccccc...c.2...xxxx
x.....ccc.2.ccccccccccc......c.xxxx
x....ccccc...ccccccccccc....cc.xxxx
x.2.ccccccc...ccccccccccc..ccc.xxxx
x.................2.......cccc.xxxx
x...c..ccccccc.ccccccc...ccccc.xxxx
x..ccc......2c.c2cccc...cccccc.xxxx
x.ccccc..ccc.c.c2ccc.2.ccccccc.xxxx
x.cccccc..cc.c.c.cc...cccccccc.xxxx
x.ccccccc..c.c.c.c...ccccccccc.xxxx
x.cccccccc...c.c....cccccccccc.xxxx
x.ccccccccc..c.c...ccccccccccc.xxxx
x..............................xxxx
xxxxxxxxxxxxxx@xxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:      mnoleg_st
ORIENT:    southwest
TAGS:      mnoleg uniq_mnoleg
: pan_lord_setup(_G, 'Mnoleg')
TILE:      c = dngn_stone_wall_yellow
MONS:      patrolling Mnoleg, neqoxec
MONS:      patrolling cacodemon, patrolling tormentor
MONS:      large abomination, tentacled monstrosity
KMONS:     w = brain worm
KMONS:     u = very ugly thing
KMONS:     U = ugly thing
NSUBST:    q = 2:u / *:.
NSUBST:    e = 2:u / *:.
NSUBST:    p = 3:u / *:.
epilogue{{
  pan_lord_epilogue('Mnoleg')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxx@xxxxxxxx
xc...........................cx
xc..ccccccc..c..c.c.c.c.c.c.ccx
xc...ccccc..ccc..c.........c.cx
xc.c2.ccc.2ccccc..c.2.2.2.c..cx
xc.cc..c..ccccccc..c.w.w.c...cx
xc.ccc...ccccccccc..c.w.c..c.cx
xc.cccc...ccccccccc..c.c..cc.cx
xc.cc.cc...ccccccccc..c..ccc.cx
xc.ccqqcc...ccccccccc...cccc.cx
xc.ccqqqcc...ccccccccc.ccccc.cx
xc.ccqqq..c...ccccc..ccccccc.cx
xc.ccqq...cc...ccc....c...c..cx
xc2cc......cc...c...cc.....cccx
xc.ccU......cc..2..cc...5...ccx
xc..cc....eeecc...ccc..565..ccx
xc...ccU.eeecc...ccccc..5..cccx
xcc...cceeecc...cc..ccc...ccccx
xccc...ccecc...cc....cccccccccx
xc.cc...ccc...cc......ccccccccx
xcp.cc...c............2cccccccx
xc.pp.c.....c...........ccccccx
xcppppcc...cc............cccccx
xc.ppppcc2cc..............ccccx
xcp.pp.pccc........1....4..cccx
xc....wccc.................cccx
xcw...cccc...............3.cccx
xc..wcccccc..........cc....cccx
xc..cccccccc.........cOc..ccccx
xc.cccccccccc2....3...cc.cccccx
xccccccccccccc..........ccccccx
xcccccccccccccccccccccccccccccx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# The eyes probably make this vault harder than the others, but it's still Mnoleg.
#
# Note: if M were to cast malign gateway, there would be enough room for it here
NAME:       evilmike_mnoleg_eyes
ORIENT:     northwest
TAGS:       mnoleg uniq_mnoleg
: pan_lord_setup(_G, 'Mnoleg')
NSUBST:     O = 1:O / *:|
TILE:       c = dngn_stone_wall_yellow
MONS:       patrolling mnoleg
MONS:       giant eyeball / golden eye / eye of devastation / \
            eye of draining / shining eye
MONS:       w:3 large abomination / w:2 tentacled monstrosity
MONS:       cacodemon
MONS:       neqoxec / brain worm
NSUBST:     C = 1:4 / *:N
NSUBST:     N = 2:4 / *:5
NSUBST:     M = 5:3 / 2=3. / *:.
NSUBST:     E = 3:2 / 2=22. / 2=2.. / *:.
SHUFFLE:    '-
NSUBST:     ' = 5:3 / 2=3. / 1:4 / 3:5 / *:.
NSUBST:     - = 5:2 / 2=2. / 1:4 / 3:5 / *:.
epilogue{{
  pan_lord_epilogue('Mnoleg')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xcccccccccccccccccccccccccccccccccccx
xcOcccccccccccccccccccccOcccccccccccx
xcc...ccccccccccccccc...ccccccccccccx
xcc.ccMcccccccccccccMcc.ccccccccccccx
xcc.ccM.ccccccccccc.Mcc.ccccccccccccx
xcccMMC..ccccccccc..CMMcccccccccccccx
xcccc.....ccccccc.....ccccc''cccccccx
xccccc.....ccccc.....ccccc''''ccccccx
xcccccc.............ccccc''''''cccccx
xccccccc...EEE.....ccccc''''''''ccccx
xcccccccc.EEEEE...ccccc''''''''''cccx
xcccccccc.EE1EE...cccc.'''''''''''ccx
xcccccccc.EEEEE...ccc'''cc'''''''..cc
xcccccccc..EEE....cc'''cccc'''''.....
xcccccccc.........cc'''cccc'''cc....@
xccccccc...........cc'''cc'''cc......
xcccccc.............cc''''''cc.....cc
xccccc.....ccccc.....cc''''cc.....ccx
xcccc.....ccccccc.....cc''cc.....ccxx
xcccMMC..ccccc--cc.....cccc.....ccxxx
xcc.ccM.ccccc----cc.....cc.....ccxxxx
xcc.ccMccccc------cc..........ccxxxxx
xcc...ccccc---cc---cc........cccxxxxx
xcOccccccc---cccc---cc......ccccxxxxx
xcccccccc----cccc---cc......ccccxxxxx
xccccccc------cc---cc........cccxxxxx
xcccccc-----------cc........N.ccxxxxx
xcccccc----------cc.....cc.N2..cxxxxx
xccccccc--------cc.....cccc..N.cxxxxx
xcccccccc------cc.....cccccc...cxxxxx
xccccccccc-----c.....cccccccccccxxxxx
xcccccccccc---......ccxxxxxxxxxxxxxxx
xccccccccccc-......ccxxxxxxxxxxxxxxxx
xcccccccccccc.....ccxxxxxxxxxxxxxxxxx
xccccccccccccc...ccxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxc.@.cxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    mnoleg_grunt
ORIENT:  west
TAGS:    mnoleg uniq_mnoleg
: pan_lord_setup(_G, 'Mnoleg')
TILE:    c = dngn_stone_wall_yellow
SHUFFLE: Od, 1e
KFEAT:   d = .
KFEAT:   e = .
MONS:    patrolling Mnoleg, patrolling cacodemon, neqoxec
MONS:    tentacled monstrosity, large abomination, very ugly thing band
epilogue{{
  pan_lord_epilogue('Mnoleg')
}}
MAP
xxxxx@.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxc..cccc.............cccccccccccc..cx
xxxxc..ccc.......2.......cccccccccc...cx
xxxxc..cc....ccccc...cc.3.cccccccc..2.cx
xxxxc........cccc...cccc..ccccccc.....cx
xxxxc.3......ccc...ccccc..cccccc......cx
xxxxc....cc..cc...ccccc...ccccc...cc..cx
xxxxc...ccc..c...ccccc...ccccc...ccc..cx
xxxxc..ccc...c..cccccc..ccccc...ccc...cx
xxxxccccc...cc..cccccc..cccc...ccc...ccx
xxxxcccc...ccc.6.ccccc...cc...ccc...cccx
xxxxccc...ccccc...ccccc......ccc...ccccx
xxxxcc...ccccccc...ccccc.3..ccc...cccccx
xxxxc...ccc..cccc..5..ccc..ccc...ccc..cx
xxxxc..ccc....ccc.2....cccccc...ccc...cx
xxxxc..cc.6....cc..cc.3..ccc...ccc..3.cx
xxxxc.5....cc.5..4.cd1cc...5..ccc.....cx
xxxxc.....ccc..5...cceOc.4..5.cc....5.cx
xxxxc.3..ccc...ccc..3.cc..cc....6.cc..cx
xxxxc...ccc...cccccc....2.ccc....ccc..cx
xxxxc..ccc...ccc..ccc..5..cccc..ccc...cx
xxxxccccc...ccc..3.ccccc...ccccccc...ccx
xxxxcccc...ccc......ccccc...ccccc...cccx
xxxxccc...ccc...cc...ccccc.6.ccc...ccccx
xxxxcc...ccc...cccc..cccccc..cc...cccccx
xxxxc...ccc...ccccc..cccccc..c...ccc..cx
xxxxc..ccc...ccccc...ccccc...c..ccc...cx
xxxxc..cc...ccccc...ccccc...cc..cc....cx
xxxxc......cccccc..ccccc...ccc......3.cx
xxxxc.....ccccccc..cccc...cccc........cx
xxxxc.2..cccccccc.3.cc...ccccc....cc..cx
xxxxc...cccccccccc.......2.......ccc..cx
xxxxc..cccccccccccc.............cccc..cx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.@xx
ENDMAP


#######################################################################
# Lom Lobon
#
NAME:      lom_lobon
TAGS:      lom_lobon uniq_lom_lobon no_rotate
ORIENT:    north
: pan_lord_setup(_G, 'Lom Lobon')
MONS:      patrolling Lom Lobon, giant orange brain, rakshasa, wizard
epilogue{{
  pan_lord_epilogue('Lom Lobon')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxwwwwwwwwwwww.......wwwwwwwxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxwwwwwwwwwwwwbbbwwwwwww.......wwwwwwwxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxwwwwwwwwwwwwbbbbbbbbbbbwwwwww.........wwwwwwxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxwwwwwwwwwwwwbbbbwwwwwwwwwbbbbwwwwww.........wwwwwwxxxxxxxxxxxxxxx
xxxxxxxxxxxxxwwwwwwwbbbbbbbbwwwwwwwwwwwwwwwbbbwwwww...........wwwwwxxxxxxxxxxxxx
xxxxxxxxxxxxwwwwwbbbb......bbbwwwwwwwwwwww...bbwwwww.............wwwxxxxxxxxxxxx
xxxxxxxxxxxxwwwbbb...........bbbwwwwww........bbwwwww.............wwxxxxxxxxxxxx
xxxxxxxxxxxwwwbb...............bbwwww..........bwwwwww.............wwxxxxxxxxxxx
xxxxxxxxxxxwwbb........1O.......bbww...........bbwwww..............wwxxxxxxxxxxx
xxxxxxxxxxwwwb...................bw......2......bwww.....U....2.....wwxxxxxxxxxx
xxxxxxxxxxwwbb...................bb.............bww.................wwxxxxxxxxxx
xxxxxxxxxxwwbb..3................bbb............bbw..............4..wwxxxxxxxxxx
xxxxxxxxxwwbbb...................b.b............4....................wwxxxxxxxxx
xxxxxxxxxwwbwbb.................bb.......U......4..........U..........wxxxxxxxxx
xxxxxxxxxwwbwwbb...............bb..b............bbw..............4.....xxxxxxxxx
xxxxxxxxxwwbbwwbbb...........bbb..bb............bwww...................xxxxxxxxx
xxxxxxxxxwwwbwwwwb..b..2..bbbb....b.............bwww...................xxxxxxxxx
xxxxxxxxxxwwbwwww...bbbbbbb.......bw.....3.....bbwwww...U.....3.......xxxxxxxxxx
xxxxxxxxxxwwbbww.................bbww........wwbwwwww.................xxxxxxxxxx
xxxxxxxxxxwwwbbw................bbwwwww....wwwbbwwww..................xxxxxxxxxx
xxxxxxxxxxwwwwbb...4...U........bwwwwwwwwwwwwbbwww....................xxxxxxxxxx
xxxxxxxxxxxwwwwbbb...........bbbbbwwwwwwwwwbbbwww....................xxxxxxxxxxx
xxxxxxxxxxxwwwwwwbbbb.....bbbbwwwbbbbwwwbbbbwwww....................xxxxxxxxxxxx
xxxxxxxxxxxwwwwwwwwwbbbbbbbwwwwwwwwwbbbbbwwwww......4.....4........xxxxxxxxxxxxx
xxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww......................xxxxxxxxxxxxxx
xxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwww.......................xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwww........................xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxwwwwwww......................xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx...@.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:      lom_lobon_st
TAGS:      lom_lobon no_pool_fixup uniq_lom_lobon
ORIENT:    northwest
: pan_lord_setup(_G, 'Lom Lobon')
MONS:      patrolling Lom Lobon
MONS:      blizzard demon, green death, wizard, draconian annihilator
MONS:      sixfirhy
SUBST:     0 = 23444445566
NSUBST:    Q = 1:O / *:4
NSUBST:    B = 1:. / *:b
TILE:      v = dngn_metal_wall_blue
epilogue{{
  pan_lord_epilogue('Lom Lobon')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxwxxwwwwwwwxxxxxxxwwwwwwwwwxxxxxxxxxxxx
xxxxxxxxwxwwwwbbbbbbbwwxxwwwbbbbbbbwwwxxxxxxxxxxx
xxxxxxxwwwwwbbbb...bbbbwwwbbbb...bbbbwwwxxwxxxxxx
xxxxwxxwwwwbb....0....bbwbb....0....bbwwwwwwxxxxx
xxxwwwwwwwbb...........bbb...........bbwwwwwwxxxx
xxxwwwwwwbb...wwwwwww.......wwwwwww...bbwwwwxxxxx
xxxxwwwwbb...wwwwwwwww.....wwwwwwwww...bbwwwwxxxx
xxwwwwwbb...wwbbbbbbbww...wwbbbbbbbww...bbwwwwxxx
xxxwwwbb...wwbbwwwwwbbw...wbbwwwwwbbww...bbwwwxxx
xxxwwbb...wwbbwwwwwwwbb...bbwwwwwwwbbww...bbwwwxx
xxwwbb...wwbbwwvvvwwwwbbBbbwwwwvvvwwbbww...bbwxxx
xwwwbb..wwbbwwvv.vvwwwwb.bwwwwvv.vvwwbbww..bbwwxx
xxwbb...wwbwwwv.Q.vwwwwb.bwwwwv.Q.vwwwbww...bbwwx
xxwbb.0.wwbwwwvv.+vwwwwb.bwwwwv+.vvwwwbww.0.bbwwx
xwwbb...wwbwwwwvvv.wwwbb.bbwww.vvvwwwwbww...bbwxx
xxwwbb..wwbbwwwwwww.wbb...bbw.wwwwwwwbbww..bbwxxx
xxwwbb...wwbbwwwwwww.........wwwwwwwbbww...bbwxxx
xwwwwbb...wwbbwwwwwb.........bwwwwwbbww...bbwwxxx
xxxwwwbb...wwbbwwwbb.........bbwwwbbww...bbwwxxxx
xxxxwwwbb.....bbbbb...........bbbbb.....bbwwwwxxx
xxxwwwwwb.....B.........1.........B.....bwwwwwxxx
xxxwwwwbb.....bbbbb...........bbbbb.....bbwwwwwxx
xxwwwwbb...wwbbwwwbb.........bbwwwbbww...bbwwwxxx
xwwwwbb...wwbbwwwwwb.........bwwwwwbbww...bbwwwxx
xxwwbb...wwbbwwwwwww.........wwwwwwwbbww...bbwwxx
xxwwbb..wwbbwwwwwww.wbb...bbw.wwwwwwwbbww..bbwwxx
xwwbb...wwbwwwwvvv.wwwbb.bbwww.vvvwwwwbww...bbwwx
xwwbb.0.wwbwwwvv.+vwwwwb.bwwwwv+.vvwwwbww.0.bbwxx
xxwbb...wwbwwwv.Q.vwwwwb.bwwwwv.Q.vwwwbww...bbwwx
xxxwbb..wwbbwwvv.vvwwwwb.bwwwwvv.vvwwbbww..bbwwxx
xxwwbb...wwbbwwvvvwwwwbbBbbwwwwvvvwwbbww...bbwwxx
xxxxwbb...wwbbwwwwwwwbb...bbwwwwwwwbbww...bbwwwxx
xxxxwwbb...wwbbwwwwwbbw...wbbwwwwwbbww...bbwwwxxx
xxxxxwwbb...wwbbbbbbbww...wwbbbbbbbww...bbwwwwxxx
xxxxwwwwbb...wwwwwwwww.....wwwwwwwww.......wwwxxx
xxxwwwwwwbb...wwwwwww.......wwwwwww...b.....wwwxx
xxxxwwwwwwbb...........bbb...........bb.......wxx
xxxxxxwwwwwbb....0....bbwbb....0....bbw....6....x
xxxxxxwxwwwwbbbb...bbbbwwwbbbb...bbbbwww......6..
xxxxxxxxxwwwwwbbbbbbbwwwwwwwbbbbbbbwwwwww.6......
xxxxxxxxxwxwwwwwwwwwwwwwxxxwwwwwwwwwwxxxw........
xxxxxxxxxxxxxxxxwwxxxxxxxxxxxxxxwwxwxxxxxx.......
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..@..
ENDMAP

NAME:      lom_lobon_spiral_st
TAGS:      lom_lobon no_pool_fixup patrolling uniq_lom_lobon
ORIENT:    northeast
: pan_lord_setup(_G, 'Lom Lobon')
MONS:      patrolling Lom Lobon, titan, vapour, wizard
KMONS:     S = storm dragon
KMONS:     d = draconian annihilator
KMONS:     e = deep elf annihilator
COLOUR:    c = white
KFEAT:     S = deep_water
NSUBST:    D = 1:S / *:w
TILE:      c = dngn_stone_wall_white
epilogue{{
  pan_lord_epilogue('Lom Lobon')
}}
MAP
cccccccccccccccccccccccccccccccccccccccc
cbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbbc
cbbbbbb.....bb+..2..bbbbbwwwbbbbbbbbbbbc
cbbbb.........bb...bbbwwwwwwwwwbbbbbbbbc
cbbb...........bbObbwww.....wwwwwbbbbbbc
cbb.............bbbww.........wwwwbbbbbc
cbb.............bbww.....w......wwwbbbbc
cb.......1.......bw....www.......wwbbbbc
cb...............bw...wwwww...V..wwwbbbc
cb...............bb..wwwwwww......wwbbbc
cbb.............bbb.....wwwww.....wwbbbc
cbb.............bbbb...2..wwwww....wwbbc
cbbb...........bbbbbbb.......www...wwwbc
cbbbbb.......bbbbbbbbbbbbbb...bbb...wwwc
cbbbbbbb...bbbbbbbbbbbbbwwww....bb..wwwc
cbbbbbb...bbbbbbbbbbbwwwwwwwww...bb..wwc
cbbbbb..wbbbbbbbbbbwwwwwwwwwwww...b..wwc
cbbb...wwwbbbbbbbbwwwwwwwwwwwwww..bb.wwc
cbb...wwwwwbbbbbbbwwwwww..wwwwww...b..wc
cbb.d..wwwwwbbbbb.wwwww.ee.wwwww...b..wc
cbb.....wwwwwwbbb.wwwwww..wwwwww...b..wc
cbb...d.wwww....2..wwwwwwwwwwwww..bb..Dc
cwb.........www.....wwwwwwwwwww...bw..wc
cwbb...wwwwwwwwbbb...wwwwwwwww...bbw..wc
cwbbbdwwwwwwwwbbbbb....wwww.....bbww.wwc
cwwbbbbwwwwbbbbwwwbbb.........bbbww..wwc
cwwSbbbbbbbbwwwwwwwwbbb....bbbbwww...wwc
cwwwwwbbbbwwwwwwwwwwwwbbbbbbwwwDww...wwc
cwwwwwwwwwwwww.4.wwwwwwwwwwwwwwwww....wc
cwwwwwwwwww........wwwwwwwwwwwwww.....wc
cwwwwwwww...........wwwwwwwwwwww.....wwc
cwwwwwww.....U...U......wwwwwwww.....wwc
cwwwwbbbb..................wwww.....wwwc
cwwbbb..............ww..............wwDc
cwbb..3...........wwwwww.....2.....wwwwc
cwb$.........ww..wwwwwwwww.......wwwwwwc
cwb$..3.....wwww..wwwwwwwww....wwwwwwwwc
cwbb3...3.bbwwww...wwwwwwwwwwwwwwwwwwwcc
cwwbbb$$bbbwwwwww....wwwwwwwwwwwwwwwwccc
ccwwwbbbbwwwww...w....wwwwwwwwwwwwwwcccc
cccwwwwwwwwww..4.....4.wwwwwwwwwwwcccccc
cccccwwwwww............wwwwwwwwwcccccccc
cccccccccccccccc..@..ccccccccccccccccccc
ENDMAP

NAME:    lom_lobon_grunt
TAGS:    lom_lobon no_pool_fixup uniq_lom_lobon
ORIENT:  north
: pan_lord_setup(_G, 'Lom Lobon')
MONS: patrolling Lom Lobon
MONS: draconian annihilator / deep elf annihilator / \
      wizard w:20 / rakshasa w:20 / giant orange brain / \
      blizzard demon / green death / titan w:5 / \
      spriggan air mage w:5 / lich w:5
SUBST:   W = ww.
SUBST:   - = ..w
epilogue{{
  pan_lord_epilogue('Lom Lobon')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxwwwwwwwwwwwxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxwwwwwwbbbbbbbbbwwwwwwxxxxxxxxwwwwwxxxxxxxxxxxxx
xxxxxxxxxwwwwbbbbwwwwwwwbbbbwwwwwwwwwwwwwwwwwwwxxxxxxxxxx
xxxxxxxxwwwbbbwwwwWWWWWWwwwbbbwwwwwwwwwWWWwwwwwwxxxxxxxxx
xxxxxxxwwwbbwwwwWW------WWWwwbbwwwwwwwW---Wwwwwwwwwxxxxxx
xxxxxxwwwbbwwwWW--..2...---WWwbbwwwwwW-$$$-Wwwwwwwwwwxxxx
xxxxxwwwbbwwwW--...........--WwbbwwwwW-.U.-Wwwwwwwwwwxxxx
xxxxwwwbbwwwW-...------......-WwbbwwwW-2.2-Wwwwwwwwwwwxxx
xxxwwwbbwwwW-..--WWWWWW----.2.-WwbbwwwW---Wwwwwwwwwwwwwxx
xxxwwbbwwwW-..-WWwwwwwwWWWW--..-WwbbwwwWWWwwwwwwwwwwwwwwx
xxwwwbwwwbb...bbwwwwwwwwwwwWW-..-WwbwwwwwwwwWWWWwwwwwwwwx
xxwwbbwbbb..2..bbbwwwwwwwwwwW-..-WwbbwwwwwWW----WWwwwwwwx
xxwwbwbb.........bbwwwwwwwwwwW-..-WwbwwwwW--.2.2--Wwwwwwx
xwwwbbb...........bbwwwwwwwwwW-..-WwbwwwwW-.......-Wwwwwx
xwwbbb.............bwwwwwwwwwW-..-WwbbwwwW-...U....-Wwwwx
xwwbbbbb...........bbwwwWWWwwwW-2.-WwwwwW-...2.2...-Wwwwx
xwwbb..bb...........bwwW---WwwW-...-WWWW-.2..---....-Wwwx
xwwbb...b...........bwW-$.2-WwW-....----....-WWW-...-Wwwx
xwwbbO2.+.2...1...2.bwW-$U.-WwW-.U.2.......-WwwwW-...-Wwx
xwwbb...b...........bwW-$.2-WwW-....-------WwwwwwW-2.-Wwx
xwwbb..bb...........bwwW---WwwW-...-WWWWWWWwwwwwwW-..-Wwx
xwwbbbbb...........bbwwwWWWwwwW-2.-WwwwwwwwwwwwwwW-..-Wwx
xwwbbb.............bwwwwwwwwwW-..-WwbbwwxxxxxxwwwW-..-Wwx
xwwwbbb...........bbwwwwwwwwwW-..-WwbwwwxxxxxxxwwW-.2-Wwx
xxwwbwbb.........bbwwwwwwwwwwW-..-WwbwwxxxxxxxxxwW-..-Wwx
xxwwbbwbbb..2..bbbwwwwwwwwwwW-..-WwbbwwxxxxxxxxxwW-..-Wwx
xxwwwbwwwbb...bbwwwwwwwwwwwWW-..-WwbwwwxxxxxxxxxW-...-Wxx
xxxwwbbwwwW-..-WWwwwwwwWWWW--..-WwbbwwxxxxxxxxxW-.2.-Wxxx
xxxwwwbbwwwW-..--WWWWWW----.2.-WwbbwwwxxxxxxxxxW-...-Wxxx
xxxxwwwbbwwwW-...------......-WwbbwwwxxxxxxxxxW-....-Wxxx
xxxxxwwwbbwwwW--...........--WwbbwwwxxxxxxxxxW-....-Wxxxx
xxxxxxwwwbbwwwWW--..2...---WWwbbwwwxxxxxxxxxxW-...-Wxxxxx
xxxxxxxwwwbbwwwwWW------WWWwwbbwwwxxxxxxxxxxxW-...-Wxxxxx
xxxxxxxxwwwbbbwwwwWWWWWWwwwbbbwwwxxxxxxxxxxxW-.2.-Wxxxxxx
xxxxxxxxxwwwwbbbbwwwwwwwbbbbwwwwxxxxxxxxxxxxW-...-Wxxxxxx
xxxxxxxxxxwwwwwwbbbbbbbbbwwwwwwxxxxxxxxxxxxxW-..-Wxxxxxxx
xxxxxxxxxxxxwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxxxW-..-Wxxxxxxx
xxxxxxxxxxxxxxxwwwwwwwwwwwxxxxxxxxxxxxxxxxxxW-..-Wxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx-.@@.-xxxxxxx
ENDMAP


#############################################################################
# Cerebov
#
NAME:      cerebov
TAGS:      cerebov uniq_cerebov
ORIENT:    northeast
: pan_lord_setup(_G, 'Cerebov')
MONS:      patrolling Cerebov, Balrug, brimstone fiend
epilogue{{
  pan_lord_epilogue('Cerebov')
}}
TILE:       v = dngn_metal_wall_brown
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
...............................xxxx
.............vvvvv.............xxxx
.............v$$$v.............xxxx
.............v|||v.............xxxx
.............v$$$v.............xxxx
.vvvvv...vvvvvv+vvvvvv...vvvvv.xxxx
.v|$|vvvvv...........vvvvv$|$v.xxxx
.v$|$+.....vvvvvvvvv.....+|$|v.xxxx
.v|$|v.vvvvvvvvOvvvvvvvv.v$|$v.xxxx
.vvvvv.vvvvvv..3..vvvvvv.vvvvv.xxxx
...v...vv.....vvv.....vv...v...xxxx
...v.vvvv....vv1vv....vvvv.v...xxxx
...v.vv......v...v......vv.v...xxxx
...v.vvvv.............vvvv.v...xxxx
...v...vv..2.......2..vv...v...xxxx
.vvvvv.vv..2.......2..vv.vvvvv.xxxx
.v|$|v.vv.............vv.v$|$v.xxxx
.v|$|+.vv...vv...vv...vv.+$|$v.xxxx
.v|$|v.vv...vv+++vv...vv.v$|$v.xxxx
.vvvvv.vvvvvvv...vvvvvvv.vvvvv.xxxx
....v..vvvvvvv...vvvvvvv..v....xxxx
....vv...................vv....xxxx
.....vv.vv+vv..2..vv+vv.vv.....xxxx
......vvv|||v.....v$$$vvv......xxxx
........v|$|vv...vv$|$v........xxxx
........v|||v.....v$$$v........xxxx
........vvvvv.....vvvvv........xxxx
...............................xxxx
...............@...............xxxx
ENDMAP

NAME:      cerebov_st
TAGS:      cerebov patrolling uniq_cerebov
ORIENT:    north
: pan_lord_setup(_G, 'Cerebov')
TILE:      v = dngn_metal_wall_brown
MONS:      patrolling Cerebov, balrug, brimstone fiend
MONS:      red devil, sun demon, crimson imp
epilogue{{
  pan_lord_epilogue('Cerebov')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xx.....................................................................xx
xx...............................6.....6...............................xx
xx..................................6..................................xx
xx.............................vvv.....vvv.............................xx
xx...vvvvv.....................vvv.v.v.vvv.....................vvvvv...xx
xx...v|$|v..vv..vv..vv..vv..vv.vvv.....vvv.vv..vv..vv..vv..vv..v|||v...xx
xx...v$|$vvvvvvvvvvvvvvvvvvvvvvvvvvv.vvvvvvvvvvvvvvvvvvvvvvvvvvv|$|v...xx
xx...v|$|v.......................vvv.vvv.......................v|||v...xx
xx...vvvv+.vvvvvvvvvvvvvvvvvvvvv.v.....v.vvvvvvvvvvvvvvvvvvvvv.+vvvv...xx
xx....vv...vvvvvvvvvvvvvvvvvvvvv+vvv.vvv+vvvvvvvvvvvvvvvvvvvvv...vv....xx
xx.....v.vvvvvvv...........v.......v.v.......v.v.....5..5.+..vvv.v.....xx
xx....vv.vvvvvvv+v+vvvvvvv+v.v.v.v.....v.v.v.v.v..v..v..v.v.$||v.vv....xx
xx....vv.vv||v.......v$$vv+v.................v.+.........3v.$||v.vv....xx
xx.....v.vv||v.v.1.v.v$$v6.v.v.............v.v.v..v..v..v.v.$$$v.v.....xx
xx....vv.vv||v.v...v.v$$vO3v......2...2......v.v.....5..5.+.$$$v.vv....xx
xx....vv.vv+vv.......vv+vvvv.v.............v.v.vvvvvvvvvvvvvvvvv.vv....xx
xx.....v.vv..2.......2..v..+.................+.............+...v.v.....xx
xx....vv.vv..2.......2..v..v.v.............v.vvvvvvvvvvvvvvv...v.vv....xx
xx....vv.vv.............v..v......2...2......v...v..4..v$|$v.6.v.vv....xx
xx.....v.vv....v...v....v..v.v.............v.v...+..2..+|$|v.2.v.v.....xx
xx....vv.vv...vv+++vv...v..v.................v.4.v..4..v$|$v...v.vv....xx
xx....vv.vvvvvv.....vvvvv..v.v.v.v.....v.v.v.v.4.vvvvvvvvvvv...v.vv....xx
xx.....v.vvv...............v.......v.v.......v...vvvvvvvvvvv+vvv.v.....xx
xx....vv...vvvvvvvvvvvvvvvvvvvvv+vvv.vvv+vvvvv...+...........v...vv....xx
xx...vvvv+.vvvvvvvvvvvvvvvvvvvvv.v.....v.vvvvvvvvvvvvvvvvvvvvv.+vvvv...xx
xx...v$$$v.......................vvv.vvv.......................v$$$v...xx
xx...v|||vvvvvvvvvvvvvvvvvvvvvvvvvvv.vvvvvvvvvvvvvvvvvvvvvvvvvvv$|$v...xx
xx...v$$$v..vv..vv..vv..vv..vv.vvv.....vvv.vv..vv..vv..vv..vv..v$$$v...xx
xx...vvvvv.....................vvv.v.v.vvv.....................vvvvv...xx
xx.............................vvv.....vvv.............................xx
xx..................................6..................................xx
xx...............................6.....6...............................xx
xx.....................................................................xx
xx.................................@@@.................................xx
ENDMAP

NAME:       evilmike_cerebov_lava
TAGS:       cerebov patrolling uniq_cerebov
ORIENT:     northwest
: pan_lord_setup(_G, 'Cerebov')
SHUFFLE:    XY
SUBST:      X : v, Y : .v
SUBST:      L : lll.
SUBST:      U : W., W : V., V : v.
KPROP:      l = no_cloud_gen
TILE:       v = dngn_metal_wall_brown
MONS:       Cerebov, balrug, brimstone fiend, sun demon
NSUBST:     Q = 1:3 / 2:|
NSUBST:     B = 3:2 / 3:4
NSUBST:     C = 2:2 / 2:|
epilogue{{
  pan_lord_epilogue('Cerebov')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xx..........................................
xx..........................................
xx..............vvvvvvv.....................
xx...vvvvv......v%|||%v......vvvvv..........
xx...v|$$v......v%|Q|%v......v$$|v..........
xx...v$C$vvvvvvvv%|||%vvvvvvvv$C$v..........
xx...v$$$vvvvvvvvvv+vvvvvvvvvv$$$v...lll....
xx...vvvv+v.................v+vvvv..lllll...
xx.....vvv..vVvWvU.B.UvWvVv..vvv....llvll...
xx.....vv..vv.............vv..vv....lllll...
xx.....vv.vvB.............Bvv.vv.....lll....
xx.....vv.V.....LLL.lll.....V.vv............
xx.....vv.v....LLLL.llll....v.vv............
xx.....vv.W...LLLL...llll...W.vvvvvvvvvv....
xx.....vv.v...LLLL...llll...v.vvvvvvvvvvvv..
xx..vvvvv.U..LLLL.....llll..........vllvvv..
xx..v%%%v...LLvvv......llll...vWvWv.v4...vv.
xx..v|||v..LLvv.v.Y....llll...2.....+.....4.
xx..v|Q|+..LLvOX3.Y...1lllll.4......+.2....@
xx..v|||v..LLvv.v.Y....llll...2.....+.....4.
xx..v%%%v...LLvvv......llll...vWvWv.v4...vv.
xx..vvvvv.U..LLLL.....llll..........vllvvv..
xx.....vv.v...LLLL...llll...v.vvvvvvvvvvvv..
xx.....vv.W...LLLL...llll...W.vvvvvvvvvv....
xx.....vv.v....LLLL.llll....v.vv............
xx.....vv.V.....LLL.lll.....V.vv............
xx.....vv.vvB.............Bvv.vv.....lll....
xx.....vv..vv.............vv..vv....lllll...
xx.....vvv..vVvWvU.B.UvWvVv..vvv....llvll...
xx...vvvv+v.................v+vvvv..lllll...
xx...v$$$vvvvvvvvvv+vvvvvvvvvv$$$v...lll....
xx...v$C$vvvvvvvv%|||%vvvvvvvv$C$v..........
xx...v|$$v......v%|Q|%v......v$$|v..........
xx...vvvvv......v%|||%v......vvvvv..........
xx..............vvvvvvv.....................
xx..........................................
xx..........................................
ENDMAP

NAME:   cerebov_grunt
TAGS:   cerebov uniq_cerebov
ORIENT: northwest
: pan_lord_setup(_G, 'Cerebov')
MONS:   patrolling Cerebov, balrug, brimstone fiend
MONS:   sun demon, red devil / crimson imp w:20
epilogue{{
  pan_lord_epilogue('Cerebov')
}}
TILE:   v = dngn_metal_wall_brown
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxx...........................
xxxx...........vvvvv...........
xxxx...........v|||v...........
xxxx...........v|||v...........
xxxx.........vvv|||vvv.........
xxxx........vvvvv+vvvvv........
xxxx.......vvv...4...vvv.......
xxxx......vvv.........vvv......
xxxx.....vvv....vvv....vvv.....
xxxx.vvvvvv..2.vvOvv.2..vvvvvv.
xxxx.v|$$v....vv$3$vv....v$||v.
xxxx.v||$+4.vvv$vvv$vvv.4+$$|v.
xxxx.v|$$v..vll2$$$2llv..v$||v.
xxxx.vvvvv..vll$.1.$llv..vvvvv.
xxxx....vv..vvl.2.2.lvv..vv....
xxxx....vv...vl.vvv.lv...vv....
xxxx....vvv..v.lllll.v..vvv....
xxxx.....vv.2..lllll..2.vv.....
xxxx.....vvv....lll....vvv.....
xxxx......vv...........vv......
xxxx......vv.4.......4.vv......
xxxx.....vvv+v.......v+vvv.....
xxxx.....v$$$vvv...vvv|||v.....
xxxx.....v$|$vvv+++vvv|$|v.....
xxxx.....v$$$vvv...vvv|||v.....
xxxx.....vvvvv.5...5.vvvvv.....
xxxx...........................
xxxx..........5..2..5..........
xxxx...........................
xxxx...........5...5...........
xxxx.............@.............
ENDMAP


##############################################################################
# Gloorx Vloq
#
NAME:      gloorx_vloq
TAGS:      gloorx_vloq uniq_gloorx_vloq
ORIENT:    southwest
: pan_lord_setup(_G, 'Gloorx Vloq')
TILE:      c = dngn_stone_wall_darkgray
SHUFFLE:   1O
MONS:      patrolling Gloorx Vloq, Executioner, demonic crawler, shadow wraith
epilogue{{
  pan_lord_epilogue('Gloorx Vloq')
}}
MAP
xxxxxxxxxxxxxxxxxx@.xxxxxxxxxxxxxxx
xxxx..............................x
xxxx..............................x
xxxx..............................x
xxxx.x.x.x.x.x.x.x..x.x.x.x.x.x.x.x
xxxx..............................x
xxxx.x.xxxx+xxxxxxxxxxxx+xxxxxx.x.x
xxxx...xx....................xx...x
xxxx.x.x..ccccc..4..4..ccccc..x.x.x
xxxx...x.cc.3............3.cc.x...x
xxxx.x.x.c..ccccc.cc.ccccc..c.x.x.x
xxxx...x.c.cc.....cc.....cc.c.x...x
xxxx.x.x.c.c.2...cccc...2.c.c.x.x.x
xxxx...x...c...ccc..ccc...c...+...x
xxxx.x.x.3.....2..1O..2.....3.x.x.x
xxxx...+...c...ccc..ccc...c...x...x
xxxx.x.x.c.c.2...cccc...2.c.c.x.x.x
xxxx...x.c.cc.....cc.....cc.c.x...x
xxxx.x.x.c..ccccc.cc.ccccc..c.x.x.x
xxxx...x.cc.3............3.cc.x...x
xxxx.x.x..ccccc..4..4..ccccc..+.x.x
xxxx...xx....................xx...x
xxxx.x.xxxx+xxxx+xxxxxxxx+xxxxx.x.x
xxxx..............................x
xxxx.x.x.x.x.x.x.x..x.x.x.x.x.x.x.x
xxxx..............................x
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:      gloorx_vloq_st
TAGS:      gloorx_vloq uniq_gloorx_vloq
ORIENT:    southeast
: pan_lord_setup(_G, 'Gloorx Vloq')
TILE:      c = dngn_stone_wall_darkgray
MONS:      patrolling Gloorx Vloq, Executioner, shadow wraith
MONS:      soul eater, unseen horror
epilogue{{
  pan_lord_epilogue('Gloorx Vloq')
}}
MAP
xxxxxxxxxxxxxxxxxx.@.xxxxxxxxxxxxxxxxxx
xxxxx.............................xxxxx
xxxx...xxx.x.xxx.x.x.x.xxx.x.xxx...xxxx
xxx....xxx.x.xxx.x3x3x.xxx.x.xxx....xxx
xx...................................xx
x..5..ccccccccccccccccccccccccccc..5..x
xx...xcxxxxxxxxxxxxxxxxxxxxxxxxxcx...xx
x.x.x.cx.x4x.x.x.x.x.x.x.x.x4x.xc.x.x.x
x..x..cxx.x.x.x.x.xOx.x.x.x.x.xxc..x..x
x.x.x.cx.x.x.x.x.xxxxx.x.x.x.x.xc.x.x.x
xx...xcxx.x.x.x.x4...4x.x.x.x.xxcx...xx
x.x.x.cx.x.x.x.x.x...x.x.x.x.x.xc.x.x.x
x..x..cxx.x.x.x.x.....x.x.x.x.xxc..x..x
x.x.x.cx.x.x2x.x.x...x.x.x2x.x.xc.x.x.x
xx...xcxx.x.x.x.x.....x.x.x.x.xxcx...xx
x.x.x.cx.x.x.x.x.x.1.x.x.x.x.x.xc.x.x.x
x3.x.3cxx.x.x.x.x.....x.x.x.x.xxc3.x.3x
x.x.x.cx2x.x.x.x.x...x.x.x.x.x2xc.x.x.x
xx...xcxx.x.x.x.x.....x.x.x.x.xxcx...xx
x.x.x.cx.x.x.x.x.x...x.x.x.x.x.xc.x.x.x
x..x..cxx.x.x.x.x4...4x.x.x.x.xxc..x..x
x.x.x.cx.x.x.x.x.x...x.x.x.x.x.xc.x.x.x
xx...xccx.x.x.x.x.....x.x.x.x.xccx...xx
xxx.x.xccx.x.x.x.x...x.x.x.x.xccx.x.xxx
xxxx....ccc2x.x.x.....x.x.x2ccc....xxxx
xxxxx.....cccc.x.x...x.x.cccc.....xxxxx
xxxxxxx......ccccx...xcccc......xxxxxxx
xxxxxxxxx.......cc+++cc.......xxxxxxxxx
xxxxxxxxxxxx.......2.......xxxxxxxxxxxx
xxxxxxxxxxxxxxx.........xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:       evilmike_gloorx_vloq_box
TAGS:       gloorx_vloq uniq_gloorx_vloq
ORIENT:     southeast
: pan_lord_setup(_G, 'Gloorx Vloq')
SUBST:      X : xx.
SUBST:      V : x.
: if crawl.coinflip() then
SUBST:      C : c, d : c
: else
SUBST:      C : ., d : +
: end
TILE:       c = dngn_stone_wall_darkgray
MONS:       patrolling Gloorx Vloq
MONS:       executioner, shadow demon, shadow wraith, demonic crawler
KMONS:      O = demonic crawler
epilogue{{
  pan_lord_epilogue('Gloorx Vloq')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.4..........................4.xx
x4x.V.x.x.V.x.x.V.x.x.V.x.x.V.4xx
x..x.x.V.x.x.V.x2x.V.x.x.V.x2V.xx
x.V.........................x..xx
x..x.........................x.xx
x.x....ccccccccccccccccccc..V..xx
x..V...cxxxxxxxxxxxxxxxxxc...x.xx
x.x....cxxxxxxxxxxxxxx...c..x..xx
x..x...cxxxxxxxxxxxxx..X.c...V.xx
x.V....cxxxxxxxxxxx......c..x..xx
x..x...cxxxxxxxx.....X.X.c...x.xx
x.x..cccxxxx.............c..V..xx
x...3c5=.2.........X.X.X.cc..x.xx
....cc5c.................+3.x..xx
@...cO5c1....2...X.X.X.X.dC..V.xx
....cc5c.................+3.x..xx
x...3c5=.2.........X.X.X.cc..x.xx
x.x..cccxxxx.............c..V..xx
x..x...cxxxxxxxx.....X.X.c...x.xx
x.V....cxxxxxxxxxxx......c..x..xx
x..x...cxxxxxxxxxxxxx..X.c...V.xx
x.x....cxxxxxxxxxxxxxx...c..x..xx
x..V...cxxxxxxxxxxxxxxxxxc...x.xx
x.x....ccccccccccccccccccc..V..xx
x..x.........................x.xx
x.V.........................x..xx
x..x.x.V.x.x.V.x2x.V.x.x.V.x2V.xx
x4x.V.x.x.V.x.x.V.x.x.V.x.x.V.4xx
x.4..........................4.xx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   gloorx_vloq_grunt
TAGS:   gloorx_vloq uniq_gloorx_vloq
ORIENT: west
: pan_lord_setup(_G, 'Gloorx Vloq')
TILE:   c = dngn_stone_wall_darkgray
MONS:   patrolling Gloorx Vloq, Executioner, demonic crawler, shadow wraith
epilogue{{
  pan_lord_epilogue('Gloorx Vloq')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxx.......................xxxxxx
xxxxx...3.x...x...x...x...x.3...xxxx
xxxx.............................xxx
xxx...x..ccccccccccccccccc....x...xx
xxx.3....c...x...x...x...cc....x..xx
xx.......c....x.x.x.x.x.x.cc.......x
xx.x.x.x.c.....x...x...x...cc....3.x
xx....4..+......x.x.x.x.x.x.cc.....x
xx.......+.......x...x.2.x...cc..x.x
xx....4..+......x.x.x.x.x.x.x.cc...x
xx.x.x.x.c.....x...x...x...x...c...x
xxx.....xc....ccccccccc.x.x.x.xc...x
xx.x...x.c...ccccc.2.x...x.2.xcc.x.x
xxx.x.x.xccccc....x.x.x.x.x.x.xc...x
xx.x.x.x.cc........x...x...x...c.4.x
xxx.x.x.xc.c......x.x.x.x.x.x.xc....
xx.x.x.x.cOc.1...x...x.2.x...xcc.x.@
xxx.x.x.xc.c......x.x.x.x.x.x.xc....
xx.x.x.x.cc........x...x...x...c.4.x
xxx.x.x.xccccc....x.x.x.x.x.x.xc...x
xx.x...x.c...ccccc.2.x...x.2.xcc.x.x
xxx.....xc....ccccccccc.x.x.x.xc...x
xx.x.x.x.c.....x...x...x...x...c...x
xx....4..+......x.x.x.x.x.x.x.cc...x
xx.......+.......x...x.2.x...cc..x.x
xx....4..+......x.x.x.x.x.x.cc.....x
xx.x.x.x.c.....x...x...x...cc....3.x
xx.......c....x.x.x.x.x.x.cc.......x
xxx.3....c...x...x...x...cc....x..xx
xxx...x..ccccccccccccccccc....x...xx
xxxx.............................xxx
xxxxx...3.x...x...x...x...x.3...xxxx
xxxxxxx.......................xxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP


#############################################################################
# Pandemonium lesser demon vaults: repeating rune vaults.
#############################################################################
# Pandemonium demon vaults are selected by tag. They must have the tag "pan",
# or they won't even be considered. Tags are case-sensitive.
#
# Tagging these levels also ensures they won't be generated in the main
# dungeon.
#
# The first block consists of repeating vaults featuring a rune and Pan lord.
# TAG: allow_dup
# : pan_demonic(_G, 'P', true)     -- place the rune with a pandemonium lord
# : pan_demonic(_G, 'P')           -- place the rune without lord; have to
#                                  -- put down the Pan lord manually
#
# Of these maps, rand_demon_1 to rand_demon_9 are ancient heritage.

NAME:  rand_demon_1
TAGS:  pan allow_dup
MONS:  pandemonium lord, greater demon
: pan_demonic_rune(_G, 'P')
MAP
............
.xx.xx.x.xx.
..x.x2.x.x..
..x.x..x.x..
..x.x..x.x..
..x.x..x.x..
..x.x1Px.x..
..x.x..x.x..
..x.x..x.x..
..x.x.2x.x..
.xx.x.xx.xx.
............
ENDMAP

###################################

NAME:  rand_demon_2
TAGS:  pan allow_dup
MONS:  greater demon, common demon, common demon
: pan_demonic_rune(_G, 'P', true)
MAP
............
.xxxxxxxx2x.
.2.....xx.x.
.xxxxxx3x.x.
.xx3x..xx.x.
.x.x.11.x.x.
.x.x.P1.x.x.
.x.xx..x3xx.
.x.x3xxxxxx.
.x.xx.....2.
.x2xxxxxxxx.
............
ENDMAP

###################################

NAME:  rand_demon_3
TAGS:  pan allow_dup
MONS:  common demon, common demon
: pan_demonic_rune(_G, 'P', true)
MAP
............
.x.x.x2x.x..
..x.x2x2x.x.
.x.x.x1x.x..
..x2x1x1x2x.
.x2x1xPx1x2.
..x2x1x1x2x.
.x.x.x1x2x..
..x.x2x2x.x.
.x.x.x2x.x..
..x.x.x.x.x.
............
ENDMAP

###################################

NAME:  rand_demon_4
TAGS:  pan allow_dup
MONS:  pandemonium lord, any demon, any demon
: pan_demonic_rune(_G, 'P')
MAP
............
.xxxxxxxxx..
.x$+*+3+|x..
.xxxxxxx+x..
.x2+3+2x|x..
.x+xxxxx+x..
.x3+*x1+Px..
.x+x+xxxxx..
.x*x2+3+2=..
.xxxxxxxxx..
............
............
ENDMAP

###################################

NAME:  rand_demon_5
TAGS:  pan allow_dup
MONS:  pandemonium lord, any demon, any demon
: pan_demonic_rune(_G, 'P')
MAP
............
...xxxxxx...
..xx....xx..
.xx......xx.
.x..3232..x.
.x..2|P3..x.
.x..3.|2..x.
.x..2123..x.
.xx......xx.
..xx....xx..
...xxxxxx...
............
ENDMAP

###################################

NAME:  rand_demon_6
TAGS:  pan allow_dup
MONS:  pandemonium lord, any demon, any demon
: pan_demonic_rune(_G, 'P')
MAP
............
............
......2.....
............
.3..........
..........2.
.....1P.....
............
............
.2.......3..
............
............
ENDMAP

###################################

NAME:  rand_demon_7
TAGS:  pan allow_dup
MONS:  any demon, greater demon
: pan_demonic_rune(_G, 'P', true)
MAP
............
.xxx....xxx.
.x|xx=xxx|x.
.xx+....+xx.
..x.x++x.x..
..x.+P1+.=..
..=.+12+.x..
..x.x++x.x..
.xx+....+xx.
.x|xxx=xx|x.
.xxx....xxx.
............
ENDMAP

###################################

NAME:  rand_demon_8
TAGS:  pan allow_dup
MONS:  pandemonium lord, greater demon, any demon
: pan_demonic_rune(_G, 'P')
MAP
............
....xxxxxxx.
..xxx....1x.
.xx..2..P.x.
.x........x.
.xx.......x.
..xx33..2.x.
....33...xx.
.....x...x..
..F..xx.xx..
......xxx...
............
ENDMAP

###################################

NAME:  rand_demon_9
TAGS:  pan allow_dup
# a crapload of greater demons, you fight them in small sets
MONS:  pandemonium lord, any demon, greater demon
: pan_demonic_rune(_G, 'P')
MAP
............
.xxxxxxxxxx.
.x2+3+3+3xx.
.x+xxxxxx2x.
.x3x....x+x.
.x+x.P..x2x.
.x3x..1.x+x.
.x+x....x3x.
.x2xxxx+x+x.
.xx2+2+3x3x.
.xxxxxxxx=x.
............
ENDMAP

###################################

NAME:   evilmike_mini_pan_diamond
TAGS:   pan allow_dup
SUBST:  m : nx, n : mnxx.
MONS:   greater demon, common demon, any demon
: pan_demonic_rune(_G, 'P', true)
MAP
    .....
   ..m.m..
  ..mm2mm..
 ..mm.n.mm..
..mm3...3mm..
.mm...1...mm.
..2n.1P1.n2..
.mm...1...mm.
..mm3...3mm..
 ..mm.n.mm..
  ..mm2mm..
   ..m.m..
    .....
ENDMAP

###################################

NAME:   evilmike_mini_pan_thingy
TAGS:   pan allow_dup
MONS:   greater demon, common demon, any demon, pandemonium lord
: pan_demonic_rune(_G, 'P')
MAP
     .....
    ..x+x.
  ...xx.x.
  .cccc.x.
 ..c|1c.x.
..xc3c2.x......
.xxcc2c3xxxxxx.
.+.....4.....+.
.xxxxxx3c2ccxx.
......x.2c3cx..
     .x.c1Pc..
     .x.cccc.
     .x.xx...
     .x+x..
     .....
ENDMAP

###################################

NAME:   evilmike_mini_pan_other_thingy
TAGS:   pan allow_dup
SUBST:  d : +x
SUBST:  X : c., C : ccx
MONS:   greater demon, common demon, any demon
: pan_demonic_rune(_G, 'P', true)
MAP
@...........@
.............
xxxxxd+dxxxxx
xxxxC...Cxxxx
xxxCC...CCxxx
xxxC.....Cxxx
xxCC.....CCxx
xCC..1.1..CCx
xC..3XcX3..Cx
xC.2.cPc.2.Cx
xC....c....Cx
xCC.3...3.CCx
xxCC..1..CCxx
xxxCC*|*CCxxx
xxxxCCCCCxxxx
xxxxxxxxxxxxx
ENDMAP

###################################

NAME:   evilmike_mini_pan_3x3
TAGS:   pan allow_dup
MONS:   greater demon, common demon, any demon
NSUBST: 1 = 1:P / 3:1 / 2:2 / 2:3 / 1:|
SHUFFLE: =A
SUBST:  A = x
SUBST:  = = +
: pan_demonic_rune(_G, 'P', true)
MAP
.............
.xxx.xxx.xxx.
.x1x.x1x.x1x.
.xx+.x+x.+xx.
.............
.xxx.A=A.xxx.
.x1+.=1=.+1x.
.xxx.A=A.xxx.
.............
.xx+.x+x.+xx.
.x1x.x1x.x1x.
.xxx.xxx.xxx.
.............
ENDMAP

###################################

NAME:   evilmike_mini_pan_corridor
TAGS:   pan allow_dup
SUBST:  Y : Xx, X : x., Z : c.
MONS:   greater demon, common demon, any demon
: pan_demonic_rune(_G, 'P', true)
MAP
xxxxxxxxxxxxxxxxxxx
xxxxxxxcccccxxxxxxx
xxxxxxcc1..ccxxxxxx
xXXXXXY3Z2Z3YXXXXXx
@...2...cPc...2...@
xXXXXXY3Z2Z3YXXXXXx
xxxxxxcc..1ccxxxxxx
xxxxxxxcccccxxxxxxx
xxxxxxxxxxxxxxxxxxx
ENDMAP

###################################

NAME:   evilmike_mini_pan_pillars
TAGS:   pan allow_dup
MONS:   greater demon, common demon, any demon, pandemonium lord
SUBST:  X : x.., Y : x+, C : cx
: pan_demonic_rune(_G, 'P')
MAP
xxxxxxxxxxxxxx.@
xc|.........|x..
xc.C.C.C.C.C.x..
xc....2.2....x..
xc.C.3...3.C.x..
xc..2.1X1.2..Y..
xcPC..X4X..C.+..
xc..2.1X1.2..Y..
xc.C.3...3.C.x..
xc....2.2....x..
xc.C.C.C.C.C.x..
xc|.........|x..
xxxxxxxxxxxxxx.@
ENDMAP

###################################

# I'm sorry
NAME:   evilmike_mini_pan_murderfest
WEIGHT: 2
TAGS:   pan allow_dup
MONS:   greater demon, any demon, pandemonium lord
NSUBST: | = 1:P / *:|
SUBST:  + : +., X : =c, c : cccx
# I don't want this vault to be an autoexplore trap
MARKER: G = lua:props_marker { stop_explore = "ominous statue." }
: pan_demonic_rune(_G, 'P')
MAP
xxxxxxxxxxxxxxxxx
xcccccccccccccccx
xc|c.........c|cx
xcc+..2.1..2.+ccx
xc..1.......1..cx
xc.2..=====....cx
xccccXX...==.2.cx
...........=...cx
@.......G..=.3.cx
...........=...cx
xccccXX...==...cx
xc.2..=====..2.cx
xc..1.......1..cx
xcc+.2..1.2..+ccx
xc|c.........c|cx
xcccccccccccccccx
xxxxxxxxxxxxxxxxx
ENDMAP

###################################

NAME:    evilmike_mini_pan_rooms
WEIGHT:  3
TAGS:    pan allow_dup
SUBST:   + : +.
SHUFFLE: 123|P
MONS:    any demon, any demon, any demon
MONS:    greater demon / common demon, common demon
: pan_demonic_rune(_G, 'P', true)
MAP
xxxxxxxxxxxxxx
xx5..xxxxx..4x
xx.|..+.+..P.x
xx4..xx.xx..5x
xxxxxxx.xxxxxx
......x.xx..4x
@.....+.+..3.x
......x.xx..5x
xxxxxxx.xxxxxx
xx4..xx.xx..5x
xx.1..+.+..2.x
xx5..xxxxx..4x
xxxxxxxxxxxxxx
ENDMAP

###################################

NAME:   evilmike_mini_pan_layers
TAGS:   pan allow_dup
SUBST:  x = x:9 +:1
MONS:   greater demon, common demon, any demon
: pan_demonic_rune(_G, 'P', true)
MAP
         ...
        ..x..
       ...2...
      ..xxxxx..
     .....3.....
    ..xxxxxxxxx..
   .......3.......
  ..xxxxxxxxxxxxx..
 ........2|1........
..xxxxxxxxxxxxxxxxx..
.........3P3.........
..xxxxxxxxxxxxxxxxx..
 ........1|2........
  ..xxxxxxxxxxxxx..
   .......3.......
    ..xxxxxxxxx..
     .....3.....
      ..xxxxx..
       ...2...
        ..x..
         ...
ENDMAP

###################################

NAME:   evilmike_mini_pan_corner
TAGS:   pan allow_dup
SUBST:  X : x., Y : c.
MONS:   greater demon, common demon, any demon
: pan_demonic_rune(_G, 'P', true)
MAP
xxxxxx@xxxxxx
xxxxxX.xxxxxx
xxxxXX.xxxxxx
xxxXXX.xxxxxx
xxXXXX.xxxxxx
xXXXXX.Yccccx
@.......YYYcx
xxxxxY...2Ycx
xxxxxcY.213cx
xxxxxcY21P3cx
xxxxxcYY33Ycx
xxxxxcccccccx
xxxxxxxxxxxxx
ENDMAP

###################################

NAME:    evilmike_mini_pan_branches
TAGS:    pan allow_dup
SHUFFLE: 4A
SUBST:   A = 1
MONS:    greater demon, common demon, any demon, pandemonium lord
: pan_demonic_rune(_G, 'P')
MAP
xxxxxxxxxxxxxxxx
xxxxxxxxxxccccxx
xxxxxxxxxxcP.ccx
xxxxxxxxxxccc.cx
xxxxxxxxxxx|c4cx
xxxxxxxx*xx1c.cx
xxxx%xxx3xx.c.cx
xxxx2xxx.xx.c.cx
xxxx.xxx.xx.c.cx
@.............cx
xxxx.xxx.xx.c.cx
xxxx2xxx.xx.c.cx
xxxx%xxx3xx.c.cx
xxxxxxxx*xx1c.cx
xxxxxxxxxxx|cAcx
xxxxxxxxxxccc.cx
xxxxxxxxxxc|.ccx
xxxxxxxxxxccccxx
xxxxxxxxxxxxxxxx
ENDMAP

###################################

NAME:  evilmike_mini_pan_T
TAGS:  pan allow_dup
SUBST: + : +..
MONS:  greater demon, common demon, any demon, pandemonium lord
: pan_demonic_rune(_G, 'P')
MAP
xxxxxxxxxxxxxxxx
xxxxxxxcccxxxxxx
xxxxxxxcP+...xxx
xxxxxxxccc.1.xxx
xxxxxxxxxx3.2xxx
xxxxxxxxxx...xxx
xxxxxxxxxx...xxx
..............xx
@.....3......4xx
..............xx
xxxxxxxxxx...xxx
xxxxxxxxxx...xxx
xxxxxxxxxx2.3xxx
xxxxxxxccc.1.xxx
xxxxxxxc|+...xxx
xxxxxxxcccxxxxxx
xxxxxxxxxxxxxxxx
ENDMAP

###################################

# based slightly on rand_demon_3
NAME:    evilmike_mini_pan_possible_glass
TAGS:    pan allow_dup
SHUFFLE: mnxcll
MONS:    greater demon, common demon, any demon
: pan_demonic_rune(_G, 'P', true)
MAP
 ...........
.............
..m.m.m.m.m..
...m2m.m2m...
..m2m.1.m2m..
...m.....m...
..m.3.P.3.m..
...m.....m...
..m2m.1.m2m..
...m2m.m2m...
..m.m.m.m.m..
.............
 ...........
ENDMAP

###################################

NAME:   evilmike_mini_pan_pillars_b
TAGS:   pan allow_dup
SUBST:  X : x.
SUBST:  Y : mmxxl
MONS:   greater demon, common demon, any demon, pandemonium lord
NSUBST: D = 1:P / 3:1 / 3:2 / 3:3 / 3:|
: pan_demonic_rune(_G, 'P')
MAP
@.xxxxxxxxxxx
..xXDXDXDXDXx
..x.Y.Y.Y.YDx
..xX.X.X.X.Xx
..x.Y.Y.Y.YDx
..xX.X.X.X.Xx
....Y.Y4Y.YDx
..xX.X.X.X.Xx
..x.Y.Y.Y.YDx
..xX.X.X.X.Xx
..x.Y.Y.Y.YDx
..xXDXDXDXDXx
@.xxxxxxxxxxx
ENDMAP

###################################

NAME: evilmike_mini_pan_doors
TAGS: pan allow_dup
MONS: greater demon, common demon, any demon
: pan_demonic_rune(_G, 'P', true)
MAP
............
..x+xxxx+x..
.xx......xx.
.+.x3..2x.+.
.x.2c++c3.x.
.x..+|1+..x.
.x..+1P+..x.
.x.3c++c2.x.
.+.x2..3x.+.
.xx......xx.
..x+xxxx+x..
............
ENDMAP

###################################

NAME:   evilmike_mini_pan_doors_b
TAGS:   pan allow_dup
# The doors are not treated as large gates, but individually:
MARKER: X = lua: props_marker { connected_exclude="true" }
SUBST:  X = +
NSUBST: D = 2:1 / 2:2 / 2:3 / 1:P / 1:|
MONS:   greater demon, common demon, any demon
: pan_demonic_rune(_G, 'P', true)
MAP
xxxxxxxxxxxxx
xxxxDxDxDxDxx
xxx.........x
xxxXXXXXXXXXx
@...........x
xxxXXXXXXXXXx
xxx.........x
xxxxDxDxDxDxx
xxxxxxxxxxxxx
ENDMAP

###################################

NAME:   evilmike_mini_pan_V
TAGS:   pan allow_dup
MONS:   greater demon, common demon, any demon, pandemonium lord
: pan_demonic_rune(_G, 'P')
MAP
x.@.xxxxxxxxxxxxxxxxx.@.x
xx...xxxxxxxxxxxxxxx...xx
xxx.3.xxxxxxxxxxxxx.3.xxx
xxxx...xxxxxxxxxxx...xxxx
xxxxx.2.xxxxxxxxx.2.xxxxx
xxxxxx...xxcccxx...xxxxxx
xxxxxxx.1.xcPcx.1.xxxxxxx
xxxxxxxx...c+c...xxxxxxxx
 xxxxxxxx..3.3..xxxxxxxx
  xxxxxxxx..4..xxxxxxxx
   xxxxxxxx3.3xxxxxxxx
    xxxxxxxx|xxxxxxxx
     xxxxxxxxxxxxxxx
ENDMAP

###################################

NAME: nicolae_pan_door_chamber
TAGS: pan allow_dup
WEIGHT: 5
MONS: any demon, common demon, greater demon
MARKER: + = lua:props_marker { connected_exclude="true" }
MARKER: + = lua:restrict_door()
MARKER: - = lua:props_marker { connected_exclude="true" }
MARKER: - = lua:restrict_door()
NSUBST: + = 1:P / 2:2 / 2:3 / *:+
SUBST: - = +
: pan_demonic_rune(_G, 'P', true)
MAP
   x..@..x
 xxx.....xxx
 xx...1...xx
xx.-------.xx
...-+++++-...
...-+++++-...
@.1-+++++-1.@
...-+++++-...
...-+++++-...
xx.-------.xx
 xx...1...xx
 xxx.....xxx
   x..@..x
ENDMAP

###################################

NAME: nicolae_pan_diamonds
TAGS: pan allow_dup
MONS: pandemonium lord, greater demon, any demon
NSUBST: P = 1:P / *:.
: pan_demonic_rune(_G, 'P')
MAP
       c
      cxc
     cxmxc
    cxmPmxc
   cxm...mxc
    cxm.mxc
   cxm...mxc
  cxm..2..mxc
   cxm...mxc
  cxcxm.mxcxc
 cxmxm...mxmxc
 .m.m.3.3.m.m.
@m.2...1...2.m@
 .m.m.3.3.m.m.
 cxmxm...mxmxc
  cxcxm.mxcxc
   cxm...mxc
  cxm..2..mxc
   cxm...mxc
    cxm.mxc
   cxm...mxc
    cxmPmxc
     cxmxc
      cxc
       c
ENDMAP

###################################

NAME: nicolae_pan_giant_door
TAGS: pan allow_dup no_trap_gen
MONS: any demon
MARKER: = = lua:restrict_door()
: pan_demonic_rune(_G, 'P', true)
MAP
     ..@..
   .........
 ....G...G....
 @..xxx=xxx..@
 ...xc1=1cX...
  ..xc1=1cX..
    xc1=1cX
    xc1=1cX
    xc1=1cx
    ccc=ccc
    c.1.1.c
    c1.P.1c
    ccccccc
ENDMAP

###################################

NAME: nicolae_pan_hourglass
TAGS: pan allow_dup
NSUBST: P = 1:P / *:.
MONS: common demon, greater demon
: pan_demonic_rune(_G, 'P', true)
MAP
   xxxxxxxxx
  xm...P...mx
 xm.2.....2.mx
 xm...xxx...mx
 xm..xxxxx..mx
 xm..1xxx1..mx
  xm.......mx
   xm..2..mx
    xm.1.mx
    ..m1m..
    @11m11@
    ..m1m..
    xm.1.mx
   xm..2..mx
  xm.......mx
 xm..1xxx1..mx
 xm..xxxxx..mx
 xm...xxx...mx
 xm.2.....2.mx
  xm...P...mx
   xxxxxxxxx
ENDMAP

###################################

NAME: nicolae_pan_double_arrow
TAGS: pan allow_dup
MONS: greater demon, common demon
: pan_demonic_rune(_G, 'P', true)
MAP
       c
      ccc
     ccxcc
    ccxxxcc
   ccxxxxxcc
  ccxxx.xxxcc
 ccxxx...xxxcc
ccxxx.....xxxcc
 xxx..2.2..xxx
  x.........x
  .2.x...x.2.
 ...xx.1.xx...
...xxx...xxx...
..xxxx...xxxx..
@xxxxx.P.xxxxx@
..xxxx...xxxx..
...xxx...xxx...
 ...xx.1.xx...
  .2.x...x.2.
  x.........x
 xxx..2.2..xxx
ccxxx.....xxxcc
 ccxxx...xxxcc
  ccxxx.xxxcc
   ccxxxxxcc
    ccxxxcc
     ccxcc
      ccc
       c
ENDMAP

###################################

NAME: nicolae_pan_glass_arcs
TAGS: pan allow_dup
MONS: any demon, greater demon
: pan_demonic_rune(_G, 'P', true)
MAP
     ccccc
  ccccccccccc
  ...ccccc...
  n.1..c..1.n
 ..nn..2..nn..
 ...nn...nn...
 @1.nn.P.nn.1@
 ...nn...nn...
 ..nn..2..nn..
  n.1..c..1.n
  ...ccccc...
  ccccccccccc
     ccccc
ENDMAP

###################################

NAME: nicolae_pan_performance_anxiety
TAGS: pan allow_dup no_trap_gen
WEIGHT: 1
KPROP: tb = no_rtele_into
MONS: greater demon, common demon
NSUBST: t = 10:p / *:., b = 10:p / *:.
KMONS: p = human / elf / orc / naga / centaur / draconian / \
   ogre / minotaur / troll / kobold / mummy / ghoul / spriggan / dwarf / \
   deep dwarf / felid / octopode / vampire / base draconian / demigod / \
   demonspawn / merfolk / halfling
: pan_demonic_rune(_G, 'P', true)
MAP
      ccccc
    ccctttccc
   cctttttttcc
   cttnnnnnttc
  cctnn.2.nntcc
  ctnn.1.1.nntc
..cnn.1...1.nnc..
@....2..P..2....@
..cnn.1...1.nnc..
  cbnn.1.1.nnbc
  ccbnn.2.nnbcc
   cbbnnnnnbbc
   ccbbbbbbbcc
    cccbbbccc
      ccccc
ENDMAP

###################################

NAME:    grunt_pan_in_and_out
TAGS:    pan allow_dup no_trap_gen
MONS:    greater demon, common demon
KMONS:   d = pandemonium lord
: pan_demonic_rune(_G, 'e')
MAP
............
.c+cccccc+c.
.c.c.de.c.c.
.c2c....c2c.
.c.c.11.c.c.
.c2c....c2c.
.c.cc++cc.c.
.c2cc..cc2c.
.c.cc..cc.c.
.c2..11..2c.
.cccccccccc.
............
ENDMAP

NAME:    grunt_pan_spiral
TAGS:    pan allow_dup no_trap_gen
MONS:    greater demon, common demon
KMONS:   d = pandemonium lord
: pan_demonic_rune(_G, 'e')
MAP
............
.c+cccccccc.
.c.cc2.2..+.
.c.c.cccccc.
.c2c1c.1.cc.
.c.c.decc2c.
.c2ccfg.c.c.
.cc.1.c1c2c.
.cccccc.c.c.
.+..2.2cc.c.
.cccccccc+c.
............
ENDMAP

NAME:    grunt_pan_zigzag
TAGS:    pan allow_dup no_trap_gen
MONS:    greater demon, common demon
KMONS:   d = pandemonium lord
: pan_demonic_rune(_G, 'e')
MAP
............
.cccccccccc.
.c..2..2..+.
.c.2.cccccc.
.cc.1.cc....
..cc.d1cc...
...cc1e.cc..
....cc.1.cc.
.cccccc.2.c.
.+..2..2..c.
.cccccccccc.
............
ENDMAP

NAME: grunt_pan_pentagram
TAGS: pan allow_dup no_trap_gen
: pan_demonic_rune(_G, 'P', true)
MONS: greater demon, common demon
MAP
    ...........
  ...cccc+cccc...
 ..ccc.......ccc..
 .cc.cc..2..cc.cc.
..c...+cc.cc+...c..
.c+...c.ccc.c...+c.
.c..2.c1ccc1c.2..c.
.c....cc+.+cc....c.
.c..cccc.P.cccc..c.
.c.cc.1+...+1.cc.c.
.cc+ccccc+ccccc+cc.
.c......c1c......c.
.cc..2..c.c..2..cc.
..cc....c+c....cc..
 ..+c....c....c+..
  ..ccc..c..ccc..
   ...ccccccc...
     .........
ENDMAP

#############################################################################
# Pandemonium lesser demon vaults: standalone.
#############################################################################
# If the map is repeating (TAG: allow_dup) and features a rune, it does not
# go here.
#

NAME:    lemuel_draining_boxes
TAGS:    pan
MONS:    pandemonium lord, eye of draining, any demon
SHUFFLE: aA/bB/cC/dD/eE/fF, eE/gG, fF/hH
SUBST:   a=m, b=m, c=m, d=m, e=m, f=m, g=., h=.
SUBST:   A=2, B=2, C=2, D=2, E=2, F=2, G=., H=.
# the two center boxes are granted;
# of the six outer boxes, one or two may be missing
SUBST:   n = m
KFEAT:   m = iron_grate
KMONS:   O = eye of draining
KITEM:   O = demonic rune of Zot
MAP
                  ....bbb..
.......           ....bBb..
.aaa...           ....bbb..
.aAa...           .........
.aaa.......................
...........................
.............mmm.............
    .........m2m..........ccc
    .........mmm3.........cCc
...............333........ccc
..............33133nnn.......
.fff...........333.nOn.......
.fFf............3..nnn...
.fff.....................
.........................
.............................
  .........        ...ddd....
  ...eee...        ...dDd....
  ...eEe...        ...ddd....
  ...eee...        ..........
ENDMAP

###################################

NAME:  lemuel_hellion_island
TAGS:  pan
MONS:  hellion
SUBST: L=l.
KMONS: O = pandemonium lord
KITEM: O = demonic rune of Zot
MAP
    ..LlllL..
  ..LlllllllL..
 .LlllllllllllL.
.LlllllL1LlllllL.
.LllllL1.1LllllL.
LllllL1|1|1LllllL
llllL1.1O1.1Lllll
llllL.1|1|1.Lllll
LllllL.1.1.LllllL
.LllllL.1.LllllL.
.LlllllLLLlllllL.
 .LlllllllllllL.
  ..LlllllllL.
    ..LlllL..
ENDMAP

###################################
# XXX: Undiggable glass has been converted to grates.
NAME:    lemuel_hall_of_pain
TAGS:    pan
MONS:    pandemonium lord, any demon
MONS:    smoke demon / weight:1 brimstone fiend
SHUFFLE: xc
KFEAT:   m = iron_grate
SHUFFLE: O1/AB
SUBST:   A=|, B=|
KITEM:   O = demonic rune of Zot
MAP
.......@.......
xxxxxx+++xxxxxx
xOx.xl...lx.xAx
x1x3xl.2.lx3xBx
x.x.ml...lm.x.x
x2x.xl...lx.x2x
x.x3xl...lx3x.x
x2x.ml...lm.x2x
x.x3xl...lx3x.x
x2x.xl.2.lx.x2x
x.x.ml...lm.x.x
x.x3xl...lx3x.x
x2x.xl...lx.x2x
x.x.ml...lm.x.x
x.x3xl.2.lx3x.x
x2xxxl2.2lxxx2x
x....2.2.2....x
xxxxxxxxxxxxxxx
ENDMAP

###################################

NAME:  grunt_pan_frozen_over
TAGS:  pan no_pool_fixup no_monster_gen
SUBST: W = wWW.
KMONS: O = ice statue
KITEM: O = demonic rune of Zot
KMONS: P = pandemonium lord
MONS:  blizzard demon, ice devil / blue devil w:20
: set_random_mon_list("blizzard demon / ice devil / blue devil")
MAP
ccccccccccccccccccc
.WwcwW1.1O1.1WwcwW.
2WwcwW.1.P.1.WwcwW2
.WwccwW.1.1.WwccwW.
 .WwccwW.1.WwccwW.
 .WwwccwWWWwccwwW.
  2WWwWWwwwWWwWW2
   ..WWwwwwwWW.
     ..WWWWW..
       .2.2.
ENDMAP

#############################################################################
# Pandemonium vaults: non-panlord-related.
#############################################################################

###################################
# A couple of small vaults featuring blood fountains.

NAME:   pan_blood_fountain
TAGS:   extra allow_dup transparent
DEPTH:  Pan
CHANCE: 10%
SUBST:  ' = '..
SUBST:  . = " / .:80
KPROP:  '" = bloody
SUBST:  '=. , "=.
MAP
 ...
.'''.
.'Y'.
.'''.
 ...@
ENDMAP

NAME:   pan_blood_fountain_2
TAGS:   extra allow_dup transparent
DEPTH:  Pan
CHANCE: 5%
SUBST:  Y = YYV
SUBST:  ' = '..
SUBST:  . = " / .:80
KPROP:  '" = bloody
SUBST:  '=. , "=.
MAP
 @.....
 '''.'''
.'Y'.'Y'.
.'''''''.
 ..'Y'..
  .'''.
   ...
ENDMAP

NAME:   pan_blood_fountain_small
TAGS:   extra allow_dup transparent
DEPTH:  Pan
CHANCE: 15%
MAP
Y
ENDMAP

###################################

NAME:   pan_surprise
TAGS:   pan extra
KMONS:  1 = executioner
# It may be a surprise for the executioner as well if the player has high
# stealth -- an alarm trap would be found by most chars who dare Pan and thus
# spoil the fun.
MAP
xxxx
xx.x
x1x@
xxx
ENDMAP

###################################

NAME:   pan_captured_pigsty
TAGS:   pan extra
MONS:   red devil, holy swine
KFEAT:  o = iron_grate
{{
local function swineherd_die(monster, killer_type, killer_index, silent, wizard)
  local p = dgn.find_marker_positions_by_prop("pigsty_gate", 1)[1]
  if (p == nil) then
    crawl.mpr("Error: someone stolen the pigsty gate!")
    return
  end
  if dgn.grid(p.x, p.y) ~= dgn.fnum("iron_grate") then
    return
  end
  dgn.grid(p.x, p.y, "floor")
  if (you.see_cell(p.x, p.y)) then
    crawl.mpr("The grate goes up!")
  else
    crawl.mpr("You hear a sound of rusty metal sliding.")
  end
end
}}
MARKER: 1 = lua:MonPropsMarker:new {monster_dies_lua_key = swineherd_die}
MARKER: o = lua:props_marker {pigsty_gate=1}
MAP
xxxxx
x222x
x222x
xxoxx
@..1@
ENDMAP

###################################

NAME:   pan_disco_hall
TAGS:   pan no_rotate
ORIENT: float
COLOUR: D = random
COLOUR: .x-0123456789P = disco
# WALL_ZOT_BLUE and FLOOR_PEBBLE are the bases for coloured variants of the
# tiles thereof.
RTILE:  x = wall_zot_blue
FTILE:  D.-0123456789 = floor_pebble
SUBST:  - = 0:1 / .:10
KFEAT:  D = granite_statue
TILE:   D = dngn_disco_ball
: set_feature_name("granite_statue", "weirdly glowing ball")
MARKER: D = lua:props_marker { veto_fragmentation="veto", \
                               veto_disintegrate="veto", \
                               veto_shatter="veto" }
MONS:   col:random name:demonic name_adjective plant tile:mons_demonic_plant
MONS:   red devil ; demon trident good_item ego:distortion
MONS:   red devil ; lajatang good_item ego:distortion
MONS:   red devil ; demon blade good_item ego:distortion
KMONS:  P = pandemonium lord
KFEAT:  P = floor
FTILE:  P = floor_hall
KFEAT:  _ = floor
FTILE:  _ = floor_hall
MAP
                   xx@xx
                   x1.1x
                   x...x
   xxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxxxx
   x........................x---------------x
   x.x.x.x.x.x.x.x.x.x.x.x..x---------------x
   x........................x---------------xxxxxx
   x.x.x.x.x.x.x.x.x.x.x.x..x---------------_....x
xxxx.........................---------------_..2.x
x1.x.........................---------------_....x
@...........................D-------P-------_..3.x
x1.x.........................---------------_....x
xxxx.........................---------------_..4.x
   x.xxxxxxxxxxxxxxxxxxxxx..x---------------_....x
   x.x0.0x..0x0.0x0.0x..0x..x---------------xxxxxx
   x.x0..x0..x...x0..x...x..x---------------x
   x........................x---------------x
   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

### Holy Pan Level ############################################################
#
# The angels have gained a foothold in pandemonium.
#
# This level's main feature is a TSO fortress with a lot of loot. Smaller
# features are a temple of Zin, a weird Ely pasture in a cave, and a river for
# some reason. There's also a cave with a pearl dragon and phoenixes.
#
# This level has a lot of open space where it mostly is up to the RNG to place
# monsters.
#
# Four exits are given, with one placed randomly and the others in a room safe
# to teleport into.
#
# My opinion is that in the long term this concept would work better as a
# portal vault (for pan, even).
#
NAME:   evilmike_holy_pan
TAGS:   pan no_pool_fixup
ORIENT: encompass
NSUBST: . = > / .
ITEM:   w:5 any / potion of curing
ITEM:   any / potion of heal wounds
ITEM:   acquire any
ITEM:   potion of cure mutation
ITEM:   potion of restore abilities
ITEM:   any
NSUBST: i = 2=ghh / 3=ghhiii / *:i
MONS:   angel
MONS:   cherub
MONS:   angel
MONS:   ophan
MONS:   ophan
MONS:   shedu band
MONS:   daeva
KMONS:  8 = phoenix
KMONS:  9 = pearl dragon
KMONS:  0 = apis
KMONS:  A = apis
KMONS:  B = daeva god:Zin
KMONS:  Z = patrolling silver star
KMONS:  S = patrolling seraph
KFEAT:  _ = altar_the_shining_one
KFEAT:  E = altar_elyvilon
KFEAT:  Z = altar_zin
KFEAT:  > = transit_pandemonium
TILE:   c = wall_church
COLOUR: cGH = yellow
TILE:   v = dngn_metal_wall_white
COLOUR: v& = white
TILE:   GH = dngn_golden_statue
FTILE:  _124SDcG|*' = floor_limestone
FTILE:  "E0 = floor_grass
COLOUR: "0 = green
FTILE:  -ZBghi&v = floor_vault
SUBST:  D& = +, '"- = ., H = G, C = c
: set_feature_name("granite_statue", "golden statue")
: set_feature_name("metal_wall", "silver wall")
: set_random_mon_list("angel / cherub / ophan / daeva / phoenix / " ..
:                     "apis / shedu band / apis / shedu band / angel")
# Do not place if you are with a holy god. It's just free loot in that case.
veto {{
    if you.god() == "the Shining One" or you.god() == "Zin" or you.god() == "Elyvilon" then
        return true
    else
        return false
    end
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxCCCCCxxxxxxxxxxxxwwwwxxxxxxxxxwwwxxxwwwxxxxxxCCCCCCCCCCCCCCCCCxxxxxxxxxxx
xxxxxxxC%%%CxxxxxxxxxxwwwwwwwwwxxxwwwxwwwwwwwwxxxxxxCCCCC.......CCCCCxxxxxxxxxxx
xxxxxxxC%.%CxxxxxxxxxxwwwwwwwwwwwwxxxwwwwwwwwxxxxxxxC...+.>.>.>.+...Cxxxxxxxxxxx
xxxxxxCCC+CCCxxxxxx""""wwwwwwwwwwwwwwwwwwwwCCCCCCCCCC.CCC.......CCC.Cxxxxxxxxxxx
xxxCCCC.....Cxxxx""0"""wwwwwwwwwww"wwwwwwww+........+.CCCCCC+CCCCCC.Cxxxxxxxxxxx
xxxCddC.A...CCC"""""""""wwwwwwww""0"wwwwwwwCCCCCCCCCC.CCCCCC.CCCCCC.Cxxxxxxxxxxx
xxxCd.+9......+""0"www0"""wwwww""E""wwwwwwwxxxxxxxxxC.CCCCCC.CCCCCC.Cxxxxxxxxxxx
xxxCddC.A...CCCx""""ww"""""wwwww"""wwwwwwwxxxxxxxxxxC.C7CCCC+CCCC7C.Cxxxxxxxxxxx
xxxCCCC.....Cxxxxx""""""""""wwwww""wwwwwxxxxxxxxxxxxC.CC.........CC.Cxxxxxxxxxxx
xxxxxxCCC+CCCxxxxx""0"""xx"""wwwwwwwwwwxxxxxxxxxxxxxC..+.C.C.C.C.+..Cxxxxxxxxxxx
xxxxxxxCe.eCxxxxxxx""""xxxxxx""wwwwwwwxxxxxxxxxxxxxxCCCC.........CCCCxxxxxxxxxxx
xxxxxxxCeeeCxxxxxxxxx"xxxxxx""wwwwwwwxxxxxxxxxxxxxxxCCCCCCCC+CCCCCCCCxxxxxxxxxxx
xxxxxxxCCCCCxxxxxxxxxxxxxxxx"wwwwwwwwww.xxxxxxxxxxxxxxxxxxxC.Cxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxwwwwwxwwwww.xxxxxxxxxxxxxxxxxxxC.Cxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxwwwwwwwwww..xxxxxxxxxxxxxxxxxxxxC.Cxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx..wwwwwwwwwwww.xxxxxxxxxxxxxxxxxxxxC.Cxxxxxxxxxxxxxxxxxx
xCCCCCCCCCCCxxxxxxxxxxx..wwwwxwwwwwww.xx..xxxxx..xxx..xxxxxC+Cx..xxxxxxxxxxxxxxx
xCf73......Cxxxxxxx.....wwwwxxwwwww........xx..........xxxx........xxxxxxxxxxxxx
xCCCCCCCC..Cxxxx.......wwwwxxwwww...............xx...................xxxxxxxxxxx
xxxxxxxCCC.Cxx........wwwwwwwwcccc.............................cccc...xxxxxxxxxx
xxxxxxxxxC+Cx...xx...wwwwwwwwwc1*ccccccccccccccccccccccccccccccc*1c...xxxxxxxxxx
xxxxxxxxx......xxx..wwwwwwwww.c*'ccccccccccccccccccccccccccccccc'*c...xxxxxxxxxx
xxxxxxxx.....xxxx...wwwwwwwww.ccc+..........3.......3..........+ccc...xxxxxxxxxx
xxxxxxxx....xxxxx..wwwwwwwww...cc...............5...............cc...xxxxxxxxxxx
xxxxxxx....xxxxx...wwwwwwww....cc..www.ccccccccccccccccccc.www..cc...xxxxxxxxxxx
xxxxxx.....xxxx....wwwwwwwww...cc..wHw.cccccccc|||cccccccc.wHw..cc....xxxxxxxxxx
xxxxxx....xxxx...wwwwwxxwwww...cc..www.ccw4''|||||||''4wcc.www..cc....xxxxxxxxxx
xxxxxx....xxx...wwwwwxxwwww....cc......ccw''||||'||||''wcc......cc....xxxxxxxxxx
xxxxxx...xxx...wwwwwwwwwww..xx.cc..ccc+cccccccccSccccccccc+ccc..cc.....xxxxxxxxx
xxxxx....xx....wwwwwwwwww..xx..cc..c'''ccwwwcccc'ccccwwwcc***c..cc.....xxxxxxxxx
xxxx...........wwwxwwwwww.xxx..cc..c1'1cGw'2'cc1_1cc'2'wGc***c..cc......xxxxxxxx
xxxx..........wwwwxxwwww..xxx..cc..ccccccw''''c'4'c''''wcccccc..cc........xxxxxx
xxxx...xxx....wwwxxxwwww.xxx...cc..ccwwcGw''''c'''c''''wGcwwcc..cc.........xxxxx
xxxxx..xxx...wwwwxxxwww.xxxx...cc..cc22ccw''''''G''''''wcc22cc..cc..........xxxx
xxxxx...xxx..wwwxxxwww...xx....cc..cc''D'''''''''''''''''D''cc..cc..........xxxx
xxxxx...xx...wwwwwwww..........cc..cc''ccw'''''''''''''wcc''cc..cc.....xx....xxx
xxxx...xxx..wwwwwwww...xx......cc..cc''cGw'''''''''''''wGc''cc..cc....xxxx...xxx
xxx...xxx..wwwwwww...xxxxxx....cc..cc''ccwwwwwwwwwwwwwwwcc''cc..cc....xxxx...xxx
xxx........wwwwww...xxxxxxx....cc..cc''cccGcGcGcGcGcGcGccc''cc..cc...xxxx....xxx
xxx.......wwwwww....xxxxxxxx...cc..cc''ccccccccccccccccccc''cc..cc.....x....xxxx
xxx......wwwwwww...xxxxxxxxx...cc..cc'''''''''''''''''''''''cc..cc..........xxxx
xx.....wwwwwwwww..xxxxxxxxx....cc..cc''''2'''G'''''G'''2''''cc..cc.........xxxxx
xx....wwwwwwwww...xxxxxxxxx....cc..cc4'''''''''''''''''''''4cc..cc........xxxxxx
xx...wwwwwwwww.....xxxxxxxx....cc..cc''cccccccDcDcDccccccc''cc..cc........xxxxxx
xx..wwwwwwwww.....xxxxxxxx.....cc..ccwwcccccG'''''''Gcccccwwcc..cc........xxxxxx
xx.wwwwwwwww.......xxxxxx......cc..cccccccccc'''''''cccccccccc..cc...xx....xxxxx
xxwwwwwwww....xx....xxx........cc...........cccDDDccc...........cc...xxx....xxxx
xwwwwwww.....xxxx.........x....cc......5........3........5......cc....xx....xxxx
xwwwwww.....xxxx.........xx...ccc+.............................+ccc.........xxxx
xwwww........xx...............c*'cccccccccccc.......cccccccccccc'*c........xxxxx
xww...........................c1*cccccccccccc.......cccccccccccc*1c.......xxxxxx
xxxx..xx..v.vvvvvvvvvvvvvv....cccc..........H.......H..........cccc......xxxxxxx
xxxx..xx...vvv----v-----iv.v................................x..........xxxxxxxxx
xxxx...x..vvv-Bv-----vvBiv.vv...............H.......H......xxx........xxxxxxxxxx
xxxxx.....vvi-----v-----iv........v.........6.......6......xxx.........xxxxxxxxx
xxxxx.....vv------vvvvvvvvvvvvvv......xx....H.......H.....xxx.....xx..x..xxxxxxx
xxxxx.....vv---------vBv-v-v-v-v.v...xxxx.......................xxxxx....xxxxxxx
xxxxx.....vg-Z---vvv-&---------&......xxxx..................xxxxxxxxxxxx...x.8xx
xxxxxx....vv---------vBv-v-v-v-v.v.....xxx.............xxxxxxxxxxxxxxxx........x
xxxxxx....vv------vvvvvvvvvvvvvv...............xxxxxxxxxxxxxxx$$$xxxxx..xxxx...x
xxxxxxx...vvi-----v-----iv........v......xxxxxxxxxxxxxxxxxxxx$.88xxxx8.xxxx..xxx
xxxxxxxx..vvv-Bv-----vvBiv.vv.........xxxxxxxxxxxxxx$$$$$xxxxxx8...xxxxxxx....xx
xxxxxxxx...vvv----v-----iv.v......xxxxxxxxxxxxxxxxx$$$9$$$xxxxxx..x..xxx....x..x
xxxxxxxx..v.vvvvvvvvvvvvvv....xxxxxxxxxxxxxxxxxxxxx$$$$$$..xxxxxxx........xxxx8x
xxxxxxxxx..................xxxxxxxxxxxxxxxxxxxxxxxxx$$$.....xxxxxxx......xxxxxxx
xxxxxxxxxxx..............xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx....wwxxx....xxxxxxxxxxx
xxxxxxxxxxxxx........xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxwwwwwxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Random vault and minivault balancers for Pan.

NAME: pan_mini_dummy_balancer
DEPTH: Pan
TAGS: dummy
WEIGHT: 100
MAP
x
ENDMAP

NAME: pan_dummy_balancer
DEPTH: Pan
TAGS: dummy
ORIENT: float
WEIGHT: 100
MAP
x
ENDMAP

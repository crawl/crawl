{{
  function invisibility_setup(e)
    if you.race() == "Coglin" then
      e.kitem("n = scarf of invisibility")
    else
      e.kitem("n = amulet of invisibility")
    end
  end
}}

NAME:   dungeon_sprint_fedhas
TAGS:   sprint no_item_gen no_trap_gen no_pool_fixup no_rotate no_hmirror no_vmirror
DESC:   Sprint IV: "Fedhas' Mad Dash"
ORDER:  4
ORIENT: encompass
SUBVAULT: o : fedhas_sprint_temple
KMONS:  A = fungus
KMONS:  B = spriggan hd:3
KMONS:  C = oklob sapling hp:7
KMONS:  D = oklob sapling hp:15 name:wizard n_suf \
            spells:spit_acid.50.natural;magic_dart.36.wizard;cantrip.18.wizard
KMONS:  E = scorpion
KMONS:  F = wandering mushroom
KMONS:  G = scrub nettle
KMONS:  H = killer bee
KMONS:  I = queen bee
KMONS:  L = oklob plant
KMONS:  J = spriggan druid
KMONS:  M = oklob plant hp:30 name:conjurer n_suf \
            spells:spit_acid.67.natural;stone_arrow.30.wizard;\
                   force_lance.30.wizard
KMONS:  N = ballistomycete spore
# set the monster list for shadow creatures to draw on. This is based on the
# old rod of swarms (these oklobs used to directly cast summon swarm).
: dgn.set_random_mon_list("killer bee w:4 / scorpion w:4 / river rat w:2 / hornet / vampire mosquito / butterfly")
KMONS:  O = oklob plant hp:30 name:summoner n_suf \
            spells:spit_acid.67.natural;call_imp.30.wizard;\
                   shadow_creatures.30.wizard
KMONS:  P = oklob plant hp:30 name:shifter n_suf \
            spells:spit_acid.67.natural;blink_other.60.wizard
KMONS:  Q = demonic crawler
KMONS:  R = spriggan rider
KMONS:  T = spriggan
KMONS:  U = spriggan berserker
KMONS:  V = Agnes
KMONS:  W = spriggan defender
KMONS:  X = spriggan rider ; storm_bow
KMONS:  Y = bush
KMONS:  Z = alderking
KMONS:  a = hornet
KMONS:  b = ghost moth
KMONS:  d = oklob plant name:meteorologist n_suf \
            spells:spit_acid.67.natural;lightning_bolt.30.wizard;\
                   airstrike.30.wizard
KMONS:  e = oklob plant name:demonologist n_suf \
            spells:spit_acid.67.natural;summon_demon.40.wizard;\
                   malign_gateway.20.wizard
KMONS:  f = oklob plant name:annihilator n_suf \
            spells:spit_acid.67.natural;poison_arrow.20.wizard;\
                   orb_of_destruction.20.wizard;bolt_of_cold.20.wizard
KMONS:  g = the Enchantress
KMONS:  h = moth of wrath
KMONS:  m = river rat
KMONS:  n = scorpion
KMONS:  u = rat
KITEM:  M = any book
KITEM:  # = Orb of Zot
KFEAT:  Z = any shop
: invisibility_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xcccccccccccccccccccccccccccccccccccccccccccccccccccccx
xc{..Ac....uc.$$$$coooooooooooooc.*+$BBc.E..ccZcc|ccccx
xcAA..+.%...+.c%%.+ooooooooooooo+.Dc$E$c.F.G+*|cc+ccccx
xcAAAAc...m.cCcn..coooooooooooooc..ccc+cc.**ccZc$$ccccx
xccccccccccccccc+ccoooooooooooooc.*ccZZ$+..Fccccc..R.cx
xcHH.t........+...coooooooooooooccccccccccccccccc...Zcx
xc.I....t.....c$C$ccccccccccccccccccc..L.%*ccc....Tt.cx
xcHH...t..t..ccccccttttt..t*ccc.+.*+.....*%+Q+..Tt..Zcx
xcc+cccc+ccccccC|+......t..tccM.c.Nc.A....Pcccccccccccx
xc*.c$$$$+......Fct..ttt.tJ.ccc.cc+cc.A.O..cccccccccccx
xcL*cG|cccG.G.L..c.tt.*t.$$t+..$ct.tccccccccccccccccccx
xcccccccccccccccccccccccccccccccc.....ccccccccccccccccx
xc......................ccccccc..t..t....cccccccccccccx
xcZa..N...............Y.ccccc.U.t......t...tccccccccccx
xcZQ..........T......XY..........t.V...Z.Z..W|ccccccccx
xcZb..................Y...ccccccccccccccccccccccccccccx
xccccccccccc+cccccccccccccccccccccccccccccccccccccccccx
xccccccP.........ccccc..ccccc..AZZccc...e|ccccccccccccx
xcccccc.......A.dccc.R..+......O.%cc...Y.U+nccccccccccx
xcccccccc.A....cccccaQa.cccccY....+.....f|ccccccccccccx
xcccccccccc...cccccc$$$$ccccccccccccccccccccccccccccccx
xcccccccccc....cccc$$$$$cccccZZZcL.L.L.L.L.L.cccccccccx
xcccccccccc........$$$$$+$$$$Z$Z+............+.......cx
xcccccccccc........$$$$$cccccZZZc.L.L.L.L.L.Lccccccc.cx
xccccccccccccccccccccccccccccccccccccccccccccccccccc.cx
xcc........t.Wt..t..t.Rt..t.Ut..t..t..t..t.Wt..t..t..cx
xcc.g..........................J.......J.............cx
xcc........t.Jt..t..t.Rt..t.Ut..t..t..t..t.Wt..t..t..cx
xccc++++ccccccccccccccccccccccccccccccccccccccccccccccx
xcc......cccccccccccccc.....ccccc....cccccc...h..cccccx
xcc......ccccccccc.......h........b................Wtcx
xccc.......e...............I...............ccccccc..#cx
xccc.Q..........a........R........U................Wtcx
xccc..............cccccc.....cccc....cccccc...h..cccccx
xccc.......f......ccccccccccccccccccccccccccccccccccccx
xcccccccccccccccccccccccccccccccccccccccccccccccccccccx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   sprint_temple
TAGS:   fedhas_sprint_temple no_rotate no_hmirror no_vmirror
# No Fedhas or Ashenzari.
KFEAT:  b = altar_beogh
KFEAT:  c = altar_cheibriados
KFEAT:  d = altar_dithmenos
KFEAT:  e = altar_elyvilon
KFEAT:  g = altar_gozag
KFEAT:  h = altar_hepliaklqana
KFEAT:  j = altar_jiyva
KFEAT:  k = altar_kikubaaqudgha
KFEAT:  l = altar_lugonu
KFEAT:  m = altar_makhleb
KFEAT:  n = altar_nemelex_xobeh
KFEAT:  o = altar_okawaru
KFEAT:  q = altar_qazlal
KFEAT:  r = altar_ru
KFEAT:  s = altar_sif_muna
KFEAT:  t = altar_trog
KFEAT:  u = altar_uskayaw
KFEAT:  v = altar_vehumet
KFEAT:  w = altar_wu_jian
KFEAT:  x = altar_xom
KFEAT:  y = altar_yredelemnul
KFEAT:  z = altar_zin
KFEAT:  1 = altar_the_shining_one
KMONS:  P = oklob sapling name:priest n_suf \
            spells:spit_acid.50.natural;cantrip.36.priest;smiting.18.priest
MAP
.T.b.c.d.e.T.
..g.h.j.k.l..
...m.nPo.q...
..r.s.t.u.v..
.w.x.y.z.1.T.
ENDMAP

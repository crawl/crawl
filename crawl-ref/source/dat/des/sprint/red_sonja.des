NAME:   dungeon_sprint_1
TAGS:   sprint no_item_gen no_trap_gen no_pool_fixup no_rotate no_hmirror no_vmirror
DESC:   Sprint I: "Red Sonja"
ORDER:  1
ORIENT: encompass
MARKER: L = lua:fog_machine { \
             pow_min = 2, pow_max = 4, delay = 5, \
             size = 4, walk_dist = 8, spread_rate= 33, \
             cloud_type= "purple smoke" }
MARKER: S = lua:fog_machine { \
             pow_min = 2, pow_max = 5, delay = 10, \
             size = 3, walk_dist = 5, spread_rate= 5, \
             cloud_type= "noxious fumes" }
MARKER: e = lua:fog_machine { \
             pow_min = 3, pow_max = 4, delay = 20, \
             size = 3, walk_dist = 14, spread_rate= 2, \
             cloud_type= "freezing vapour" }
MARKER: j = lua:fog_machine { \
             pow_min = 2, pow_max = 3, delay = 2, \
             size = 10, walk_dist = 5, spread_rate= 4, \
             cloud_type= "steam" }
MARKER: H = lua:fog_machine { \
             pow_min = 2, pow_max = 3, delay = 4, \
             size = 10, walk_dist = 6, spread_rate= 4, \
             cloud_type= "foul pestilence" }
MONS:   Ijyb, ogre, Sigmund, centaur warrior, Sonja, deep troll, hornet
SUBST:  r = ~.....
KFEAT:  ~ = net trap
KITEM:  I = potion of curing
KITEM:  $ = gold q:27
KMONS:  D = kobold demonologist
KMONS:  E = glass eye
KMONS:  F = oklob plant
KMONS:  I = ballistomycete spore
KMONS:  J = unseen horror
KMONS:  P = jelly
KMONS:  Q = wight
KMONS:  R = demonic crawler
KMONS:  S = gargoyle
KMONS:  b = meliai
KMONS:  d = sin beast
KMONS:  f = lindwurm
KMONS:  g = hog
KMONS:  h = daeva
KMONS:  i = 12-headed hydra
KMONS:  k = golden dragon
KMONS:  l = shadow dragon
KMONS:  n = hellwing
KMONS:  o = deep elf annihilator
KMONS:  p = merfolk siren
KMONS:  q = mummy priest
KMONS:  s = Tiamat
KMONS:  t = fire giant
KMONS:  G = Lom Lobon
KMONS:  u = redback
KMONS:  v = shining eye
KMONS:  z = the Lernaean hydra
KFEAT:  A = armour shop
KMONS:  a = deep elf master archer
KFEAT:  m = iron_grate
KFEAT:  Y = any shop
KITEM:  Q = any good_item
KITEM:  Z = Orb of Zot
SUBVAULT: B : red_sonja_temple
SUBVAULT: C : red_sonja_temple_small
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccxxx
xxxc{.x.1+..x.3+$$xCCx$$+..Dx**xL$$f+gggggg+i$c..+$$$$c|||$$$$$$cxxx
xxxc..+..x.2+..x$$+CC+$$x.E.xx+x$xxxxggLggcc$E+..x$$$$cc||$$$$$$cxxx
xxxc..BBBBBBBBBBBBBCCccccD..+RRx$x*xxxgggccccccc+x$$j$$ccccc$$$$cxxx
xxxc..BBBBBBBBBBBBBccc**ccccxRR+$x+x$xxx+c|**cc..x$$$$$c$$$c$$$$cxxx
xxxc..BBBBBBBBBBBBBWWW.4c$$cc+cccc..$mbc.cc$$$c..xx$$$$c*Y*c$$$$cxxx
xxxc..BBBBBBBBBBBBBWWWL.+.7c.....c..$mmc..mmm$c..wxxx+xc$$$c$$$$cxxx
xxxcTTBBBBBBBBBBBBBWWW.4c$$c.....+..$$$cc.mhm$c..wn|x*xxxx+c$$$$cxxx
xxxcxxBBBBBBBBBBBBBx+xxxcc+ccc.S.c....dc..mmm$c.pwoxx+xL...cc$$$cxxx
xxxc$$BBBBBBBBBBBBB****xxxF+$c...ccc++cc.cc..$c..wxx........cc$$cxxx
xxxcccBBBBBBBBBBBBBx+xxx*xxx$c..ccc..ccc..c+ccc..wx..........c$$cxxx
xxxcY........+$.$+$$6$$+*Axx.c..cccc$$ccc.JJcFw..xx..........cc$cxxx
xxxccc++++cccx$5$xxxxxxxxxxx$cc+c$$ce$c$cccJcqwp.c.....l|.....cecxxx
xxxc..E....cccc.ccc.......xxucc*c$$$$$$$$$cJccc77c.....|k.....c$cxxx
xxxcv......vc$c+c$cJJ......+$cccc$$$$$$$$$cJc.+77cc..........cc$cxxx
xxxc..c..E..c$I$I$c+++xxxxxxuc**+$$$$$$cccc+crxx+cc$$$$$$$$$$c$$cxxx
xxxcv......vc$***$cPPPP...ExLccccc$$$ccc....crxWWWcc$$$$$$$$cc$$cxxx
xxxc..E..c..cccccccPPP....x+.c***ccccc...ccccrxWWWWcc******cc$$$cxxx
xxxcv......vcxQQQQ.......Wxccc*t...t...cccrrrrxWWWWWcccccccc..$$cxxx
xxxc..c..E..cxQQQ.......WWxc***....cccccrrrrccxxWWWWcWWWWWc.S..$cxxx
xxxcv......vcxx.......WWWxxc**..t.ccrrrrrrrrc$$x+WWW+WWzWW+....$cxxx
xxxccc++++ccccxxx..WWWWxxxcc*.t..ccrrrrrrrrrc$$$xxWWcWWWWWcWWW..cxxx
xxxcY......Ycccc+xxxxxx+cccc$$$$$crrrrrrHrrrc$$$$cccccc+cccccWWWcxxx
xxxc........ccYYccccccccYYccmmmmmcccccrrGrrrc$$$$cWWwwwwwwwwccWWcxxx
xxxcY......YcccYYYYYYYYYYcccwwwwwwwwscrrrrrrcccxxxxWWWWwwwwwwwwWcxxx
xxxccccccccccxccccccccccccxcmmmmmcccccrrrrrr+||x||+$$aWWWWWWWcwwcxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxc..Z..+rrrrrrrrrrc||+||x$$$$$$$$WWWcwcxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxccccccccccccccccccccccccccccccccccccccxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: red_sonja_temple_1
TAGS: red_sonja_temple no_item_gen no_trap_gen no_pool_fixup no_rotate no_hmirror no_vmirror
# No Ashenzari or Beogh.
MONS: jelly, yak
KITEM: $ = gold q:27
KFEAT: C = altar_cheibriados
KFEAT: D = altar_dithmenos
KFEAT: E = altar_elyvilon
KFEAT: F = altar_fedhas
KFEAT: G = altar_gozag
KFEAT: H = altar_hepliaklqana
KFEAT: K = altar_kikubaaqudgha
KFEAT: N = altar_nemelex_xobeh
KFEAT: O = altar_okawaru
KFEAT: Q = altar_qazlal
KFEAT: R = altar_ru
KFEAT: S = altar_sif_muna
KFEAT: T = altar_trog
KFEAT: U = altar_uskayaw
KFEAT: V = altar_vehumet
KFEAT: W = altar_wu_jian
KFEAT: Y = altar_yredelemnul
KFEAT: Z = altar_zin
KFEAT: ' = altar_the_shining_one
KFEAT: s = any shop
MAP
xxxxx+xxxxx+x
xxxx$$c$$$c*c
xEZxxxc$$cc*c
x'OTFQcc+cW.+
xK.s..2+...Sc
xY..UHccV1x+x
cDRCGNccx+x*x
ccccccccx.xxx
ENDMAP

NAME: red_sonja_temple_small_1
TAGS: red_sonja_temple_small no_item_gen no_trap_gen no_pool_fixup no_rotate no_hmirror no_vmirror
MONS: slime creature
KFEAT: J = altar_jiyva
KFEAT: L = altar_lugonu
KFEAT: M = altar_makhleb
KFEAT: X = altar_xom
MAP
JL
.1
MX
ENDMAP

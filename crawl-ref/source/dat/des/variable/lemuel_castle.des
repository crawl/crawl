###############################################################################
# Lemuel's Castle - Sub-vault version
#
# Originally a castle by Lemuel in large.des. Now randomised by carwin using
# subvaults, and using its own des file due to size.
#
# A castle with a randomized interior. Contains depth-appropriate inhabitants
# and loot. Based on Lemuel's Castle vault. Interior is built as a subvault
# with the tag "lemuel_castle_interior." Feel free to make more!
# To make sure the subvault orients correctly with the bridge, the two
# characters on the bottom left and bottom right of each subvault should be
# left empty (spaces).

{{
-- configuration for Lemuel Castle vault and its subvaults
function castle_lemuel_subvault_setup(e)
         e.tags("no_monster_gen");
         e.tags("no_rotate");
         e.colour("? = brown")
         e.ftile("? = floor_mud")
         e.kfeat("? = floor")
         e.kfeat("~ = alarm trap w:1 / floor")
         e.subst("X = c +:1")
         e.subst("8 = 89")
         e.tile("c = wall_stone_smooth")
         e.ftile(".~*XG|+$ = floor_cobble_blood")
         e.ftile("12348 = floor_cobble_blood")
         e.ftile("= = floor_cobble_blood")
         e.kfeat(", = floor")
         e.kfeat("m = iron_grate")
         if you.absdepth() < 7 then
            e.subst("* = ...$")
            e.subst("| = *.")
            e.mons("gnoll")
            e.mons("gnoll / gnoll shaman w:5")
            e.mons("ogre / gnoll sergeant")
            e.mons("jackal")
            e.mons("gnoll")
            e.mons("jackal")
         elseif you.absdepth () < 15 then
            e.subst("* : *$")
            e.subst("| : %*")
            e.mons("orc");
            e.mons("orc knight w:2 / orc priest w:3 / orc warrior")
            e.mons("orc sorcerer / orc warlord / orc knight")
            e.mons("warg")
            e.mons("orc")
            e.mons("warg")
         elseif you.absdepth () < 24 then
            e.subst("* : *$")
            e.subst("| = |*")
            e.mons("deep elf mage")
            e.mons("deep elf knight w:5 / deep elf archer w:5 / "
                   .. "deep elf mage w:30")
            e.mons("deep elf annihilator / deep elf sorcerer / "
                   .. "deep elf high priest / deep elf death mage / "
                   .. "deep elf demonologist")
            e.mons("nothing")
            e.mons("deep elf mage")
            e.mons("nothing")
         elseif you.absdepth () < 27 then
            e.subst("* = *$")
            e.mons("two-headed ogre / cyclops / deep troll / ogre mage w:5")
            e.mons("ettin / fire giant / frost giant / stone giant / \
                    deep troll shaman w:5")
            e.mons("titan")
            e.mons("nothing")
            e.mons("cyclops")
            e.mons("yak / death yak w:5")
            e.mons("plant / w:1 bush")
         else
            e.subst("* = *$")
            e.subst("w : wl")
            e.nsubst("1 = 14:1 / *:.")
            e.mons("red draconian / white draconian / green draconian / \
                    black draconian / purple draconian / nothing w:30")
            e.mons("yellow draconian monk / purple draconian annihilator / \
                    green draconian monk / golden dragon w:5")
            e.mons("red draconian scorcher / white draconian knight / \
                    purple draconian stormcaller / black draconian shifter")
            e.mons("lindwurm / wind drake w:5 / death drake w:5 / \
                    swamp drake / nothing w:5")
            e.mons("red draconian / green draconian / white draconian / \
                    black draconian / purple draconian")
            e.mons("lindwurm / wind drake w:5 / death drake w:5 / \
                    swamp drake / nothing w:5")
            e.mons("plant / w:1 bush")
        end
        e.mons("plant")
        if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
            e.kfeat("3 = enter_zot")
            -- Ugly hack to get around kfeat overwriting the mons definition.
            e.kmons("3 = red draconian scorcher / white draconian knight / "
                    .. "purple draconian stormcaller / black draconian shifter")
        end
end
}}


###############################################################################
# Lemuel Castle Interior Subvaults
###############################################################################

############################################################
# Lemuel Castle Interior original
NAME:   lemuel_castle_interior_original
TAGS:   lemuel_castle_interior
: castle_lemuel_subvault_setup(_G)
MAP
+c....+~......+..+......~+.***c+
.c||..ccccccccc++ccccccccc.***c.
1c||2.c1...1..1321..1...1c.***c1
.cccccc..1...211112...1..cccccc.
.+....+....1...22...1....+....+.
.+....+..1...1....1...1..+....+.
.cccccccccccccc++cccccccccccXcc.
1c1.1..ccccccc....ccccccc..m..c1
.c.2...+.....+....+.....+..m.8c.
+c1.1..ccccccc....ccccccc..m..c+
  cccccccccccc++++cccccccccccc
ENDMAP

############################################################
# Lemuel Castle Interior : Carwin Interior #1
NAME:   lemuel_castle_interior_carwin_1
TAGS:   lemuel_castle_interior
: castle_lemuel_subvault_setup(_G)
MAP
+c....+........cc........+...*c+
.c.11.ccc+cccccccccccc+ccc..2.c.
.c|.2.X*m.1..........1.m|c....c.
.ccm+mc|m.....1111.....m|cm+mcc.
......c|m..1...22......m*c..1...
......c.+....1132.2....+.X..1...
.cccccccccccccccccccccccccccccc.
.c........1..m....m..1........c.
.+.12........+....+..........2+.
+c........1..m....m..1........c+
  cccccccccccc++++cccccccccccc
ENDMAP

############################################################
# Lemuel Castle Interior : Carwin Interior #2
NAME:   lemuel_castle_interior_carwin_2
TAGS:   lemuel_castle_interior
: castle_lemuel_subvault_setup(_G)
SUBST:  $ = $:5 .
ITEM:   nothing w:20 / potion of ambrosia ident:type
MAP
+c....+.......1.............$*c+
...1..cccccccccccccccccccccccXc.
cc....c......cG..Gc.1.......c|c1
*cc++cc..cG..+....+...12....m|c.
|....~12.cG..+....+...23....+.c.
$....~.2.....c....c...12....m*c.
*cc++ccccccccc....ccccccc++ccXc+
cc~~~~c.m444dc....c2..$c.~~.....
...12.c.cm4mmc....c$cc.c12......
+c....+...2..c....c...........c+
  cccccccccccc++++cccccccccccc
ENDMAP

############################################################
# Lemuel Castle Interior : Carwin Interior #3
NAME:   lemuel_castle_interior_carwin_3
TAGS:   lemuel_castle_interior
: castle_lemuel_subvault_setup(_G)
SUBST:  $ = $:5 .
MAP
+c....+...*c$*|cc|*$cccccc$...c+
.c.1..ccccccmc1321cmcccccc....c.
.c..2........c.12.c...........c.
.cc++c.........ww.....2..1c++cc.
.....c.2.......ww.........c.....
.2...c...1...c..$.c...1...c..1..
.ccccc+cccccccccXcccccccc+ccccc.
.c.1.c.~..1..+....+2....~.c2..c.
.~.c$c.~2....c....c.....~.c$c1~.
+c..2c.~..1..c....c..1..~.c...c+
  cccccccccccc++++cccccccccccc
ENDMAP

############################################################################
NAME:     lemuel_castle_with_subvaults
TAGS:     layout_city
DEPTH:    D:3-, Depths
PLACE:    Depths:$
ORIENT:   northwest
: castle_lemuel_subvault_setup(_G)
SUBVAULT: A : lemuel_castle_interior
SUBST:    A = .
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x,,,,,,7,,,,,,,,,,,,,,,,,6,6,,,,,,7,,,,,,,,,,,,,,,,x
x,,,,,,,,,,,,,,7,,,,,,,,6,6,6,,,7,,,,,,,,,,,,,7,,,,x
x,,,,wwwwwwwwwwwwwwwwwwwwWwwwwwwwwwwwwwwwwwwwww,,,,x
x,,,,wwwwwwwwwwwwwwwwwwwwWwwwwwwwwwwwwwwwwwwwww,,,,x
x,,,,wwccnccwwwwwwwwwwwwwWwwwwwwwwwwwwwwccnccww,,,,x
x,,,,wwc1.1c4....4.......4......4....4..c1.1cww,,,,x
x,,,,wwc.1.cXccccccccccccccccccccccccccXc.1.cww7,,,x
x,,,,wwcccAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAcccww,,,,x
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www,,,,x
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www,,7,x
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www,,,,x
x,,,,WWW.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.WWW,,,,x
x,7,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www,,,,x
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www,,,,x
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www,,,,x
x,,,,www.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.www,,,,x
x,,,,wwcccAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAcccww7,,,x
x,,,7wwc.1.cAAAAAAAAAAAAAAAAAAAAAAAAAAAAc.1.cww,,,,x
x,,,,wwc1.1c............................c1.1cww,,,,x
x,,,,wwccnccwwwwwwwwwwww????wwwwwwwwwwwwccnccww,,,,x
x,,,,wwwwwwwwwwwwwwwwwww????wwwwwwwwwwwwwwwwwww,,,,x
x7,,,wwwwwwwwwwwwwwwwwww????wwwwwwwwwwwwwwwwwww,,,,x
x,,,,,,,,,,,,,,,,,,656,,,@,,,,656,,,,,,,,,,,,7,,,,,x
ENDMAP

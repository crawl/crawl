################################################################################
# The Grid: some number of 9x9 rooms, one of which contains a large prize and
# some big monsters, and the remainder of which have easier monsters and smaller
# prizes (if they have either one at all).
#
# Additional vaults welcome. To guarantee that islands are not produced,
# subvaults must meet one condition. Let '?' represent squares in the subvault:
#      @
#  ?????????
#  ?????????
#  ?????????
#  ?????????
# @?????????@
#  ?????????
#  ?????????
#  ?????????
#  ?????????
#      @
# All four @ symbols must be reachable from each other through the subvault.
#
# HACK: the_grid subvaults use " " instead of "x" for their walls. This is so
# the main vault can use SUBST: to make all walls in all subvaults the same
# type (we don't want one crystal room+two stone rooms+one metal room; we want
# four crystal rooms, or four stone rooms, or four metal rooms.)
#
# A few themed subvaults have their own crystal/metal/stone/whatever walls, but
# those are a small minority of the total wall squares in the vault.
#
# Walls:
# 45%: stone
# 20%: metal
# 15%: crystal
# 10%: translucent stone
# 6%:  amorphous mixture of two opaque materials (stone, metal, crystal)
# 2%:  amorphous mixture of one opaque material and translucent stone
# 2%:  amorphous mixture of stone, metal, crystal, translucent stone, plus the
#      occasional statue or missing wall. Xom thinks this is hilarious, etc.
#      (The missing wall option is omitted for some main vaults where it would
#      create islands)
#
# Rock walls are never included, this is because a digging monster could ruin
# the vault.
#
# Some of the main vaults have a 50% chance of "spiral mode" where the subvaults
# must be done in sequence (unless walls are destroyed) with the prize subvault
# at the end. In the rest of the cases, all subvaults are open on all 4 sides.
# There is a 33% chance of the subvaults having doors between them, otherwise
# they have floor.
################################################################################

{{
function the_grid_main_setup(e)
  e.tags('uniq_thegrid')
  e.subst('" : x+')
  e.subst('+ : +..')
  e.subst('ABCDEFGHI = x')
  e.subst('x : c:45 v:20 b:15 n Z:8 Q:2')
  e.subst('Z : X:4 Y:3 Z:2 U:1 V:1 W:1')
  e.subst('X = cv')
  e.subst('Y = cb')
  e.subst('Z = vb')
  e.subst('U = cn')
  e.subst('V = vn')
  e.subst('W = bn')
  e.subst('Q = ccvbn G:1 `:1')
  e.kprop('` = no_tele_into')
end
}}

default-depth: D:7-, Depths, Elf

NAME:     minmay_the_grid_2x2
SUBVAULT: A : the_grid
SUBVAULT: B : the_grid
SUBVAULT: C : the_grid
SUBVAULT: D : the_grid_prize
: the_grid_main_setup(_G)
MAP
.......................
.xxxxx"xxxxxxxxx"xxxxx.
.xAAAAAAAAAxBBBBBBBBBx.
.xAAAAAAAAAxBBBBBBBBBx.
.xAAAAAAAAAxBBBBBBBBBx.
.xAAAAAAAAAxBBBBBBBBBx.
.+AAAAAAAAA+BBBBBBBBB".
.xAAAAAAAAAxBBBBBBBBBx.
.xAAAAAAAAAxBBBBBBBBBx.
.xAAAAAAAAAxBBBBBBBBBx.
.xAAAAAAAAAxBBBBBBBBBx.
.xxxxx"xxxxxxxxx+xxxxx.
.xDDDDDDDDDxCCCCCCCCCx.
.xDDDDDDDDDxCCCCCCCCCx.
.xDDDDDDDDDxCCCCCCCCCx.
.xDDDDDDDDDxCCCCCCCCCx.
."DDDDDDDDD+CCCCCCCCC".
.xDDDDDDDDDxCCCCCCCCCx.
.xDDDDDDDDDxCCCCCCCCCx.
.xDDDDDDDDDxCCCCCCCCCx.
.xDDDDDDDDDxCCCCCCCCCx.
.xxxxx"xxxxxxxxx"xxxxx.
.......................
ENDMAP

NAME:     minmay_the_grid_plus
WEIGHT:   5
SUBVAULT: A : the_grid
SUBVAULT: B : the_grid
SUBVAULT: C : the_grid
SUBVAULT: D : the_grid
SUBVAULT: E : the_grid_prize
: the_grid_main_setup(_G)
MAP
          .............
          .xxxxx+xxxxx.
          .xBBBBBBBBBx.
          .xBBBBBBBBBx.
          .xBBBBBBBBBx.
          .xBBBBBBBBBx.
          .+BBBBBBBBB+.
          .xBBBBBBBBBx.
          .xBBBBBBBBBx.
          .xBBBBBBBBBx.
...........xBBBBBBBBBx...........
.xxxxx+xxxxxxxxx+xxxxxxxxx+xxxxx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.+DDDDDDDDD+EEEEEEEEE+AAAAAAAAA+.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xxxxx+xxxxxxxxx+xxxxxxxxx+xxxxx.
...........xCCCCCCCCCx...........
          .xCCCCCCCCCx.
          .xCCCCCCCCCx.
          .xCCCCCCCCCx.
          .+CCCCCCCCC+.
          .xCCCCCCCCCx.
          .xCCCCCCCCCx.
          .xCCCCCCCCCx.
          .xCCCCCCCCCx.
          .xxxxx+xxxxx.
          .............
ENDMAP

NAME:     minmay_the_grid_plus_closed
WEIGHT:   5
SUBVAULT: A : the_grid
SUBVAULT: B : the_grid
SUBVAULT: C : the_grid
SUBVAULT: D : the_grid
SUBVAULT: E : the_grid_prize
: the_grid_main_setup(_G)
MAP
          .............
      .....xxxxx+xxxxx.....
    ...xxxxxBBBBBBBBBxxxxx...
   ..xxxxxxxBBBBBBBBBxxxxxxx..
  ..xxxxxxxxBBBBBBBBBxxxxxxxx..
  .xxxxxxxxxBBBBBBBBBxxxxxxxxx.
 ..xxxxx....BBBBBBBBB....xxxxx..
 .xxxxx..xxxBBBBBBBBBxxx..xxxxx.
 .xxxx..xxxxBBBBBBBBBxxxx..xxxx.
 .xxxx.xxxxxBBBBBBBBBxxxxx.xxxx.
..xxxx.xxxxxBBBBBBBBBxxxxx.xxxx..
.xxxxx.xxxxxxxxx+xxxxxxxxx.xxxxx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.+DDDDDDDDD+EEEEEEEEE+AAAAAAAAA+.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xDDDDDDDDDxEEEEEEEEExAAAAAAAAAx.
.xxxxx.xxxxxxxxx+xxxxxxxxx.xxxxx.
..xxxx.xxxxxCCCCCCCCCxxxxx.xxxx..
 .xxxx.xxxxxCCCCCCCCCxxxxx.xxxx.
 .xxxx..xxxxCCCCCCCCCxxxx..xxxx.
 .xxxxx..xxxCCCCCCCCCxxx..xxxxx.
 ..xxxxx....CCCCCCCCC....xxxxx..
  .xxxxxxxxxCCCCCCCCCxxxxxxxxx.
  ..xxxxxxxxCCCCCCCCCxxxxxxxx..
   ..xxxxxxxCCCCCCCCCxxxxxxx..
    ...xxxxxCCCCCCCCCxxxxx...
      .....xxxxx+xxxxx.....
          .............
ENDMAP

NAME:     minmay_the_grid_3x2
SUBVAULT: A : the_grid
SUBVAULT: B : the_grid
SUBVAULT: C : the_grid
SUBVAULT: D : the_grid
SUBVAULT: F : the_grid
SUBVAULT: E : the_grid_prize
: the_grid_main_setup(_G)
MAP
.................................
.xxxxx"xxxxxxxxx"xxxxxxxxx"xxxxx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
."AAAAAAAAA+BBBBBBBBB+CCCCCCCCC".
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xxxxx+xxxxxxxxx"xxxxxxxxx+xxxxx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
."DDDDDDDDD+EEEEEEEEE"FFFFFFFFF".
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xxxxx"xxxxxxxxx"xxxxxxxxx+xxxxx.
.................................
ENDMAP

NAME:     minmay_the_grid_net
SUBVAULT: A : the_grid
SUBVAULT: B : the_grid
SUBVAULT: C : the_grid
SUBVAULT: D : the_grid
SUBVAULT: F : the_grid
SUBVAULT: E : the_grid_prize
: the_grid_main_setup(_G)
MAP
                    .............
                    .xxxxx+xxxxx.
                    .xBBBBBBBBBx.
                    .xBBBBBBBBBx.
                    .xBBBBBBBBBx.
                    .xBBBBBBBBBx.
                    .+BBBBBBBBB+.
                    .xBBBBBBBBBx.
                    .xBBBBBBBBBx.
                    .xBBBBBBBBBx.
.....................xBBBBBBBBBx...........
.xxxxx+xxxxxxxxx+xxxxxxxxx+xxxxxxxxx+xxxxx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxCCCCCCCCCx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxCCCCCCCCCx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxCCCCCCCCCx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxCCCCCCCCCx.
.+DDDDDDDDD+EEEEEEEEE+FFFFFFFFF+CCCCCCCCC+.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxCCCCCCCCCx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxCCCCCCCCCx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxCCCCCCCCCx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxCCCCCCCCCx.
.xxxxx+xxxxxxxxx+xxxxxxxxx+xxxxxxxxx+xxxxx.
...........xAAAAAAAAAx.....................
          .xAAAAAAAAAx.
          .xAAAAAAAAAx.
          .xAAAAAAAAAx.
          .+AAAAAAAAA+.
          .xAAAAAAAAAx.
          .xAAAAAAAAAx.
          .xAAAAAAAAAx.
          .xAAAAAAAAAx.
          .xxxxx+xxxxx.
          .............
ENDMAP

NAME:     minmay_the_grid_4x2
SUBVAULT: A : the_grid
SUBVAULT: B : the_grid
SUBVAULT: C : the_grid
SUBVAULT: D : the_grid
SUBVAULT: F : the_grid
SUBVAULT: G : the_grid
SUBVAULT: H : the_grid
SUBVAULT: E : the_grid_prize
: the_grid_main_setup(_G)
MAP
...........................................
.xxxxx"xxxxxxxxx"xxxxxxxxx"xxxxxxxxx"xxxxx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCxHHHHHHHHHx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCxHHHHHHHHHx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCxHHHHHHHHHx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCxHHHHHHHHHx.
."AAAAAAAAA+BBBBBBBBB+CCCCCCCCC+HHHHHHHHH".
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCxHHHHHHHHHx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCxHHHHHHHHHx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCxHHHHHHHHHx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCxHHHHHHHHHx.
.xxxxx+xxxxxxxxx"xxxxxxxxx"xxxxxxxxx+xxxxx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxGGGGGGGGGx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxGGGGGGGGGx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxGGGGGGGGGx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxGGGGGGGGGx.
."DDDDDDDDD+EEEEEEEEE"FFFFFFFFF+GGGGGGGGG".
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxGGGGGGGGGx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxGGGGGGGGGx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxGGGGGGGGGx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFxGGGGGGGGGx.
.xxxxx"xxxxxxxxx"xxxxxxxxx+xxxxxxxxx"xxxxx.
...........................................
ENDMAP

NAME:     minmay_the_grid_3x3
SUBVAULT: A : the_grid
SUBVAULT: B : the_grid
SUBVAULT: C : the_grid
SUBVAULT: D : the_grid
SUBVAULT: F : the_grid
SUBVAULT: G : the_grid
SUBVAULT: H : the_grid
SUBVAULT: I : the_grid
SUBVAULT: E : the_grid_prize
: the_grid_main_setup(_G)
MAP
.................................
.xxxxx"xxxxxxxxx"xxxxxxxxx"xxxxx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.+AAAAAAAAA+BBBBBBBBB+CCCCCCCCC".
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xAAAAAAAAAxBBBBBBBBBxCCCCCCCCCx.
.xxxxx"xxxxxxxxx"xxxxxxxxx+xxxxx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
."DDDDDDDDD+EEEEEEEEE"FFFFFFFFF".
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xDDDDDDDDDxEEEEEEEEExFFFFFFFFFx.
.xxxxx+xxxxxxxxx"xxxxxxxxx+xxxxx.
.xGGGGGGGGGxHHHHHHHHHxIIIIIIIIIx.
.xGGGGGGGGGxHHHHHHHHHxIIIIIIIIIx.
.xGGGGGGGGGxHHHHHHHHHxIIIIIIIIIx.
.xGGGGGGGGGxHHHHHHHHHxIIIIIIIIIx.
."GGGGGGGGG+HHHHHHHHH+IIIIIIIII".
.xGGGGGGGGGxHHHHHHHHHxIIIIIIIIIx.
.xGGGGGGGGGxHHHHHHHHHxIIIIIIIIIx.
.xGGGGGGGGGxHHHHHHHHHxIIIIIIIIIx.
.xGGGGGGGGGxHHHHHHHHHxIIIIIIIIIx.
.xxxxx"xxxxxxxxx"xxxxxxxxx"xxxxx.
.................................
ENDMAP

################################################################################
# Themed rooms
#
NAME:    minmay_the_grid_dragon_a
TAGS:    the_grid unrand
MONS:    steam dragon / acid dragon
MONS:    fire dragon / ice dragon / swamp dragon w:5
MONS:    shadow dragon / iron dragon / golden dragon / storm dragon / \
         quicksilver dragon / pearl dragon
: if you.absdepth() < 10 then
SUBST:   $ = $$$$$%
: elseif you.absdepth() < 16 then
SUBST:   1 = 2, $ = $$$$$*
: else
SUBST:   1 = 3, $ = $$$$$|
: end
MAP
    ....
 $$   ...
 $$$$  ..
  $$$.  .
. $$1.$ .
.  $..$ .
..  $....
...   ...
 .......
ENDMAP

NAME:    minmay_the_grid_dragon_b
TAGS:    the_grid unrand
MONS:    steam dragon / acid dragon
MONS:    fire dragon / ice dragon / swamp dragon w:5
MONS:    shadow dragon / iron dragon / golden dragon / storm dragon / \
         quicksilver dragon / pearl dragon
: if you.absdepth() < 10 then
SUBST:   $ = $$$$$%
: elseif you.absdepth() < 16 then
SUBST:   1 = 2, $ = $$$$$*
: else
SUBST:   1 = 3, $ = $$$$$|
: end
MAP
 .......
..     ..
.  $$$  .
. $$$$$ .
. $$1.$ .
. $..$$ .
.  $..  .
..  .  ..
 .......
ENDMAP

NAME:   minmay_the_grid_tri_star
TAGS:   the_grid unrand
MONS:   wizard / ogre mage / orc sorcerer w:5 / deep elf mage w:5
MONS:   deep elf mage / orc sorcerer / kobold demonologist w:5
MONS:   deep elf sorcerer / deep elf annihilator / lich / ancient lich w:5
ITEM:   any wand / any jewellery / any book / any magical staff
ITEM:   any randart jewellery / any misc / any weapon good_item w:5 / \
        any armour good_item w:5
: if you.absdepth() < 8 then
NSUBST: d = 1:d / *:.
: elseif you.absdepth() < 14 then
SUBST:  1 = 2
NSUBST: d = 1:e / *:.
: else
SUBST:  1 = 3, d = e
: end
MAP
   .....
  ....b..
 ....bb..
...1db...
...bbd1..
.bbbdb...
....1bbb.
 ......b.
  .......
ENDMAP

NAME:  minmay_the_grid_armoury
TAGS:  the_grid unrand
MONS:  orc knight band, orc warlord band
MONS:  centaur warrior band / yaktaur captain band
ITEM:  any weapon / any armour / any missile
: if you.absdepth() < 10 then
SUBST: 0 = 1
: elseif you.absdepth() < 13 then
SUBST: 0 = 2
: else
SUBST: 0 = 3
: end
MAP
.........
.vvvvvvv.
.vd...dv.
.vv...vv.
.vd.0.dv.
.vv...vv.
.vd...dv.
.vvv+vvv.
.........
ENDMAP

NAME:   minmay_the_grid_airy
TAGS:   the_grid unrand
DEPTH:  D:13-, Depths, Elf
MONS:   spriggan air mage
NSUBST: 1 = 2:1 / 3 = 11... / *:.
MAP
111*b11bb
11bbbbb1b
bb1111bb1
b1bbbb1b1
1bb*b1bb1
b1bb1bb1b
b1bbbbb1b
1b11b11bb
*bbb1bb1*
ENDMAP

NAME:   minmay_the_grid_artifice
TAGS:   the_grid unrand
DEPTH:  D:13-, Depths, Elf
ITEM:   any misc
MONS:   crystal guardian
NSUBST: 1 = 2:1 / 4 = 1. / *:.
MAP
 .......
..bbnbb..
.bn111nb.
.b11111b.
.n11d11..
.b11111b.
.bn111nb.
..bbnbb..
 .......
ENDMAP

################################################################################
# Generic rooms
NAME:    minmay_the_grid_empty
TAGS:    the_grid unrand
MAP
.........
.........
.........
.........
.........
.........
.........
.........
.........
ENDMAP

NAME:    minmay_the_grid_spin_star
TAGS:    the_grid unrand
SUBST:   b : .:140 xcvbnGTUV09%* t:5 Y:5
SUBST:   9 = 9..
MAP
 .... ..
. .   . .
.. ... ..
  .999. .
. .9b9. .
. .999.
.. ... ..
. .   . .
 .. ....
ENDMAP

NAME:    minmay_the_grid_expand
TAGS:    the_grid unrand
SUBST:   9 = 9..
MAP
 .....999
.  ....99
. .  ...9
.. ..   .
.. . ...
... .   .
9.. . ..
99. . . .
999. . .*
ENDMAP

NAME:    minmay_the_grid_all_tris
TAGS:    the_grid unrand
SUBST:   9 = 9.
MAP
9.  .. .9
 . . .  .
  ..  ..
 .  .. .
..   9  .
   ..  ..
 .  . .
9. . .. .
  ..   9.
ENDMAP

NAME:    minmay_the_grid_outer_diamond
TAGS:    the_grid unrand
MAP
   ...
  ..0..
 ... ...
...   ...
.0     0.
...   ...
 ... ...
  ..0..
   ...
ENDMAP

NAME:    minmay_the_grid_outer_circle
TAGS:    the_grid unrand
MAP
  .....
 ...0...
...   ...
..     ..
.0     0.
..     ..
...   ...
 ...0...
  .....
ENDMAP

NAME:    minmay_the_grid_diamond_grid
TAGS:    the_grid unrand
MAP
 . ... .
... . ...
 . . . .
. .0.0. .
.. . . ..
. .0.0. .
 . . . .
... . ...
 . ... .
ENDMAP

NAME:    minmay_the_grid_spikes
TAGS:    the_grid unrand
SUBST:   9 = 9.
MAP
. . . ...
... . ..
..9......
  ...9
... 9....
... .....
 .. ...
... . ...
. . . . .
ENDMAP

NAME:    minmay_the_grid_fan
TAGS:    the_grid unrand
MAP
.  ......
. ...
. ..  ..
.  . ....
..  8  ..
.... .  .
 ..  .. .
    ... .
......  .
ENDMAP

NAME:    minmay_the_grid_box_x
TAGS:    the_grid unrand
SUBST:   9 = 9.
MAP
.........
.      9.
. *   9 .
.  . 9  .
.   9   .
.  9 .  .
. 9   . .
.9      .
.........
ENDMAP

NAME:    minmay_the_grid_stretched_grid
TAGS:    the_grid unrand
SUBST:   9 = 9.
MAP
 ..  ..
.  ..  ..
 ..  ..
.  ..  ..
 .9  9.
.  .9  ..
 ..  ..
.  ..  ..
 ..  ..
ENDMAP

NAME:    minmay_the_grid_eight
TAGS:    the_grid unrand
MAP
 .....
.. ...
.   ...
.. ......
....8....
...... ..
  ...   .
   ... ..
   .....
ENDMAP

NAME:    minmay_the_grid_aliased_spiral
TAGS:    the_grid unrand
SUBST:   9 = 9.
MAP
........
9     ...
  ..9  ..
 ...99  .
...   9 .
... * . .
 ..  .  .
 ...   ..
  .......
ENDMAP

NAME:    minmay_the_grid_tree
TAGS:    the_grid unrand
SUBST:   9 = 9.
MAP
 ...9...
 .......
 .......
  +   +
.... ....
 ... ...
 + + + +
 . . . .
9.......9
ENDMAP

NAME:    minmay_the_grid_zigzag_pyramid
TAGS:    the_grid unrand
SUBST:   9 = 9.
MAP
.........
.       .
.....9 ..
..     ..
... 9....
...   ...
...9. ...
 ...  ..
 ... ...
ENDMAP

NAME:    minmay_the_grid_gridbox
TAGS:    the_grid unrand
SUBST:   b : .:140 xcvbnGTUV09%* t:5 Y:5
SUBST:   0 = 0.
MAP
.........
. . . . .
.. . . ..
. .000. .
.. 0b0 ..
. .000. .
.. . . ..
. . . . .
.........
ENDMAP

NAME:    minmay_the_grid_slots
TAGS:    the_grid unrand
SUBST:   9 = 9:4 .:11 *:1
MAP
.....9 9.
.   .   .
.9 9.9 9.
.   .   .
.9 9.9 9.
.   .   .
.9 9.9 9.
.   .   .
.9 9.....
ENDMAP

NAME:    minmay_the_grid_odd_star
TAGS:    the_grid unrand
SUBST:   9 = 9.
MAP
... . ...
. ..... .
..  9  ..
 . 9 9 .
..9 * 9..
 . 9 9 .
..  9  ..
. ..... .
... . ...
ENDMAP

NAME:    minmay_the_grid_large_cup
TAGS:    the_grid unrand
SUBST:   b : .:140 xcvbnGTUV09%* t:5 Y:5
NSUBST:  0 = 2 = 09 / 4 = 09.. / *:.
MAP
  .......
 ...    .
..   000.
.  00000.
. *00000.
.  00000.
..   000.
 ...    .
  .......
ENDMAP

NAME:    minmay_the_grid_medium_cups
TAGS:    the_grid unrand
SUBST:   b : .:140 xcvbnGTUV09%* t:5 Y:5
SUBST:   0 = 009..
MAP
0.......0
.   .   .
. %0. % .
.   0 0 .
...0b0...
. 0 0   .
. % .0% .
.   .   .
0.......0
ENDMAP

NAME:    minmay_the_grid_small_cups
TAGS:    the_grid unrand
SUBST:   b : .:140 xcvbnGTUV09%* t:5 Y:5
SUBST:   0 = 009.
MAP
   .....
....   .
.  . % .
. %000...
.  0b0  .
...000% .
 . % .  .
 .   ....
 .....
ENDMAP

NAME:    minmay_the_grid_tiny_cups
TAGS:    the_grid unrand
SUBST:   0 = 0.
SUBST:   b : .:140 xcvbnGTUV09%* t:5 Y:5
MAP
0 0 . 0 0
 .......
0.   .b.0
 . * ...
..  9  ..
 ... * .
0.b.   .0
 .......
0 0 . 0 0
ENDMAP

NAME:    minmay_the_grid_broken_lines
TAGS:    the_grid unrand
SUBST:   0 = 0.
MAP
.........
. 00    .
.........
     00
.........
  00
.........
.    00 .
.........
ENDMAP

NAME:    minmay_the_grid_garden
TAGS:    the_grid unrand
SUBST:   ' = ".
SUBST:   " = tt110
MONS:    plant
NSUBST:  0 = 1:0 / 9 = 0.. / *:.
MAP
.'''.''''
'.'''.'.'
''.'.'.''
'.'."'.''
.''"T"'..
'''.".'''
''.'.'.''
'.''.''.'
''''.''''
ENDMAP

NAME:    minmay_the_grid_doors
TAGS:    the_grid unrand
MAP
.........
. +   + .
. ..9.. .
.   +   .
.+.9*9.+.
.   +   .
. ..9.. .
. +   + .
.........
ENDMAP

NAME:    minmay_the_grid_pillar_ring
TAGS:    the_grid unrand
SUBST:   b : .:140 xcvbnGTUV09%* t:5 Y:5
SUBST:   0 = 0.
MAP
.........
.... ....
.. ... ..
...000...
. .0b0. .
...000...
.. ... ..
.... ....
.........
ENDMAP

NAME:    minmay_the_grid_plusring
TAGS:    the_grid unrand
MAP
  .....
 .......
... . ...
..  0  ..
...0 0...
..  0  ..
... . ...
 .......
  .....
ENDMAP

NAME:    minmay_the_grid_holed_spiral
TAGS:    the_grid unrand
MAP
.........
.     . .
....... .
. .   . .
. ..9 . .
. . . . .
. . .....
. . .   .
. .......
ENDMAP

NAME:    minmay_the_grid_paths
TAGS:    the_grid unrand
MAP
... .....
. .     .
. ... ...
.   . .
. ..9.. .
  . .   .
... ... .
.     . .
..... ...
ENDMAP


NAME:    minmay_the_grid_mess
TAGS:    the_grid unrand
MAP
. . 0. ..
 ..  .. .
......  .
. . .  .
0 .   ..0
. ..... .
...   ...
 .  ... .
... 0 . .
ENDMAP

NAME:    minmay_the_grid_minigrid_small
TAGS:    the_grid unrand
SUBST:   0 = 09..
MAP
.........
. . . . .
...0.0...
. 0 0 0 .
...0*0...
. 0 0 0 .
...0.0...
. . . . .
.........
ENDMAP

NAME:    minmay_the_grid_minigrid_small_alt
TAGS:    the_grid unrand
SUBST:   0 = 0..
MAP
 . . . .
.........
 . 0 0 .
..0.0.0..
 . 0 0 .
..0.0.0..
 . 0 0 .
.........
 . . . .
ENDMAP

NAME:    minmay_the_grid_minigrid_medium
TAGS:    the_grid unrand
SUBST:   0 = 00.
MAP
 ..  ..
.  ..  ..
.  0.  ..
 ..  0.
 .0  0.
.  00  ..
.  ..  ..
 ..  ..
 ..  ..
ENDMAP

NAME:    minmay_the_grid_minigrid_large
TAGS:    the_grid unrand
SUBST:   0 = 000.
MAP
.........
.   0   .
.   .   .
.   .   .
.0..0..0.
.   .   .
.   .   .
.   0   .
.........
ENDMAP

NAME:    minmay_the_grid_x
TAGS:    the_grid unrand
MAP
 .......
. ..... .
.. ... ..
... 0 ...
...0 0...
... 0 ...
.. ... ..
. ..... .
 .......
ENDMAP

NAME:    minmay_the_grid_zigzags
TAGS:    the_grid unrand
SUBST:   b : .:140 xcvbnGTUV09%* t:5 Y:5
MAP
    ..
  0  ..
 ...  .0
.. ....
.  .b.  .
  .... ..
 0.  ...
  ..  0
   ..
ENDMAP

NAME:    minmay_the_grid_rooms
TAGS:    the_grid unrand
SUBST:   0 = 000.
SUBST:   b : .:140 xcvbnGTUV09%* t:5 Y:5
MAP
    .
 00 . 00
 00 ..00
  . .
....b....
    . .
 00.. 00
 00 . 00
    .
ENDMAP

NAME:    minmay_the_grid_embedded
TAGS:    the_grid unrand
MAP
    .
 9. ...9
 .... ..
 .    .
....* ...
  .    .
 .. ....
 9... .9
    .
ENDMAP

NAME:    minmay_the_grid_square_x
TAGS:    the_grid unrand
MAP
0... ...0
.  ...  .
.  .9.  .
...   ...
 .9 *.9.
...   ...
.  .9.  .
.  ...  .
0... ...0
ENDMAP

################################################################################
# Prize subvaults.

NAME:    minmay_the_grid_prize_trominoes
TAGS:    the_grid_prize unrand
SUBST:   T : .TG
: if you.absdepth() < 16 then
SUBST:   8 = 8.
: end
MAP
  | . |
 8  .  8
| .8... |
  . ..8
..  T  ..
  8.. .
| ...8. |
 8  .  8
  | . |
ENDMAP

NAME:  minmay_the_grid_prize_four_corners
TAGS:  the_grid_prize unrand
SUBST: Z = .:60 x:40 8Gcvbmt T:3 U:3 V:3 Y:1
SUBST: A : +. A:2, B : +. A:2
SUBST: A = +.W
MAP
**9 . 9**
**. . .**
9.B . B.9
   A8A
...8Z8...
   A8A
9.B . B.9
**. . .**
**9 . 9**
ENDMAP

NAME:    minmay_the_grid_prize_four_corners_diamond_a
TAGS:    the_grid_prize unrand
WEIGHT:  5
KITEM:   | = |, |
SUBST:   8 = 88.
MAP
|8  .  8|
8. ... .8
  .. ..
 ..   ..
..     ..
 ..   ..
  .. ..
8. ... .8
|8  .  8|
ENDMAP

NAME:    minmay_the_grid_prize_four_corners_diamond_b
TAGS:    the_grid_prize unrand
WEIGHT:  5
KITEM:   | = |, |
SUBST:   8 = 88.
MAP
|8  .  8|
8. . . .8
  . . .
 . . . .
. . . . .
 . . . .
  . . .
8. . . .8
|8  .  8|
ENDMAP

NAME:    minmay_the_grid_prize_shrinking_boxes
TAGS:    the_grid_prize unrand
KITEM:   | = |,|,|,|,|,|,|,|
SUBST:   8 = 88.
MAP
.........
.       .
. 88.88 .
. . + . .
. . | . .
. .   . .
. ..... .
.   +   .
..88.88..
ENDMAP

NAME:    minmay_the_grid_prize_inner_circle
TAGS:    the_grid_prize unrand
SUBST:   b : xcvbnTG|8
NSUBST:  8 = 2 = 8. / *:8
MAP
 .......
..     ..
.  ***  .
. ***** .
. **b*8 .
. ****8 .
.  *888 .
..     88
 ......8
ENDMAP

NAME:    minmay_the_grid_prize_big_room
TAGS:    the_grid_prize unrand
NSUBST:  8 = 4:8 / 8 = 8. / *:.
SUBST:   b : xcvbnTG|8
MAP
*** . ...
***  .. .
**.888...
  88888 .
..88b88..
  88888 .
**.888...
***  .. .
*** . ...
ENDMAP

NAME:    minmay_the_grid_prize_diamond_cup
TAGS:    the_grid_prize unrand
SUBST:   b : xcvbnTG|8
SUBST:   8 = 8..
KMONS:   | = 8
KITEM:   | = |
MAP
   . .
  .   .
 .  8  .
.  |||  .
 .8|b|8
.  |||  .
 .  8  .
  .   .
   . .
ENDMAP

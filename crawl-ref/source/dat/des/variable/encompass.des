###############################################################################
# encompass.des: Full level (encompassing) vaults for the main dungeon go here.
#                For encompass vaults that go in branches (eg branch endings)
#                use the des file for that branch.
###############################################################################

# Setups for the below.
{{
    function grunt_profane_halls_setup(e)
        if you.absdepth() >= 24 then
            e.kmons('1 = base draconian')
            e.kmons('2 = nonbase draconian')
            e.kmons('3 = draconian zealot')
        else
            e.kmons('1 = deep elf fighter / deep elf conjurer / '
                    .. 'deep elf summoner / deep elf knight w:5 / '
                    .. 'deep elf annihilator w:5 / '
                    .. 'deep elf demonologist w:5')
            e.kmons('2 = deep elf knight')
            e.kmons('3 = deep elf high priest')
        end
        e.subst('% = * %:20 .')
        e.lua_marker(':', fog_machine { cloud_type = "flame",
                                        pow_min = 10, pow_max = 10, delay = 10,
                                        size = 1, spread_rate = 0,
                                        walk_dist = 0, excl_rad = 0 })
    end
}}

default-depth: D:12-26

###############################################################################
# The River Lethe
#
# Should be turned into a timed, non-announced portal vault
# using the maprot (or unmappable) flag.
#
# The two orange statues are placed for maximum pain -- one you're likely to
# first see while flying, the other is hard to target with disintegration
# through its protective plants.
#
# Lemuel
#
NAME:    lemuel_river_lethe
ORIENT:  encompass
TAGS:    no_rotate
DEPTH:   D:16-26
SUBST:   . = .:100 !:1 6:4
SUBST:   ; = .:200 !:1 1:3
SUBST:   1 = 1:20 .
SUBST:   3 = 3:20 .
KFEAT:   ! = ^
MONS:    plant
MONS:    wolf / warg / hell hound
MONS:    nothing
MONS:    wight w:50 / wraith / shadow wraith / phantasmal warrior /\
         skeletal warrior / flayed ghost
MONS:    dancing weapon
MONS:    human simulacrum w:60 / elf simulacrum / ogre simulacrum /\
         centaur simulacrum / troll simulacrum / vampire / wraith / hungry ghost
MONS:    eye of draining / brain worm / death drake w:5 / shadow /\
         shadow dragon w:1
KITEM:   A = potion of experience / any good_item
KMONS:   A = Maud
KFEAT:   A = >
KFEAT:   B = |
KMONS:   B = neqoxec
KFEAT:   C = b
KMONS:   C = lich w:20/shadow fiend
SUBST:   b = b|
SUBST:   | = |*
ITEM:    any scroll, potion of experience
SHUFFLE: dD/eE/fF/gG
SUBST:   D=+, d=|, E=x, e=%%*, F=x, f=%, G=x, g=%
SHUFFLE: PQ
SUBST:   Q=xw
KMONS:   P = orange crystal statue
KFEAT:   P = floor
KMONS:   k = giant eyeball
KITEM:   k = potion of experience / any good_item
NSUBST:  r = 1:+ / *:x
NSUBST:  R = 1:+ / *:x
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccccccccxxxxxx
xxAB.r$xxxwwwxxxxcccccccccccccccccccccccccccxxxxxxxxcc..1....|kcxxxxxx
xwBB.r$$Rwwwwxxxxc.....+...+...........c***c;;xxxxx;cc.....1.||cxxxxxx
xw...r$$Rxwwwxxxxc..C..ccccc...........c***c;;;xxx;;cc.1.1...1.cxxxxxx
xww..r$$Rxwwwwxxxc.....cxxxc...........c***c;;;;xx;;;+.....1..1cxxxxxx
xww..xxxRwwww;xxxc..}..cxxxc...........c...c;;;;xx;;cc..1.111..cxxxxxx
xwww.xxxwww;;;;xxccccccccxxcc+ccc+ccccccc+cc;;;;xx;;cc.11111...cxxxxxx
xwwwxxxwwww;;;;;xxxxxxxxxxxc...c...c.......c;;;;x;;;cc..111H..1cxxxxxx
xwwwxxxwwwww;;7;;xxxxxxxxxxc...c...c.w.....c;;;;;;;;cc.111111..cxxxxxx
xwwwxxxwwwww;;;;;;;xxxxxxxxc...+...c.w.....+;;;;;;;;cc..111..1.cxxxxxx
xwwwxxxwwww;;7;;;;;;;;xxx;;c...c...c.w.....c;;;;;;;;ccccccccccccxxxxxx
xwwwxxxwww;;;;;;;;;;;;;;;;;cc+ccc+cc..w....c;;;;;;;;;;xxxxxxxxxxxxxxxx
xwwwxxxwww;;7;;;;;;;;;;;;;;+.......+..w....cccc;;;;;;xxxxxxxxxxxxxxxxx
xwwwwxx;ww;;;;;;;;;;;;;;;;;cccc+cccc...w...c$$c;;;;;;;;xxxxxxxxxxxxxxx
xwwwwxx;w;;7;;;;cccc;;;;;;;c.......c...w...c$$c;;;;;;;;;;;xxxxxxxxxxwx
xwwwwxx;;;;;;;;cccccc;;;;;;c.......c+ccwwccc+cc;;;;;;;;;;;;;xxxxxxxxwx
xwwwwxx;;;7;;;cc*4.+;;;;;;;c.......c.c..ww....c;;;;;7;;;;;;;;xxxxxxwwx
xxwwwwx;;;;;;;;cccccc;;;;;;c6.6.6.6c.c...ww...c;;;;;;;;;;;;;;;xxxxxwwx
xxxwwwxx;;;;;;;;cccc;;;;;;;ccccccccc.c...ww...c;;;;;;;;;;;;;;;xxxxxwwx
xxxwwwxx;;;;;;;;;;;;;;;;;;;c$$+....+.c....www.c;;;;;;;ccccccccxxxxxwwx
xxxwwxx;;cccc;;;;;;;;;;;;;;c$$cccccc.c.....wwwc;;;;;;;c.....$$Dddxxwwx
xxxwwwx;cccccc;;;;;;xx;;;;;c$$c....c+cccccccwww;;;;;;;c.....$$xdd+wwwx
xxxwwwxcc*4.+;;;;;;xxxx;;;;cccc....c.....c...www;;ccccc.......xxxxxwwx
xxxwwwxxcccccc;;;;xxxxx;;;;;;;c....c.....+...www;;c...c.......xeexxwwx
xxxwwwxx;cccc;;;;xxxxxx;;;;;;;c....c.....c...www;cc.66c.......Eee+wwwx
xxxwwwxx;;;;;;;;xxx5..c;;;;;;;cccccc.....cccwww;;;+.66+.......xxxxxwwx
xxxwwwxx;;;;;;;;;xx5..c;;;;;;;;;;;;c.....c;wwww;;cc.66c.......Fffxxwwx
xxxwwwxx;;;;;;;;;;x5..+;;;;;;;;;;;;cccccccwwww;;;;c...c.......xff+wwwx
xxxwwwxx;cccc;;;;xxx..c;;;;;;;;;;;;;;;;;wwwwww;;;;ccccc.......xxxxxwwx
xxxwwwxxcccccc;;;;;xx.c;;;;;;;;;;;;;;;;wwwwwww;;;;;;;;c.....$$xggxxwwx
xxxwwwxcc*4.+;;;;;;xxxx;;;;;;;;;;;;;;;wwwwwwwww;;;;;;;c.....$$Ggg+wwwx
xxxwwwxxcccccc;;;;xxxwxx;;;;;;x;;;;;wwwwwwwwwww;;;;;;;ccccccccxxxxxwwx
xxxwwwxxxcccc;;x;;;xwwxxx;;;;;xx7wwwwwwwwwwwwww;;;;;;;;;;;;;;;xwwwwwwx
xxxwwwxxxxwww;;;;;;wwwwxw;;;;xxxwwwwwwwwwwwxxwww;;;;7;;;;;wwwwwwwwwwwx
xxxwwwxxxxxwwwwwwwxwwwwwwwwwxxxwwwwwwwwwwwwxxxwwwwww;;wwwwwwwwwwwwwwxx
xxxxwwwwxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxwwwwwwwwwwwwwwwwwwwwwxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxwwwwwwwxxxxxwwwwwwwwwwwwwwwwwwwxxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxwwwwwwwwwwwwwwwwwwxxxxxxx
xxxxxwwwwwwwwwwwwwwwwQxwwwwwwwxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxx
xxxxxxwwwwwwwwwwwwwxxxxwwwwwwwwwwwwwxxxPwwwwwwwwwwxxwwwwwwwwxxxxxxxxxx
xxxxxxxwwwwwxxxxxxxxxxwwwwwwwxxwwwwxxxxxwwwwwwwwwxxxwwxwwwwxxxxxxxxxxx
xxxxxxxx;7;;xxx222xxxxxxwwwwxxwww2222xxxxx22wwwwwwxxxxxxxxxxxxxxxxxxxx
xxxxxxxx;;;xxx;;;;;xx2222wwwwww;;;;;;;;xxxx22;;;;wwwwxxxxxxxxxxxxxxxxx
xxxxxxxxx;xxxx;;;;;;;;;;;;ww;;;;;;;;;;;;;;;;;;;;;;;;wwwxxxxxxxxxxxxxxx
xxxxxxxxxxxx;;;;2;;;;;;;;;;;;;;;;;);;;;;;2;;;;;;;;;;wwwxxxxxxxxxxxxxxx
xxxxxxxxxxxx;;;;;;;;;;;;;;;;;;;;;[](;;;;;;;;;;;;;;;;2wxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx+xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx{xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
# Sea of Fire
#
# By minmay.
#
# All arrivals from above (and one from below) bubbles just lead to downward
# escape hatches.  The other two staircases from below lead to bubbles with
# some loot.  For the main fight, you have to fly or teleport.
# The opposition is nasty, but this map is deep enough.
NAME:    minmay_sea_of_fire
TAGS:    no_rotate
ORIENT:  encompass
DEPTH:   D:16-24
MONS:    hell hound / nothing
MONS:    lindwurm
MONS:    fire drake / steam dragon w:1
MONS:    fire giant / dragon / mottled dragon w:5 / efreet w:2
MONS:    smoke demon
MONS:    hell knight / nothing w:5
KMONS:   7 = molten gargoyle
KFEAT:   7 = l
KMONS:   8 = iron golem
KMONS:   9 = Xtahua, hellephant
KMONS:   $ = 0 / nothing w:90
KITEM:   $ = $
NSUBST:  = = 1:= / *:v
SUBST:   = = +v
SUBST:   ' : l.
: _G.set_random_mon_list("fire bat w:30 / fire drake / dragon / hell hound / " ..
:     "efreet / fire crab / fire giant w:3 / mottled dragon / " ..
:     "fire elemental / crimson imp / fire vortex / molten gargoyle")
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxvvvvvvvvvvvvvvvvvvvvxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxllllxxxxxxxxxlllllxxxll.3..v$$$$$$$$v......0..vxxxxxx.....xxxxxxx
xxxxxxxxxxxxxllllllllllxxxxllllllllllll...=$$$$$$$$+....0....vxxxx....0....xxxxx
xxxxxxxxxxxlllllxxllllllllllllllllllllllllvv=$$$$$$v..vvvvv.0vxll0..>.......xxxx
xxxxxxxxxlllllllllllllllllllllllllllllllllllv$$$$$$v4.v...v..vllll.....x..0..xxx
xxxxxxxllxxxxxllllllllllllllllllllllllllllllvvv$$$$v..v.].v..vllllll.0.......xxx
xxxxxxlxxx..1xxx..lllxx.0..llxxlllllllllllllllv$$$$v......v..vlllllllll.......xx
xxxxxlxx..1.....x...........xx..llllllllllllllvvvvvvvvvvvvv++vllllllllxx.0.x..xx
xxxxlxx..xxxx1..1.x........0......lllllllllllllxxxxxllxxxll.3llllllllllxxxxx..xx
xxxxlx.1xx{.x.........x............llllllllllllllxxllllllll..llllllllllllxxxx.xx
xxxxlx..xxx.xl1..1..............2>.lllllllllllllllllllll.......lllllllllllxxx..x
xxxllxx..1.xxl.........0.....0.....lllllxllllllllllll...x..........lllllllxx...x
xxxlllxx..xxlll1...x.............xlllllxxllllllllllx........x...xll.llllllx[...x
xxlllllxxxxlllllll........0....llxxllxxxllllllllll.....x...xx6.6xlll.lllllxxx..x
xllllllllllllllllllll....x.llll.llxxxxxxllllllllllx...xx6.6xxx..xxll.llllllxxxxx
xllllllllllllllllllllllllllllll.llllxxlllllllllllxx6.6xx..xxxxxxxxlll.lllllllxxx
xlllllllllllllllllllllllllllllll.llllllllllllllllxxxx.xxxxxxxxxxxxllll.lllllllxx
xxllllllllllllllllllllllllllllll.lllllllllllllllxxxxxxxxxxxxxxxxxllllll..lllllxx
xxxllllllllllxxllllllllllllllllxx.llllllllllllllxxxxxxxxxxxxxxxxllllllx..ll...xx
xxxlllllllllxxxxllllllllllllllxxxx...llllllllllllxxxxxxxxxxxxxllllllll..3..lll<x
xxlllllllll..>xxlllllllvvvvvvvvvvxxx3.xxxllllllllllxxxxxxlllllllllllllll..lllllx
xlllllllll....xxllllllxv..0....*vxxxxxxvvvvvvvvlllllllllllllllllllllllllllllllxx
xlllllllll..3..xllllllxv..0...4*vxxxxxxv.4v...vlllllllvvv7vvv7vvv7vvv7vvv7vvvlxx
xxlllllllll....llllllxxv..vvvvvvvvvvvvvv..v.}.vxllllllvvvvvvvvvvvvvvvvvvvvvvvllx
xxllllllllll..lllllllxxv...........0......v...vxllllllvv.4***||v...v9.**|||vvllx
xxllllllllllllllllllllxv....0.0.....0.....v...vxxlllll7v4.***||v.>.v...***|v7llx
xxxllllllllllllllllllllv.vv.....vvvvvvvv......vxxlllllvv..vvvvvv...vv....*vvvllx
xxxllllllllllllllllllllv..v.0.0.vlxxxxxv......vxllllllvv4......vv+vv.......vvlxx
xxxllllllllxxllllllllllv4<v.....vllllxxvvvvvvvvlllllllvv...4...v...v.......vvlxx
xxxlllllllxxxxxllllllllvvvv..vvvvlllllxxxlllllllllllll7vvvvv..vv...vvvv++vvv7lxx
xxllllllllxxxxllllllllllll..3.llllllllllllllllllllllllvv4.....v6...6v8....8vvlxx
xllllllllllxxllllllllllllll..llllllllllllllllllllxxlllvv......v.....v......vvllx
xllllllllllllllllllllllllllll.llllllllllllllllllxxxxllvv......v6...6v......vvllx
xxlllllllllllllllllllllllllll.lllllllllllllllllxxxxxll7v......v.....v8....8v7llx
xxllllllllllllllllllllllllllx.xllllll...lllllllxxxxlllvv.....4v6...6vvv++vvvvllx
xlllllllllllllllxxlllllllll...xxllll..0....xlllllxxlllvv..vvvvvv...vv......vvlxx
xllllllllllllllxxxxlllllll.xxx.xll......xx....llllllllvv.......v...v.......vvlxx
xllllllxxllllllxxvvvvllll.lxxxl...lll..xx...0..lllllll7v..............v..v.v7lxx
xxlllllxxxlllllllv00v.l..llllllllllll...0......lllllllvv.......v...v.......vvlxx
xxlllllllllllllllv00v..lllllllllllllll.........lllllllvv......vv...vv......vvlxx
xxxllllllllllllllvvv.vvvlllllllllllllll..x....llllllllvv1vvvv1v3...3v.v..v.vvlxx
xxx...lllllllllllv**v00vlllllllllllllll..0..xlllllllll7v1v**v1v.....v......v7lxx
xxx1....lllllllllv**v00vllllllllllllllll.....lllllllllvv1....1v.....v4....4vvlxx
xxxx.x.1.llllllllvvvvvvvllllllllllllllxx...0..lllllllxvvvvvvvvv.....vvvvvvvvvllx
xxxx1..xxxxxlllllllxxxxxllllllllllllllxxxx....llllllxxvvv7vvv7v+++++v7vvv7vvvllx
xxxx..xxxxxxxxllllllxxllllllllxxxllllllxxxx..llllllxxxxxlllllll8...8lllllllllllx
xxxx.3xxxxxxxxxxlllllllllllllxxxxxllllllxx.llllllllxxxxlllllllll8.8llllllllllllx
xxx...xxxxxxxxxxxllllllllllllxxxxxlllllll.lllllllllllxxlllllllllllllllllllllllxx
xxx..xxxxxxxxxxxxxlllllllllllllxxxlllllll.llllllllllllllllllllllllllllllllllllxx
xx...xxx>%%xxxxxxxllllllllllllllllllllll.lllllllllllllllllllllllllllllllllllllxx
xx...xxxx%%.xxxxxxlllllllllllllllllllllll.lllllllllllllllllxxlllllllxxxxxllllllx
x.1..xxxxxx.xxxxxllllllllxxllllllllllllll.lllllllllllllllllxxxxllllxxxxxxxxllllx
x.....xx...xxxxxlllllllllxxxlllllllllllll.llllll....lllllllxxxlllllxxx$$$xlxlllx
x.x.1...xxxxxxxxlllllllll..xllllllllllll.lllllll.0....lllllllllllllxx$$$$$xxllxx
x......xxxxxxxxlllllll...x......llllllll.llllll.x...0.llllllllllllxxx$>$$xxxllxx
x.1..x..xxxxxxlllllll.ll.....lll.lllll...xlllll...>...llllllllllllxx$$$$xxxlllxx
xx........llll''''ll'lll.v++vllll..l...3...ll...0...x.llllllllllllxxx$$xxxxlllxx
xx.1..........llll''lllvvv..vvvllll....x.....xx....0..xxlllllllllllxxxxxxxlllxxx
xxx........xllllllllllvv......vvllllxx...xxxxxxx..x..xxxxxllllllllllxxxllllllxxx
xxxx..x.1..xxlllllllllv.0......vlllllxxxxxxxxxxxx....xxxxlllllllllllllllllllxxxx
xxx.......1.xxlllllllvv...vv0..vvlllllxxxxllxxxxxx.xxxxllllllllllllllllllllxxxxx
xxx.x..3....xxlllllllv...v%%v..0vllllllllllllllxxxxxxxlllllllllllllllllllxxxxxxx
xx..x.......xlllllllxv0..v%%v...vllllllllllllllllxxxxlll..1.llll..1.llllxxx...xx
xx.xx...x..llllllllxxvv...vv...vvllllllllllllllllllllll1llll.1...x....lxxx).x..x
x.xxxx1...lllllllllxxxv..0.....vllllllllllll.xlllllll..llllllll.1....xxxxxxxxx1x
x.xxxxxx..lllllllllllxvv.....0vvlllllllllll....lll..1lllllllllll..xx1....xxx1..x
x.xxxxxxxllllllllllllllvvv..vvvllllllllllll..5....llllllllllllxxxxxxxx...1....xx
xx(xxxxxxxxllllllllllllllv++v...lllllllllxxxx<..llllllllllllxxxxxxxxxxxxx..1.xxx
xxxxxxxxxxxxxxxllllxxxlll........llllxxxxxxxxx...llllllllxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# The New Box Level (dp)
#
NAME:    box_level_dp
DEPTH:   D:20-26
ORIENT:  encompass
TAGS:    no_pool_fixup no_rotate
NSUBST:  A = 1:+ / *:b
NSUBST:  B = 1:+ / *:b
NSUBST:  C = 1:+ / *:b
NSUBST:  D = 1:+ / *:b
NSUBST:  E = 1:+ / *:x
KFEAT:   ^ = teleport trap / Zot trap
SUBST:   ':''l. , '=l.
SUBST:   ":""W. , "=Ww..
ITEM:    ring of teleportation
ITEM:    royal jelly / honeycomb w:20 / nothing w:30
ITEM:    scroll of teleportation
KFEAT:   T = teleport trap
KMONS:   w = electric eel / jellyfish / nothing w:120
KFEAT:   w = w
KMONS:   l = fire vortex / lava snake / lava fish / fire elemental w:1 /\
             nothing w:50
KFEAT:   l = l
KMONS:   z = killer bee
KITEM:   z = royal jelly / honeycomb w:20 / nothing w:120
: if crawl.one_chance_in(3) then
KMONS:   ! = orc / orc warrior / orc knight / nothing w:30
KMONS:   & = orc warlord / orc knight / nothing w:20
: elseif crawl.coinflip() then
KMONS:   ! = yaktaur / nothing
KMONS:   & = yaktaur captain / nothing
: else
KMONS:   ! = naga / nothing
KMONS:   & = naga warrior / greater naga
: end
SUBST:   * = * % % $ $ f
SUBST:   ; = 9 0
SUBST:   !=%$, &=*%
SUBST:   V = *
COLOUR:  a = yellow
TILE:    a = wall_wax
SUBST:   a = x
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.......xccccccxxxxxxx.........................................w"..xxx
x.(...{.xc....cbbbbbxx................xxx...................."w......x
x.......++.U..++.].++.....'''....'l'....xxx................."ww"..)..x
x...[...xc....cbbbbbxx...'''..''lllll'....xx...vvEvvEv......"ww"....xx
x.......xcccccccxxxxxx.......'llllllll.....x...vd...9v...."www".....xx
xxxxxxxxxxxxxxxxxxxxxx.......'lllllll'.....x...E.....E.."ww.w".....xxx
x.............................'lllll'.....xx...v9...Vv.www..w".....xxx
x....""w.............".xxx......'ll'......x....vvEEEEvwww.www.....xxxx
x.."wwwww"..........w....xxxx............xx....."wwwwwwww.ww"....xxxxx
x.wwwwwwww"........w........xxxxxxx....xxx........"www"...ww"...xxxxxx
x..wwwwwww"......"ww......""......xxxxxx....."...........ww"....xxxxxx
xx..""ww".......www....."wwwwwwwwww........."www"......wwwww"....xxxxx
xxx.........."wwww"..""wwwwwwwwwwwwwwwwww".www"...wwwwwwwwwwww.....xxx
xxxxx....""wwwwwwwwwwwwwwww.......wwwwwwwwwwwwwwwwwwwwwww.....xx....xx
xxx..""wwwwwwwwwwwwwwww"......G.G......"wwwwxxww".........xxxxx.....xx
x."wwwwwwwwwwwwwxxx__xxxxxxxxxx+xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx....x
xwwwwwwww".."wwwxww.."w........................................>xx...x
xxwwww""....."xwxw"...ww........................................xx...x
xxww".........xxxw..............................................xx..xx
xww............xx"w...ccccccccccccccccccncccccccccccccccccc.....xx..xx
xw"...xxx.......x..w..c^..*...*...*...*...*...*...*...*...c.....x...xx
xw....x!x..xxx..x..w..c...................................c.....x...xx
xxx...x+x..x!x..x.....c*..;...;...;...;...;...;...;...;..*c.....xx..xx
xxx....1..1+!x..x.....c...................................c.....xx...x
x..........xxx.xx.....c.......bbbbbbbbbb.bbbbbbbbbb.......c.....xx..xx
x..xxxx........xx.....c.......bTTTTTTTTTbTTTTTTTTTb.......c.....x...xx
x..x!!+1..xxxx..x.....c*..;...bTbbbAbbbTbTbbbBbbbTb...;..*c.....x..xxx
x..xxxx..1+!!x..x.....c.......bTb8...|bTbTb|...8bTb.......c.....xx.xxx
x.........xxxx..x.....c.......bTA|...|ATbTB|...|BTb.......c.....xx.xxx
x.......1.......x.....c.......bTb|...8bTbTb8...|bTb.......c.....x...xx
xxx....121....xxx.....c*..;...bTbbbAbbnTbTnbbBbbbTb...;..*c.....x...xx
x!x...cc+cc...xxx.....c.......bTTTTTTTTTbTTTTTTTTTb.......c.....xx..xx
x&+1..c!2!c...xxx.....n........bbbbbbbbbTbbbbbbbbb........n.....x..xxx
x!x...c&&&c...xxx.....c.......bTTTTTTTTTbTTTTTTTTTb.......c.....x..xxx
xxx...ccccc...xxx.....c*..;...bTbbbDbbnTbTnbbCbbbTb...;..*c.....x.xxxx
x...............x.....c.......bTb|...8bTbTb|...8bTb.......c.....x.xxxx
xx...1...xxxx...x.....c.......bTD|...|DTbTC|...|CTb.......c.....x..x.x
xx..x+x.1+!!x...x.....c.......bTb8...|bTbTb8...|bTb.......c.....xx...x
x...x!x..xxxx...x.....c*..;...bTbbbDbbbTbTbbbCbbbTb...;..*c.....xxx.xx
x...xxx.........x.....c.......bTTTTTTTTTbTTTTTTTTTb.......c.....xxx.xx
x.........xxxxxxx.....c.......bbbbbbbbbb.bbbbbbbbbb.......c.....xxx..x
xxxx......x***99+.....c...................................c.....xxxx.x
xxxx..xx..xxxxxxx.....c*..;...;...;...;...;...;...;...;..*c.....x....x
x.....xx........x.....c...................................c.....x.xx.x
x.xx.....xxx....x.....c...*...*...*...*...*...*...*...*..^c.....x.xx+x
x.xx....1+!x..xxx.....ccccccccccccccccccncccccccccccccccccc.....xxx..x
x....xx..xxx..xxx...............................................xx..xx
x....xx.......xxx.........................................z..z..x....x
xx............xxx<.............................................zxa...x
xx..x......xx...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxazzxx
xxx........xx.....xxx...xx.x..xx...xxaaaaaaaeaaea.a..aaaaaaaaaaaaazzxx
xxxx..xx..............x...x...xx.xx.xaaazazzaaza.a.aa.za.z.aaaa.a.aazx
xxxxxxxxxxx....x..xx.x.xx.x..x..xx}..xaeazaazzazzaa..aa..aa....z.aeaxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# A mockup of the Forest branch (1KB)
#
NAME:    spriggan_forest
DEPTH:   D:19-26
ORIENT:  encompass
TAGS:    no_pool_fixup
MONS:    spriggan, spriggan rider, spriggan druid, spriggan berserker
MONS:    spriggan air mage, spriggan defender, the Enchantress
SUBST:   . = .:300 / 1:2 / 2:1 / 3:1 / 4:1 / 5:1
SUBST:   T = t / t / P / .
KMONS:   P = plant / plant / bush / nothing
KMONS:   R = plant
KMONS:   Q = bush
KMONS:   d = spriggan spells:pain;bolt_of_draining;animate_dead name:necromancer n_suf col:magenta
KMONS:   e = bat skeleton name:skeletal_bat n_rpl n_spe n_des n_zom
KMONS:   K = giant firefly
KITEM:   f = dagger / short sword / sabre / flail / whip / blowgun / bow
KITEM:   g = robe / cloak / cap / buckler / steam dragon armour w:1
KMONS:   L = spriggan col:white name:baker n_suf ; robe . dagger
KITEM:   h = bread ration q:2
KMONS:   M = yak
KMONS:   N = spriggan name:shepherd n_suf ; robe . quarterstaff
KITEM:   i = cheese q:3
COLOUR:  "MN = green
COLOUR:  C = white
KFEAT:   C = stone_wall
TILE:    C = wall_marble
KFEAT:   _ = altar_trog
: _G.set_random_mon_list("place:Forest:3")
MAP
tttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttt
ttttPP.Tttt...tttttttttttttttttttttttttttttttttttttttttttttttttTTTTTttTTTTtttttt
tccccT.....ttt..ttttttttttttttttttttttttttttttTtTTtttttttQTtttTT.T.Ttt.T.TTTtttt
tc%4c...Tttttttt.ttttttttttttttttttttttttttttT*....QQPtt..ccccc....cccc.TT.Ttttt
tc..+..Ttttttttt..ttttttttttttttttttttttCCCCCCCCCCCCCC....c..1c....c.1c.t.TTtttt
tc.4c...Ttttttt.t..tttttttttttttttttttttC.6.C6..6C$*|CtT..c.1.+....+..cT.TT.tttt
tcccct.Tttttt..tttt........tttttttttttttC7..+....+$*|Ctt..ccccc....cccc.TT.Ttttt
tttT..tttttt.tttttttttttttt.ttttttttttttC.6.C6..6C$*|CttQ..................ttttt
ttTPTTtttt...ttttttttttttttt..ttttttttttCCCCCC++CCCCCCttttccccc....cccc......ttt
ttTTtttttt..ttttttttttttttttt.ttttttttttCgggC....CfffCttttchxLc....c1.c.....tttt
tttttttt...ttttttttttttttttttt.tttttttttCggg+..1.+fffCttttch..+....+.5c..1...ttt
tttttt.tttt..ttttttttttttttttt.tttttttttCgggC1...CfffCttttccccc....cccc...1.tttt
tttttt.ttttt.tttttttttttttt.....ttttttttCCCCCC++CCCCCCttttt...................tt
ttttt.ttttt..ttttttttttt...tttT..tttttttCR.....1....RCccccccccc....Y.........ttt
ttt..tttttt..ttttttttt..tttttttt.tttttttC..1........1CcKxKxKxKc......2......tttt
tt.ttttttt.tttttttt...ttttttttttt.ttttttCR1..5..5..1RCc.22.1..c.....22......tttt
tt.tttttt.ttttttt...1.tttttttttttt.tttttCCCCCC++CCCCCCc+ccccccc....2.2.....ttttt
ttt.tttt..tttttt...22..tttttttttttt.tttt..................................tttttt
tttt..[t.ttttttt..1..2.)tttttttttttt.....................................ttttttt
ttttt....ttttttt.t.1...tttttttttttt.tt..TTttttt............Y..........ttt.tttttt
ttttttt..tttttt.tttttttttttttttttt.tttttttttttttttttt...............tttttt...ttt
ttttttttt..ttt.tttttttttttttttt....ttttttttttttttttttttt.........tttttttttttt(tt
ttttttttttt...tttttttttttttttt.tt..ttttttttttttttttttttttttt....tttttttttttttttt
tttttttttttttt.tttttttttttttt.tttt.tttttttttttttttttttttttt.tt...ttttttttttttttt
tttttttttttttt.ttttttttttttt.WWWWt.ttttttttttttttttttttttt.ttttt...ttttttttttttt
ttttttttttttt.tttttttttttttt.Wwwwt.ttttttttttttttttttttt..ttttttt..ttttttttttttt
tttttttttttttt.ttttttttttttt.tWWWWt..tttttttttttttttt.t.ttttttttt..ttttttttttttt
tttttttttttttT.ttttttttttttt.ttWwwwwt.tttttttttttttttt.ttttttttttt..tttttttttttt
ttttttttttttt..ttttttttttttt.tttwwwwww.ttttttttttttttt.ttttttttttt..tttttttttttt
tttttttttttt...tttttttttttt.tttwwwwwwww.....tttttttttt.ttttttttttt.ttttttttttttt
ttttttttt.....ttttttttttttt.ttttwwwwwwwwwwwt.tttttttt.ttttttttttt.t.tttttttttttt
ttttttt..tt..tttttttttttttt.ttwwwwwwwwwwwwwwt.tttttt.ttttttttttt..tt....tttttttt
ttttT..tttttttttttttttttttt.ttwwwwwwwwwwwwwwt.......tttttttttttt.ttttttt...ttttt
tttt....tttttttttttttttttttt.wwwwwwwwwwwwwwww..tttttttttttttttt.ttttttttt.33tttt
ttt....T..3...tttttttttttttt.wwwwwwwwwwwwwwwt.ttttttttttttttttt.ttttttttt3..tttt
tttT...ttttttt....tttttttttt.wwwwwwwwwwwwwwt.ttttttttttttttttt.ttttttttttt..tttt
ttt..]..ttttttttt......ttttt.Wwwwwwwwwwwwtt.tttttttttttttttttt.ttttttttttt.ttttt
ttt........tttttttttttt.......WWwwwwwwwttt.tttttttttttttttt...tttttttttttttttttt
tttt..111.tttttttttttttttttttt...........ttttttttttttt.....ttttttttttttttt..tttt
ttttT.1...tttttttttttttttttttttttt.tttttttttttttttttt.ttt.ttttttttttttttttt.tttt
ttttTT.1....ttttttttttttttttttttt.tttttttttttttttttt.ttttt.ttttttttttttttt..tttt
ttttttt.tttt...ttttttttttttttttt..tttttttttttttttt..ttttttt..ttttttttttttt.ttttt
tttttt.tttttttt...ttttttttttttt.tttttttttttttttt..ttttttttttt....tttttttt.tttttt
ttttt..tttttttttt.ttttttttttttt.tttttttttttttttt.ttttttttttttttt.........ttttttt
ttttt.ttttttttttt.tttttttttttt.3.tttttttttttttt.ttttttttttttttt.ttt..t..tttttttt
tttt..tttttttttt..tttttttttttt3G3ttttttttttttt.tttttt.tttttttt.ttttttt....tttttt
ttttt.ttttttttt..ttttttttttttt.3.tttttttttttt.t.tttttt.....tt.tttttttttt...ttttt
tttt.tttttttttt.t..tttttttttttttttttttttttt..ttt.tttttttttt...ttttttttttt..ttttt
tttt.ttttttttt..ttt...ttttttttttttttttttttt.ttttt.ttttttttwwwtttttttttttt...tttt
ttttt.tttttttt.tttttt...tttttttttttttttt...tttttt.tttttttttwwttttttttttttt..tttt
ttttt.tttttttt{tttttttt......ttttttttt.....ttttt.ttttttttttttttttttttttttt.ttttt
tttt.tttttttttttttttttttTT..t...tttt..tttttttttt.tttttttttttttttttttttttt.tttttt
tttt.ttttttttttttttttttttt.ttttt....ttttttttttttt.ttttttttttttttttttttttt.cccctt
ttttt.ttttttttttttttttttt.tttttttttt.tttttttttttt..tttttttt..tt...ttttttt.+.1ctt
tttttt.ttttttttttttttttt.tttttttttt..tttttttttttttt.tttttt.t...ttt..tttttTc1%ctt
ttttttt.ttttttttttttttt.ttttttttttt....xxxxxtttttttt..tttt...tttttt.ttttttcccctt
ttttttt.tttttttttttttt.ttttttttttttT...x..4xtttttttttt....ttttttttt.tttttttxxxxx
ttttttt.tttttttttttttt.ttttttttttttT...+...xtttttttttttttttttttttttt.tttxxxxxxxx
txxxtt.ttttttttttt....ttttttttttttttt.tx..4xttttttttttttttttttttttttt..xxxxxxxxx
txi+"""""""tt......t.....ttttttttttttt.x.4_xttttttttttttttttttttttttt....xxxxxxx
txxx""""""""".tttttttt..TTtttttttttttttxxxxxttttttttttttttttttttttt......}xxxxxx
ttt""N"""""""TTtttttttt....ttttttttttttttttttttttttttttttttttttt.....xxxxxxxxxxx
ttT""MM"MM"TTTttttttttttttt..tttttttttttttttttttttt...ttttttttt....xxxxxxxxxxxxx
tt""""M""""Tttttttttttttttttt..ttt.......ttttttttt.......tttt...xxxxxxxxexxxexxx
tt".MM""NTTtttttttttttttttttttt....tttt.......ttt...ttttt.......xxxxx..exxxxeexx
tt""M""""Ttttttttttttttttttttttttttttttt...ttt..t..ttttttttxxxxx......xx.exx..xx
ttt""""TTTtttttttttttttttttttttttttttttttttttttt.tttttttxxxxxxxxxx....xxx.d.xxxx
tttttttttttttttttttttttttttttttttttttttttttttttttttttttttxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Zero! Nine! Eight!
#
NAME:    hangedman_slaughterboxes
TAGS:    no_pool_fixup no_monster_gen no_item_gen patrolling
ORIENT:  encompass
DEPTH:   D:16-26
MONS:    butterfly, plant
KMONS:   S = 0 band
KMONS:   # = 9 band
KFEAT:   ^ = stone_arch
KFEAT:   - = enter_vaults
SHUFFLE: :;, `~, 34, 67, !?, XZ / yz
SUBST:   % = %:40 $:20 ., * = *:30 %, | = |:30 *
SHUFFLE: ABCDE / BADCE / AGHIC / ACGHE, BDF
SUBST:   ACF = ., BDGHI = x, E : .x
SHUFFLE: JKLMN / KJMLN / JPQRL / JLPQN, KMO
SUBST:   JLO = ., KMPQR = x, N : .x
SHUFFLE: cdefg / dcfeg / cijke / ceijg, dfh
SUBST:   ceh = ., dfijk = x, g : .x
SHUFFLE: nopqr / onqpr / ntuvp / nptur, oqs
SUBST:   nps = ., oqtuv = x, r : .x
SUBST:   X = xx', y= x'', Z = xx., z = x..
SUBST:   ;` = ., :~ = x, T = TTV, U = TUU
SUBST:   7? = ., 36! = 0, Y : 09, 5 = 09, 4 = 9
SUBST:   = : ttx2, " : WW', _  = C, @ = G
NSUBST:  ' = 7:S / 2:# / 6 = S:2 #:1 .:2 / *:.
: if you.absdepth() > 20 then
SUBST:   & : a:45 c:8 vb x:2, a : c:15 vb
MARKER:  ^ = lua:props_marker { portal=1 }
: else
SUBST:   & : a:80 x:15 c:3 v:1 b:1, a : c:38 v:2 b:2
:    if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
NSUBST:  ^ = 1:- / *:^
:    end
: end
MAP
   xxxxxxxxxxxxxxx      xxxxx       xxxxx
  xxxxxxxxxxxxxxxxx    xxxxxxx    xxxxxxaaaaaaaaaaaaaaa
 xxxZZZZZZZZZZZZxxxxxxxxxXXXxxxxxxxxxZZaaaaaaaaaaaaaaaaa
xxxZ............ZxxxxxxXX'''XXxxxxxZZ..aaalalalalalalaaaa
xxZ..`x~x`x~x`x..ZZXXXX'''x'''XXZZZ....aalllllllllllllaaaa
xxZ.`x........`....'''''''x''''''......aal%%%%*****|||laaa
xxZ.x..~xxx`x.x....'x'''x'''x'''x...{..aal%........38|llaa
xxZ.~.~x''''`.~...x'''x'''x'''x''.x..[.aal%.&4&&..&&3|laaa
xxZ.x.x'`~`'x.x....'x'''x'''x'''x...(..aal%.48&&.&&&.*llaa
xxZ.`.x'~0~'x.`....''''''''''''''......aal%.&&&...&..*laaa
xxZ.x.x'`~`'x.x..aaaaaaaaaaaaaaaaaaaaaaaal%.&&.|.....*llaa
xxZ.~.`''''x~.~.aaaaraaaaaaaEaaaaaaaaaaal&&.....|.&&.*laaa
xxZ.x.x`xxx~..x.aa9...0.&.0..67aalllllll&&&+.....&&&.%llaa
xxZ.`........x`.aa.nop.&+&.ADC6aal%%%%%%&&.m+...&&84.%laaa
xxZ.x`x~x`x~x`..ar.qrq..&..BEB.Eal%90&...+m.m+..&&4&.%llaa
xxZ.............aa.pon.&+&.CDA.aal%0.&.0..+m.m+......%laaa
xxxZ.......aaaaaaa....9.&.....0aal%&&......+m.&&%%%%%%llaa
 xxxZ.....aaaaNaaaa.a.aaaa.&.&.aal%......<..+&&&lllllllaaa
 xxxZ.BAD.aa9....aaa+a+a0a&+&+&aaaaa+a+a5....&&laaaaaaaaaax
  xxX'CEA'aa.JKL..aaa....a.&.&.aaaa.a.aaa..0.%laaaaaaaaaaxx
  xxX'DCB'aN.MNM.a+acde..a...!?.&......a+....%laa.......Zxx
  xxX'''''aa.LKJ..a.fgf.%a.nop!&+&.]1U..a..&&%laa..)....Zxx
  xxX'''''aa....9a+.edc0%a.qrq..&..1@1.a+.&.0%laa.>.>..Zxxx
  xxX'LKJ'aa.&.&.aa...08*a.pon.&+&.U1}..a.&09%laa......Zxx
  xxX'MNM'aa&+&+&a0..%%*aa9.....&......aa%%%%%laa..x..Zxxx
 xxxX'JKL'aa.&.&.aaaaaaaa+aaaaaaa..&.&.aallllllaa.....Xxx
 xxX''''''aa....0.&.....0aa&...+.a&+&+&aaaaaaaaaa'x'x'Xxx
 xxX''''''aa.ABC.&+&.JKL.aY&.&.a+a.&.&.aaaaaEaaaa'''''Xxx
 xxX'CBA''aE.DED..&..MNM.a..efc..a...!?.&.0..67aa''x''Xxx
 xxX'DED''aa.CBA.&+&.LKJ.a.&dgf&.a.nqp!&+&.ADC6aa''''''Xxx
 xxX'ABC''aa9.....&.0....a%0cde..a.oro..&..BEB.Ea'x'x''Xxxx
 xxX''''''aaaaEaaaaa.&.&.a*80&.&&a.pqn.&+&.CDA.aa'''''''Xxx
 xxX''''''aaaaaaaaaa&+&+&a|*%..Yaa9.....&.....0aa''x'xx'Xxx
 xxX'pon''aawwwwwwaa.&.&.aaaaaaaa+aaaaaaaa.&.&.aa'''''''Xxx
 xxX'qrq''aaw2112waa....5.&.....0aa*%%..0a&+&+&aa'x'x''Xxxx
 xxX'nop''aaw..T.Wa..DCB.&+&.JML.a*80...aa.&.&.aa''''''Xxx
 xxX''''''aaw....W+a.AEC..&..KNK.a%0cfe.+a9...0aa''x''Xxx
 xxX''''''aaw.2..Wa..BAD.&+&.LMJ.a%.dgd.a..nqp.aa'''''Xxx
 xxxX'edc'aaw....W+a......&.0....a..efca+a.oro.ra'x'x'Xxx
  xxX'fgf'aaw...@waaa.a.aaaa.&.&.a....aaa..pqn.aa'''''Xxx
  xxX'cde'aaw._..wwa+a+aaaaa&+&+&a0a+a+aaa....9aa..x..Zxxx
  xxX'''''aaw....@wwWWWWwwaa.&.&.aaaa.a.aaaaraaaa.....Zxxxx
  xxX'''''.aW......@....2waa....0.&.9....aaaaaaa.......Zxxx
  xxX'cde'.+W.._.......T1waa.ADC.&+&.JML.aa.............Zxx
  xxZ'fgd'.+W....._..2..1waE.BEB..&..KNK.Na..;x:x;x:x;x.Zxx
 xxxZ.efc..aww..........2waa.CDA.&+&.LMJ.aa.;x........;.Zxx
xxxxZ......aawWWWwwwwwwwwwaa9.....&.....9aa.x..:xxx;x.x.Zxx
xxxX'''='..aaa++aaaaaaaaaaaaaaEaaaaaaaNaaaa.:.:x'''';.:.Zxx
xxX''''''........aaaaaaaaaaaaaaaaaaaaaaaaa..x.x';:;'x.x.Zxx
xxX''''''=....''''''''''''''''''''''''''....;.x':0:'x.;.Zxx
xxX'='^'''''.cde''efc''''''''''''LMJ''BAD...x.x';:;'x.x.Zxx
xxX'''""'''=.fgd''dgd''pqn''CDA''KNK''CEA...:.;''''x:.x.Zxx
xxX'''""^'''.efc''cfe''oro''BEB''JML''DCB...x.x;xxx:..x.Zxx
xxX''^''''''..'''''''''nqp''ADC'''''''''....;........x;.Zxx
xxX'''''='''ZZZXXXXXXX''''''''''XXXXXXXXZZ..x;x:x;x:x;..Zxx
xxxX'''''''XxxxxxxxxxxXXXXXXXXXXxxxxxxxxxxZ............Zxxx
 xxxXXXXXXXxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxZZZZZZZZZZZZxxx
  xxxxxxxxxxx        xxxxxxxxxxxxx       xxxxxxxxxxxxxxxx
   xxxxxxxxx                              xxxxxxxxxxxxxx
ENDMAP

################################################################################
# Profane Halls, by Grunt.
#
default-depth:

NAME: grunt_profane_halls
TAGS: no_monster_gen
DEPTH: D:19-27
ORIENT: encompass
SHUFFLE: {[(<
: if you.absdepth() == 27 then
SUBST: }])> = G
: else
SHUFFLE: }])>
: end
SUBVAULT: d : grunt_profane_temple
SUBVAULT: e : grunt_profane_temple
SUBVAULT: f : grunt_profane_temple
SUBVAULT: g : grunt_profane_temple
NSUBST: D = 1:K / 1:L / 1:M / 1:N
: grunt_profane_halls_setup(_G)
: if you.absdepth() >= 24 then
KMONS: K = storm dragon
KMONS: L = shadow dragon
KMONS: M = quicksilver dragon
KMONS: N = golden dragon
: else
KMONS: K = deep elf blademaster
KMONS: L = deep elf master archer
KMONS: M = deep elf death mage
KMONS: N = deep elf sorcerer
: end
KMONS: 0 = 0 band
KMONS: 9 = 9 band
KMASK: 1290KLMN = !no_monster_gen
: if you.absdepth() == 27 then
SUBVAULT: H : grunt_zot_portal
: else
SUBVAULT: H : grunt_profane_loot
: end
MAP
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccdddddddddddddddcccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccdddddddddddddddcccccccccccccccccccccccccccccccc
ccccccccccccccc.ccccccccccccccccdddddddddddddddcccccccccccccccc.ccccccccccccccc
cccccccccccccc..ccccccccccccccccdddddddddddddddcccccccccccccccc..cccccccccccccc
ccccccccccccc.G.cccGcGcGGcGcGcccdddddddddddddddcccGcGcGGcGcGccc.G.ccccccccccccc
ccccccccc.......c..............cdddddddddddddddc..............c.......ccccccccc
ccccccccc.{.....+......0.......+ddddddddddddddd+.......0......+.....[.ccccccccc
ccccccccc.......+......0.......+ddddddddddddddd+.......0......+.......ccccccccc
ccccccccc...U...c..............cdddddddddddddddc..............c...U...ccccccccc
cccccccc........cccGcGcGGcGcGcccdddddddddddddddcccGcGcGGcGcGccc........cccccccc
ccccccc.G.....}.ccccccccccccccccdddddddddddddddcccccccccccccccc.].....G.ccccccc
cccccc..........cccccccccccccccccdddddddddddddccccccccccccccccc..........cccccc
cccccccccc++ccccccccccccccccc...cdddddddddddddc...ccccccccccccccccc++cccccccccc
ccccccccc....ccccccccccccc......+ddddddddddddd+......ccccccccccccc....ccccccccc
ccccccccG....Gccccccccccc.......++ddddddddddd++.......cccccccccccG....Gcccccccc
ccccccccc....cccccccccc%...9.....+ddddddddddd+.....9...%cccccccccc....ccccccccc
ccccccccG....GccccccccU*.........ccdddddddddcc.........*UccccccccG....Gcccccccc
ccccccccc.00.cccccccc%*.......cccccdddddddddccccc.......*%cccccccc.00.ccccccccc
ccccccccG....Gcccccc........ccc%%.ccdddddddcc.%%ccc........ccccccG....Gcccccccc
ccccccccc....cccccc.......++c......ccdddddcc......c++.......cccccc....ccccccccc
ccccccccG....Gcccc...9...c+.........ccccccc.........+c...9...ccccG....Gcccccccc
ccccccccc....cccc.......cc........1....:....1........cc.......cccc....ccccccccc
cccccccccc++ccccc......cc..............1..............cc......ccccc++cccccccccc
cccceeeeeeeeeeecc++...cc.....G....1..G...G..1....G.....cc...++ccfffffffffffcccc
cccceeeeeeeeeeeeee++ccc%...........G...D...G...........%ccc++ffffffffffffffcccc
cccceeeeeeeeeeeeeeeeccc%.....1..1.2.........2.1..1.....%cccffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeecc.........G.:%HHHHH%:.G.........ccffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeecc..........%HHHHHHH%..........ccfffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec.......G..HHHHHHHHH..G.......cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec..........HHHHHHHHH..........cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec:.G..1..D.HHHHHHHHH.D..1..G.:cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec..........HHHHHHHHH..........cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec.......G..HHHHHHHHH..G.......cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeecc..........%HHHHHHH%..........ccfffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeecc.........G.:%HHHHH%:.G.........ccffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeccc%.....1..1.2.........2.1..1.....%cccffffffffffffffffcccc
cccceeeeeeeeeeeeee++ccc%...........G...D...G...........%ccc++ffffffffffffffcccc
cccceeeeeeeeeeecc++...cc.....G....1..G...G..1....G.....cc...++ccfffffffffffcccc
cccccccccc++ccccc......cc..............1..............cc......ccccc++cccccccccc
ccccccccc....cccc.......cc........1....:....1........cc.......cccc....ccccccccc
ccccccccG....Gcccc...9...c+.........ccccccc.........+c...9...ccccG....Gcccccccc
ccccccccc....cccccc.......++c......ccgggggcc......c++.......cccccc....ccccccccc
ccccccccG....Gcccccc........ccc%%.ccgggggggcc.%%ccc........ccccccG....Gcccccccc
ccccccccc.00.cccccccc%*.......cccccgggggggggccccc.......*%cccccccc.00.ccccccccc
ccccccccG....GccccccccU*.........ccgggggggggcc.........*UccccccccG....Gcccccccc
ccccccccc....cccccccccc%...9.....+ggggggggggg+.....9...%cccccccccc....ccccccccc
ccccccccG....Gccccccccccc.......++ggggggggggg++.......cccccccccccG....Gcccccccc
ccccccccc....ccccccccccccc......+ggggggggggggg+......ccccccccccccc....ccccccccc
cccccccccc++ccccccccccccccccc...cgggggggggggggc...ccccccccccccccccc++cccccccccc
cccccc..........cccccccccccccccccgggggggggggggccccccccccccccccc..........cccccc
ccccccc.G.....).ccccccccccccccccgggggggggggggggcccccccccccccccc.>.....G.ccccccc
cccccccc........cccGcGcGGcGcGcccgggggggggggggggccGcGccGGcGcGccc........cccccccc
ccccccccc...U...c..............cgggggggggggggggc..............c...U...ccccccccc
ccccccccc.......+......0.......+ggggggggggggggg+.......0......+.......ccccccccc
ccccccccc.(.....+......0.......+ggggggggggggggg+.......0......+.....<.ccccccccc
ccccccccc.......c..............cgggggggggggggggc..............c.......ccccccccc
ccccccccccccc.G.cccGcGcGGcGcGcccgggggggggggggggccGcGccGGcGcGccc.G.ccccccccccccc
cccccccccccccc..ccccccccccccccccgggggggggggggggcccccccccccccccc..cccccccccccccc
ccccccccccccccc.ccccccccccccccccgggggggggggggggcccccccccccccccc.ccccccccccccccc
ccccccccccccccccccccccccccccccccgggggggggggggggcccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccgggggggggggggggcccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

# Here just in case people want to see the original base layout.
NAME: grunt_profane_temple_base
TAGS: grunt_profane_temple allow_dup
WEIGHT: 0
: grunt_profane_halls_setup(_G)
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
%%...........%%
%G...........G%
...............
......1.1......
...............
.......U.......
...............
..1.........1..
...............
....1.....1....
.....G...G.....
 .............
 ....G...G....
 .............
  ...G...G...
  ...........
   ..G...G..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_geh
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
KMONS: 4 = red devil / hell hound / flaming corpse w:5
KMONS: 5 = hellion / balrug
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
ll...l%%%l...ll
l....l%%%l....l
...............
..ll...5...ll..
..l4..lll..4l..
..l..lllll..l..
..l.5lllll5.l..
..l..lllll..l..
..l4..lll..4l..
..ll...5...ll..
...............
 ....l...l....
 ....l4.4l....
 ....l...l....
  ...l4.4l...
  ...l...l...
   ..l4.4l..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_coc
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
KMONS: 4 = blue devil / large simulacrum / small simulacrum / freezing wraith
KMONS: 5 = blizzard demon / ice dragon
KMONS: 8 = ice statue name:chilling_statue name_replace name_descriptor \
           hd:15 spells:bolt_of_cold;ozocubu's_refrigeration;\
           ozocubu's_refrigeration;freezing_cloud;\
           ozocubu's_refrigeration;ozocubu's_refrigeration \
           tile:mons_chilling_statue
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
wwww.......wwww
www....5....www
ww...........ww
w...4.www.4...w
.....wwwww.....
....ww%%%ww....
..4.ww%8%ww.4..
....ww%%%ww....
.....wwwww.....
....4.www.4....
...W.......W...
 ..WW.5.5.WW..
 ...W.....W...
 ...WW...WW...
  ..4W4.4W4..
  ...W...W...
   .........
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_tar
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
KMONS: 4 = skeletal warrior w:5 / small skeleton / large skeleton / \
           small zombie / large zombie / flying skull band
KMONS: 5 = bone dragon / tormentor / lich w:5
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
.%%%.......%%%.
.ccc...4...ccc.
...............
.4....ccc....4.
.....c...c.....
...4..5V5..4...
.....c...c.....
.4....ccc....4.
...............
.ccc...4...ccc.
..5..G...G..5..
 .....4.4.....
 ....G...G....
 .............
  ...G...G...
  ...........
   ..G...G..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_dis
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
KMONS: 4 = iron devil
KMONS: 5 =iron dragon / iron troll / iron golem
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
COLOUR: G = cyan
TILE: G = dngn_statue_iron_golem
: set_feature_name("granite_statue", "iron statue")
KMASK: 12345 = !no_monster_gen
MAP
%%.5.......5.%%
%G...........G%
.....4...4.....
....4.v.v.4....
.....v...v.....
...4...G...4...
.....v...v.....
....4.v.v.4....
.....4...4.....
...............
.G...G...G...G.
 ..5.......5..
 ....G...G....
 .............
  ...G...G...
  ...........
   ..G...G..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_loot
TAGS: grunt_profane_loot no_monster_gen
: grunt_profane_halls_setup(_G)
SHUFFLE: }])>
SHUFFLE: KLMN
: if you.absdepth() >= 24 then
KMONS: K = storm dragon
KMONS: L = shadow dragon
KMONS: M = quicksilver dragon
KMONS: N = golden dragon
: else
KMONS: K = deep elf blademaster
KMONS: L = deep elf master archer
KMONS: M = deep elf death mage
KMONS: N = deep elf sorcerer
: end
KITEM: KLMN> = acquire any
KITEM: 12 = any
MAP
  cc+cc
 cc1%1cc
cc2%K%2cc
c1%***%1c
+%L*>*N%+
c1%***%1c
cc2%M%2cc
 cc1%1cc
  cc+cc
ENDMAP

NAME: grunt_zot_portal
TAGS: grunt_zot_portal
COLOUR: c = lightmagenta
TILE: c = wall_zot_magenta
COLOUR: JO.1+ = magenta
FTILE: JO.1+ = floor_hall
KFEAT: O = enter_zot
KMONS: 1 = place:Zot:1 band
MARKER: J = lua:fog_machine { cloud_type = "magical condensation", \
                              pow_min = 3, pow_max = 5, \
                              delay_min = 5, delay_max = 15, \
                              size = 1, walk_dist = 4, \
                              spread_rate = 70 }
MAP
  cc+cc
 cc...cc
ccJ.1.Jcc
c.......c
+.1.O.1.+
c.......c
ccJ.1.Jcc
 cc...cc
  cc+cc
ENDMAP

################################################################################
# You are in a maze of twisty little passages, all alike.
#
NAME: grunt_twisty_little_passages
TAGS: no_monster_gen
DEPTH: D:16-26
ORIENT: encompass
SHUFFLE: defghijk
SUBST: def = c
SUBST: ghi = v
SUBST: jk = b
SHUFFLE: {[(<
SHUFFLE: }] / )> / HI / JK
SHUFFLE: }]
SHUFFLE: )>
SHUFFLE: HI
SHUFFLE: JK
: if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
KFEAT: H = enter_vaults
: else
KFEAT: H = stone_arch
MARKER: H = lua:props_marker { portal=1 }
: end
KITEM: J = acquire any
KFEAT: IK = stone_arch
MARKER: I = lua:props_marker { portal=1 }
MARKER: K = lua:props_marker { portal=1 }
KMONS: 8 = 8 band
KMONS: 9 = 9 band
KMONS: 0 = 0 band
KMASK: 098 = !no_monster_gen
NSUBST: | = 2:*
SUBST: % = %.
MAP
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccdeeeeeeeeeeeeeeeeeefffffffffffffffffffffffffffffffffffffffffkkkkkkkkkkkkcccc
ccccddeeeeeeeeeeeeeeeeecffffffffffffffffffffffffffffffffffffffffkkkkkkkkkkkkcccc
ccccdd.....eeee........ecff%%.................................%%ik......{.kkcccc
ccccdd.9....eee.0.......ecf%%..0...........................0...%ik.......[kkcccc
ccccdd..ce...eee....ee...ecf....ffffffffffffffffffffffffff......ik..kk....kkcccc
ccccdd..dce...eee...eee...ecf...ffffffffffffffffffffffffff..8...ik..kkk...kkcccc
ccccdd...dce...eee...eee...ecf...ffff..............%*ffff.....0.ik...kkk..kkcccc
ccccddc...dce...eee...eee...ecf...fff...89.........%%fff........ikk...kkkkkkcccc
ccccddcc...dce...eee...eee...ecf...fff......ff......fff.....ii..ikkk...kkkkkcccc
ccccddccc...dce...eee...eee...ecf...fff.}].fff.....fff.....iii..ikkkk...kkkkcccc
ccccddcccc...dce...eee...eee.0.ecf...fff|*fff.0...fff.....iiii..ikkkkk...kkkcccc
ccccdd..ccc...dce...ee...eee...eecf...ffffff.....fff.....iiiii..ik..kkk...kkcccc
ccccdd.0.ccc..dgge......eee...eeeecf...ffff.....fff.....iiiiii..ik.0.kkk..kkcccc
ccccdd....cc..dggge..0.eee...eee..ecf...ff.....fff..0..iii%*ii..ik....kk..kkcccc
ccccdd........dgggce..eee...eee.0..ecf.0......fff.....iii.%%ii..ik........kkcccc
ccccdd........dg%%gceeee...eee......ecf......fff.....iii....ii..ik........kkcccc
ccccdd..dd....dg%%.gcee...eee...ee...ecf.%%.fff.....iii.....ii..ik..kk....kkcccc
ccccdd..ddd.0.dg....gce.0.eee.0.eee...ecf%%fff.....iii......ii..ik..kkk.0.kkcccc
ccccdd...ddd..dg.....gce...eee...eee...ecffff.....iii.......ii..ik...kkk..kkcccc
ccccddd...dddddg.0....gce...eee...eee...ecff.....iii...0....ii..ikk...kkkkkkcccc
ccccdddd...ddddg..gg...gce...eee...eee...ec.....iii.....ii..ii..ikkk...kkkkkcccc
ccccddddd...dddg..ggg...gce...eee...eee....0...iii.....iii..ii..ikkkk...kkkkcccc
ccccdddddd...ddg..gggg...gce...eee...eee.9....iii.....iii...ii..ikkkkk...kkkcccc
ccccdd..ddd...dg..ggggg...gce...eee...eee....cii%%...iii....ii..ik..kkk...kkcccc
ccccdd.0.ddd..dg..gg.ggg...gce...ee.0.eee...jjci%%..iii|).9.ii..ik.0.kkk..kkcccc
ccccdd....dd..dg..gg..ggg...gce......eee...jjjjci...iii*>.8.ii..ik....kk..kkcccc
ccccdd........dg..gg...ggg...gce.0..eee...jjj..jci.0.iii....ii..ik........kkcccc
ccccdd........dg..gg....ggg.0.gce..eee...jjj..0.jci...iii...ii..ik........kkcccc
ccccdd..dd....dg..gg.8.H*ggg...gceeee...jjj......jci...iii..ii..ik..kk....kkcccc
ccccdd..ddd.0.dg..gg.9.I|ggg..%%gcee...jjj.0.jj...jci...iiiiii..ik..kkk.0.kkcccc
ccccdd...ddd..dg..gg....ggg...%%ggc....jjj...jjj...jci...iiiii..ik...kkk..kkcccc
ccccddd...dddddg..gg...ggg.....ggg....9.jjj...jjj...jci...iiii..ikk...kkkkkkcccc
ccccdddd...ddddg..gg..ggg.....ggg...0....jjj...jjj...jci...iii..ikkk...kkkkkcccc
ccccddddd...dddg..gg..gg.....ggg.....cj...jjj...jjj...jci...ii..ikkkk...kkkkcccc
ccccdddddd...ddg..gg....0...ggg.....hhcj...jjj...jjj...jci....0.ikkkkk...kkkcccc
ccccdd..ddd...dg..gg.......ggg.....hhhhcj...jjj...jjj...jci.....ik..kkk...kkcccc
ccccdd.0.ddd..dg..gg......ggg.....hhh%%hcj...jjj.0.jjj...jci....ik.0.kkk..kkcccc
ccccdd....dd..dg..gg.....ggg.....hhh.%%.hcj...jj...jjj.0.jjci.%%ik....kk..kkcccc
ccccdd........dg..gg....ggg.....hhh......hcj......jjj...jjjjci%%ik........kkcccc
ccccdd........dg..gg%%.ggg.....hhh......0.hcj..0.jjj...jjj..jciiik........kkcccc
ccccdd..dd....dg..gg*%ggg..0..hhh.....hh...hcj..jjj...jjj.0..jcckk..kk....kkcccc
ccccdd..ddd.0.dg..gggggg.....hhh.....hhhh...hcjjjj...jjj......jckk..kkk.0.kkcccc
ccccdd...ddd..dg..ggggg.....hhh.....hhhhhh...hcjj...jjj...jj...jck...kkk..kkcccc
ccccddd...dddddg..gggg.....hhh...0.hhh*|hhh...hcj.0.jjj...jjj...jck...kkkkkkcccc
ccccdddd...ddddg..ggg.....hhh.....hhh.JK.hhh...hcj...jjj...jjj...jck...kkkkkcccc
ccccddddd...dddg..gg.....hhh......hh......hhh...hcj...jjj...jjj...jck...kkkkcccc
ccccdddddd...ddg........hhh%%.........89...hhh...hcj...jjj...jjj...jck...kkkcccc
ccccdd..ddd...dg.0.....hhhh*%..............hhhh...hcj...jjj...jjj...jck...kkcccc
ccccdd...ddd..dg...8..hhhhhhhhhhhhhhhhhhhhhhhhhh...hcj...jjj...jjj...jck..kkcccc
ccccdd....dd..dg......hhhhhhhhhhhhhhhhhhhhhhhhhh....hcj...jj....jjj...jc..kkcccc
ccccdd(.......dg%...0...........................0..%%hcj.......0.jjj....9.kkcccc
ccccdd.<......dg%%.................................%%hhcj........jjjj.....kkcccc
ccccdddddddddddchhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhcjjjjjjjjjjjjjjjjjkkcccc
ccccdddddddddddhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhhjjjjjjjjjjjjjjjjjjkcccc
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
cccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

###############################################################################
#  This vault excercises varied old ideas for the Vaults branch:
#  a primarily humanoid-based monster set, a focus on placing groups,
#  and a pattern-based layout emphasizing lots of distinct rooms.
#  Of course, HangedMan twisted these ideas thoroughly.
#  Many thanks to elliott for writing the band placement lua!
#
NAME:    hangedman_spin_cycle
TAGS:    no_monster_gen no_item_gen no_trap_gen patrolling
DEPTH:   D:16-26
ORIENT:  encompass
KMONS:   - = plant
KMONS:   _ = plant col:random name:demonic name_adjective
KMONS:   no = human ; dart ego:exploding w:21 | dart ego:dispersal w:2 | \
                      dart ego:none w:1 | wand of lightning w:1 . \
                      mundane flail | mundane falchion | mundane trident . \
                      mundane ring mail | mundane scale mail | mundane chain mail / \
              human ; mundane blowgun . needle ego:confusion | \
                      needle ego:slow | needle ego:poisoned . \
                      mundane flail | mundane falchion | mundane trident . \
                      mundane ring mail | mundane scale mail | mundane chain mail
KMONS:   p = giant eyeball
KMONS:   q : great orb of eyes w:7 / guardian serpent w:3
KMONS:   r = vault guard
: local bands = {
:     [42 - you.absdepth() * 2] =
:         { "cyclops", "kobold demonologist", "naga", "Azrael", "Kirke" },
:     [65 - you.absdepth() * 3] =
:         { "boggart", "deep elf fighter", "necromancer" },
:     [29 - you.absdepth()] =
:         { "centaur warrior", "orc knight", "ogre mage", "skeletal warrior",
:           "yaktaur", "deep troll" },
:     [you.absdepth() * 2 - 37] =
:         { "deep elf knight", "orc high priest" },
:     [you.absdepth() * 2 - 40] =
:         { "harpy", "hell knight", "ghoul", "naga mage", "naga warrior",
:           "orc warlord", "vampire mage", "yaktaur captain" },
:     [you.absdepth() - 20] =
:         { "deep dwarf death knight", "deep elf high priest",
:           "merfolk impaler", "merfolk javelineer", "Margery", "Saint Roka" },
: }
: local kmonsters = {}
: for weight, monsters in pairs(bands) do
:     if weight > 0 then
:         for _, mons in ipairs(monsters) do
:             if not you.uniques(mons) then
:                 table.insert(kmonsters, mons .. " band w:" .. weight)
:             end
:         end
:     end
: end
: kmons("s = " .. table.concat(kmonsters, " / "))
KITEM:   ? = any ring randart w:390 / ring of hunger unrand:ring_of_the_octopus_king
KITEM:   ! = ring of invisibility / ring of regeneration / \
             ring of teleport control / ring of slaying / \
             ring of wizardry / any good_item ring no_uniq w:75
KITEM:   | = | / any good_item
KFEAT:   ^ = known teleport trap
KFEAT:   ~ = teleport trap
KFEAT:   aD = stone_arch
KFEAT:   A = enter_hell
KFEAT:   & = enter_pandemonium
KFEAT:   @ = enter_abyss
KFEAT:   B = enter_vaults
KFEAT:   C : C / any shop
SHUFFLE: <d` / {e1 / (f2 / [g3, >h4 / }i5 / )j6 / ]k7, (f2 / )j6, 15
: if you.absdepth() > 20 then
SUBST:   ( = D
MARKER:  D = lua:props_marker { portal=1 }
:     if crawl.random2(you.depth()-15) > (you.depth()-18) then
SUBST:   A&@ = a, _ = -, l = W
:     else
SHUFFLE: aA&@
:     end
: else
SUBST:   A&@ = a, _ = -, l = W
:     if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
NSUBST:  ( = 1:B / *:(
SUBST:   f = r, 2 = '
:     end
: end
SUBST:   {} = **|||, ( = T, ) : TTCCC, [ = !, ] = ?
NSUBST:  < = 1:{ / 1:( / 1:[, > = 1:} / 1:) / 1:]
SUBST:   d = 0, e = 009, f = %, g = 0, h = 0, i = 009, j = %, k = 0
SUBST:   ` = >, 1 = 9988s, 2 = 009, 3 = s, 4 = `, 5 = 998ss, 6 = 009, 7 = s
SHUFFLE: })]` / {([>, ?!, no / pq, pq / pq / pq / qp / pp / qq
SHUFFLE: LM / LM / LM / LM / LM / LM / LM / ML / ML / MM
SHUFFLE: NO / NO / NO / NO / NO / NO / NO / ON / ON / OO
SHUFFLE: PQ / PQ / PQ / PQ / PQ / QP, RS / RS / RS / RS / SR
SUBST:   LNPR : x, MOQS : ~, J : ~xxx, K : ~xxxx, c : x:7 c:1, v : v:7 c:1
NSUBST:  ~ = 2:~ / 1:~. / 75:H / 225:I / *:.
SUBST:   H = ~ .:4490, I = ~ .:9990
COLOUR:  "-_ = green
COLOUR:  '`089rs%*|!? = white
FTILE:   ^"aA@&-_U = floor_grass
: if crawl.coinflip() then
FTILE:   '`<{([>})]089rs%*|!?BCTDEF = floor_vault
: else
FTILE:   '`<{([>})]089rs%*|!?BCTDEF = floor_pebble_darkgray
: end
MAP
                          vvvvvvv
                         vv'%<%'vv
                 vvvv    vd'''''dv    vvvv
                vv%'vvvvvv'''`'''vvvvvv'%vv
               vv{''evvcccc+ccc+ccccvvg''[vv
               v%'1'''cc...........cc'''3'%v
               v'''cc+c.......N.....c+cc'''v
               vve'c.....L~~~~OO~......c'gvv
                vv'+....~MM~~~~N~~~....+'vv
          xxxx xcccc..~~~~L~~~~OO~~~~..ccv  xxxx
         xxJJxxx~~~..~~~~~MM~~~~N~~Q~~..ccxxxKKxx
         xJJJJxx~~~~px~~~~~L~~~~OO~~x~~~~~xxKKKKx
         xJJJJx~~~~~xn~~~~~MM~~~~N~~~P~~~~~xKKKKx
         xxJJ~~~~~~~~~~~~~~.L...~~~~~~~~~~~~~KKxx
    vvvv  xxx~www~~~~~~~~~.......~~~~~~~~www~xxxx
   vv%'vv xx~~www~x~~~.....c+c+c....~~~x~www~~~xx vvvvv
  vv(''fvvc~~~ww~R~~~.....cc'''cc.....~~R~ww~~~~cvv'%vvv
  v%'2'''cc~~~~~RS~~..c+ccc''4''ccc+c..~SR~~~~~~cvf''(vv
  v'''cc+c.~~~~x~~...cc''kch'''hci''cc..~~x~~~~~c'''2'%v
  vvf'c....~~~~~~....+'7''cc%>%cc''5'+..~~~~~~~.c+cc'''v
   vv'+...~~P~~~..c+cck''%bbbbbbb%''ic...~~~~xq....c'fvv
   vvcc..~~x~~~..cc'jbbb%]bbbbbbb}%bbc+c..~~~ox~...+'vv
   vcc...~Q~~~~..+'6'bbbbbbblllbbbbbbj'cc..~~~~~~..ccvv
   vc....~~~~~~..c'''%bbbbbbl^lbbbbbb'6'+..~~~~~~...ccv
 vvvc...~~~ON~~..cj'%)bblllll"llllbb%'''c..~~~~~~....cv
vvg'c..~~ONO~~..ccccbbbbla""""""Albb)%'jc..~~~~~~~...cvvv
v'''+..ONO~~~..cc'icbbbbl"""U"_""lbbbbcccc..~~~~~ML..c'evv
v%''c.NO~~~~~..+'''%bblll"_""""""lbbbbck'cc..~~MLM~..+'''v
v['3c..~~~~~ML.c'5'}bbl^"""""""U"lllbb%'''+..MLM~~~..c''%v
v%''c..~~~MLM..+'''%bblll"U"""""""^lbb]'7'c.LM~~~~~..c1'{v
v'''+..~MLM~~..cc'icbbbbl""""""_"lllbb%'''+..~~~~~ON.c''%v
vvg'c..LM~~~~~..ccccbbbbl""_"U"""lbbbbck'cc..~~~ONO..+'''v
 vvvc...~~~~~~~..ch'%>bbl@""""""&lbbbbcccc..~~ONO~~..c'evv
   vc....~~~~~~..c'''%bbllll"lllllbb>%'hc..~~NO~~~~..cvvv
   vcc...~~~~~~..+'4'bbbbbbl^lbbbbbb%'''c..~~~~~~~...cv
   vvcc..~~~~~~..cc'hbbbbbblllbbbbbbb'4'+..~~~~Q~...ccv
  vvv'+...~xo~~~..c+cbb%]bbbbbbb}%bbbh'cc..~~~x~~..ccvv
  vvd'c....qx~~~~...ck''%bbbbbbb%''icc+c..~~~P~~...+'vvv
  v'''cc+c.~~~~~~~..+'7''cc%)%cc''5'+....~~~~~~....c'dvv
  v%'`'''c~~~~~x~~..cc''kcj'''jci''cc...~~x~~~~.c+cc'''v
  vv<''dvc~~~~~~RS~..c+ccc''6''ccc+c..~~SR~~~~~cc'''`'%v
   vv%'vvc~~~~ww~R~~.....cc'''cc.....~~~R~ww~~~cvvd''<vv
    vvvv cxx~~www~x~~.....c+c+c.....~~~x~www~~xx vv'%vv
          xxx~www~~~~~~~~.......~~~~~~~~~www~xxx  vvvv
         xxKK~~~~~~~~~~~~~..L..~~~~~~~~~~~~~~JJxx
         xKKKKx~~~~~P~~~N~~~MM~~~~~~nx~~~~~xJJJJx
         xKKKKxx~~~~~x~~OO~~~L~~~~~~xp~~~~xxJJJJx
         xxKKxxxxc..~~Q~~N~~~MM~~~~~~..~~~xxxJJxx
          xxxx  vcc..~~~~OO~~~L~~~~~..ccccc xxxx
               vv'+....~~~N~~~MM~~....+'vv
              vve'c.....~~OO~~~L~.....c'gvv
              v'''cc+c.....N.......c+cc'''v
              v%'1'''cc...........cc'''3'%v
              vv{''evvcccc+ccc+ccccvvg''[vv
               vv%'vvvvvv'''2'''vvvvvv'%vv
                vvvv    vf'''''fv    vvvv
                        vv'%(%'vv
                         vvvvvvv
ENDMAP

###############################################################################
# encompass.des: Full level (encompassing) vaults for the main dungeon go here.
#                For encompass vaults that go in branches (eg branch endings)
#                use the des file for that branch.
###############################################################################

# Setups for the below.
{{
    function grunt_profane_halls_setup(e)
        if you.absdepth() == 27 then
            e.kmons('1 = base draconian / nothing w:20')
            e.kmons('2 = nonbase draconian / base draconian w:20')
            e.kmons('3 = draconian zealot')
        else
            e.kmons('1 = deep elf fighter / deep elf conjurer / '
                    .. 'deep elf summoner / deep elf knight w:5 / '
                    .. 'deep elf annihilator w:5 / '
                    .. 'deep elf demonologist w:5 /'
                    .. 'nothing w:68')
            e.kmons('2 = deep elf knight / deep elf fighter w:20')
            e.kmons('3 = deep elf high priest')
        end
        e.subst('% = * %:20 .')
        e.lua_marker(':', fog_machine { cloud_type = "flame",
                                        pow_min = 10, pow_max = 10, delay = 10,
                                        size = 1, spread_rate = 0,
                                        walk_dist = 0, excl_rad = 0 })
    end
}}

default-depth: D:12-, Depths:2-, !Depths:$

###############################################################################
# The River Lethe
#
# The two orange statues are placed for maximum pain -- one you're likely to
# first see while flying, the other is hard to target with disintegration
# through its protective plants.
#
# Lemuel
#
NAME:    lemuel_river_lethe
ORIENT:  encompass
TAGS:    no_rotate uniq_d_encompass
DEPTH:   Depths:2-, !Depths:$
SUBST:   . = .:100 !:1 6:4
SUBST:   ; = .:200 !:1 1:3
SUBST:   1 = 1:20 .
SUBST:   3 = 3:20 .
KFEAT:   ! = ^
MONS:    plant
MONS:    wolf / warg / hell hound
MONS:    nothing
MONS:    wight w:50 / wraith / shadow wraith / phantasmal warrior /\
         skeletal warrior / flayed ghost
MONS:    dancing weapon
MONS:    human simulacrum w:60 / elf simulacrum / ogre simulacrum /\
         centaur simulacrum / troll simulacrum / vampire / wraith / hungry ghost
MONS:    eye of draining / brain worm / death drake w:5 / shadow /\
         shadow dragon w:1
KITEM:   A = potion of experience / any good_item
KMONS:   A = Maud
KFEAT:   A = >
KFEAT:   B = |
KMONS:   B = neqoxec
KFEAT:   C = b
KMONS:   C = lich w:20/shadow fiend
SUBST:   b = b|
SUBST:   | = |*
ITEM:    any scroll, potion of experience
SHUFFLE: dD/eE/fF/gG
SUBST:   D=+, d=|, E=x, F=x, G=x
NSUBST:  e = 1:y / 1:* / *:%
NSUBST:  f = 1:y / *:%
NSUBST:  g = 1:y / *:%
KFEAT:   y = >
KITEM:   y = any / w:1 any good_item
SHUFFLE: PQ
SUBST:   Q = xw
KMONS:   P = orange crystal statue
KFEAT:   P = floor
KMONS:   k = giant eyeball
KITEM:   k = potion of experience / any good_item
NSUBST:  r = 1:+ / *:x
NSUBST:  R = 1:+ / *:x
: if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
KFEAT:   V = enter_vaults
: else
KFEAT:   V = floor
: end
MARKER:  O = lua:props_marker { portal=1 }
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccccccccxxxxxx
xxAB.r$xxxwwwxxxxcccccccccccccccccccccccccccxxxxxxxxcc..1....|kcxxxxxx
xwBB.r$$Rwwwwxxxxc.....+...+...........c***c;;xxxxx;cc.....1.||cxxxxxx
xw..Or$$Rxwwwxxxxc..C..ccccc...........c***c;;;xxx;;cc.1.1...1.cxxxxxx
xww..r$$Rxwwwwxxxc.....cxxxc...........c***c;;;;xx;;;+.....1..1cxxxxxx
xww..xxxRwwww;xxxc..}..cxxxc...........c...c;;;;xx;;cc..1.111..cxxxxxx
xwww.xxxwww;;;;xxccccccccxxcc+ccc+ccccccc+cc;;;;xx;;cc.11111...cxxxxxx
xwwwxxxwwww;;;;;xxxxxxxxxxxc...c...c.......c;;;;x;;;cc..111O..1cxxxxxx
xwwwxxxwwwww;;7;;xxxxxxxxxxc...c...c.w.....c;;;;;;;;cc.111111..cxxxxxx
xwwwxxxwwwww;;;;;;;xxxxxxxxc...+...c.w.....+;;;;;;;;cc..111..1.cxxxxxx
xwwwxxxwwww;;7;;;;;;;;xxx;;c...c...c.w.....c;;;;;;;;ccccccccccccxxxxxx
xwwwxxxwww;;;;;;;;;;;;;;;;;cc+ccc+cc..w....c;;;;;;;;;;xxxxxxxxxxxxxxxx
xwwwxxxwww;;7;;;;;;;;;;;;;;+.......+..w....cccc;;;;;;xxxxxxxxxxxxxxxxx
xwwwwxx;ww;;;;;;;;;;;;;;;;;cccc+cccc...w...c$$c;;;;;;;;xxxxxxxxxxxxxxx
xwwwwxx;w;;7;;;;cccc;;;;;;;c.......c...w...c$$c;;;;;;;;;;;xxxxxxxxxxwx
xwwwwxx;;;;;;;;cccccc;;;;;;c.......c+ccwwccc+cc;;;;;;;;;;;;;xxxxxxxxwx
xwwwwxx;;;7;;;cc*4.+;;;;;;;c.......c.c..ww....c;;;;;7;;;;;;;;xxxxxxwwx
xxwwwwx;;;;;;;;cccccc;;;;;;c6.6.6.6c.c...ww...c;;;;;;;;;;;;;;;xxxxxwwx
xxxwwwxx;;;;;;;;cccc;;;;;;;ccccccccc.c...ww...c;;;;;;;;;;;;;;;xxxxxwwx
xxxwwwxx;;;;;;;;;;;;;;;;;;;c$$+....+.c....www.c;;;;;;;ccccccccxxxxxwwx
xxxwwxx;;cccc;;;;;;;;;;;;;;c$$cccccc.c>....wwwc;;;;;;;c.....$$Dddxxwwx
xxxwwwx;cccccc;;;;;;xx;;;;;c$$cccccc+cccccccwww;;;;;;;c.....$$xdd+wwwx
xxxwwwxcc*4.+;;;;;;xxxx;;;;ccccccccc.....c...www;;ccccc.......xxxxxwwx
xxxwwwxxcccccc;;;;xxxxx;;;;;;;cccccc.....+...www;;c...c.......xeexxwwx
xxxwwwxx;cccc;;;;xxxxxx;;;;;;;cccccc.....c...www;cc.66c.......Eee+wwwx
xxxwwwxx;;;;;;;;xxx5..c;;;;;;;cccccc.....cccwww;;;+.66+...O...xxxxxwwx
xxxwwwxx;;;;;;;;;xx5V.c;;;;;;;;;;;;c.....c;wwww;;cc.66c.......Fffxxwwx
xxxwwwxx;;;;;;;;;;x5..+;;;;;;;;;;;;cccccccwwww;;;;c...c.......xff+wwwx
xxxwwwxx;cccc;;;;xxx..c;;;;;;;;;;;;;;;;;wwwwww;;;;ccccc.......xxxxxwwx
xxxwwwxxcccccc;;;;;xx.c;;;;;;;;;;;;;;;;wwwwwww;;;;;;;;c.....$$xggxxwwx
xxxwwwxcc*4.+;;;;;;xxxx;;;;;;;;;;;;;;;wwwwwwwww;;;;;;;c.....$$Ggg+wwwx
xxxwwwxxcccccc;;;;xxxwxx;;;;;;x;;;;;wwwwwwwwwww;;;;;;;ccccccccxxxxxwwx
xxxwwwxxxcccc;;x;;;xwwxxx;;;;;xx7wwwwwwwwwwwwww;;;;;;;;;;;;;;;xwwwwwwx
xxxwwwxxxxwww;;;;;;wwwwxw;;;;xxxwwwwwwwwwwwxxwww;;;;7;;;;;wwwwwwwwwwwx
xxxwwwxxxxxwwwwwwwxwwwwwwwwwxxxwwwwwwwwwwwwxxxwwwwww;;wwwwwwwwwwwwwwxx
xxxxwwwwxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxwwwwwwwwwwwwwwwwwwwwwxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxwwwwwwwxxxxxwwwwwwwwwwwwwwwwwwwxxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxwwwwwwwwwwwwwwwwwwxxxxxxx
xxxxxwwwwwwwwwwwwwwwwQxwwwwwwwxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxx
xxxxxxwwwwwwwwwwwwwxxxxwwwwwwwwwwwwwxxxPwwwwwwwwwwxxwwwwwwwwxxxxxxxxxx
xxxxxxxwwwwwxxxxxxxxxxwwwwwwwxxwwwwxxxxxwwwwwwwwwxxxwwxwwwwxxxxxxxxxxx
xxxxxxxx;7;;xxx222xxxxxxwwwwxxwww2222xxxxx22wwwwwwxxxxxxxxxxxxxxxxxxxx
xxxxxxx>;;;xxx;;;;;xx2222wwwwww;;;;;;;;xxxx22;;;;wwwwxxxxxxxxxxxxxxxxx
xxxxxxxxx;xxxx;;;;;;;;;;;;ww;;;;;;;;;;;;;;;;;;;;;;;;wwwxxxxxxxxxxxxxxx
xxxxxxxxxxxx;;;;2;;;;;;;;;;;;;;;;;);;;;;;2;;;;;;;;;;wwwxxxxxxxxxxxxxxx
xxxxxxxxxxxx;;;;;;;;;;;;;;;;;;;;;[](;;;;;;;;;;;;;;;;2wxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx+xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx{xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
# Sea of Fire
#
# By minmay.
#
# All arrivals from above (and one from below) bubbles just lead to downward
# escape hatches.  The other two staircases from below lead to bubbles with
# some loot.  For the main fight, you have to fly or teleport.
# The opposition is nasty, but this map is deep enough.
NAME:    minmay_sea_of_fire
TAGS:    no_rotate uniq_d_encompass
ORIENT:  encompass
DEPTH:   Depths:2-, !Depths:$
MONS:    hell hound / nothing
MONS:    lindwurm
MONS:    fire drake / steam dragon w:1
MONS:    fire giant / fire dragon / mottled dragon w:5 / efreet w:2
MONS:    smoke demon
MONS:    hell knight / nothing w:5
KMONS:   7 = molten gargoyle
KFEAT:   7 = l
KMONS:   8 = iron golem
KMONS:   9 = Xtahua, hellephant
KMONS:   $ = 0 / nothing w:90
KITEM:   $ = $
NSUBST:  = = 1:= / *:v
SUBST:   = = +v
SUBST:   ' : l.
: _G.set_random_mon_list("fire bat w:30 / fire drake / fire dragon / " ..
:     "hell hound / efreet / fire crab / fire giant w:3 / mottled dragon / " ..
:     "fire elemental / crimson imp / fire vortex / molten gargoyle")
: if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
KFEAT:   V = enter_vaults
: else
KFEAT:   V = floor
: end
MARKER:  % = lua:props_marker { portal=1 }
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxvvvvvvvvvvvvvvvvvvvvxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxllllxxxxxxxxxlllllxxxll.3..v$$$$$$$$v......0..vxxxxxx.....xxxxxxx
xxxxxxxxxxxxxllllllllllxxxxllllllllllll...=$$$$$$$$+....0....vxxxx....0....xxxxx
xxxxxxxxxxxlllllxxllllllllllllllllllllllllvv=$$$$$$v..vvvvv.0vxll0..>.......xxxx
xxxxxxxxxlllllllllllllllllllllllllllllllllllv$$$$$$v4.v...v..vllll.....x..0..xxx
xxxxxxxllxxxxxllllllllllllllllllllllllllllllvvv$$$$v..v.].v..vllllll.0.......xxx
xxxxxxlxxx..1xxx..lllxx.0..llxxlllllllllllllllv$$$$v......v..vlllllllll.......xx
xxxxxlxx..1.....x...........xx..llllllllllllllvvvvvvvvvvvvv++vllllllllxx.0.x..xx
xxxxlxx..xxxx1..1.x........0......lllllllllllllxxxxxllxxxll.3llllllllllxxxxx..xx
xxxxlx.1xx{.x.........x............llllllllllllllxxllllllll..llllllllllllxxxx.xx
xxxxlx..xxx.xl1..1..............2>.lllllllllllllllllllll.......lllllllllllxxx..x
xxxllxx..1.xxl.........0.....0.....lllllxllllllllllll...x..........lllllllxx...x
xxxlllxx..xxlll1...x.............xlllllxxllllllllllx........x...xll.llllllx[...x
xxlllllxxxxlllllll........0....llxxllxxxllllllllll.....x...xx6.6xlll.lllllxxx..x
xllllllllllllllllllll....x.llll.llxxxxxxllllllllllx...xx6.6xxx..xxll.llllllxxxxx
xllllllllllllllllllllllllllllll.llllxxlllllllllllxx6.6xx..xxxxxxxxlll.lllllllxxx
xlllllllllllllllllllllllllllllll.llllllllllllllllxxxx.xxxxxxxxxxxxllll.lllllllxx
xxllllllllllllllllllllllllllllll.lllllllllllllllxxxxxxxxxxxxxxxxxllllll..lllllxx
xxxllllllllllxxllllllllllllllllxx.llllllllllllllxxxxxxxxxxxxxxxxllllllx..ll...xx
xxxlllllllllxxxxllllllllllllllxxxx...llllllllllllxxxxxxxxxxxxxllllllll..3..lll<x
xxlllllllll..>xxlllllllvvvvvvvvvvxxx3.xxxllllllllllxxxxxxlllllllllllllll..lllllx
xlllllllll....xxllllllxv..0....*vxxxxxxvvvvvvvvlllllllllllllllllllllllllllllllxx
xlllllllll..3..xllllllxv..0...4*vxxxxxxv.4v...vlllllllvvv7vvv7vvv7vvv7vvv7vvvlxx
xxlllllllll....llllllxxv..vvvvvvvvvvvvvv..v.}.vxllllllvvvvvvvvvvvvvvvvvvvvvvvllx
xxllllllllll..lllllllxxv...........0......v...vxllllllvv.4***||v...v9.**|||vvllx
xxllllllllllllllllllllxv....0.0.....0.....v...vxxlllll7v4.***||v.>.v...***|v7llx
xxxllllllllllllllllllllv.vv.....vvvvvvvv......vxxlllllvv..vvvvvv...vv....*vvvllx
xxxllllllllllllllllllllv..v.0.0.vlxxxxxv......vxllllllvv4......vv+vv.......vvlxx
xxxllllllllxxllllllllllv4<v.....vllllxxvvvvvvvvlllllllvv...4...v...v.......vvlxx
xxxlllllllxxxxxllllllllvvvv..vvvvlllllxxxlllllllllllll7vvvvv..vv...vvvv++vvv7lxx
xxllllllllxxxxllllllllllll..3.llllllllllllllllllllllllvv4.....v6...6v8....8vvlxx
xllllllllllxxllllllllllllll..llllllllllllllllllllxxlllvv......v.....v......vvllx
xllllllllllllllllllllllllllll.llllllllllllllllllxxxxllvv......v6...6v......vvllx
xxlllllllllllllllllllllllllll.lllllllllllllllllxxxxxll7v......v.....v8....8v7llx
xxllllllllllllllllllllllllllx.xllllll...lllllllxxxxlllvv.....4v6...6vvv++vvvvllx
xlllllllllllllllxxlllllllll...xxllll..0....xlllllxxlllvv..vvvvvv...vv......vvlxx
xllllllllllllllxxxxlllllll.xxx.xll......xx....llllllllvv.......v...v.......vvlxx
xllllllxxllllllxxvvvvllll.lxxxl...lll..xx...0..lllllll7v..............v..v.v7lxx
xxlllllxxxlllllllv00v.l..llllllllllll...0......lllllllvv.......v...v.......vvlxx
xxlllllllllllllllv00v..lllllllllllllll.........lllllllvv......vv...vv......vvlxx
xxxllllllllllllllvvvVvvvlllllllllllllll..x....llllllllvv1vvvv1v3...3v.v..v.vvlxx
xxx...lllllllllllv**v00vlllllllllllllll..0..xlllllllll7v1v**v1v.....v......v7lxx
xxx1....lllllllllv**v00vllllllllllllllll.....lllllllllvv1....1v.....v4....4vvlxx
xxxx.x.1.llllllllvvvvvvvllllllllllllllxx...0..lllllllxvvvvvvvvv.....vvvvvvvvvllx
xxxx1..xxxxxlllllllxxxxxllllllllllllllxxxx....llllllxxvvv7vvv7v+++++v7vvv7vvvllx
xxxx..xxxxxxxxllllllxxllllllllxxxllllllxxxx..llllllxxxxxlllllll8...8lllllllllllx
xxxx.3xxxxxxxxxxlllllllllllllxxxxxllllllxx.llllllllxxxxlllllllll8.8llllllllllllx
xxx...xxxxxxxxxxxllllllllllllxxxxxlllllll.lllllllllllxxlllllllllllllllllllllllxx
xxx..xxxxxxxxxxxxxlllllllllllllxxxlllllll.llllllllllllllllllllllllllllllllllllxx
xx...xxx>%%xxxxxxxllllllllllllllllllllll.lllllllllllllllllllllllllllllllllllllxx
xx...xxxx%%.xxxxxxlllllllllllllllllllllll.lllllllllllllllllxxlllllllxxxxxllllllx
x.1..xxxxxx.xxxxxllllllllxxllllllllllllll.lllllllllllllllllxxxxllllxxxxxxxxllllx
x.....xx...xxxxxlllllllllxxxlllllllllllll.llllll....lllllllxxxlllllxxx$$$xlxlllx
x.x.1...xxxxxxxxlllllllll..xllllllllllll.lllllll.0....lllllllllllllxx$$$$$xxllxx
x......xxxxxxxxlllllll...x......llllllll.llllll.x...0.llllllllllllxxx$>$$xxxllxx
x.1..x..xxxxxxlllllll.ll.....lll.lllll...xlllll...>...llllllllllllxx$$$$xxxlllxx
xx........llll''''ll'lll.v++vllll..l...3...ll...0...x.llllllllllllxxx$$xxxxlllxx
xx.1..........llll''lllvvv..vvvllll....x.....xx....0..xxlllllllllllxxxxxxxlllxxx
xxx........xllllllllllvv......vvllllxx...xxxxxxx..x..xxxxxllllllllllxxxllllllxxx
xxxx..x.1..xxlllllllllv.0......vlllllxxxxxxxxxxxx....xxxxlllllllllllllllllllxxxx
xxx.......1.xxlllllllvv...vv0..vvlllllxxxxllxxxxxx.xxxxllllllllllllllllllllxxxxx
xxx.x..3....xxlllllllv...v%%v..0vllllllllllllllxxxxxxxlllllllllllllllllllxxxxxxx
xx..x.......xlllllllxv0..v%%v...vllllllllllllllllxxxxlll..1.llll..1.llllxxx...xx
xx.xx...x..llllllllxxvv...vv...vvllllllllllllllllllllll1llll.1...x....lxxx).x..x
x.xxxx1...lllllllllxxxv..0.....vllllllllllll.xlllllll..llllllll.1....xxxxxxxxx1x
x.xxxxxx..lllllllllllxvv.....0vvlllllllllll....lll..1lllllllllll..xx1....xxx1..x
x.xxxxxxxllllllllllllllvvv..vvvllllllllllll..5....llllllllllllxxxxxxxx...1....xx
xx(xxxxxxxxllllllllllllllv++v...lllllllllxxxx<..llllllllllllxxxxxxxxxxxxx..1.xxx
xxxxxxxxxxxxxxxllllxxxlll........llllxxxxxxxxx...llllllllxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# The New Box Level (dp)
#
NAME:    box_level_dp
DEPTH:   Depths:2-, !Depths:$
ORIENT:  encompass
TAGS:    no_pool_fixup no_rotate uniq_d_encompass
NSUBST:  A = 1:+ / *:b
NSUBST:  B = 1:+ / *:b
NSUBST:  C = 1:+ / *:b
NSUBST:  D = 1:+ / *:b
NSUBST:  E = 1:+ / *:x
NSUBST:  N = 1:= / *:n
KFEAT:   ^ = teleport trap / Zot trap
SUBST:   ':''l. , '=l.
SUBST:   ":""W. , "=Ww..
ITEM:    ring of teleportation
ITEM:    royal jelly / honeycomb w:20 / nothing w:30
ITEM:    scroll of teleportation
ITEM:    amulet of stasis
NSUBST:  T = 1:. / *:T
KFEAT:   T = known teleport trap
KMONS:   w = water elemental / elemental wellspring w:1 / \
             merfolk javelineer w:2 / merfolk impaler w:2 / \
             octopode w:3 / nothing w:300
KFEAT:   w = w
KMONS:   l = fire vortex / lava snake / lava fish / fire elemental w:1 /\
             nothing w:50
KFEAT:   l = l
KMONS:   z = killer bee
KITEM:   z = royal jelly / honeycomb w:20 / nothing w:120
: if crawl.one_chance_in(3) then
KMONS:   ! = orc / orc warrior / orc knight / nothing w:30
KMONS:   & = orc warlord / orc knight / nothing w:20
: elseif crawl.coinflip() then
KMONS:   ! = yaktaur / nothing
KMONS:   & = yaktaur captain / nothing
: else
KMONS:   ! = naga / nothing
KMONS:   & = naga warrior / greater naga
: end
NSUBST:  * = 1:g / *:*
SUBST:   * = * % % $ $ f
SUBST:   ; = 9 0
SUBST:   !=%$, &=*%
KITEM:   V = *
COLOUR:  a = yellow
TILE:    a = wall_wax
SUBST:   a = x
: if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
KFEAT:   V = enter_vaults
: else
KFEAT:   V = floor
: end
MARKER:  P = lua:props_marker { portal=1 }
KFEAT:   P = stone_arch
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.......xccccccxxxxxxx.........................................w"..xxx
x.(...{.xc....cbbbbbxx................xxx...................."w......x
x.......++.U..++.].++.....'''....'l'....xxx................."ww"..)..x
x...[...xc....cbbbbbxx...'''..''lllll'....xx...vvEvvEv......"ww"....xx
x.......xcccccccxxxxxx.......'llllllll.....x...vd...9v...."www".....xx
xxxxxxxxxxxxxxxxxxxxxx.......'lllllll'.....x...E.....E.."ww.w".....xxx
x.............................'lllll'.....xx...v9...Vv.www..w".....xxx
x....""w.............".xxx......'ll'......x....vvEEEEvwww.www.....xxxx
x.."wwwww"..........w....xxxx............xx....."wwwwwwww.ww"....xxxxx
x.wwwwwwww"........w........xxxxxxx....xxx........"www"...ww"...xxxxxx
x..wwwwwww"......"ww......""......xxxxxx....."...........ww"....xxxxxx
xx..""ww".......www....."wwwwwwwwww........."www"......wwwww"....xxxxx
xxx.........."wwww"..""wwwwwwwwwwwwwwwwww".www"...wwwwwwwwwwww.....xxx
xxxxx....""wwwwwwwwWWwwwwww.......wwwwwwwwwwwwwwwwwwwwwww.....xx....xx
xxx..""wwwwwwwwwwwwWWww"......G.G......"wwwwxxww".........xxxxx.....xx
x."wwwwwwwwwwwwwxxx__xxxxxxxxxx+xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx....x
xwwwwwwww".."wwwxww.."w........................................>xx...x
xxwwww""....."xwxw"...ww........................................xx...x
xxww".........xxxw..............................................xx..xx
xww............xx"w...ccccccccccccccccccNcccccccccccccccccc.....xx..xx
xw"...xxx.......x..w..c^..*...*...*...*...*...*...*...*...c.....x...xx
xw....x!x..xxx..x..w..c...................................c.....x...xx
xxx...x+x..x!x..x.....c*..;...;...;...;...;...;...;...;..*c.....xx..xx
xxx....1..1+!x..x.....c...................................c.....xx...x
x..........xxx.xx.....c.......bbbbbbbbbbTbbbbbbbbbb.......c.....xx..xx
x..xxxx........xx.....c.......b.........b.........b.......c.....x...xx
x..x!!+1..xxxx..x.....c*..;...b.bbbAbbb.b.bbbBbbb.b...;..*c.....x..xxx
x..xxxx..1+!!x..x.....c.......b.b8...|b.b.b|...8b.b.......c.....xx.xxx
x.........xxxx..x.....c.......b.A|.P.|A.b.B|.P.|B.b.......c.....xx.xxx
x.......1.......x.....c.......b.b|...8b.b.b8...|b.b.......c.....x...xx
xxx....121....xxx.....c*..;...b.bbbAbbn.b.nbbBbbb.b...;..*c.....x...xx
x!x...cc+cc...xxx.....c.......b.........b.........b.......c.....xx..xx
x&+1..c!2!c...xxx.....N.......Tbbbbbbbbb.bbbbbbbbbT.......N.....x..xxx
x!x...c&&&c...xxx.....c.......b.........b.........b.......c.....x..xxx
xxx...ccccc...xxx.....c*..;...b.bbbDbbn.b.nbbCbbb.b...;..*c.....x.xxxx
x...............x.....c.......b.b|...8b.b.b|...8b.b.......c.....x.xxxx
xx...1...xxxx...x.....c.......b.D|.P.|D.b.C|.P.|C.b.......c.....x..x.x
xx..x+x.1+!!x...x.....c.......b.b8...|b.b.b8...|b.b.......c.....xx...x
x...x!x..xxxx...x.....c*..;...b.bbbDbbb.b.bbbCbbb.b...;..*c.....xxx.xx
x...xxx.........x.....c.......b.........b.........b.......c.....xxx.xx
x.........xxxxxxx.....c.......bbbbbbbbbbTbbbbbbbbbb.......c.....xxx..x
xxxx......+*9*9*+.....c...................................c.....xxxx.x
xxxx..xx..xxxxxxx.....c*..;...;...;...;...;...;...;...;..*c.....x....x
x.....xx........x.....c...................................c.....x.xx.x
x.xx.....xxx....x.....c...*...*...*...*...*...*...*...*..^c.....x.xx+x
x.xx....1+!x..xxx.....ccccccccccccccccccNcccccccccccccccccc.....xxx..x
x....xx..xxx..xxx...............................................xx..xx
x....xx.......xxx.........................................z..z..x....x
xx............xxx<.............................................zxa...x
xx..x......xx...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxazzxx
xxx........xx.....xxx...xx.x..xx...xxaaaaaaaeaaea.a..aaaaaaaaaaaaazzxx
xxxx..xx..............x...x...xx.xx.xaaazazzaaza.a.aa.za.z.aaaa.a.aazx
xxxxxxxxxxx....x..xx.x.xx.x..x..xx}..xaeazaazzazzaa..aa..aa....z.aeaxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# A mockup of the Forest branch (1KB)
#
NAME:    spriggan_forest
DEPTH:   Depths:2-, !Depths:$
ORIENT:  encompass
TAGS:    no_pool_fixup uniq_d_encompass
MONS:    spriggan, spriggan rider, spriggan druid, spriggan berserker
MONS:    spriggan air mage, spriggan defender, the Enchantress
SUBST:   . = .:300 / 1:2 / 2:1 / 3:1 / 4:1 / 5:1
SUBST:   T = t / t / P / .
KMONS:   P = plant / plant / bush / nothing
KMONS:   R = plant
KMONS:   Q = bush
KMONS:   e = bat skeleton
KMONS:   K = giant firefly
KITEM:   f = dagger / short sword / sabre / flail / whip / blowgun / bow
KITEM:   g = robe / cloak / cap / buckler / steam dragon armour w:1
KMONS:   L = spriggan ; robe . dagger
KITEM:   h = bread ration q:2
KMONS:   M = yak
KMONS:   N = spriggan ; robe . quarterstaff
KITEM:   i = cheese q:3
COLOUR:  "MN = green
COLOUR:  C = white
KFEAT:   C = stone_wall
TILE:    C = wall_marble
KFEAT:   _ = altar_trog
: _G.set_random_mon_list("place:Forest:3")
veto {{
  return crawl.game_started() and dgn.br_exists("Forest")
}}
MAP
tttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttttt
ttttPP.Tttt...tttttttttttttttttttttttttttttttttttttttttttttttttTTTTTttTTTTtttttt
tccccT.....ttt..ttttttttttttttttttttttttttttttTtTTtttttttQTtttTT.T.Ttt.T.TTTtttt
tc%4c...Tttttttt.ttttttttttttttttttttttttttttT*....QQPtt..ccccc....cccc.TT.Ttttt
tc..+..Ttttttttt..ttttttttttttttttttttttCCCCCCCCCCCCCC....c..1c....c.1c.t.TTtttt
tc.4c...Ttttttt.t..tttttttttttttttttttttC.6.C6..6C$*|CtT..c.1.+....+..cT.TT.tttt
tcccct.Tttttt..tttt........tttttttttttttC7..+....+$*|Ctt..ccccc....cccc.TT.Ttttt
tttT..tttttt.tttttttttttttt.ttttttttttttC.6.C6..6C$*|CttQ..................ttttt
ttTPTTtttt...ttttttttttttttt..ttttttttttCCCCCC++CCCCCCttttccccc....cccc......ttt
ttTTtttttt..ttttttttttttttttt.ttttttttttCgggC....CfffCttttchxLc....c1.c.....tttt
tttttttt...ttttttttttttttttttt.tttttttttCggg+..1.+fffCttttch..+....+.5c..1...ttt
tttttt.tttt..ttttttttttttttttt.tttttttttCgggC1...CfffCttttccccc....cccc...1.tttt
tttttt.ttttt.tttttttttttttt.....ttttttttCCCCCC++CCCCCCttttt...................tt
ttttt.ttttt..ttttttttttt...tttT..tttttttCR.....1....RCccccccccc....Y.........ttt
ttt..tttttt..ttttttttt..tttttttt.tttttttC..1........1CcKxKxKxKc......2......tttt
tt.ttttttt.tttttttt...ttttttttttt.ttttttCR1..5..5..1RCc.22.1..c.....22......tttt
tt.tttttt.ttttttt...1.tttttttttttt.tttttCCCCCC++CCCCCCc+ccccccc....2.2.....ttttt
ttt.tttt..tttttt...22..tttttttttttt.tttt..................................tttttt
tttt..[t.ttttttt..1..2.)tttttttttttt.....................................ttttttt
ttttt....ttttttt.t.1...tttttttttttt.tt..TTttttt............Y..........ttt.tttttt
ttttttt..tttttt.tttttttttttttttttt.tttttttttttttttttt...............tttttt...ttt
ttttttttt..ttt.tttttttttttttttt....ttttttttttttttttttttt.........tttttttttttt(tt
ttttttttttt...tttttttttttttttt.tt..ttttttttttttttttttttttttt....tttttttttttttttt
tttttttttttttt.tttttttttttttt.tttt.tttttttttttttttttttttttt.tt...ttttttttttttttt
tttttttttttttt.ttttttttttttt.WWWWt.ttttttttttttttttttttttt.ttttt...ttttttttttttt
ttttttttttttt.tttttttttttttt.Wwwwt.ttttttttttttttttttttt..ttttttt..ttttttttttttt
tttttttttttttt.ttttttttttttt.tWWWWt..tttttttttttttttt.t.ttttttttt..ttttttttttttt
tttttttttttttT.ttttttttttttt.ttWwwwwt.tttttttttttttttt.ttttttttttt..tttttttttttt
ttttttttttttt..ttttttttttttt.tttwwwwww.ttttttttttttttt.ttttttttttt..tttttttttttt
tttttttttttt...tttttttttttt.tttwwwwwwww.....tttttttttt.ttttttttttt.ttttttttttttt
ttttttttt.....ttttttttttttt.ttttwwwwwwwwwwwt.tttttttt.ttttttttttt.t.tttttttttttt
ttttttt..tt..tttttttttttttt.ttwwwwwwwwwwwwwwt.tttttt.ttttttttttt..tt....tttttttt
ttttT..tttttttttttttttttttt.ttwwwwwwwwwwwwwwt.......tttttttttttt.ttttttt...ttttt
tttt....tttttttttttttttttttt.wwwwwwwwwwwwwwww..tttttttttttttttt.ttttttttt.33tttt
ttt....T..3...tttttttttttttt.wwwwwwwwwwwwwwwt.ttttttttttttttttt.ttttttttt3..tttt
tttT...ttttttt....tttttttttt.wwwwwwwwwwwwwwt.ttttttttttttttttt.ttttttttttt..tttt
ttt..]..ttttttttt......ttttt.Wwwwwwwwwwwwtt.tttttttttttttttttt.ttttttttttt.ttttt
ttt........tttttttttttt.......WWwwwwwwwttt.tttttttttttttttt...tttttttttttttttttt
tttt..111.tttttttttttttttttttt...........ttttttttttttt.....ttttttttttttttt..tttt
ttttT.1...tttttttttttttttttttttttt.tttttttttttttttttt.ttt.ttttttttttttttttt.tttt
ttttTT.1....ttttttttttttttttttttt.tttttttttttttttttt.ttttt.ttttttttttttttt..tttt
ttttttt.tttt...ttttttttttttttttt..tttttttttttttttt..ttttttt..ttttttttttttt.ttttt
tttttt.tttttttt...ttttttttttttt.tttttttttttttttt..ttttttttttt....tttttttt.tttttt
ttttt..tttttttttt.ttttttttttttt.tttttttttttttttt.ttttttttttttttt.........ttttttt
ttttt.ttttttttttt.tttttttttttt.3.tttttttttttttt.ttttttttttttttt.ttt..t..tttttttt
tttt..tttttttttt..tttttttttttt3G3ttttttttttttt.tttttt.tttttttt.ttttttt....tttttt
ttttt.ttttttttt..ttttttttttttt.3.tttttttttttt.t.tttttt.....tt.tttttttttt...ttttt
tttt.tttttttttt.t..tttttttttttttttttttttttt..ttt.tttttttttt...ttttttttttt..ttttt
tttt.ttttttttt..ttt...ttttttttttttttttttttt.ttttt.ttttttttwwwtttttttttttt...tttt
ttttt.tttttttt.tttttt...tttttttttttttttt...tttttt.tttttttttwwttttttttttttt..tttt
ttttt.tttttttt{tttttttt......ttttttttt.....ttttt.ttttttttttttttttttttttttt.ttttt
tttt.tttttttttttttttttttTT..t...tttt..tttttttttt.tttttttttttttttttttttttt.tttttt
tttt.ttttttttttttttttttttt.ttttt....ttttttttttttt.ttttttttttttttttttttttt.cccctt
ttttt.ttttttttttttttttttt.tttttttttt.tttttttttttt..tttttttt..tt...ttttttt.+.1ctt
tttttt.ttttttttttttttttt.tttttttttt..tttttttttttttt.tttttt.t...ttt..tttttTc1%ctt
ttttttt.ttttttttttttttt.ttttttttttt....xxxxxtttttttt..tttt...tttttt.ttttttcccctt
ttttttt.tttttttttttttt.ttttttttttttT...x..4xtttttttttt....ttttttttt.tttttttxxxxx
ttttttt.tttttttttttttt.ttttttttttttT...+...xtttttttttttttttttttttttt.tttxxxxxxxx
txxxtt.ttttttttttt....ttttttttttttttt.tx..4xttttttttttttttttttttttttt..xxxxxxxxx
txi+"""""""tt......t.....ttttttttttttt.x.4_xttttttttttttttttttttttttt....xxxxxxx
txxx""""""""".tttttttt..TTtttttttttttttxxxxxttttttttttttttttttttttt......}xxxxxx
ttt""N"""""""TTtttttttt....ttttttttttttttttttttttttttttttttttttt.....xxxxxxxxxxx
ttT""MM"MM"TTTttttttttttttt..tttttttttttttttttttttt...ttttttttt....xxxxxxxxxxxxx
tt""""M""""Tttttttttttttttttt..ttt.......ttttttttt.......tttt...xxxxxxxxexxxexxx
tt".MM""NTTtttttttttttttttttttt....tttt.......ttt...ttttt.......xxxxx..exxxxeexx
tt""M""""Ttttttttttttttttttttttttttttttt...ttt..t..ttttttttxxxxx......xx.exx..xx
ttt""""TTTtttttttttttttttttttttttttttttttttttttt.tttttttxxxxxxxxxx....xxx.1.xxxx
tttttttttttttttttttttttttttttttttttttttttttttttttttttttttxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Zero! Nine! Eight!
#
NAME:    hangedman_slaughterboxes
TAGS:    no_pool_fixup no_monster_gen no_item_gen patrolling uniq_d_encompass
ORIENT:  encompass
DEPTH:   Depths:2-, !Depths:$
MONS:    butterfly, plant
KMONS:   S = 0 band
KMONS:   # = 9 band
KFEAT:   ^ = stone_arch
KFEAT:   - = enter_vaults
SHUFFLE: :;, `~, 34, 67, !?, XZ / yz
SUBST:   % = %:40 $:20 ., * = *:30 %, | = |:30 *
SHUFFLE: ABCDE / BADCE / AGHIC / ACGHE, BDF
SUBST:   ACF = ., BDGHI = x, E : .x
SHUFFLE: JKLMN / KJMLN / JPQRL / JLPQN, KMO
SUBST:   JLO = ., KMPQR = x, N : .x
SHUFFLE: cdefg / dcfeg / cijke / ceijg, dfh
SUBST:   ceh = ., dfijk = x, g : .x
SHUFFLE: nopqr / onqpr / ntuvp / nptur, oqs
SUBST:   nps = ., oqtuv = x, r : .x
SUBST:   X = xx', y= x'', Z = xx., z = x..
SUBST:   ;` = ., :~ = x, T = TTV, U = TUU
SUBST:   7? = ., 36! = 0, Y : 09, 5 = 09, 4 = 9
SUBST:   = : ttx2, " : WW', _  = C, @ = G
NSUBST:  ' = 7:S / 2:# / 6 = S:2 #:1 .:2 / *:.
: if you.absdepth() > 20 then
SUBST:   & : a:45 c:8 vb x:2, a : c:15 vb
MARKER:  ^ = lua:props_marker { portal=1 }
: else
SUBST:   & : a:80 x:15 c:3 v:1 b:1, a : c:38 v:2 b:2
:    if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
NSUBST:  ^ = 1:- / *:^
:    end
: end
MAP
   xxxxxxxxxxxxxxx      xxxxx       xxxxx
  xxxxxxxxxxxxxxxxx    xxxxxxx    xxxxxxaaaaaaaaaaaaaaa
 xxxZZZZZZZZZZZZxxxxxxxxxXXXxxxxxxxxxZZaaaaaaaaaaaaaaaaa
xxxZ............ZxxxxxxXX'''XXxxxxxZZ..aaalalalalalalaaaa
xxZ..`x~x`x~x`x..ZZXXXX'''x'''XXZZZ....aalllllllllllllaaaa
xxZ.`x........`....'''''''x''''''......aal%%%%*****|||laaa
xxZ.x..~xxx`x.x....'x'''x'''x'''x...{..aal%........38|llaa
xxZ.~.~x''''`.~...x'''x'''x'''x''.x..[.aal%.&4&&..&&3|laaa
xxZ.x.x'`~`'x.x....'x'''x'''x'''x...(..aal%.48&&.&&&.*llaa
xxZ.`.x'~0~'x.`....''''''''''''''......aal%.&&&...&..*laaa
xxZ.x.x'`~`'x.x..aaaaaaaaaaaaaaaaaaaaaaaal%.&&.|.....*llaa
xxZ.~.`''''x~.~.aaaaraaaaaaaEaaaaaaaaaaal&&.....|.&&.*laaa
xxZ.x.x`xxx~..x.aa9...0.&.0..67aalllllll&&&+.....&&&.%llaa
xxZ.`........x`.aa.nop.&+&.ADC6aal%%%%%%&&.m+...&&84.%laaa
xxZ.x`x~x`x~x`..ar.qrq..&..BEB.Eal%90&...+m.m+..&&4&.%llaa
xxZ.............aa.pon.&+&.CDA.aal%0.&.0..+m.m+......%laaa
xxxZ.......aaaaaaa....9.&.....0aal%&&......+m.&&%%%%%%llaa
 xxxZ.....aaaaNaaaa.a.aaaa.&.&.aal%......<..+&&&lllllllaaa
 xxxZ.BAD.aa9....aaa+a+a0a&+&+&aaaaa+a+a5....&&laaaaaaaaaax
  xxX'CEA'aa.JKL..aaa....a.&.&.aaaa.a.aaa..0.%laaaaaaaaaaxx
  xxX'DCB'aN.MNM.a+acde..a...!?.&......a+....%laa.......Zxx
  xxX'''''aa.LKJ..a.fgf.%a.nop!&+&.]1U..a..&&%laa..)....Zxx
  xxX'''''aa....9a+.edc0%a.qrq..&..1@1.a+.&.0%laa.>.>..Zxxx
  xxX'LKJ'aa.&.&.aa...08*a.pon.&+&.U1}..a.&09%laa......Zxx
  xxX'MNM'aa&+&+&a0..%%*aa9.....&......aa%%%%%laa..x..Zxxx
 xxxX'JKL'aa.&.&.aaaaaaaa+aaaaaaa..&.&.aallllllaa.....Xxx
 xxX''''''aa....0.&.....0aa&...+.a&+&+&aaaaaaaaaa'x'x'Xxx
 xxX''''''aa.ABC.&+&.JKL.aY&.&.a+a.&.&.aaaaaEaaaa'''''Xxx
 xxX'CBA''aE.DED..&..MNM.a..efc..a...!?.&.0..67aa''x''Xxx
 xxX'DED''aa.CBA.&+&.LKJ.a.&dgf&.a.nqp!&+&.ABC6aa''''''Xxx
 xxX'ABC''aa9.....&.0....a%0cde..a.oro..&..DED.Ea'x'x''Xxxx
 xxX''''''aaaaEaaaaa.&.&.a*80&.&&a.pqn.&+&.CBA.aa'''''''Xxx
 xxX''''''aaaaaaaaaa&+&+&a|*%..Yaa9.....&.....0aa''x'xx'Xxx
 xxX'pon''aawwwwwwaa.&.&.aaaaaaaa+aaaaaaaa.&.&.aa'''''''Xxx
 xxX'qrq''aaw2112waa....5.&.....0aa*%%..0a&+&+&aa'x'x''Xxxx
 xxX'nop''aaw..T.Wa..DCB.&+&.JML.a*80...aa.&.&.aa''''''Xxx
 xxX''''''aaw....W+a.AEC..&..KNK.a%0cfe.+a9...0aa''x''Xxx
 xxX''''''aaw.2..Wa..BAD.&+&.LMJ.a%.dgd.a..nqp.aa'''''Xxx
 xxxX'edc'aaw....W+a......&.0....a..efca+a.oro.ra'x'x'Xxx
  xxX'fgf'aaw...@waaa.a.aaaa.&.&.a....aaa..pqn.aa'''''Xxx
  xxX'cde'aaw._..wwa+a+aaaaa&+&+&a0a+a+aaa....9aa..x..Zxxx
  xxX'''''aaw....@wwWWWWwwaa.&.&.aaaa.a.aaaaraaaa.....Zxxxx
  xxX'''''.aW......@....2waa....0.&.9....aaaaaaa.......Zxxx
  xxX'cde'.+W.._.......T1waa.ADC.&+&.JML.aa.............Zxx
  xxZ'fgd'.+W....._..2..1waE.BEB..&..KNK.Na..;x:x;x:x;x.Zxx
 xxxZ.efc..aww..........2waa.CDA.&+&.LMJ.aa.;x........;.Zxx
xxxxZ......aawWWWwwwwwwwwwaa9.....&.....9aa.x..:xxx;x.x.Zxx
xxxX'''='..aaa++aaaaaaaaaaaaaaEaaaaaaaNaaaa.:.:x'''';.:.Zxx
xxX''''''........aaaaaaaaaaaaaaaaaaaaaaaaa..x.x';:;'x.x.Zxx
xxX''''''=....''''''''''''''''''''''''''....;.x':0:'x.;.Zxx
xxX'='^'''''.cde''efc''''''''''''LMJ''BAD...x.x';:;'x.x.Zxx
xxX'''""'''=.fgd''dgd''pqn''CDA''KNK''CEA...:.;''''x:.x.Zxx
xxX'''""^'''.efc''cfe''oro''BEB''JML''DCB...x.x;xxx:..x.Zxx
xxX''^''''''..'''''''''nqp''ADC'''''''''....;........x;.Zxx
xxX'''''='''ZZZXXXXXXX''''''''''XXXXXXXXZZ..x;x:x;x:x;..Zxx
xxxX'''''''XxxxxxxxxxxXXXXXXXXXXxxxxxxxxxxZ............Zxxx
 xxxXXXXXXXxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxZZZZZZZZZZZZxxx
  xxxxxxxxxxx        xxxxxxxxxxxxx       xxxxxxxxxxxxxxxx
   xxxxxxxxx                              xxxxxxxxxxxxxx
ENDMAP

################################################################################
# Profane Halls, by Grunt.
#
default-depth:

NAME: grunt_profane_halls
TAGS: no_monster_gen uniq_d_encompass
PLACE: Depths:$
DEPTH: Depths:2-
WEIGHT: 2 (Depths:$), 10
ORIENT: encompass
SHUFFLE: {[(<
: if you.absdepth() == 27 then
SUBST: }])> = G
: else
SHUFFLE: }])>
: end
SUBVAULT: d : grunt_profane_temple
SUBVAULT: e : grunt_profane_temple
SUBVAULT: f : grunt_profane_temple
SUBVAULT: g : grunt_profane_temple
NSUBST: D = 1:K / 1:L / 1:M / 1:N
: grunt_profane_halls_setup(_G)
: if you.absdepth() == 27 then
KMONS: K = storm dragon
KMONS: L = shadow dragon
KMONS: M = quicksilver dragon
KMONS: N = golden dragon
: else
KMONS: K = deep elf blademaster
KMONS: L = deep elf master archer
KMONS: M = deep elf death mage
KMONS: N = deep elf sorcerer
: end
KMONS: 0 = 0 band w:15 / nothing
KMONS: 9 = 9 band / 0 band
KMASK: 1290KLMN = !no_monster_gen
: if you.absdepth() == 27 then
SUBVAULT: H : grunt_zot_portal
: else
SUBVAULT: H : grunt_profane_loot
: end
MAP
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccdddddddddddddddcccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccdddddddddddddddcccccccccccccccccccccccccccccccc
ccccccccccccccc.ccccccccccccccccdddddddddddddddcccccccccccccccc.ccccccccccccccc
cccccccccccccc..ccccccccccccccccdddddddddddddddcccccccccccccccc..cccccccccccccc
ccccccccccccc.G.cccGcGcGGcGcGcccdddddddddddddddcccGcGcGGcGcGccc.G.ccccccccccccc
ccccccccc.......c..............cdddddddddddddddc..............c.......ccccccccc
ccccccccc.{.....+......0.......+ddddddddddddddd+.......0......+.....[.ccccccccc
ccccccccc.......+......0.......+ddddddddddddddd+.......0......+.......ccccccccc
ccccccccc...U...c..............cdddddddddddddddc..............c...U...ccccccccc
cccccccc........cccGcGcGGcGcGcccdddddddddddddddcccGcGcGGcGcGccc........cccccccc
ccccccc.G.....}.ccccccccccccccccdddddddddddddddcccccccccccccccc.].....G.ccccccc
cccccc..........cccccccccccccccccdddddddddddddccccccccccccccccc..........cccccc
cccccccccc++ccccccccccccccccc...cdddddddddddddc...ccccccccccccccccc++cccccccccc
ccccccccc....ccccccccccccc......+ddddddddddddd+......ccccccccccccc....ccccccccc
ccccccccG....Gccccccccccc.......++ddddddddddd++.......cccccccccccG....Gcccccccc
ccccccccc....cccccccccc%...9.....+ddddddddddd+.....9...%cccccccccc....ccccccccc
ccccccccG....GccccccccU*.........ccdddddddddcc.........*UccccccccG....Gcccccccc
ccccccccc.00.cccccccc%*.......cccccdddddddddccccc.......*%cccccccc.00.ccccccccc
ccccccccG....Gcccccc........ccc%%.ccdddddddcc.%%ccc........ccccccG....Gcccccccc
ccccccccc....cccccc.......++c......ccdddddcc......c++.......cccccc....ccccccccc
ccccccccG....Gcccc...9...c+.........ccccccc.........+c...9...ccccG....Gcccccccc
ccccccccc....cccc.......cc........1....:....1........cc.......cccc....ccccccccc
cccccccccc++ccccc......cc..............1..............cc......ccccc++cccccccccc
cccceeeeeeeeeeecc++...cc.....G....1..G...G..1....G.....cc...++ccfffffffffffcccc
cccceeeeeeeeeeeeee++ccc%...........G...D...G...........%ccc++ffffffffffffffcccc
cccceeeeeeeeeeeeeeeeccc%.....1..1.2.........2.1..1.....%cccffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeecc.........G.:%HHHHH%:.G.........ccffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeecc..........%HHHHHHH%..........ccfffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec.......G..HHHHHHHHH..G.......cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec..........HHHHHHHHH..........cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec:.G..1..D.HHHHHHHHH.D..1..G.:cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec..........HHHHHHHHH..........cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeeec.......G..HHHHHHHHH..G.......cffffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeeecc..........%HHHHHHH%..........ccfffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeeecc.........G.:%HHHHH%:.G.........ccffffffffffffffffffcccc
cccceeeeeeeeeeeeeeeeccc%.....1..1.2.........2.1..1.....%cccffffffffffffffffcccc
cccceeeeeeeeeeeeee++ccc%...........G...D...G...........%ccc++ffffffffffffffcccc
cccceeeeeeeeeeecc++...cc.....G....1..G...G..1....G.....cc...++ccfffffffffffcccc
cccccccccc++ccccc......cc..............1..............cc......ccccc++cccccccccc
ccccccccc....cccc.......cc........1....:....1........cc.......cccc....ccccccccc
ccccccccG....Gcccc...9...c+.........ccccccc.........+c...9...ccccG....Gcccccccc
ccccccccc....cccccc.......++c......ccgggggcc......c++.......cccccc....ccccccccc
ccccccccG....Gcccccc........ccc%%.ccgggggggcc.%%ccc........ccccccG....Gcccccccc
ccccccccc.00.cccccccc%*.......cccccgggggggggccccc.......*%cccccccc.00.ccccccccc
ccccccccG....GccccccccU*.........ccgggggggggcc.........*UccccccccG....Gcccccccc
ccccccccc....cccccccccc%...9.....+ggggggggggg+.....9...%cccccccccc....ccccccccc
ccccccccG....Gccccccccccc.......++ggggggggggg++.......cccccccccccG....Gcccccccc
ccccccccc....ccccccccccccc......+ggggggggggggg+......ccccccccccccc....ccccccccc
cccccccccc++ccccccccccccccccc...cgggggggggggggc...ccccccccccccccccc++cccccccccc
cccccc..........cccccccccccccccccgggggggggggggccccccccccccccccc..........cccccc
ccccccc.G.....).ccccccccccccccccgggggggggggggggcccccccccccccccc.>.....G.ccccccc
cccccccc........cccGcGcGGcGcGcccgggggggggggggggccGcGccGGcGcGccc........cccccccc
ccccccccc...U...c..............cgggggggggggggggc..............c...U...ccccccccc
ccccccccc.......+......0.......+ggggggggggggggg+.......0......+.......ccccccccc
ccccccccc.(.....+......0.......+ggggggggggggggg+.......0......+.....<.ccccccccc
ccccccccc.......c..............cgggggggggggggggc..............c.......ccccccccc
ccccccccccccc.G.cccGcGcGGcGcGcccgggggggggggggggccGcGccGGcGcGccc.G.ccccccccccccc
cccccccccccccc..ccccccccccccccccgggggggggggggggcccccccccccccccc..cccccccccccccc
ccccccccccccccc.ccccccccccccccccgggggggggggggggcccccccccccccccc.ccccccccccccccc
ccccccccccccccccccccccccccccccccgggggggggggggggcccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccgggggggggggggggcccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

# Here just in case people want to see the original base layout.
NAME: grunt_profane_temple_base
TAGS: grunt_profane_temple allow_dup
WEIGHT: 0
: grunt_profane_halls_setup(_G)
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
%%...........%%
%G...........G%
...............
......1.1......
...............
.......U.......
...............
..1.........1..
...............
....1.....1....
.....G...G.....
 .............
 ....G...G....
 .............
  ...G...G...
  ...........
   ..G...G..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_geh
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
NSUBST: 5 = 1:6 / *:5
KMONS: 4 = red devil / hell hound / flaming corpse w:5
KMONS: 5 = sun demon
KMONS: 6 = hellion / balrug
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
ll...l%%%l...ll
l....l%%%l....l
...............
..ll...5...ll..
..l4..lll..4l..
..l..lllll..l..
..l.5lllll5.l..
..l..lllll..l..
..l4..lll..4l..
..ll...5...ll..
...............
 ....l...l....
 ....l4.4l....
 ....l...l....
  ...l4.4l...
  ...l...l...
   ..l4.4l..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_coc
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
NSUBST: 5 = 1:6 / *:5
KMONS: 4 = blue devil / simulacrum w:20 / freezing wraith
KMONS: 5 = ice devil
KMONS: 6 = blizzard demon / ice dragon
KMONS: 8 = ice statue
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
wwww.......wwww
www....5....www
ww...........ww
w...4.www.4...w
.....wwwww.....
....ww%%%ww....
..4.ww%8%ww.4..
....ww%%%ww....
.....wwwww.....
....4.www.4....
...W.......W...
 ..WW.5.5.WW..
 ...W.....W...
 ...WW...WW...
  ..4W4.4W4..
  ...W...W...
   .........
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_tar
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
NSUBST: 5 = 1:6 / *:5
KMONS: 4 = skeletal warrior w:5 / skeleton w:20 / zombie w:20 / flying skull band
KMONS: 5 = soul eater
KMONS: 6 = tormentor / reaper / lich w:5 / bone dragon w:2
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
KMASK: 12345 = !no_monster_gen
MAP
.%%%.......%%%.
.ccc...4...ccc.
...............
.4....ccc....4.
.....c...c.....
...4..5V5..4...
.....c...c.....
.4....ccc....4.
...............
.ccc...4...ccc.
..5..G...G..5..
 .....4.4.....
 ....G...G....
 .............
  ...G...G...
  ...........
   ..G...G..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_temple_dis
TAGS: grunt_profane_temple no_monster_gen
: grunt_profane_halls_setup(_G)
NSUBST: 5 = 1:6 / *:5
KMONS: 4 = iron devil
KMONS: 5 = skeletal warrior
KMONS: 6 = iron dragon / iron troll / iron troll
KFEAT: A = stone_arch
MARKER: A = lua:props_marker { portal=1 }
COLOUR: G = cyan
TILE: G = dngn_statue_iron_golem
: set_feature_name("granite_statue", "iron statue")
KMASK: 12345 = !no_monster_gen
MAP
%%.5.......5.%%
%G...........G%
.....4...4.....
....4.v.v.4....
.....v...v.....
...4...G...4...
.....v...v.....
....4.v.v.4....
.....4...4.....
...............
.G...G...G...G.
 ..5.......5..
 ....G...G....
 .............
  ...G...G...
  ...........
   ..G...G..
   ....B....
    .:%3%:.
     %2A2%
ENDMAP

NAME: grunt_profane_loot
TAGS: grunt_profane_loot no_monster_gen
: grunt_profane_halls_setup(_G)
SHUFFLE: }])>
SHUFFLE: KLMN
: if you.absdepth() >= 24 then
KMONS: K = storm dragon
KMONS: L = shadow dragon
KMONS: M = quicksilver dragon
KMONS: N = golden dragon
: else
KMONS: K = deep elf blademaster
KMONS: L = deep elf master archer
KMONS: M = deep elf death mage
KMONS: N = deep elf sorcerer
: end
KITEM: KLMN> = acquire any
KITEM: 12 = any
MAP
  cc+cc
 cc1%1cc
cc2%K%2cc
c1%***%1c
+%L*>*N%+
c1%***%1c
cc2%M%2cc
 cc1%1cc
  cc+cc
ENDMAP

NAME: grunt_zot_portal
TAGS: grunt_zot_portal
COLOUR: c = lightmagenta
TILE: c = wall_zot_magenta
COLOUR: JO.1+ = magenta
FTILE: JO.1+ = floor_hall
KFEAT: O = enter_zot
KMONS: 1 = place:Zot:1 band
MARKER: J = lua:fog_machine { cloud_type = "magical condensation", \
                              pow_min = 3, pow_max = 5, \
                              delay_min = 5, delay_max = 15, \
                              size = 1, walk_dist = 4, \
                              spread_rate = 70 }
MAP
  cc+cc
 cc...cc
ccJ.1.Jcc
c.......c
+.1.O.1.+
c.......c
ccJ.1.Jcc
 cc...cc
  cc+cc
ENDMAP
###############################################################################
#  This vault excercises varied old ideas for the Vaults branch:
#  a primarily humanoid-based monster set, a focus on placing groups,
#  and a pattern-based layout emphasizing lots of distinct rooms.
#  Of course, HangedMan twisted these ideas thoroughly.
#  Many thanks to elliott for writing the band placement lua!
#
NAME:    hangedman_spin_cycle
TAGS:    no_monster_gen no_item_gen no_trap_gen patrolling uniq_d_encompass
DEPTH:   Depths:2-, !Depths:$
ORIENT:  encompass
KMONS:   - = plant
KMONS:   _ = plant col:random name:demonic name_adjective tile:mons_demonic_plant
KMONS:   no = vault sentinel
KMONS:   p = giant eyeball
KMONS:   q : great orb of eyes w:7 / guardian serpent w:3
KMONS:   r = vault guard
: local bands = {
:     [42 - you.absdepth() * 2] =
:         { "cyclops", "kobold demonologist", "naga", "Azrael", "Kirke" },
:     [65 - you.absdepth() * 3] =
:         { "boggart", "deep elf fighter", "necromancer", "centaur warrior", },
:     [29 - you.absdepth()] =
:         { "orc knight", "ogre mage", "skeletal warrior",
:           "yaktaur", "spriggan rider" },
:     [you.absdepth() * 2 - 37] =
:         { "deep elf knight", "orc high priest", "deep troll shaman" },
:     [you.absdepth() * 2 - 40] =
:         { "harpy", "hell knight", "naga mage", "vampire mage",
:           "orc warlord", "vampire knight", "yaktaur captain",
:           "deep elf high priest", "ancient champion" },
:     [you.absdepth() - 20] =
:         { "deep dwarf death knight", "spriggan defender", "satyr",
:           "merfolk aquamancer", "merfolk javelineer",
:           "Margery", "Saint Roka", "Sojobo" },
: }
: local kmonsters = {}
: for weight, monsters in pairs(bands) do
:     if weight > 0 then
:         for _, mons in ipairs(monsters) do
:             if not you.uniques(mons) then
:                 table.insert(kmonsters, mons .. " band w:" .. weight)
:             end
:         end
:     end
: end
: kmons("s = " .. table.concat(kmonsters, " / "))
KITEM:   ? = any ring randart w:390 / ring of hunger unrand:ring_of_the_octopus_king
KITEM:   ! = ring of invisibility / ring of regeneration / \
             ring of teleport control / ring of slaying / \
             ring of wizardry / any good_item ring no_uniq w:75
KITEM:   | = | / any good_item
KFEAT:   ^ = known teleport trap
KFEAT:   ~ = teleport trap
KFEAT:   aD = stone_arch
KFEAT:   A = enter_hell
KFEAT:   & = enter_pandemonium
KFEAT:   @ = enter_abyss
KFEAT:   B = enter_vaults
KFEAT:   C : C / any shop
SHUFFLE: <d` / {e1 / (f2 / [g3, >h4 / }i5 / )j6 / ]k7, (f2 / )j6, 15
: if you.absdepth() > 20 then
SUBST:   ( = D
MARKER:  D = lua:props_marker { portal=1 }
:     if crawl.random2(you.depth()-15) > (you.depth()-18) then
SUBST:   A&@ = a, _ = -, l = W
:     else
SHUFFLE: aA&@
:     end
: else
SUBST:   A&@ = a, _ = -, l = W
:     if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
NSUBST:  ( = 1:B / *:(
SUBST:   f = r, 2 = '
:     end
: end
SUBST:   {} = **|||, ( = T, ) : TTCCC, [ = !, ] = ?
NSUBST:  < = 1:{ / 1:( / 1:[, > = 1:} / 1:) / 1:]
SUBST:   d = 0, e = 009, f = %, g = 0, h = 0, i = 009, j = %, k = 0
SUBST:   ` = >, 1 = 9988s, 2 = 009, 3 = s, 4 = `, 5 = 998ss, 6 = 009, 7 = s
SHUFFLE: })]` / {([>, ?!, no / pq, pq / pq / pq / qp / pp / qq
SHUFFLE: LM / LM / LM / LM / LM / LM / LM / ML / ML / MM
SHUFFLE: NO / NO / NO / NO / NO / NO / NO / ON / ON / OO
SHUFFLE: PQ / PQ / PQ / PQ / PQ / QP, RS / RS / RS / RS / SR
SUBST:   LNPR : x, MOQS : ~, J : ~xxx, K : ~xxxx, c : x:7 c:1, v : v:7 c:1
NSUBST:  ~ = 2:~ / 1:~. / 75:H / 225:I / *:.
SUBST:   H = ~ .:4490, I = ~ .:9990
COLOUR:  "-_ = green
COLOUR:  '`089rs%*|!? = white
FTILE:   ^"aA@&-_U = floor_grass
: if crawl.coinflip() then
FTILE:   '`<{([>})]089rs%*|!?BCTDEF = floor_vault
: else
FTILE:   '`<{([>})]089rs%*|!?BCTDEF = floor_pebble_darkgray
: end
MAP
                          vvvvvvv
                         vv'%<%'vv
                 vvvv    vd'''''dv    vvvv
                vv%'vvvvvv'''`'''vvvvvv'%vv
               vv{''evvcccc+ccc+ccccvvg''[vv
               v%'1'''cc...........cc'''3'%v
               v'''cc+c.......N.....c+cc'''v
               vve'c.....L~~~~OO~......c'gvv
                vv'+....~MM~~~~N~~~....+'vv
          xxxx xcccc..~~~~L~~~~OO~~~~..ccv  xxxx
         xxJJxxx~~~..~~~~~MM~~~~N~~Q~~..ccxxxKKxx
         xJJJJxx~~~~px~~~~~L~~~~OO~~x~~~~~xxKKKKx
         xJJJJx~~~~~xn~~~~~MM~~~~N~~~P~~~~~xKKKKx
         xxJJ~~~~~~~~~~~~~~.L...~~~~~~~~~~~~~KKxx
    vvvv  xxx~www~~~~~~~~~.......~~~~~~~~www~xxxx
   vv%'vv xx~~www~x~~~.....c+c+c....~~~x~www~~~xx vvvvv
  vv(''fvvc~~~ww~R~~~.....cc'''cc.....~~R~ww~~~~cvv'%vvv
  v%'2'''cc~~~~~RS~~..c+ccc''4''ccc+c..~SR~~~~~~cvf''(vv
  v'''cc+c.~~~~x~~...cc''kch'''hci''cc..~~x~~~~~c'''2'%v
  vvf'c....~~~~~~....+'7''cc%>%cc''5'+..~~~~~~~.c+cc'''v
   vv'+...~~P~~~..c+cck''%bbbbbbb%''ic...~~~~xq....c'fvv
   vvcc..~~x~~~..cc'jbbb%]bbbbbbb}%bbc+c..~~~ox~...+'vv
   vcc...~Q~~~~..+'6'bbbbbbblllbbbbbbj'cc..~~~~~~..ccvv
   vc....~~~~~~..c'''%bbbbbbl^lbbbbbb'6'+..~~~~~~...ccv
 vvvc...~~~ON~~..cj'%)bblllll"llllbb%'''c..~~~~~~....cv
vvg'c..~~ONO~~..ccccbbbbla""""""Albb)%'jc..~~~~~~~...cvvv
v'''+..ONO~~~..cc'icbbbbl"""U"_""lbbbbcccc..~~~~~ML..c'evv
v%''c.NO~~~~~..+'''%bblll"_""""""lbbbbck'cc..~~MLM~..+'''v
v['3c..~~~~~ML.c'5'}bbl^"""""""U"lllbb%'''+..MLM~~~..c''%v
v%''c..~~~MLM..+'''%bblll"U"""""""^lbb]'7'c.LM~~~~~..c1'{v
v'''+..~MLM~~..cc'icbbbbl""""""_"lllbb%'''+..~~~~~ON.c''%v
vvg'c..LM~~~~~..ccccbbbbl""_"U"""lbbbbck'cc..~~~ONO..+'''v
 vvvc...~~~~~~~..ch'%>bbl@""""""&lbbbbcccc..~~ONO~~..c'evv
   vc....~~~~~~..c'''%bbllll"lllllbb>%'hc..~~NO~~~~..cvvv
   vcc...~~~~~~..+'4'bbbbbbl^lbbbbbb%'''c..~~~~~~~...cv
   vvcc..~~~~~~..cc'hbbbbbblllbbbbbbb'4'+..~~~~Q~...ccv
  vvv'+...~xo~~~..c+cbb%]bbbbbbb}%bbbh'cc..~~~x~~..ccvv
  vvd'c....qx~~~~...ck''%bbbbbbb%''icc+c..~~~P~~...+'vvv
  v'''cc+c.~~~~~~~..+'7''cc%)%cc''5'+....~~~~~~....c'dvv
  v%'`'''c~~~~~x~~..cc''kcj'''jci''cc...~~x~~~~.c+cc'''v
  vv<''dvc~~~~~~RS~..c+ccc''6''ccc+c..~~SR~~~~~cc'''`'%v
   vv%'vvc~~~~ww~R~~.....cc'''cc.....~~~R~ww~~~cvvd''<vv
    vvvv cxx~~www~x~~.....c+c+c.....~~~x~www~~xx vv'%vv
          xxx~www~~~~~~~~.......~~~~~~~~~www~xxx  vvvv
         xxKK~~~~~~~~~~~~~..L..~~~~~~~~~~~~~~JJxx
         xKKKKx~~~~~P~~~N~~~MM~~~~~~nx~~~~~xJJJJx
         xKKKKxx~~~~~x~~OO~~~L~~~~~~xp~~~~xxJJJJx
         xxKKxxxxc..~~Q~~N~~~MM~~~~~~..~~~xxxJJxx
          xxxx  vcc..~~~~OO~~~L~~~~~..ccccc xxxx
               vv'+....~~~N~~~MM~~....+'vv
              vve'c.....~~OO~~~L~.....c'gvv
              v'''cc+c.....N.......c+cc'''v
              v%'1'''cc...........cc'''3'%v
              vv{''evvcccc+ccc+ccccvvg''[vv
               vv%'vvvvvv'''2'''vvvvvv'%vv
                vvvv    vf'''''fv    vvvv
                        vv'%(%'vv
                         vvvvvvv
ENDMAP

################################################################################
# A bizarre, imposing crystal construct, radiating great elemental magic
# that lures in and alters enemies often rare, strange, and nasty.
# Vaguely, vaguely inspired by Crawl Alternative's Jade Caves
# and the non-livings sets for the proposed Radiant Caverns.
# Stairs and greater threats are scattered throughout the level:
# anticipating dangers and skipping through the level may be quite difficult.
#
NAME:    hangedman_crystal_crosses_colossus
TAGS:    no_monster_gen no_item_gen no_pool_fixup uniq_d_encompass
DEPTH:   Depths:2-, !Depths:$
ORIENT:  encompass
MONS:    deep elf conjurer / deep elf knight w:7 / wizard / ogre mage / \
         centaur warrior w:8 ; bow ego:flame | bow ego:frost . arrow / \
         two-headed ogre w:5 ; great mace ego:freezing ident:type . \
         great mace ego:flaming ident:type / efreet w:5 / \
         rakshasa w:5 ; wand of flame | wand of frost | nothing
MONS:    very ugly thing w:20 / shapeshifter / unseen horror w:15 / \
         chaos spawn / fire elemental / water elemental w:5 / \
         air elemental / earth elemental w:5 / \
         insubstantial wisp / gargoyle w:5 / wolf spider simulacrum w:3 / \
         elephant simulacrum w:3 / deep troll simulacrum w:3 / \
         lindwurm perm_ench:shapeshifter w:7 / lindwurm w:3 / \
         dancing weapon w:5 ; trident ego:flaming | trident ego:freezing | \
         war axe ego:flaming | war axe w:15 ego:freezing |  \
         scimitar w:15 ego:freezing | scimitar ego:flaming | \
         dire flail w:5 ego:flaming | dire flail w:5 ego:freezing
MONS:    frost giant w:5 / fire giant w:5 / naga mage / hell knight / \
         base draconian w:20 / spriggan air mage / deep troll earth mage / \
         yaktaur captain ; crossbow ego:flame | crossbow ego:frost . bolt / \
         rakshasa w:5 ; wand of fire | wand of cold | \
         glaive w:2 ego:flaming | glaive w:2 ego:freezing
MONS:    lorocyproca w:5 / fire dragon perm_ench:shapeshifter w:12 / fire dragon w:3 / \
         ice dragon perm_ench:shapeshifter w:12 / ice dragon w:3 / \
         metal gargoyle w:5 / glowing shapeshifter / ghost moth w:1 / \
         boulder beetle / anaconda simulacrum w:4 / harpy simulacrum w:4 / \
         hydra simulacrum w:4 / fire dragon simulacrum w:3 / fire crab w:5 / \
         dancing weapon w:15 ; glaive ego:flaming | glaive ego:freezing | \
         battleaxe w:5 ego:flaming | battleaxe ego:freezing | \
         great sword ego:flaming | great sword w:5 ego:freezing | \
         great mace ego:flaming | great mace ego:freezing
KMONS:   A = fire giant ; battleaxe ego:freezing ident:type
KMONS:   B = frost giant ; great sword ego:flaming ident:type
KMONS:   C = rakshasa ; robe good_item . wand of cold | wand of fire | \
                      wand of lightning . \
                      glaive ego:freezing | glaive ego:flaming . \
                      potion of heal wounds | potion of might | \
                      potion of speed | nothing w:20
KMONS:   D = tengu reaver
KMONS:   E = deep elf annihilator ; robe randart | mundane ice dragon armour | \
                                  mundane fire dragon armour | robe w:1 . \
                                  scimitar ego:flaming | scimitar ego:freezing
KMONS:   F = ettin ; great mace ego:flaming ident:type . \
                     great mace ego:freezing ident:type
KMONS:   G = deep elf blademaster ; mundane crystal plate armour . \
                                  sabre ego:flaming ident:type . \
                                  sabre ego:freezing ident:type
KMONS:   H = ancient lich w:5 / lich
KMONS:   I = titan
KMONS:   J = golden dragon w:19 / golden dragon perm_ench:shapeshifter w:1 / \
             quicksilver dragon w:14 / \
             quicksilver dragon perm_ench:shapeshifter w:1
KMONS:   K = glowing shapeshifter hd:16 / glowing shapeshifter hd:15
KMONS:   L = crystal golem
KFEAT:   V = enter_vaults
SHUFFLE: ABCD
NSUBST:  "~ = 1:{ / 1:[ / 1:( / 1:} / 1:] / 1:) / 1:<<. / 1:>>. / 3:T
:  if you.absdepth() > 20 then
NSUBST:  A = 1:A / 1:F / *:1, B = 1:B / 1:G / *:1
NSUBST:  C = 1:CC1 / 1:H / *:1, D = 1:D / 1:II1 / *:1
MARKER:  T = lua:props_marker { portal=1 }
: _G.set_random_mon_list("place:D:" .. you.absdepth() .. " w:150 / " ..
:   "fire giant / frost giant / base draconian w:5 / rakshasa w:5 / " ..
:   "glowing shapeshifter w:8 / simulacrum w:5 / dancing weapon w:2 / " ..
:   "fire crab w:5 / boulder beetle / chaos spawn w:5 / very ugly thing band w:5")
:  else
NSUBST:  A = 1:A / *:1, B = 1:B / *:1, C = 1:C / *:1, D = 1:DD3 / *:1
: _G.set_random_mon_list("place:D:" .. you.absdepth() .. " w:130 / " ..
:   "deep elf conjurer / deep elf knight w:5 / rakshasa w:5 / " ..
:   "shapeshifter w:5 / simulacrum w:5 / air elemental w:5 / " ..
:   "lindwurm w:5 / boulder beetle w:5 / chaos spawn / ugly thing band")
:    if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
NSUBST:  T = 1:V / *:T
:    end
:  end
SHUFFLE: ABCDEF, GHIJKL, "X / ~Y / ^Z, +-=
SUBST:   "X = bX..., ~Y = bbY.., ^Z = b., v : v:3 b:2, c : c:3 b:2, d = c
SUBST:   + = +:14 .:1, - = +:4 .:1, = = +:2 .:3, * = *:9 |:1, % = %:14 *:5 .:1
SHUFFLE: XY
SUBST:   X : w:60 l., Y : l:40 w., l = lll., 1 : 1:99 .:1
KPROP:   l = no_cloud_gen
:  local Z = you.absdepth()
:  if you.absdepth() * 4 - 74 <= 0 then
:    subst("1 = 1:" .. math.max(72 - Z * 3, 3) ..
:             " 2:" .. math.max(52 - Z * 2, 3))
:  elseif you.absdepth() * 4 - 78 <= 0 then
:    subst("1 = 1:" .. math.max(72 - Z * 3, 3) ..
:             " 2:" .. math.max(52 - Z * 2, 3) ..
:             " 3:" .. Z * 4 - 74)
:  else
:    subst("1 = 1:" .. math.max(72 - Z * 3, 3) ..
:             " 2:" .. math.max(52 - Z * 2, 3) ..
:             " 3:" .. Z * 4 - 74 .. " 4:" .. Z * 4 - 78)
:  end
TILE:    b = wall_emerald
TILE:    c = wall_crystal_squares
TILE:    v = dngn_metal_wall_darkgray
LFLOORTILE: floor_pebble_green
LFLOORCOL:  white
MAP
                                          bbb
                                         bbYbb
                                         b.%.b
                                         b.Y.b
          bbb                            b.B.b    bbb
         bbXbb                       bbbbc...cbbbbbYbb
         b.%.b                      bb...........b...b
         b.X.b                      bY.Y...Y...Y.-.Y.b
         b.A.b    bbb               bb...........b...b
     bbbbc...cbbbbbXbb               bb-bb...bbbbb...cbbbb
    bb...........b...b               b...b.1.b...........bb
    bX.X...X...X.-.X.b               b.Y.b.Y.b%Y1..Y..BY%Yb
    bb...........b...b    bbb        b...b.%.b...........bb
     bb-bb...bbbbb...bbbbbbXbb   bbbbb...bbbbvbbbb...cbbbb
     b...b.1.b....1......b...b  bb......1....b.%.b...b
     b.X.b.X.b%"...X...X.-.X.b  bY.Y...Y...~%b.~.b.Y.b
     b...b.%.b......1....b...b  bb....1......b...b...b
 bbbbc...bbbbvbbbb...bbbbb...bbbbbb-bb...bbbbb...bb-bb
bb...........b.%.b...b....1......b...b...b......1....bb
bX%XA..X..1X%b.".b.".b%"...X...X.+.Z.b.~.b%~...Y...Y.Yb
bb...........b...b.%.b......1....b...b.%.b....1......bb
 bbbbc...bbbbb...bbbbvbbbb...bbbbb...bbbbvbbbb...bbbbb
     b...b....1......b.%.b...b......1....b.%.b...b
     b.X.-.X...X..."%b.Z.b.".b.Z...Z...Z%b.~.b.Y.b
     b...b......1....b...b.%.b....B......b...b...b
     bbXbbbbbb...bbbbb...bbbbv+d+b...bbbbb...bb-bb
      bbb    b...b....1......b...b...b......1....bb
             b.X.-.Z...Z...Z%b.Z.b.Z.b%~...Y...Y.Yb
             b...b......A....b...b.%.b....1......bb
             bbXbbb+bb...bbbbb...bbbbvbbbb...bbbbb
              bbbb...b...+....A*B....+...b...b
                 b.Y.b.Z.d.Z..*Z*..Z.d.Z.b.Y.b
                 b...b...+....C*D....+...b...bbbb
             bbbbb...bbbbvbbbb...bbbbb...bb+bbbXbb
            bb......1....b.%.b...b....D......b...b
            bY.Y...Y...~%b.Z.b.Z.b%Z...Z...Z.=.X.b
            bb....1......b...b...b......1....b...b    bbb
             bb=bb...bbbbb...b+d+vbbbb...bbbbb...bbbbbbXbb
             b...b...b......C....b.%.b...b....1......b...b
             b.Y.b.~.b%Z...Z...Z.b.".b.Z.b%"...X...X.=.X.b
             b...b.%.b....1......b...b.%.b......1....b...b
         bbbbb...bbbbvbbbb...bbbbb...bbbbvbbbb...bbbbb...cbbbb
        bb......1....b.%.b...b....1......b.%.b...b...........bb
        bY.Y...Y...~%b.Y.b.Z.+.X...X..."%b.".b.".b%X1..X..DX%Xb
        bb....1......b...b...b......1....b...b.%.b...........bb
         bb=bb...bbbbb...bb=bbbbbb...bbbbb...bbbbvbbbb...cbbbb
         b...b...b......1....bb  b...b....1......b.%.b...b
         b.Y.b.~.b%~...Y...~.Yb  b.X.=.X...X..."%b.X.b.X.b
         b...b.%.b....1......bb  b...b......1....b.1.b...b
     bbbbc...bbbbvbbbb...bbbbb   bbXbbbbbb...bbbbb...bb=bb
    bb...........b.%.b...b        bbb    b...b...........bb
    bY%YC..Y..1Y%b.Y.b.Y.b               b.X.=.X...X...X.Xb
    bb...........b.1.b...b               b...b...........bb
     bbbbc...bbbbb...bb=bb               bbXbbbbbc...cbbbb
         b...b...........bb               bbb    b.D.b
         b.Y.=.Y...Y...Y.Yb                      b.X.b
         b...b...........bb                      b.%.b
         bbYbbbbbc...cbbbb                       bbXbb
          bbb    b.C.b                            bbb
                 b.Y.b
                 b.%.b
                 bbYbb
                  bbb
ENDMAP

################################################################################
# Giving players the runaround...

default-depth:

NAME: grunt_runaround
TAGS: no_monster_gen uniq_d_encompass
PLACE: Depths:$
DEPTH: Depths:2-
WEIGHT: 2 (Depths), 10
ORIENT: encompass
SUBVAULT: B : grunt_runaround_doors_1
SUBVAULT: C : grunt_runaround_doors_2
SUBVAULT: D : grunt_runaround_doors_3
SUBVAULT: E : grunt_runaround_doors_4
: if you.absdepth() == 27 then
KFEAT:  Z = stone_wall
TILE:   Z = wall_zot_magenta
COLOUR: Z = lightmagenta
SUBVAULT: F : grunt_runaround_zot
NSUBST: < = 1:{ / 1:[ / 1:( / 2:< / *=A
: elseif you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
KFEAT: Z = metal_wall
SUBVAULT: F : grunt_runaround_vaults
NSUBST: < = 1:{ / 1:[ / 1:( / 1:] / 1:) / *=A
: else
KFEAT: Z = metal_wall
SUBVAULT: F : grunt_runaround_loot
NSUBST: < = 1:{ / 1:[ / 1:( / 1:] / 1:) / *=A
: end
{{
local room_centres = { 'd', 'e', 'f', 'g', 'h', 'i', 'j', 'k',
                       'H', 'I', 'J', 'K', 'L', 'M', 'N', 'O',
                       'P', 'Q', 'R', 'S', 'T', 'U', 'V', 'W' }
for i = 1, #room_centres do
  subst(room_centres[i] .. ' : .:21 w l c v b:2')
end
}}
#KMASK: 09 = !no_monster_gen
SUBST: 0 = 0.
SUBST: 9 = 90
KMONS: 0 = 0 band
: if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
KMONS: 9 = human hd:8 ; ring mail w:5 | scale mail | chain mail w:5 \
                        . long sword | falchion | war axe | broad axe | \
                          mace | flail \
                        . nothing w:20 | buckler | shield
: else
KMONS: 9 = 9 band
: end
KFEAT: G = granite_statue / fountain_blue / fountain_sparkling w:1 / \
           floor w:21
MARKER: A = lua:props_marker { portal=1 }
KFEAT:  A = floor
MAP
                  cccccccccccccccc
                ccc.......<......ccc
             cccc..................cccc
           ccc..c..0.dddGddGddd.0..c..ccc
          cc....BB................BB....cc
         cc...0..c.cBBccccccccBBc.c..0...cc
        cc......cccc.....cc.....cccc......cc
       cc..kk.ccc...HH...CC...II...ccc.ee..cc
      cc<.Gk.BB..HHGH..0.CC.0..IGII..BB.eG.<cc
     cc..kk.cc...H.......cc.......I...cc.ee..cc
    cc..Gk.cc..0....cCCccccccCCc....0..cc.eG..cc
   cc..kk.ccc.....ccc..........ccc.....ccc.ee..cc
   c...k.cccCC..CCc.c..0.QQ.0..c.cCC..CCccc.e...c
  cc....Bc..CCccc...DD..QGGQ..DD...cccCC..cB....cc
  c..0.cB....ccc..0..c........c..0..ccc....Bc.0..c
  c....c..0..cc.PP...cDDccccDDc...RR.cc..0..c....c
 cccB.cc....Cc.PG..DDc...ZZ...cDD..GR.cC....cc.Bccc
 c..Bcc.OO..C..P..cc...9.EE.9...cc..R..C..JJ.ccB..c
cc....c.O..cc.0..ccZ..9G.EE.G9..Zcc..0.cc..J.c....cc
c..0.cc.G..c....DcZEE....ZZ....EEZcD....c..G.cc.0..c
c....B.OO.cccD..D..EEZZEEZZEEZZEE..D..Dccc.JJ.B....c
c..j.B.O..C..Dccc...ZZZFFFFFFZZZ...cccD..C..J.B.f..c
c..j.c....C....D..9.ZZFFFFFFFFZZ.9..D....C....c.f..c
c..j.c..0.c.0..D.9G.EFFFFFFFFFFE.G9.D..0.c.0..c.f..c
c..G.c....c..W.c....EFFFFFFFFFFE....c.S..c....c.G..c
c<.j.ccCCcc.WG.cZEEZZFFFFFFFFFFZZEEZc.GS.ccCCcc.f..c
c..j.ccCCcc.WG.cZEEZZFFFFFFFFFFZZEEZc.GS.ccCCcc.f.<c
c..G.c....c..W.c....EFFFFFFFFFFE....c.S..c....c.G..c
c..j.c..0.c.0..D.9G.EFFFFFFFFFFE.G9.D..0.c.0..c.f..c
c..j.c....C....D..9.ZZFFFFFFFFZZ.9..D....C....c.f..c
c..j.B.N..C..Dccc...ZZZFFFFFFZZZ...cccD..C..K.B.f..c
c....B.NN.cccD..D..EEZZEEZZEEZZEE..D..Dccc.KK.B....c
c..0.cc.G..c....DcZEE....ZZ....EEZcD....c..G.cc.0..c
cc....c.N..cc.0..ccZ..9G.EE.G9..Zcc..0.cc..K.c....cc
 c..Bcc.NN..C..V..cc...9.EE.9...cc..T..C..KK.ccB..c
 cccB.cc....Cc.VG..DDc...ZZ...cDD..GT.cC....cc.Bccc
  c....c..0..cc.VV...cDDccccDDc...TT.cc..0..c....c
  c..0.cB....ccc..0..c........c..0..ccc....Bc.0..c
  cc....Bc..CCccc...DD..UGGU..DD...cccCC..cB....cc
   c...i.cccCC..CCc.c..0.UU.0..c.cCC..CCccc.g...c
   cc..ii.ccc.....ccc..........ccc.....ccc.gg..cc
    cc..Gi.cc..0....cCCccccccCCc....0..cc.gG..cc
     cc..ii.cc...M.......cc.......L...cc.gg..cc
      cc<.Gi.BB..MMGM..0.CC.0..LGLL..BB.gG.<cc
       cc..ii.ccc...MM...CC...LL...ccc.gg..cc
        cc......cccc.....cc.....cccc......cc
         cc...0..c.cBBccccccccBBc.c..0...cc
          cc....BB................BB....cc
           ccc..c..0.hhhGhhGhhh.0..c..ccc
             cccc..................cccc
                ccc......<.......ccc
                  cccccccccccccccc
ENDMAP

NAME: grunt_runaround_doors_1
TAGS: grunt_runaround_doors_1 allow_dup
: map("            AA                BB")
: map("                II        JJ")
: map(" ")
: map(" ")
: map("         XX                      KK")
: map(" ")
: map(" ")
: map(" ")
: map(" ")
: map("    W                                  L")
: map("    W                                  L")
: map(" ")
: map("H                                          C")
: map("H                                          C")
: map(" ")
: map(" ")
: map(" V                                        M")
: map(" V                                        M")
: map(" ")
: map(" ")
: map(" ")
: map(" ")
: map(" ")
: map(" ")
: map(" ")
: map(" ")
: map(" U                                        N")
: map(" U                                        N")
: map(" ")
: map("")
: map("G                                          D")
: map("G                                          D")
: map(" ")
: map("    T                                  O")
: map("    T                                  O")
: map(" ")
: map(" ")
: map(" ")
: map(" ")
: map("         SS                      PP")
: map(" ")
: map(" ")
: map("                RR        QQ")
: map("            FF                EE")
SHUFFLE: ABCDEFGH
SHUFFLE: IJKLMNOPQRSTUVWX
SUBST: ABCDEFGIJ : +, HKLMNOPQRSTUVWX : c

NAME: grunt_runaround_doors_2
TAGS: grunt_runaround_doors_2 allow_dup
: map("                  AA")
: map("                  AA")
: map(" ")
: map("              XX      II")
: map(" ")
: map("     HH  WW                JJ  BB")
: map("     HH                        BB")
: map(" ")
: map(" ")
: map("     V                          K")
: map("     V                          K")
: map(" ")
: map(" ")
: map(" ")
: map("   U                              L")
: map("   U                              L")
: map(" ")
: map(" ")
: map("GG                                  CC")
: map("GG                                  CC")
: map(" ")
: map(" ")
: map("   T                              M")
: map("   T                              M")
: map(" ")
: map(" ")
: map(" ")
: map("     S                          N")
: map("     S                          N")
: map(" ")
: map(" ")
: map("     FF                        DD")
: map("     FF  RR                OO  DD")
: map(" ")
: map("              QQ      PP")
: map(" ")
: map("                  EE")
: map("                  EE")
SHUFFLE: ABCDEFGH
SHUFFLE: IJKLMNOPQRSTUVWX
SUBST: ABCDEFGIJ : +, HKLMNOPQRSTUVWX : c

NAME: grunt_runaround_doors_3
TAGS: grunt_runaround_doors_3 allow_dup
: map("       AA        BB")
: map(" ")
: map("         II    JJ")
: map("      XX          KK")
: map(" ")
: map(" ")
: map("   W                  L")
: map("H  W                  L  C")
: map("H                        C")
: map("  V                    M")
: map("  V                    M")
: map(" ")
: map(" ")
: map(" ")
: map(" ")
: map("  U                    N")
: map("  U                    N")
: map("G                        D")
: map("G  T                  O  D")
: map("   T                  O")
: map(" ")
: map(" ")
: map("      SS          PP")
: map("         RR    QQ")
: map(" ")
: map("       FF        EE")
SHUFFLE: ABCDEFGH
SHUFFLE: IJKLMNOPQRSTUVWX
SUBST: ABCDEFGIJ : +, HKLMNOPQRSTUVWX : c

NAME: grunt_runaround_doors_4
TAGS: grunt_runaround_doors_4 allow_dup
: map("        AA ")
: map("        AA")
: map("  HH          BB")
: map("  HH  PP  II  BB")
: map(" ")
: map(" ")
: map("   O          J")
: map("   O          J")
: map("GG              CC")
: map("GG              CC")
: map("   N          K")
: map("   N          K")
: map(" ")
: map(" ")
: map("  FF  MM  LL  DD")
: map("  FF          DD")
: map("        EE")
: map("        EE")
SHUFFLE: ABCDEFGH
SHUFFLE: IJKLMNOP
SUBST:   ABCDEFGI : +, HJKLMNOP : Z
: if you.absdepth() == 27 then
KFEAT:  Z = stone_wall
TILE:   Z = wall_zot_magenta
COLOUR: Z = lightmagenta
: else
KFEAT:  Z = metal_wall
: end

NAME: grunt_runaround_loot
TAGS: grunt_runaround_loot allow_dup
SHUFFLE: }>de
SUBST: de = %
NSUBST: % = 4:| / 8:* / *=%
KMONS: 9 = 9 band
KMONS: 8 = 8 band
MAP
  ......
 ..9..9..
..8.%%.8..
.9.%%%%.9.
..%%}>%%..
..%%de%%..
.9.%%%%.9.
..8.%%.8..
 ..9..9..
  ......
ENDMAP

NAME: grunt_runaround_vaults
TAGS: grunt_runaround_vaults allow_dup
SHUFFLE: }Ode
SUBST: de = %
NSUBST: % = 4:* / 8:% / *=.
KFEAT: O = enter_vaults
MONS: vault guard
MAP
  ......
 ..1..1..
....%%....
.1.%%%%.1.
..%%}O%%..
..%%de%%..
.1.%%%%.1.
....%%....
 ..1..1..
  ......
ENDMAP

NAME: grunt_runaround_zot
TAGS: grunt_runaround_zot allow_dup
COLOUR: .1defghijO = magenta
FTILE: .1defghijO = floor_hall
SHUFFLE: defg
SHUFFLE: Ohij
SUBST: hij = .
NSUBST: 1 = 1:2 / *=1
KFEAT: O = enter_zot
KMONS: d = golden dragon
KMONS: e = quicksilver dragon
KMONS: f = storm dragon
KMONS: g = shadow dragon
MONS: base draconian
MONS: nonbase draconian
MAP
  ......
 ..1..1..
..d....e..
.1......1.
....Oh....
....ij....
.1......1.
..f....g..
 ..1..1..
  ......
ENDMAP

################################################################################
# IT IS AN EMPTY, ROTTEN WORLD.
# LIE DOWN. FORGET YOUR TROUBLES.
# SMILE.
NAME:    hangedman_pleasuredromes
TAGS:    no_monster_gen no_item_gen uniq_d_encompass
ORIENT:  encompass
PLACE:   Depths:$
DEPTH:   Depths:2-
WEIGHT:  2 (Depths:$), 10
MONS:    lindwurm / death yak / elephant / griffon / polar bear w:5 / \
         mottled dragon w:8 / giant firefly w:3 / boulder beetle w:5 / \
         red wasp / tarantella w:2 / redback w:3 / wolf spider / \
         sea snake w:5 / alligator / hell hound w:2 / rock worm w:2
MONS:    swamp dragon w:5 / fire crab w:4 / catoblepas w:4 / \
         fire dragon w:16 / ice dragon w:16 / orb spider w:1 / spiny worm w:1 / \
         dire elephant w:9 / anaconda / hydra w:15 / snapping turtle / harpy w:4
MONS:    ghost moth w:4 / death drake w:2 / golden dragon / iron dragon / \
         quicksilver dragon w:15 / shadow dragon w:20 / storm dragon w:20 / \
         hell beast w:4 / hellephant w:4 / apis w:1 / ancient bear / \
         alligator snapping turtle w:5 / emperor scorpion w:5
MONS:    very ugly thing / very large slime creature / great orb of eyes / \
         shapeshifter hd:4 w:5 / unseen horror / large abomination w:1 / \
         eye of draining / shadow wraith w:5 / shadow / \
         air elemental / dancing weapon w:4 ; short sword good_item
MONS:    giant orange brain / eye of devastation / golden eye w:5 / \
         shapeshifter hd:6 w:5 / shapeshifter hd:7 / shapeshifter hd:8 w:5 / \
         titanic slime creature / water elemental w:5 / hydra simulacrum w:3 / \
         anaconda simulacrum w:3 / harpy simulacrum w:3 / eidolon w:5 / \
         dire elephant simulacrum w:1 / dancing weapon w:5 ; long sword good_item
MONS:    giant eyeball w:45 / bone dragon w:15 / tentacled monstrosity w:15 / \
         glowing shapeshifter hd:11 w:30 / glowing shapeshifter hd:12 w:15 / \
         azure jelly / thorn hunter w:5 / flayed ghost / \
         dancing weapon w:5 ; glaive good_item
MONS:    centaur warrior / yaktaur w:5 / orc knight / \
         deep elf priest w:5 / deep troll / boggart w:5 / \
         rakshasa w:3 / wizard / necromancer / spriggan rider w:5 / \
         vault sentinel w:2 / cyclops w:5 / kobold demonologist w:3 / \
         skeletal warrior w:5 / vampire
KMONS:   8 = orc high priest / orc sorcerer / naga warrior w:5 / naga mage / \
         yaktaur captain w:5 / ogre mage / deep elf knight w:15 / \
         hell knight / efreet w:1 / deep elf conjurer / \
         guardian serpent w:5 / phantasmal warrior w:5 / vampire mage w:4
KMONS:   9 = fire giant / frost giant w:11 / stone giant w:11 / ettin w:11 / \
         orc warlord / ironbrand convoker w:2 / spriggan berserker w:1 / \
         deep troll shaman w:5 / deep troll earth mage w:8 / \
         minotaur w:5 ; any armour good_item . any weapon good_item / \
         vampire knight / tengu reaver w:4 / base draconian w:5 / \
         spriggan air mage w:6 / deep elf death mage w:6 / \
         satyr w:2 / ancient champion w:6
KMONS:   0 = titan w:15 / lich / sphinx / deep elf annihilator / \
         deep elf sorcerer / deep elf high priest w:5 / greater naga w:5 / \
         spriggan defender w:5 / shadow demon w:5 / reaper w:5
KMONS:   & = draconian knight / draconian monk w:15 / \
         draconian annihilator / deep elf demonologist / \
         deep elf blademaster w:15 / deep elf master archer w:15 / \
         daeva w:5 / angel w:5 / lich w:15 / ancient lich w:15 / \
         mummy priest w:5 / balrug w:5 / executioner w:5
KMONS:   Z = base draconian
KFEAT:   z = known zot trap w:499 / floor w:1
KFEAT:   Z = enter_zot
SHUFFLE: Y], y}, {([>, HI / HI / IH, JK / JK / KJ
SHUFFLE: NO / NO / ON, PQ / PQ / QP, RS / RS / SR, lm / lm / ml
NSUBST:  ] = 1:) / 1:]
SUBST:   B : b.., C : cb., D : x.., E : xx., F : xx.
SUBST:   HJOQRm = x, IKNPSl = ., L : xx., M : xx., j : x.., k : x.., n : x.
SUBST:   b : b:6 v:1 c:1, @ = 114777, ^ = 224999
: local Z = you.absdepth()
: if you.absdepth() < 21 then
:   subst("2 = !:" .. 22 - Z .. " 2:20")
:   subst("3 = 2:" .. 25 - Z .. " 3:" .. Z + 4)
:   subst("5 = $:" .. 22 - Z .. " 5:20")
:   subst("6 = 5:" .. 25 - Z .. " 6:" .. Z + 4)
:   subst("8 = ?:" .. 22 - Z .. " 8:20")
:   subst("9 = 8:" .. 24 - Z .. " 9:" .. Z + 5)
SUBST:  & = 3:2 6:1 0:4 9:2
:     if you.where() == dgn.level_name(dgn.br_entrance("Vaults")) then
SUBST: y = V
:     end
SUBST:  yY = T
: else
SUBST:  y = Y, & = &:26 0:1
:   subst("9 = 9:90 0:" .. Z - 11)
:   subst("8 = 9:" .. Z - 14 .. " 8:20")
:   subst("5 = 6:" .. Z - 14 .. " 5:20")
:   subst("2 = 3:" .. Z - 14 .. " 2:20")
:     if you.where() == dgn.level_name(dgn.br_entrance("Zot")) then
SUBST:  )]} = ., > = <, " = `
NSUBST: ' = 1:Z / 2 : Z:1 `:4 / *:`
:     end
: end
SUBST:  '" = .
: subst("1 = 1:" .. math.max(58 - Z * 2, 16) .. " 2:" .. math.max(Z - 15, 1))
: subst("4 = 4:" .. math.max(58 - Z * 2, 16) .. " 5:" .. math.max(Z - 15, 1))
: subst("7 = 7:" .. math.max(58 - Z * 2, 16) .. " 8:" .. math.max(Z - 15, 1))
SUBST:  ! = 1, $ = 4, ? = 7, 7 = 7:499 .:1
MARKER: Y = lua:props_marker { portal=1 }
FTILE:  Z` = floor_marble
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxJ.............xxxxxx
xxxxxxxxxxx......c.........xxx
xxxxxxxxx"".....cbc..x......xxx
xxxxxxxxx".....cbcbc.x......Mxxxx
xxxxxx'"7.....xF.b.cc...n....Lxxxx
xxxxxx""1....x...x..cc.x.......xxx
xxxxxx74.........b..7cc.........xxx
xxxx"".....x..xxb.bxb.cc..8.....xxx
xxJx".....x..E...b...b.cc@..n...xxx
xx.......x.......xx.4x.8cc.x.....xx
xx.............F...x.b*..cc......xx
xx.....x..E........xb.bxb.cc.xx..xx
xx....x..x...........b.7.b9cc....xxxxxxxxxxxxx
xx...cF..x..F....%...xx.5x..cc...xxxxxxxxxxxxxx
xx..cb...b......%*.....x.b...bc..xxxx@.%.%.@.xxx
xx.cbcbxb.bx...%*.*%...xb.bxbcbc.xxxxx........xxx
xx..cb...b.x.....*%....x.b...bc..xxxxxx........xxx
xx...cc..x..xx...%...xx..x..cc...xxxxxxx%.......xxx
xx....cc7b71.b.......b%65b8cc....xxxxxxxx...I.x.xxxx
xx..xx.cc.bcb.bx...xb.bxb.cc..xx.xx..xxxxx...H.^..xxx
xx......cc..*b.x...x%b%*|cc......xx....xxxx.x...x..xxx
xx.....x.cc8.x1.xxx.3x*&cc.......xx.......xx...H....xxx
xxxx..n..@cc.b72.b..2b|cc.....xMx.x.xLx.....x.x.I....xx
 xxx....8..cc.bxb.bxb.cc.8.xxx...xxx...xxx...x......@xx
  xx........cc9..b..8cc.........x...x........xx......xx
  xxxxL....x.cc..x..cc...x.@..SR..@..RS..@.x..xx....%xx
  xxxxxM..n...cc.b.cc....x....SR.....RS....x..xxx%...xx
     xxx.....x.cbcbc.....x....SR.....RS....x..xxxx..%xx
    xxcc.....x..cbc..x..x..SSSJ.x.Q.x.HSSS..x..xxxx..xx
    xcc..........c...x..L..RRR.K.cPc.I.RRR..M..xxxxx@xx
   xxc.xx...............x.x...x.J...H.x...x.x...xxxxxxx
   xc@.xx...xxxxxxxxxxxx.x.....c.>.[.c.....x....xxxxxxx
   xc....xx.....nnnnxxxxxx.@..NO..z..ON..@.xxxxxxxxxxxxxxx
  xc.....xx........nx....x.....c.(.{.c.....x.xxxxxxxxxxxxxx
  xc.......xx......nx...x.x...x.H...J.x...x.x............xxxxxx
  xxc......xx.......xx..M..RRR.I.cPc.K.RRR..L......c......xxxxx
   xc...j....xx......x..x..SSSH.x.Q.x.JSSS..x..Y..cbc........xx
   xxc.......xx.c....x...x....SR.....RS....x]....xbcbx.......xxx
    xc..........b....xx..x....SR.....RS....x....x..b..x......Lxxxxx
    x@cc......cb%bc...x..x.@..SR..@..RS..@.x.x.x..1c...x.x....Mxxxx
    xc..cc..cc.@b.....KJ........x...x.....y....b..7b81.b.........xx
    xc....cc....c.xx..KJ...xxx...xxx...xxx.}.xb.bcbcbcb.bx.......xxx
    xxc........c..xx...KJ.....xLx.x.xMx]....x..b..%b%..b..x......xxx
     xc.....k..c....xx..KJJ.......xx.....x.x..1c.1ccc1.c1..x.x....xxx
     xxc..%...c.....xx...KKxx.....xx.......b.47b*cc&cc%b92.b.......xx
     xxc..%%..c.......xx....xxxx..xx..Y..xb.bcbcbc6x6cbcbcb.bx.....xx
     xc..k.....c......xx.......xxxxx....x..b..*b9..x..9b*..b..x....xx
    xxc........c...j....xx......nnnx...x...c.4cc.7|x|*4cc7.c...x...xx
    xc....cc....c.......xx.c......nx..cb.74c%cc3.|x%*|.2cc*b82.bc..xx
    xc@.cc..cc."b".........b......nx.cbcbccccc&xxx%|%xxxccbcbcbcbc.xx
    x.cc......cb'bc......cb%bc....nx..cb..8c%cc3.|*%x.....%C...Bx..xx
    xc........1"b".cc..cc.@b.......x...x..4c.4cc.*|x....C..x...x...xx
   xxc.mm....ll4c....cc....c.xx....x....x..b..%c91.x.....%.x..x....xx
   xc..mm...%ll..c........c..xx....x.....xb.bccccc5x....D.x........xx
   xc......xx%...c.....k..c....xx..........b..9c*ccc.C%D...........xx
  xc%.....%xx.....c..%...c.....xx........x.x.5.c.4cc.....D.x.......xx
  xc%....xx%......c..%%..c.......xx.........x..b..*b%..x.........HOxx
  xxc...%xx......c..k.....c......xx..........xb.bcbcCxx..x.......Pxxx
   xc..ll%...mm..c........c...j....xx..........b..5b........"""JRlxxx
   xxc2ll....mm.c....cc....c.......xx.cxM....x.x..8c........"'"jxxxxx
    xc^5........c@.ccxxcc..c.........ccxxL......x..b..x....."""kxxxxx
    xxcc......cc.ccxxxxxxcc@cc.....@ccxxxx.......xbcBx......Jjklxxxxx
     xxxcc%%ccxxxxxx    xxxxxxcc..ccxxx xx........cbx.......Rxxxxxxxx
       xxxccxxx              xxxccxxx   xxxxx......c......HPlxxxxxxxx
         xxxx                  xxxx     xxxxxx............Oxxxxxxxxxx
                                            xxxxxxxxxxxxxxxxxxxxxxxxx
                                             xxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

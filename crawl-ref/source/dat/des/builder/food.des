##########################################################################
# Guarantee at least some really minimal food supply.
##########################################################################

NAME:   first_bread
DEPTH:  D:2-5
CHANCE: 100% (D:5), 25%
TAGS:   uniq_first_food extra transparent
ITEM:   bread ration
MAP
d
ENDMAP

##########################################################################
# Midgame food supply.
#
# These should give around 20k nutrition.  Currently all strict
# carnivores have a large buffer so they can survive lulls in edible supply
# well until Crypt, so all-veggie is ok.
#
# normal/herbi/carni

# 17.6k/26.6k/0
NAME:   the_bakery
DEPTH:  D:7-14
CHANCE: 100% (D:14), 15%
TAGS:   uniq_second_food extra chance_second_food
ITEM:   bread ration q:2
: if crawl.one_chance_in(8) then
:   dgn.delayed_decay(_G, 'e', 'spriggan name:baked n_adj n_spe n_des corpse')
: else
SUBST: =e = x
: end
SUBST: = = +
MONS:   spriggan col:white name:baker n_suf ; robe . dagger
MAP
xxxxxx
xdd=ex
x.xxxx
x.1xxx
x+xxxx
ENDMAP

# 18.6k, min 14k
NAME:   david_nohive_1
DEPTH:  D:8-14
CHANCE: 100% (D:14), 15%
TAGS:   uniq_second_food extra chance_second_food
MONS:   queen bee, killer bee
ITEM:   w:2 honeycomb / w:1 royal jelly
COLOUR: a = yellow
TILE:   a = wall_wax
SUBST:  a = x
MAP
............
....aaaa....
..a2a2aaaa..
..aada2a2a..
.aa2ada2aaa.
.a2ada1ada2.
.aa2adada2a.
.aaa2a2a2aa.
..a2ada2aa..
...aa2aa2a..
....aaaa....
............
ENDMAP

# 20.6k/46.5k/0
NAME:     blue_anna_alchemist
DEPTH:    D:8-14
CHANCE:   100% (D:14), 15%
TAGS:     uniq_second_food extra chance_second_food mini_float
SUBVAULT: Z : fruiting_plant
SUBVAULT: Y : fruiting_plant
SUBVAULT: X : fruiting_plant
SUBVAULT: W : fruiting_plant
KITEM:    p = potion of curing / potion of heal wounds / \
              potion of restore abilities w:1 / potion of poison w:3 / \
              potion of strong poison w:1 / potion of confusion w:1 / nothing
KMONS:    1 = deep dwarf artificer
MAP
ZZZZZ..YYYYY
ZZZZZ..YYYYY
ZZZZZ..YYYYY
ZZZZZ..YYYYY
............
............
XXXXX..WWWWW
XXXXX..WWWWW
XXXXX..WWWWW
XXXXX..WWWWW
x.x.c++c.x.x
ccccc..ccccc
cppc....cppc
cp.+.1..+.pc
cccc....cccc
cccccccccccc
ENDMAP

# 23.2k/19.9k/13k
NAME:   hangedman_fish_farm
DEPTH:  D:7-14
CHANCE: 100% (D:14), 15%
TAGS:   uniq_second_food extra chance_second_food no_monster_gen no_pool_fixup \
        patrolling
KMONS:  A = merfolk ; spear
KMONS:  B = big fish / nothing
KMONS:  C = big fish
KFEAT:  gBC = shallow_water
ITEM:   meat ration q:2, bread ration q:3, throwing net
:       dgn.delayed_decay(_G, 'g', 'big fish corpse')
MAP
xxxxxx
xgxedx
+..fAx
xCBBBx
xxxxxx
ENDMAP

# 18k/18.8k/16.6k (min 18k/12k/12k), plus perishable 6k/6k/0
NAME:   hangedman_pestilent_swarm
DEPTH:  D:8-14
CHANCE: 100% (D:14), 15%
TAGS:   uniq_second_food extra chance_second_food no_monster_gen patrolling
MONS:   spider / giant firefly / brain worm w:5 / goliath beetle / giant centipede
MONS:   yellow wasp w:7 / rock worm w:5 / boring beetle / soldier ant w:13 / giant slug
ITEM:   cheese q:3 / beef jerky q:3 / lemon q:3, potion of porridge, honeycomb q:6
MAP
.xxxxxxx
@xx.x.xcccc
 ..x1x2cdec
@xx.x.x+fdc
.x1x.x2cccc
 xxxxxxx
ENDMAP

# 25k (min 20k)
NAME:   hangedman_little_slice_of_home
DEPTH:  D:8-14
CHANCE: 100% (D:14), 10%
TAGS:   uniq_second_food extra chance_second_food
KMONS:  A = jelly
KMONS:  B = ooze
: if you.absdepth() > 10 then
KMONS:  C = slime creature
: end
KITEM:  C = royal jelly q:4 / royal jelly q:6
KFEAT:  C = altar_jiyva w:1 / . w:20
KMASK:  W = no_monster_gen
MAP
 .Wxxxxx
.Wxxxxxxx
@.xBBAxxx
 W=BxA=Cx
@.xBBAxxx
.Wxxxxxxx
 .Wxxxxxx
ENDMAP

# 22.3k/18.8k/19k (min 9.6k)
NAME:     hangedman_ranch
DEPTH:    D:8-14
CHANCE:   100% (D:14), 10%
TAGS:     uniq_second_food extra chance_second_food no_monster_gen patrolling
SUBVAULT: Z : fruiting_plant
KMONS:    A = three-headed hydra / w:5 two-headed hydra
KMONS:    B = sheep
KMONS:    C = yak
KMONS:    D = big kobold
KMONS:    E = big kobold ; nothing
KMONS:    F = plant
KITEM:    C = cheese q:4
KITEM:    E = falchion ego:flaming ident:type / short sword ego:flaming
KITEM:    H = meat ration q:2
KITEM:    I = sabre
KFEAT:    EF = shallow_water
MAP
cccccccccc
cAH+...CCc
cIDcD..BBc
c+ccm+++cc
x.WFZZZZZx
xFWWZZZZZx
xWEFZZZZZx
xFW.ZZZZZx
xFFW.W.W.x
xxxxx@xxxx
ENDMAP

# 15.3k/34.2k/0
NAME:   hangedman_tree_tricks
DEPTH:  D:8-14
CHANCE: 100% (D:14), 10%
TAGS:   uniq_second_food extra chance_second_food no_monster_gen
NSUBST: T = 1:. / 1:t
KMONS:  1 = wandering mushroom
KMONS:  2 = oklob sapling
KITEM:  12A_ = apricot q:2 / pear q:2 / apple q:2 / banana q:2 / lemon q:2 / orange q:2
KFEAT:  _ = altar_fedhas
MAP
 tttttttttt
tttt.ttttttt
@tt1t.tttttt
t.tt.t.Tt2tt
t.t.ttT.1.tt
t.t.ttt1_Att
t.t1tt2.Attt
tt.ttttttttt
 tttttttttt
ENDMAP

# 16.8k/21.3/8k
NAME:   hangedman_tell_no_tales
DEPTH:  D:9-14
CHANCE: 100% (D:14), 10%
TAGS:   uniq_second_food extra chance_second_food no_monster_gen no_item_gen patrolling
MONS:   spriggan skeleton / centaur skeleton
MONS:   troll skeleton / hungry ghost
MONS:   shadow imp
: if you.absdepth() < 13 then
KMONS:  A = trapdoor spider w:6 / unseen horror / nothing w:4
: else
KMONS:  A = vapour w:4 / trapdoor spider w:6 / unseen horror / nothing w:2
: end
ITEM:  sultana q:1 / grape w:8 q:1
ITEM:  sultana mimic q:1 w:8 / grape mimic w:8 q:1
ITEM:  honeycomb q:2, bread ration q:3
: if crawl.coinflip() then
:     dgn.delayed_decay(_G, 'h', 'spriggan skeleton')
: else
:     dgn.delayed_decay(_G, 'h', 'ogre skeleton')
: end
: if crawl.coinflip() then
:     dgn.delayed_decay(_G, 'A', 'spriggan skeleton')
: else
:     dgn.delayed_decay(_G, 'A', 'ogre skeleton')
: end
SUBST:  D : DDDDE
NSUBST: D = 1:A / 2:d / 2:f / 1:h, E = 1:d / 1:e / 2:f / 2:h
MAP
 cccccccccc
ccxx1xh=DDc
c.x.x..mDDc
@.d.x2.mDDc
c.x.x..cc+c
ccxx1x.c3gc
 cccccccccc
ENDMAP

# 20.6k/23k/12.5k (min 18k/6k/0)
NAME:    hangedman_stone_soup
DEPTH:   D:8-14
CHANCE:  100% (D:14), 10%
TAGS:    uniq_second_food extra chance_second_food patrolling no_rotate no_vmirror
# Branch representatives working together to cook some soup:
# D, Orc, Elf, Snake, Shoals, Vaults, Crypt, Hells, Zot.
KMONS:   A = kobold
KMONS:   B = orc
KMONS:   C = deep elf soldier
KMONS:   D = naga
KMONS:   E = merfolk
KMONS:   F = human
KMONS:   G = necrophage
KMONS:   H = crimson imp
KMONS:   I = draconian
KITEM:   1 = stone q:1 no_pickup / large rock q:1 w:5
KITEM:   Ya = royal jelly / meat ration / bread ration
KITEM:   b = honeycomb / cheese / apricot q:2 / apple q:2 / choko q:2
KFEAT:   1Yab = shallow_water
KMASK:   1Yac = no_monster_gen
MARKER:  Y = lua:fog_machine { cloud_type = "steam", pow_min = 1, \
                 pow_max = 10, delay = 60, size = 1, walk_dist = 0 }
MARKER:  Z = lua:fog_machine { cloud_type = "flame", pow_min = 100, pow_max = 100, \
                 delay = 10, size = 1, walk_dist = 0, excl_rad = -1 }
NSUBST:  c = 2:a / *:b
SHUFFLE: ABCDEFGHI
MAP
 .......
..A...B..
..vv.vv..
..vcccv..
..vY1cv..
..vcccv..
.CvvvvvD.
...Z.Z...
 .......
ENDMAP

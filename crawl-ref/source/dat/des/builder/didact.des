##############################################################################
#
# This file contains vaults and lua for didactical purposes --
# it is included in clua/loadmaps.lua for syntax checking. None of the maps
# here will be used in real games.
#
##############################################################################

###################################################################
# Multilevel portal vault structure:
# This is how a multilevel portal vault should be laid out. All
# stairs are one-way; the player cannot return to a level once left.

NAME: multilevel_portal_vault_entry
# Demo vault, disabled.
WEIGHT: 0
MARKER: O = lua:one_way_stair { \
              dst="multilevel portal vault", \
              desc="An entrance to a multilevel portal vault" \
            }
KFEAT: O = enter_portal_vault
MAP
O
ENDMAP

NAME: portal_level_1
TAGS: multilevel_portal_vault
: portal_vault(_G, "multilevel_portal_vault")
# portal_next() links all downstairs to the vault with the specified tag.
: portal_next(_G, "multilevel_portal_vault_b")
MAP
xxxxxx
xA..]x
xxxxxx
ENDMAP

NAME: portal_level_2
: portal_vault(_G, "multilevel_portal_vault_b")
: portal_next(_G, "multilevel_portal_vault_c")
MAP
xxxxxx
xAWW]x
xxxxxx
ENDMAP

NAME: portal_level_3
: portal_vault(_G, "multilevel_portal_vault_c")
KFEAT: < = exit_portal_vault
MAP
xxxxxxx
xlllllx
xA...<x
xlllllx
xxxxxxx
ENDMAP

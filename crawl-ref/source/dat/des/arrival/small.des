##############################################################################
# Small maps for D:1 arrival vaults.
# Small maps which are also very simple should go to arrival_simple.des.
# See arrival_guide.txt for details.
##############################################################################

###############################################################################
# Probably looks better in tiles
NAME:   evilmike_arrival_home_sweet_home
WEIGHT: 3
TAGS:   arrival no_monster_gen
ORIENT: float
COLOUR: ' = brown
COLOUR: `1 = green
COLOUR: - = darkgrey
COLOUR: t = lightgreen
FTILE:  `1t = floor_grass
FTILE:  ' = floor_dirt
FTILE:  -{V = floor_vault
TILE:   c = wall_stone_smooth
MONS: bush
SUBST: `'-: .
MAP
........@........
.................
..11111`'`11111..
..1`````'`````1..
..1``t``'``t``1..
..1`````'`````1..
..ccmccc+cccmcc..
..c---c---c---c..
..c{--+---+---m..
..ccccccc-c---m..
..c-----c-c---c..
..m-----+-ccccc..
..m-----c-+---c..
..c-----c-c-VVc..
..cccmccccccmcc..
.................
ENDMAP

###############################################################################
NAME:   evilmike_arrival_crescent
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxxxxxxxx
xx.{.xxxxx
xxx...xxxx
x>xx...xxx
xw.x...xxx
xw.xx...xx
xww.x...xx
xwwWxx...x
xwwwW+...+
xwwWxx...x
xww.x...xx
xw.xx...xx
xw.x...xxx
x<xx...xxx
xxx...xxxx
xx@@@xxxxx
ENDMAP

###############################################################################
NAME:    evilmike_arrival_come_back
WEIGHT:  2
TAGS:    arrival
ORIENT:  float
NSUBST:  % = 1:1 / 1:0 / 1:> / *=%...
MONS:    gnoll / ogre / bullfrog / centaur
MAP
xxxxxxxxxx
x.......{x
x.xxxxxx.x
x.x%%%%x.x
..x%%%%x.x
..m%%%%x.x
..m%%%%x.x
..mmmxxx.x
.........x
 .....xxxx
ENDMAP

###############################################################################
NAME:    evilmike_arrival_hilbert_curve
TAGS:    arrival no_monster_gen no_pool_fixup
ORIENT:  float
SUBST:   c:xxxcccwl
NSUBST:  { = 1:{ / *:.
MAP
.................
.ccc.ccc.ccc.ccc.
.c{c.c{c.c{c.c{c.
.c.ccc.c.c.ccc.c.
.c.....c.c.....c.
.ccc.ccc.ccc.ccc.
...c.c.....c.c...
.ccc.ccccccc.ccc.
.c.............c.
.c.ccccc.ccccc.c.
.c{c..{c.c{..c{c.
.ccc.ccc.ccc.ccc.
.....c.....c.....
.ccc.ccc.ccc.ccc.
.c.c..{c.c{..c.c.
.c.ccccc.ccccc.c.
.................
ENDMAP

###############################################################################
NAME:   evilmike_arrival_fibonacci_small
TAGS:   arrival no_monster_gen
ORIENT: float
WEIGHT: 6
NSUBST: U = 1:+ / *:x, V = 1:+ / *:x, W = 1:+ / *:x, X = 1:+ / *:x
SUBST:  .= .:2500 1
MONS:   rat
MAP
    xxxxxxxxxxxx
    xxx........x
    xxx........x
xxxxxxx........x
xxxxxxx........x
xxxxxxx........x
x.....W........x
x.....W........x
x.....W........x
x.....xxxxxXXXXx
x.....x{+.x
xxxxVVxxx+x
xxxx...U..x
xxxx...U..x
 xxx...xxxx
 xxxxxxxxxx
ENDMAP

###############################################################################
NAME:    evilmike_arrival_grusome_pit
WEIGHT: 1
TAGS:    arrival no_rotate
ORIENT:  northwest
# the exit will be placed elsewhere 25% of the time
SUBST:   { = {:30 .
ITEM:    q:1 chunk, skeleton, corpse
SUBST:   Y = A.
KFEAT:   A = known shaft trap
SUBST:   ` = df `:60
SUBST:   . = ' .:120
SUBST:   ` = ''., V = Xx, U = Xxxx, x = X x:60
KPROP:   X' = bloody
SUBST:   X = x, ' = .
SUBST:   . = .:320 d:5 e:20 f:5
SUBST:   y : x
MAP
yyyyyyyyyyyy
yyVVVVVyyyyy
yVV...VVyyyy
yV.YYY`VVyyyyyyyyyyyyyyyyyyyyyyy
yV.YAY`.Vyyyyyyyyyyyyyyxxxxyxxxy
yV.YYY`.UUyyyyxxxxxxyxxx..xxxxxy
yVV.```` UUUyUU....xxx.........@
yyVVVVU.``.UUU................xy
yyyyyyUU.`````......`.`.`.`...xy
     yyUUU..`.``...``........xxy
     yyyyUUUU..``.``.......xxxyy
     yyyyyyyUU............xxyyyy
     yyyyyyyyxxxx...{..xxxxyy
     yyyyyyyyyyyxx...xxxyyyyy
     yyyyyyyyyyyyxxxxxyyyyyyy
     yyyyyyyyyyyyyyyyyyyyyyyy
ENDMAP

##############################################################################
# Was evilmike_arrival_teleporters
NAME:    evilmike_arrival_plus
TAGS:    arrival
ORIENT:  float
SHUFFLE: GTVt1`
SUBST:   `=.
SUBST:   X:Y. , Y:xx.
MONS:    plant
MAP
       x+x
      xx.xx
      xY.Yx
      xX.Xx
      xX.Xx
     xx...xx
 xxxxx..G..xxxxx
xxYXX.......XXYxx
+.....G.{.G.....+
xxYXX.......XXYxx
 xxxxx..G..xxxxx
     xx...xx
      xX.Xx
      xX.Xx
      xY.Yx
      xx.xx
       x+x
ENDMAP

##############################################################
NAME:   evilmike_arrival_rings
TAGS:   arrival no_monster_gen no_pool_fixup
ORIENT: float
NSUBST: s = 1:+ / *:x, p = 1:+ / *:x, q = 1:+ / *:x, r = 1:+ / *:x
NSUBST: _ = 1:w / *:., ' = 1:w / *:., ` = 1:w / 1:d / *:.
SUBST:  w:wlxm, x:xccv, +=+++.
ITEM:   stone
MAP
   ...........
  ..rrrrrrrrr..
 ..rr.`````.rr..
..rr.qqqqqqq.rr..
.rr.qq.'''.qq.rr.
.r.qq.ppppp.qq.r.
.r`q.pp._.pp.q`r.
.r`q'p.sss.p'q`r.
.r`q'p_s{s_p'q`r.
.r`q'p.sss.p'q`r.
.r`q.pp._.pp.q`r.
.r.qq.ppppp.qq.r.
.rr.qq.'''.qq.rr.
..rr.qqqqqqq.rr..
 ..rr.`````.rr..
  ..rrrrrrrrr..
   ...........
ENDMAP

###############################################################
# Player is usually encouraged to go towards the dangerous door with the
# bait. The monster is slow, and there's room to escape and close the door.
NAME:    evilmike_arrival_run_for_it
TAGS:    arrival no_monster_gen
ORIENT:  float
: if crawl.one_chance_in(4) then
SHUFFLE: d'
: end
SUBST:   '=.
MONS:    worm
ITEM:    stone
MAP
xxxxxxxxxxxxxxxxx
x%1.+d....{....'+
xxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
NAME:    minmay_arrival_checkered_box
TAGS:    arrival
ORIENT:  float
: if crawl.one_chance_in(4) then
COLOUR:  ' = blue
COLOUR:  " = green
FTILE:   '{ = floor_pebble_blue
FTILE:   " = floor_pebble_green
SUBST:   c = b, x = v
: else
COLOUR:  ' = brown
COLOUR:  " = lightgrey
FTILE:   '{ = floor_pebble_brown
# yes, this variation uses American spelling for some reason
FTILE:   " = floor_pebble_lightgray
: end
SUBST:   ' = ., " = .
MAP
xcx@xcx
c'"'"'c
x"'"'"x
c'"'"'c
x"'"'"x
c'"{"'c
xcxcxcx
ENDMAP

##############################################################
NAME:    minmay_arrival_split_and_join
TAGS:    arrival no_rotate
ORIENT:  float
SUBST:   T = TV
KMASK:   T = no_monster_gen
MAP
xxxxxxxxxxxx@xxxxxxxxxxxx
xxxxxxxxxxx...xxxxxxxxxxx
xxxxxxxxx...x...xxxxxxxxx
xxxxxx....xxxxx....xxxxxx
xxxx...xxxxxxxxxxx...xxxx
xxx..xxxxxxxxxxxxxxx..xxx
xxx+xxxxxxxx>xxxxxxxx+xxx
xx...xxxxxx...xxxxxx...xx
x.......................x
x..T........T........T..x
x.......................x
xx...xxxxxx...xxxxxx...xx
xxxxxxxxxxxx+xxxxxxxxxxxx
xxxxxxxx.........xxxxxxxx
xxxxxxx..t..{..t..xxxxxxx
xxxxxxxx.........xxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
NAME:    minmay_arrival_jagged
TAGS:    arrival no_monster_gen
ORIENT:  float
SUBST:   A = xx., B = x..
KPROP:   A = no_tele_into
MAP
xxxxxxxxxxxx
x{AAAAAAAAAA
xA.BBBBBBBBB
xAB.........
xAB.........
xA.BBBBBBBBB
x>AAAAAAAAAA
xxxxxxxxxxxx
ENDMAP

##############################################################
NAME:    minmay_arrival_stair_chambers
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: {[(
SUBST:   T = T V G b:3 .:3
SUBST:   [ = G
SUBST:   ( = G
MAP
xxxxxxx+xxxxxxx
xxxxx.....xxxxx
x...x.....x...x
x.{.+..T..+.[.x
x...x.....x...x
xxxxx.....xxxxx
xxxxxxx+xxxxxxx
xxxxxx...xxxxxx
   xxx.(.xxx
   xxx...xxx
   xxxxxxxxx
ENDMAP

##############################################################
NAME:    minmay_arrival_four_loops
TAGS:    arrival
ORIENT:  float
SUBST:   x : x:200 c v:5 b:5
SUBST:   b : x:200 c v:5 b:4 t:1 m:1
MAP
@xxxxxxxx  xxxxxxxx@
x+......xxxx......+x
x........xx........x
x...bb...xx...bb...x
x..bbbb..xx..bbbb..x
x..bbbb..xx..bbbb..x
x...bb...xx...bb...x
x..................x
xx................xx
 xxxxxx.{xx..xxxxxx
 xxxxxx..xx..xxxxxx
xx................xx
x..................x
x...bb...xx...bb...x
x..bbbb..xx..bbbb..x
x..bbbb..xx..bbbb..x
x...bb...xx...bb...x
x........xx........x
x+......xxxx......+x
@xxxxxxxx  xxxxxxxx@
ENDMAP


##############################################################
NAME:    minmay_arrival_stone_box
TAGS:    arrival
ORIENT:  float
NSUBST:  A = 1:+ / 1=c+ / *:c
NSUBST:  B = 1:+ / *:c
SHUFFLE: >TVGl
KMASK:   Tl = no_monster_gen
MAP
...........
...........
..cAAAAAc..
..Ac...cA..
..A.BBB.A..
..A.B>B.A..
..A.BBB.A..
..Ac...cA..
..cAAAAAc..
.{.........
...........
ENDMAP

##############################################################
NAME:    minmay_arrival_semitransparent
TAGS:    arrival
ORIENT:  float
: if crawl.coinflip() == true then
SUBST:   x : cccb
SUBST:   m : n
: else
SUBST:   x : xxxb
: end
MAP
...........
.xmxmxmxmx.
...........
...........
.....{.....
...........
...........
.xmxmxmxmx.
...........
ENDMAP

##############################################################
NAME:    minmay_arrival_solitary_statue
TAGS:    arrival
WEIGHT:  9
ORIENT:  float
SUBST:   c : cccx
MAP
ccccccc
c..{..c
c.....c
c..G..c
c.....c
c.....c
ccc+ccc
ENDMAP

NAME:    minmay_arrival_statue_studded
TAGS:    arrival
ORIENT:  float
WEIGHT:  1
SUBST:   c : cccx
KMASK:   T = no_monster_gen
MAP
...........
.GcGcGcGcG.
.ccccccccc.
.Gc..{..cG.
.cc.....cc.
.Gc..T..cG.
.cc.....cc.
.Gc.....cG.
.cccc+cccc.
.GcGc.cGcG.
...........
ENDMAP

##############################################################
NAME:    minmay_arrival_four_circles
TAGS:    arrival
ORIENT:  float
SHUFFLE: AB
SUBST:   A = ., B = x
SUBST:   x : xxxcbv
MAP
...................
..AAA...BBB...AAA..
.AAAAA.BBBBB.AAAAA.
.AAAAA.BBBBB.AAAAA.
.AAAAA.BBBBB.AAAAA.
..AAA...BBB...AAA..
...................
..BBB.........BBB..
.BBBBB.......BBBBB.
.BBBBB...{...BBBBB.
.BBBBB.......BBBBB.
..BBB.........BBB..
...................
..AAA...BBB...AAA..
.AAAAA.BBBBB.AAAAA.
.AAAAA.BBBBB.AAAAA.
.AAAAA.BBBBB.AAAAA.
..AAA...BBB...AAA..
...................
ENDMAP

##############################################################
NAME:    minmay_arrival_five_plusses
TAGS:    arrival
ORIENT:  float
SHUFFLE: {>, AB
SUBST:   A = ., B = x
SUBST:   x : xxxxxxccbvwl
KMASK:   wl = no_monster_gen
MAP
.............
..B...A...B..
.BBB.AAA.BBB.
..B...A...B..
.............
..A...x>..A..
.AAA.xxx.AAA.
..A..{x...A..
.............
..B...A...B..
.BBB.AAA.BBB.
..B...A...B..
.............
ENDMAP

##############################################################
NAME:   minmay_arrival_nine_rooms
TAGS:   arrival no_monster_gen
ORIENT: float
NSUBST: ? = 1:{ / *:TG.
SUBST:  T = TV
MAP
xxxxxxxxxxxxxx+xx
x...xxx...xxx...x
x.?.....?.....?.x
x...xxx...xxx...x
xx.xxxxxxxxxxxxxx
x...xxx...xxx...x
x.?.....?.....?.x
x...xxx...xxx...x
xxxxxxxxxxxxxx.xx
x...xxx...xxx...x
x.?.....?.....?.x
x...xxx...xxx...x
xxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
NAME:   minmay_arrival_widening_spiral_wide
TAGS:   arrival no_monster_gen
WEIGHT: 8
ORIENT: float
: if crawl.one_chance_in(4) then
SUBST:  ABCDEFGH = +
SUBST:  abcdefgh = x
: else
SUBST:  a=A, b=B, c=C, d=D, e=E, f=F, g=G, h=H
NSUBST: A = 1:+ / *:X , B = 1:+ / *:X , C = 1:+ / *:X , D = 1:+ / *:X
NSUBST: E = 1:+ / *:X , F = 1:+ / *:X , G = 1:+ / *:X , H = 1:+ / *:X
SUBST:  X:Xxxx+, X=xx+
: end
MAP
xxxxxxxxxxx
x.........H
x.........h
x.........h
x...xxxxxxxxxxxxxx
x...x.....D......x
x...x.....d......x
x...x.....d......x
x...x...xxxxxx...x
x...x...x{+..x...x
xGggxCccxxx..x...x
x...x....xxaAxeeEx
x...x....b...x...x
x...x....b...x...x
x...x....B...x...x
x...xxxxxxxxxx...x
x........f.......x
x........f.......x
x........F.......x
xxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   minmay_arrival_widening_spiral_narrow
TAGS:   arrival no_monster_gen
WEIGHT: 2
ORIENT: float
MAP
xxxxxxxxxxx
x.........+
x.xxxxxxxxx
x.x.......+
x.x.xxxxxxxxxxxxxx
x.x.x.....+......x
x.x.x.xxxxxxxxxx.x
x.x.x.x...+....x.x
x.x.x.x.xxxxxx.x.x
x.x.x.x.x{+..x.x.x
x+x+x+x+xxx..x.x.x
x.x.x.x.xxxx+x+x+x
x.x.x.x..+}x.x.x.x
x.x.x.xxxxxx.x.x.x
x.x.x....+...x.x.x
x.x.xxxxxxxxxx.x.x
x.x......+.....x.x
x.xxxxxxxxxxxxxx.x
x........+.......x
xxxxxxxxxxxxxxxxxx
ENDMAP


##############################################################
NAME:   minmay_arrival_loop
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
      xxxxxxx
    xxx.....xxx
   xx.........xx
  xx.....x.....xx
  x.....xxx.....x
 xx.....xxx.....xx
 x.....xxxxx.....x
 x.....xxxxx.....x
 x.....xxxxx.....x
 x.....xxxxx.....x
 xx.....xxx.....xx
  x.....xxx.....x
  xx.....x.....xx
   xx...{x....xx
    xxxxxx@.xxx
ENDMAP

##############################################################################
NAME:    erik_arrival_orb_chamber
TAGS:    arrival no_rotate
ORIENT:  float
ITEM:    q:1 stone
MONS:    rat
NSUBST:  { = 1:{ / *:.
MAP
  ........@........
  ........{........
...xxxxxxxxxxxxxxx...
...xxxx...1...xxxx...
...xxx..1...1..xxx...
...xx1.........1xx...
@{x....1..d..1....x{@
...xx1.........1xx...
...xxx..1...1..xxx...
...xxxx...1...xxxx...
...xxxxxxxxxxxxxxx...
  ........{........
  ........@........
ENDMAP

##############################################################################
NAME:    dpeg_arrival_lava_altar
TAGS:    arrival no_monster_gen
ORIENT:  northwest
SHUFFLE: Cc, {Y
SUBST:   ?=l. , Y=.
WEIGHT:  6
MAP
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
xxxxxx>lllllllllll..
xxxxxxlClllllllll?..
xxxxxxlllllllll??...
xxxxxxllllllllll....
xxxxxxllllllllll....
xxxxxxllllllllll....
xxxxxxlllllllll?....
xxxxxxlllllllll.....
xxxxxxllllllll?.....
xxxxxxl?ll?ll?.....{
xxxxxxll.??....
xxxxxxl.........
ENDMAP

NAME:    dpeg_arrival_water_altar
TAGS:    arrival no_monster_gen
ORIENT:  northwest
WEIGHT:  4
SHUFFLE: Cc, {Y
SUBST:   Y=.
MAP
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
xxxxxx>wwwwwwwwwwwwww.
xxxxxxwCwwwwwwwwwww...
xxxxxxwwwwwwwwwwww..
xxxxxxwwwwwwwwwwww..
xxxxxxwwwwwwwwwww...
xxxxxxwwwwwwwwwww...
xxxxxxwwwwwwwwww....
xxxxxxwwwwwwwwww....
xxxxxxwwwwwwwww.....
xxxxxxwwwwwwww.....{
xxxxxxwwwwww.....
xxxxxxw..........
ENDMAP

##############################################################################
NAME:    dpeg_arrival_fog
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: <>/>< , {}/}{
MARKER:  # = lua:fog_machine { \
             pow_max = 5, delay_min = 10, delay_max = 150, \
             size = 5, start_clouds = 1}
MAP
xxxxxx
x.{}.x
x....x
x....x
x....x
x@##.x
ENDMAP

##############################################################
NAME:    elethiomel_arrival_shortcut
TAGS:    arrival
ORIENT:  float
SHUFFLE: XY/AB
KFEAT:   X = known permanent teleport trap
SUBST:   Y=x, A=., B=+
MAP
   xxxx@xxxxxxx.@.x
   xxxx.xxxxxx...xx
   xxxx.xxxxx...xxx
   xx....xxx...xxxx
   xx.....x...xxxxx
xxxxx........xxxxxx
x].XY........xxxxxxxxx
xxxxx.{..............@
xxxxx.........xxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
NAME:    onia_arrival_pentagram
TAGS:    arrival no_monster_gen no_pool_fixup no_rotate
ORIENT:  float
SHUFFLE: x+/x+/x+/x+/cC/vV/lL/wW
SUBST:   C=+, V=+
NSUBST:  L = 1:. / *:l
MAP
       .............
       ......x......
       ......x......
       .....xxx.....
............xxx............
...........xxxxx...........
xxxxxxxxxx+xxxxx+xxxxxxxxxx
..xxxxxxxxx.....xxxxxxxxx..
....xxxxxxx.....xxxxxxx....
......xxxx...{...xxxx......
........xx.......xx........
   .....xxx.....xxx.....
   .....xxxxx+xxxxx.....
   ....xxxxxx.xxxxxx....
   ....xxxxx...xxxxx....
   ...xxxx.......xxxx...
   ...xx...........xx...
   ..x.....      ....x..
   ........      .......
ENDMAP

##############################################################################
NAME:   dpeg_arrival_chase
TAGS:   arrival no_monster_gen no_pool_fixup no_rotate
MONS:   kobold, hobgoblin / goblin / bat
ITEM:   stone
ORIENT: float
SUBST:  w : w l:1
MAP
xxxxxxxxxxxxx..........@
xxxxxwwwwwwww2..........
xxxxxxxxwwwwwww........x
xxxxx.......wwww..wwwwwx
xxww...{.......wwwwwwwwx
xwwww...........wwww...x
xwwwwww.................
xwwwwwwwwwwwwwww.......@
x1wwwwwwwwwwwwwwww......
x1d1...wwwwwwwwwwwwwww.x
x1d1...........wwwwwwwwx
x>...................wwx
xxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_rooms_b
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: cvb
NSUBST:  . = 1:d / *:.
ITEM:    stone
MAP
xxxxxxxxxxxxxxx
xcccccccx.....x
xc.....cx..G..x
xc..{..cx.....x
xc.....cxxxx+xx
xc.....cx......
xc.....+......@
xcccccccx......
xxxxxxxxx..@...
ENDMAP

##############################################################################
NAME:    dpeg_arrival_rooms_c
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: cvb
MAP
xxxxxxxxxxxx
xvcvcvcvcvcx
xc........vx
xv........cx
xc.{......vx
xv........cx
xc........vx
xvcvcv+vcvcx
xxxxxx.xxxxx
x..........x
x.G..wwww..x
x..........x
xxxxxxxxxx@x
ENDMAP

#############################################################################
NAME:    dpeg_arrival_shrine
TAGS:    arrival no_monster_gen no_pool_fixup no_rotate
: if crawl.coinflip() then
SUBST:   A=@, B=., C=x, D=l
: else
SUBST:   A=x, B=l, C=@, D=.
: end
SHUFFLE: lw
ORIENT:  float
MAP
xxxxxxxxCxxxxxxxx
xlllllllDlllllllx
xlllll.....lllllx
xll....WWW....llx
xl...WWW.WWW...lx
AB..WWW.{..WW..lx
xl...WWW.WWW...lx
xll....WWW....llx
xlllll.....lllllx
xlllllllllllllllx
xxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_water_temple_mockup
TAGS:    arrival no_monster_gen no_rotate
ORIENT:  float
SHUFFLE: cxxx
NSUBST:  . = 1:d / *:.
SUBST:   T = TVVVV
ITEM:    stone
MAP
        ccccccc@ccccccc
    ccccc.............ccccc
  ccc..........T..........ccc
 cc...T.................T...cc
cc........T.........T........cc
c.............................c
c...T..........{..........T...c
c.............................c
cc........T.........T........cc
 cc...T.................T...cc
  ccc..........T..........ccc
    ccccc.............ccccc
        ccccccccccccccc
ENDMAP

##############################################################################
NAME:    dpeg_arrival_stone_temple_mockup
TAGS:    arrival no_monster_gen no_rotate
ORIENT:  float
SHUFFLE: cxxx
NSUBST:  . = 1:d / *:.
ITEM:    stone
SUBST:   T = TVVVV
MAP
     ccccccccccc
  cccc.........cccc
 cc...............cc
 c......T...T......c
cc...T.........T...cc
c.........w.........c
c...T...........T...c
c{.....w.....w......@
c...T...........T...c
c.........w.........c
cc...T.........T...cc
 c......T...T......c
 cc...............cc
  cccc.........cccc
     ccccccccccc
ENDMAP

##############################################################################
NAME:    dpeg_arrival_lava_temple_mockup
TAGS:    arrival no_monster_gen no_rotate
ORIENT:  float
SHUFFLE: cxxx
NSUBST:  . = 1:d / *:.
ITEM:    stone
SUBST:   T = TVVVV
MAP
         ccccc
        cc...cc
       cc..{..cc
      cc.......cc
     cc..T...T..cc
    cc...........cc
   cc.T....T....T.cc
  cc...............cc
 cc..T...T...T...T..cc
cc...................cc
c......T.......T......c
c..........T..........c
cc...................cc
ccccccccccc@ccccccccccc
ENDMAP

##############################################################################
NAME:    dpeg_arrival_chambers_temple_mockup
TAGS:    arrival no_rotate
ORIENT:  float
SHUFFLE: cxxx
SUBST:   T = TVVVV
KMASK:   T = no_monster_gen
MAP
cccccccccccccc@ccccccccccccccc
cccc......................cccc
cc....c.cccc.cccc.cccc.cc...cc
c..ccc...cc...cc...cc...ccc..c
c.cccc.T.cc.T.cc.T.cc.T.cccc.c
c.cccc...cc...cc...cc...cccc.c
c...ccc.cccc.cccc.cccc.ccc...c
c.T.ccc.cccc.cccc.cccc.ccc.T.c
c............................c
cc.cc.........{..........cc.cc
c............................c
c.T.ccc.cccc.cccc.cccc.ccc.T.c
c...ccc.cccc.cccc.cccc.ccc...c
c.cccc...cc...cc...cc...cccc.c
c.cccc.T.cc.T.cc.T.cc.T.cccc.c
c..ccc...cc...cc...cc...ccc..c
cc...cc.cccc.cccc.cccc.cc...cc
cccc......................cccc
cccccccccccccccccccccccccccccc
ENDMAP

##############################################################################
NAME:    dpeg_arrival_leaves_temple_mockup
TAGS:    arrival no_monster_gen no_rotate
ORIENT:  float
SHUFFLE: cxxx
NSUBST:  . = 1:d / *:.
ITEM:    stone
SUBST:   T = TVVVV
MAP
   ccccccccccccccccc
  cc.ccccccccccccc.cc
 cc....ccccccccc....cc
cc..T....ccccc....T..cc
c.....T....c....T.....c
c..T....T.....T....T..c
c.....................c
ccccccc....{....ccccccc
 ccccccccc...ccccccccc
  cccccccc...cccccccc
   cccccc..T..cccccc
    cccc.......cccc
     cc..T...T..cc
     c.....T.....c
     ccc.......ccc
       cccc@cccc
ENDMAP

##############################################################################
NAME:   dpeg_arrival_three_same_balls
TAGS:   arrival no_rotate no_monster_gen
ORIENT: float
SUBST:  ' = .x
SUBST:  G : GTVt
NSUBST: d = 1:d / *:'
ITEM:   stone
MAP
               xxxxx
             xxx...xxx
   xxxxxxxxxxx.......xx
 xxx...xxxxxx.........x
xx.......xx......G....x
x..........xx.........x
x....{....xxxx.......xx
x.........xxxxxx...xxx
xx.......xxxxxx.xxxx
xxxx...xxxxxx...xxxx
xxxxxxxxxxx.......xx
xxx...''...........x
x'.''d..xx....G....x
x'.'xx'xxx.........x
xd..'d...xx.......xx
xx''..''..xxx...xxx
 xxxxxxxx@xxxxxxx
ENDMAP

#############################################################################
NAME:   lemuel_arrival_hallway
TAGS:   arrival no_rotate no_monster_gen
ORIENT: float
NSUBST: ! = 1:> / *:!
SUBST:  !:cvxGT
MAP
     x.@.x
     x.!.x
     x...x
     x.!.x
     x...x
     x.!.x
     x...x
     x.!.x
     x...x
xxxxxx.!.xxxxxx
x.............x
x.............x
x......{......x
x.............x
x.............x
xxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    lemuel_arrival_rhombus
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: abc
SUBST:   a:+, b=x, c=x
MAP
      x@x
    xax.xcx
   xx.....xx
  xb.......bx
 xx.........xx
xc...........ax
x.............x
x.............x
x......{......x
x.............x
x.............x
xx...........xx
 xc.........bx
  xx.......xx
   xa.....xc
    xx...xx
     bx.xa
      x@x
ENDMAP

##############################################################################
NAME:    lemuel_arrival_room
TAGS:    arrival
ORIENT:  float
SHUFFLE: '?!
SUBST:   ':.+, ?:x, !:x
MAP
xxxxxxxxxxx@xxxxxx
xxxxxxxxxxx.xxxxxx
xx..............xx
xx..............xx
xx..xx!?'xxxxx..xx
xx..'........x..xx
xx..!........x...@
xx..?........x..xx
xx..x...{....x..xx
xx..x........?..xx
@...x........!..xx
xx..x........'..xx
xx..xxxxx'?!xx..xx
xx..............xx
xx..............xx
xxxxxx.xxxxxxxxxxx
xxxxxx@xxxxxxxxxxx
ENDMAP

#########################################
# good luck! (Shiori)
NAME:    shiori_shiawase
TAGS:    arrival no_monster_gen no_rotate no_vmirror
ORIENT:  float
SHUFFLE: +=
SUBST:   =:x , x:xvcb
MAP
      x+x
      x.x
    xxx.xxx
    =..{..=
xxxxxxx.xxxxxxx
+.............+
xxxx.xxxxx.xxxx
  xx.xxxxx.xx
  +.........+
 xxxxxx.xxxxxx
 =...........=
 xxxxxx.xxxxxx
      x.x
      x.x
      x+x
ENDMAP

############################################################################
# A spiral made of rock, with lots of "windows".
NAME:    zelgadis_glass_arrival_large
TAGS:    arrival
ORIENT:  float
WEIGHT:  1
NSUBST:  X = 4:+ / *:x
MAP
mxmxmxmxmxmxxmxxmx
x................m
m.xmXmxmxmxmxmXm.x
x.m............x.m
m.x.mxmxmXmxmx.m.x
x.m.x........m.X.m
m.X.m.xmxmxx.x.m.x
x.m.x.m....x.m.x.m
m.x.m.x.mx.m.X.m.x
x.m.X.m..m.x.m.x.m
m.x.m.x.{x.m.x.m.x
x.m.x.xmxx.x.m.x.m
m.X.m......m.x.m.x
x.m.xxmXmxmx.m.x.m
m.x..........x.m.x
x.xmXmxmxmxmXm.X.m
m..............m.x
xxmxmxmxmxmxmxmx@m
ENDMAP

##############################################################################
NAME:    dpeg_arrival_stonehenge_12_columns
TAGS:    arrival no_rotate
ORIENT:  float
WEIGHT:  3
ITEM:    nothing, nothing, nothing
SHUFFLE: cccbvxxx, def}
MAP
   ........................
   ............cc..........
  d......cc....cc....cc....e
.........cc..........cc.......
....cc........................
....cc...................cc...
.........................cc...
..............................
..cc..........................
..cc...........{..........cc..
..........................cc..
..............................
...cc.........................
...cc...................cc....
........................cc....
.......cc..........cc.........
  f....cc....cc....cc......}
   ..........cc............
   ........................
ENDMAP

NAME:    dpeg_arrival_stonehenge_8_columns
TAGS:    arrival no_rotate
ORIENT:  float
WEIGHT:  7
SHUFFLE: cccbvxxx
MAP
   ....................
   .........cc.........
   .........cc.........
.....cc............cc.....
.....cc............cc.....
..........................
..........................
..cc.........}........cc..
..cc........{.........cc..
..........................
..........................
.....cc............cc.....
.....cc............cc.....
   .........cc.........
   .........cc.........
   ....................
ENDMAP

##############################################################################
NAME:   dpeg_arrival_simpleton_a
TAGS:   arrival  no_monster_gen
ORIENT: float
NSUBST: . = 1:d / *:.
ITEM:   stone
MAP
@xx           xx@
x.xxxx     xxxx.x
xx...xxx xxx...xx
 xxx...xxx...xxx
   xx.......xx
    xxx.{.xxx
   xx.......xx
 xxx...xxx...xxx
xx...xxx xxx...xx
x.xxxx     xxxx.x
@xx           xx@
ENDMAP

##############################################################################
NAME:    dpeg_arrival_simpleton_b
TAGS:    arrival
ORIENT:  float
ITEM:    nothing, nothing
SHUFFLE: =+, TVG
SUBST:   c:cxx
SUBST:   = = x
KMASK:   T = no_monster_gen
MAP
xx@...@xxxxxxxx
xx+xxx+xxxxxxxx
x.......x......
x.......=..)..@
x.T.G.T.x......
x.......xxx=xxx
x.......xcc=ccx
xxxxxxxxxc...cx
xccccccccc.{.cx
@........+...cx
xcccccccccccccx
xxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_simpleton_c
TAGS:    arrival
ORIENT:  float
ITEM:    nothing, nothing
SUBST:   d : GTVt
SHUFFLE: =+, cx
SUBST:   = = x
MAP
@.........@
...xx+xx...
...x...x...
.xxx.d.xxx.
.x.......x.
.=.d.{.d.=.
.x.......x.
.xxx.d.xxx.
...x...x...
...xx+xx...
@.........@
ENDMAP

##############################################################################
NAME:    dpeg_arrival_prison
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: ABCDEFGH
SUBST:   A={, B=', C=', D=', E=', F=', G=', H=d
SUBST:   ' = .:70 1 w:2
SUBST:   . = .:60 2:1 3:1
ITEM:    stone
MONS:    fungus, rat, giant cockroach
MAP
xxxxxxxxxxxxxxxxx
x'A'x'B'x'C'x'D'x
x'.'x'.'x'.'x'.'x
xx+xxx+xxx+xxx+xx
x'..............x
x'..............+
x'..............x
xx+xxx+xxx+xxx+xx
x'.'x'.'x'.'x'.'x
x'E'x'F'x'G'x'H'x
xxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:   dpeg_arrival_refuge
TAGS:   arrival
ORIENT: float
SUBST:  c:xxxc, %:%.T, T:TGv.
KMASK:  T = no_monster_gen
MAP
.....................
.....................
..xxxxxxxx+xxxxxxxx..
..xxxx.........xxxx..
..xx...T.....%...xx..
..x..T...c+c...%..x..
..+.....cc{cc.....+..
..+.....cc>cc.....+..
..x..%...c+c...T..x..
..xx...%.....T...xx..
..xxxx.........xxxx..
..xxxxxxxx+xxxxxxxx..
.....................
....................@
ENDMAP

##############################################################################
NAME:    dpeg_arrival_ratfight
TAGS:    arrival
MONS:    rat, goblin, bat
ORIENT:  float
SUBST:   X = x+
MAP
xxxxxxxxxxxx..@
xx.1.%1xxxxx...
x%.1.....xxx...
x1........xx...
xxx...xx..xx...
xxxxxxxxx.1x...
xxxxxxxxxx+xXxx
....xxxxxx....x
....xxxxxx.{..x
@...xxxxxxxx@xx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_houses_and_road
TAGS:    arrival
ORIENT:  float
SHUFFLE: +'
SUBST:   ' : x + ':40, '=x+
MAP
xxxxxxx.@.xxxxxxx
x.....x...x.....x
x..(..'...+.....x
x.....xx.xx.....x
xxx+xxxx.xxxx'xxx
@....xx...xx....@
.....xx...xx.....
xxx+xxxx.xxxx+xxx
x.....xx.xx.....x
x.....'...'..]..x
x.....x...x.....x
xxxxxxx.@.xxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_rhombus
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: @A
SUBST:   A=x
MAP
      xxAxx
   xxxx...xxxx
xxxx.........xxxx
@.......{.......@
xxxx.........xxxx
   xxxx...xxxx
      xxAxx
ENDMAP

############################################################
NAME:   zaba_arrival_water
TAGS:   arrival no_monster_gen no_rotate
ORIENT: float
SUBST:  T = TV, ? = wW, > = >d
ITEM:   stone
MAP
xxx@xxx@xxx@xxx
x.............x
x.G.TTT>TTT.G.x
x.............x
xw?W?wwWww?W?wx
xwwW?wwWww?Wwwx
xwwWWwwWwwWWwwx
xww?WwwWwwW?wwx
xwwwW?wWw?Wwwwx
xwwwWWwWwWWwwwx
xwww?WwWwW?wwwx
xwwwwWwWwWwwwwx
xwwwwWWWWWwwwwx
xwwww?WWW?wwwwx
xwww?WWWWW?wwwx
xww?WW...WW?wwx
xw?WW..{..WW?wx
xww?WW...WW?wwx
xwww?WWWWW?wwwx
xwwww?????wwwwx
xwwwwwwwwwwwwwx
xwwwwwwwwwwwwwx
xxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    lemuel_arrival_tunnels
TAGS:    arrival no_monster_gen
ORIENT:  float
SUBST:   ? = x.
SHUFFLE: {[(
SUBST:   [( = .
MAP
xxx
x{xx          xxxxx
x..xxxxxxxxxxxx[x?x
xx.?xxxxx(xxxx?...x
x?..xxxx..xxxx???.x
x..?x?..?xxxx?x??.x
x.?xx..xxxxx?.....x
x.?xx.?xxxx?x.x???x
xx..?.xxxx??..xxxxx
 xx...?x??x..xx
  xxx.xx....xx
    x..x.??xx
    xx.?.xxx
     x..??xx
     x?x.xxx
     xx?.?xx
      xx..?x
       xxx@x
ENDMAP

##############################################################################
NAME:    lemuel_arrival_muddy
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: {[(
SUBST:   [( = .
SUBST:   ? = w'
NSUBST:  ' = 12:W12" / 24=W"12 / *:.
SUBST:   " = . W:1
MONS:    nothing / plant, nothing / fungus
MAP
xxxxxxxxxxxxxxxxxxx
xxxxx{''1''('1ww'[x
xxxxx1'''1'''11ww'x
xxxx?x''w.11''??1'x
xxx??x''''''''2'1'x
xxx?ww''11'''''?w'x
xxxxww?''''''''''xx
xxx?w?11?'22'2'''.x
xxx?w11?''''w2''''x
xxxx???''1''???''xx
xxxxx?''1??''??'xxx
xxxx?x'''''2?''"xxx
xxxx?x'''2''''""xxx
xxxx?w''''2''"""xxx
xxxxx'22w""""""xxxx
xxxxxxx""2"""""xxxx
xxxxxxx""2""22xxxxx
xxx.....""""xxxxxxx
xx..xx......xxxxxxx
@..xxxxx.....xxxxxx
xxxxxxxxxx....xxxxx
xxxxxxxxxxxx.@.xxxx
ENDMAP

##############################################################################
NAME:    lemuel_arrival_broad_hall
TAGS:    arrival no_monster_gen
ORIENT:  float
SUBST:   ? : xxcvG.
MAP
xxxxxxxxxxx
x....(....x
x.........x
x.?.....?.x
x.........x
x.........x
x.?.....?.x
x.........x
x.........x
x.?.....?.x
x.........x
x.........x
x.?.....?.x
x.........x
x>.@.>.@.>x
ENDMAP

##############################################################################
NAME:    lemuel_arrival_behind_the_door_large
TAGS:    arrival no_monster_gen
WEIGHT:  5
ORIENT:  float
SUBST:   b : bcvxxx
MAP
xxxxxxxxxxx
x....{....x
x.........x
x.........x
x.........x
x.........x
x.........x
x.........x
x.........x
xb++bbb++bx
x....@....x
ENDMAP

NAME:    lemuel_arrival_behind_the_door_small
TAGS:    arrival no_monster_gen
WEIGHT:  5
ORIENT:  float
SUBST:   b : bcvxxx
MAP
xxxxxxxxxxx
x....{....x
x.........x
x.........x
x.........x
xb+bb+bb+bx
x....@....x
ENDMAP

##############################################################################
NAME:    dpeg_arrival_oval
TAGS:    arrival no_rotate
ORIENT:  float
SHUFFLE: @X
SUBST:   v:vxxx, c:vxxxxc
SUBST:   X=x
NSUBST:  { = 1:{ / *:.
MAP
        xxxxxxx@xxxxxxx
    xxxxx.............xxxxx
  xxx.......vxvxvxv.......xxx
 xx......xvx.......xvx......xx
 x....vxv......{......vxv....x
xx...x....{.ccccccc.{....x...xx
x...v..{..ccccccccccc..{..v...x
x...x....ccccccccccccc....x...X
x...v....ccccccccccccc....v...x
x...x..{..ccccccccccc..{..x...x
xx...v....{.ccccccc.{....v...xx
 x....xvx......{......xvx....x
 xx......vxv.......vxv......xx
  xxx.......xvxvxvx.......xxx
    xxxxx.............xxxxx
        xxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:   dpeg_arrival_pond
TAGS:   arrival no_monster_gen no_pool_fixup
ORIENT: float
SUBST:  0 = 0 .
SUBST:  ' = w'
NSUBST: . = 1:d / *:.
NSUBST: " = 1:@ / *:.
ITEM:   stone
MAP
".0.0....0."
xxwwwwwwwxxx
xwwwwwwwwwwx
xwwww'wwwwwx
xwww....'wwx
xww......'wx
x'.......'wx
x....{..'wwx
.........wwx
@........xxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_rhombi
TAGS:    arrival no_monster_gen
SHUFFLE: WYZ{/wyz(
SUBST:   {=., [=.
SUBST:   Z=x, Y=x, W=@, C=+x
SUBST:   z=@, y=+, w=x
NSUBST:  . = 1:d / *:.
ITEM:    stone
ORIENT:  float
MAP
  xZxxx
  xYxxxxxx
 xx.xxxx.xx
xx...xx.x+x
x..{..+xx.xx
xx...xxx...xx
xxx.xxx.....xx
xxxCxx...(...W
xxx.xxx.....xx
xx...xxx...xx
x..[..+xx.xx
xx...xx.x+x
 xx.xxxx.xx
  xxxxxxxx
ENDMAP

##############################################################################
###############################################################################
# Sounds of Crawl: the *ZOT*
NAME:    evilmike_arrival_sounds_zot
TAGS:    arrival no_monster_gen
ORIENT:  float
WEIGHT:  1
KPROP:   >'Z1 = no_tele_into
KFEAT:   Z = zot trap
SHUFFLE: vXc
NSUBST:  { = 1:{ / *:.
MONS:    generate_awake ooze
MAP
     v..@..v
    vvv...vvv
   vvvvv{vvvvv
  vvvvvvvvvvvvv
 vvvvvvvvvvvvvvv
vvvvvvvvvvvvvvvvv
.vvvvv>''''vvvvv.
..vvvv'''''vvvv..
@.{vvv''Z''vvv{.@
..vvvv'''''vvvv..
.vvvvv''1''vvvvv.
vvvvvvvvvvvvvvvvv
 vvvvvvvvvvvvvvv
  vvvvvvvvvvvvv
   vvvvv{vvvvv
    vvv...vvv
     v..@..v
ENDMAP



##############################################################################
NAME:    dpeg_arrival_arbitrary_a
TAGS:    arrival
ORIENT:  float
SUBST:   ' : . x ':40, '=.x
NSUBST:  . = 1:d / *:.
NSUBST:  { = 1:{ / *:.
ITEM:    stone
MAP
    xxxxxxxxxxxxxxxxx
   xx''xx''''''xx''xx
   x''..'..xxx.'x..'x
  xx'.{...xx'......'x
  x'.....x''..{...'xx
  x'....'.......''xxx
 xx'.........''xxxxxx
 x'........'xxxxxxxxx
xx'......''xxxxxxxxx.
x'.....'xxxxxxxxxxx..
x...''xxxxxxxxxxxxx.@
x.'xxxxxxxxxxxxxx....
x..'xxxxxx''.'x..'xxx
x'...'''x'.xx..'xxxxx
x'..........xxxxxxxxx
xx'..{.xxx'....''xxxx
 xx'....xxxx''...''xx
  xxxx'''x xxx''...'x
     xxxxxxxxxx..@..x
ENDMAP

##############################################################################
NAME:    dpeg_arrival_arbitrary_b
TAGS:    arrival
ORIENT:  float
SUBST:   ' : . x ':40, '=.x
NSUBST:  { = 1:{ / *:.
MAP
     xx'.@.'xxx
   xxx'..'...'x
  xx'...xxxx..xx
  x...'xxxxx'.'x
 xx.{.'xxxxxx..xx
 x'....'xxxxx'..x
xx...'...xxxxx'.'
x..''xx'..'xx...@
..'xxxxx.'.'...'x
@.'xxxx'.....{.'x
'..'x'....''...xx
x'.....{.'xxx'.'x
xx'''....xxxx'.xx
 xxxx.'xxxxxxx.xx
   xx.x'xxxxx.'xx
   x'..@''xx.@.'x
ENDMAP

##############################################################################
NAME:    dpeg_arrival_arbitrary_c
TAGS:    arrival
ORIENT:  float
SHUFFLE: TV
KMASK:   T = no_monster_gen
MAP
  xx..@..xx
 xx...x...xx
 x...xxx...x
 x...xxx...x
xx..xxxxx..xx
x...........x
x...........x
x.T...{...T.x
x...........x
x...........x
xxxx..x..xxxx
   xx...xx
   x..@..x
ENDMAP

##############################################################################
NAME:    dpeg_arrival_subterranean_lakeside_camping
TAGS:    arrival no_monster_gen
ORIENT:  float
MONS:    goblin, rat, bat
SUBST:   ? : . w ?:40, ' : . x ':40
SUBST:   ? = .w, ' = .x
MAP
      xxxxxxxxxxxxxxxxxxxx
   xxxx?xx1.'xxxxxxxx'.33x
 xxx'.??.xxx.xxx.x.x..xxxx
xx'..?w.....?.....x..xxx'x
x'.?www?.?.?www?.xx...2x.'
x'?www?.??w.wwww?...?.xx.@
xx.???.?wwww.www.?ww..x..'
xxx'..?wwww??.wwwww?.xx.xx
''xx..?www?.{.?www?.xx.xxx
@..xx?wwwww???wwwww?..xx'x
..xx..?wwwww.wwwww?..x..xx
x'.xxx.??wwww.ww.??...xxxx
xx..x....??www.??...xxxx'x
 xx...??....??.xx?.x.....x
  xx'...'xxxxxxx''...'x.2x
  x'.xxxxx'''xxxxxx''xx1xx
  xxxxxxxx.@.xxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_more_subterranean_lakeside_camping
TAGS:    arrival no_monster_gen
ORIENT:  float
NSUBST:  { = 1:{ / *:.
SUBST:   ? : . w ?:40, ' : . x ':40
SUBST:   ? = .w, ' = .x
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxx
xwwwwwwwww?......'''....''.
xwwwwww??...?w?......'.....
xwww??....w.www??...'x'..'.
xww??...?ww.w.ww.?..xx.....
xw?....?ww{www..w.?..'.....
xw?.{.?wwwwwwwww......'...@
xw?....ww{wwww..w....'x.'..
xww??...?wwww?.ww?....x.''.
xwww??.....?wwww...x..'....
xwwwwww??....???..'x....xx'
xwwwwwwww??''...........'''
xxxxxxxxxxxxxxxxx..@....'''
ENDMAP

##############################################################################
NAME:   dpeg_arrival_erosion
TAGS:   arrival
ORIENT: float
SUBST:  '=cx., "=+.x, c:xxc
MAP
xxxxxxxxxxxxxxxxxxxx
xccccccx.......x..c.
xccccccc..ccx'.x..x.
xcc....c..'..c.'+''.
xcc.{..+..c..+.x.."@
xcc....c..cxxc.'..x.
xccccccc..x....'xx'.
xcccccxxc...cc......
xxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_orchard
TAGS:    arrival
ORIENT:  float
MONS:    plant
SHUFFLE: 1x
SUBST:   x = '
NSUBST:  ' = 2:d / *:'
NSUBST:  . = '.
KITEM:   d : fruit q:1
KFEAT:   d = '
FTILE:   d'1{ = floor_grass
COLOUR:  d' = green
MAP
  .....
 .x...x.
 '1'''1'
..''{''..
 '1'''1'
 .x...x.
  .....
ENDMAP

##############################################################################
NAME:    dpeg_arrival_round_a
TAGS:    arrival
ORIENT:  float
SHUFFLE: {[(
# Equal chance of passable, blocked at ?, blocked at A, and blocked at a.
# Blocking the path at two points would create a disconnected bubble.
: local r = crawl.random2(4)
: if r == 0 then
:   subst("? = x")
:   subst("A = .")
:   subst("a = +")
: elseif r == 1 then
:   subst("? = +")
:   subst("A = .")
:   subst("a = x")
: else
:   subst("? = +")
:   subst("A = '")
:   subst("a = +")
: end
: if crawl.random2(3) == 0 then
:   subst("B = .")
:   subst("b = x")
: else
:   subst("B = '")
:   subst("b = x")
: end
SUBST:   ' = x. , v : cvxxxx
NSUBST:  { = 1:{ / *:.
MAP
xxxxxxxxxxxxxxxx.@
@...xxxxxx.Axx.a..
xxx'.xxxx.xx..xx..
xxxx.'xxx?xxxxxx..
xxxxx.xxx.xxxxxx..
xx{x.......xxxxx..
xx...vvvv..'xxxx..
x'.vvvvvvv....xx..
x..vvvvvv..xx'.x..
xx..vv....xxxxBx..
x{x'...xx{xxxx.b..
xxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_round_b
TAGS:    arrival
ORIENT:  float
SHUFFLE: {[(
SUBST:   ' : . x ':40, '=x.
NSUBST:  { = 1:{ / *:.
MAP
   x@xxxxxxxx
 xxx.xxxx...xx
xx'...xx..x..@
x'..x..'....xx
x'.......'xxxx
x'........xxxx
x'...''....xxx
xx..'xxx'...xx
 x.'xxxxxx..xx
 x..xxxxxxx.'x
 xx.'xxxxxx..x
  x..xxxxxx.{x
  xx..xxxx..xx
   xx...xx.'x
    xxx.{.{'x
      xxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_round_c
TAGS:    arrival
ORIENT:  float
SUBST:   ' : . x ':40, '=x., " : . c ":40, "=.c , c:cxx
NSUBST:  { = 1:{ / *:.
MAP
    xxxxxxx
  xxx.....xxxx
 xx'.."""....xxxxx
xx'.."ccc"..{.....xxxxxxx
x...cccccc"...cc...xxx.@
x.{.ccccc"..'.."cc..x.x.
x....cc....'xx.."c".'xx.
xx......{.'xxxx'....'xx.
 x'..''...xxxxxxxx.'xxx.
 x'..''xxxxxxxxxxxx.xxx.
 xx''xxxxxxxxxxxxxx.xxx.
  xxxxxxxxxxxxxxxxx@...@
ENDMAP

##############################################################################
NAME:    dpeg_arrival_short_road
TAGS:    arrival
ORIENT:  float
SHUFFLE: XY
SUBST:   X=+, Y=x, %=%.
NSUBST:  { = 1:{ / *:.
MAP
xxxxxx@.xxx
x{.xx..xxxx
x..x...+..x
x..x..xx.{x
x..+..xx..x
xXxx..xxxxx
x%Y..xxxxxx
xxx..x...{x
xx...+....x
xx.@xxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_court
TAGS:    arrival
ORIENT:  float
SHUFFLE: TVG
KMASK:   T = no_monster_gen
NSUBST:  { = 1:{ / *:.
MAP
xxxxx.@.xxxxx
x...........x
x.xxx...xxx.x
x.x.......x.x
x.x.T.x.T.x.x
x.....x.....x
x....xxx....x
xxxx..x..xxxx
x..+.....+..x
x{.xxx+xxx..x
x..x{....x.{x
xxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_signposts
TAGS:    arrival no_monster_gen no_rotate
ORIENT:  float
SHUFFLE: DX'/CY"
SUBST:   D = c, XC" = x, 'Y = .
SHUFFLE: cvb
NSUBST:  . = 1:d / *:.
ITEM:    stone
MAP
          xxxxxxxxxxx
     xxxxxx...xxxx..xx
xxxxxx..xD.xxx.xx.xx.xxxxx
x..Cx.x.x.xxx'xxxx.x'x....
x....xx'X.'xx'Dx..xx.x.cx.
x{.xxxXxx.x'x...'xxxx..xx@
x....xxX'CX'x.xCXx..xX.cx.
x..Dx...xx.XXx'''.Xx.x....
xxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_renting
TAGS:    arrival
ORIENT:  float
SHUFFLE: [>, {(, T), TcGv, XY
SUBST:   ':.x, X=+x, Y=+x, (=., [=.
KMASK:   T = no_monster_gen
MAP
xxxxxxxxxxxxxxxxxx..@
x'''''''xxxxxxxxxx...
x'.....'xxxx...xxx...
x'.x+x.xxx'..T..'x...
x..x.x.[xx.......Y...
xxxx.xxxxx''x+x''x...
xxxx.xxxxxxxx.xxxxxXx
@...................@
xxxxxx.xxxxxxxxxxx.xx
xxxxxx+xxxxxx...'x.xx
xxxx''.''xxxx.c..x.xx
xxx'.....'xxx.{..+.xx
xxx'.xxx.'xxx.c..xxxx
xxxx..(..xxxx...'xxxx
xxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_brainrot
TAGS:    arrival no_monster_gen
MONS:    jackal / worm
ORIENT:  float
SUBST:   l = l.
NSUBST:  { = 1:{ / *:.
MAP
xxxxxxxxxxxxxxxxx@xxx
@.xx...xx..xx..xx.xxx
xx..xl.x.xxx..x..x.xx
xxlll.xxx...xxllxx.xx
xxxll.xx.xxxx.xx.xxxx
@..xxl.x.x...x..x.xxx
xxx..lx.x..xxxxxx.x.x
xx%xx.x.xxxx...{x.x.x
xlxlxxxx.xxx....xx.xx
x.xlx.xl..x.xxllxxxxx
xx.x.x.xxx.x.x..xxxxx
x.xxxxx.lxxxx.xxlllll
x..x..x.....x.xxl%1>l
x{...x.x.xll..xxlllll
xxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_another_circle
TAGS:    arrival no_monster_gen no_rotate
ORIENT:  float
WEIGHT:  5
SHUFFLE: lw
MAP
      xxxxxxxxxxx
   xxxx.........xxxx
  xx...xxxx+xxxx...xx
 xx.xxxxxx...xxxxxx.xx
 x.xxxxxxx...xxxxxxx.x
xx.xxxxxxxx+xxxxxxxx.xx
x.xxl..xx.....xxx.lxx.x
x.xll..x.......x..llx.x
@.xll..+...{...+..llx.@
x.xll..x.......x..llx.x
x.xxl..xx.....xxx.lxx.x
xx.xxxxxxxx+xxxxxxxx.xx
 x.xxxxxxx...xxxxxxx.x
 xx.xxxxxx...xxxxxx.xx
  xx...xxxx+xxxx...xx
   xxxx.........xxxx
      xxxxxxxxxxx
ENDMAP

NAME:    dpeg_arrival_yet_another_circle
TAGS:    arrival no_monster_gen
ORIENT:  float
WEIGHT:  5
SHUFFLE: lw
MAP
      xxxxx@xxxxx
   xxxx.........xxxx
  xx...xxxx+xxxx...xx
 xx.xxxxwwwwwwwxxxx.xx
 x.xxxxxww...wwxxxxx.x
xx.xxxxx.......xxxxx.xx
x.xxxxxxxxx+xxxxxxxxx.x
x.x..x...........x..x.x
x.+..+.....{.....+..+.x
x.x..x...........x..x.x
x.xxxxxxxxx+xxxxxxxxx.x
xx.xxxxx.......xxxxx.xx
 x.xxxxxww...wwxxxxx.x
 xx.xxxxwwwwwwwxxxx.xx
  xx...xxxxxxxxx...x
   xxxx.........xxxx
      xxxxx@xxxxx
ENDMAP

##############################################################################
NAME:   matt_arrival_diagonals
TAGS:   arrival no_monster_gen
ORIENT: float
SUBST:  ':x., ":x., ?:x., _:x.
MAP
xxxxxxxxxx@xxxxxxxxxx
xx..........'......xx
x.x......_.x......x.x
x..x......x......x..x
x...x....x.x."..x...x
x....x..x...x..x....x
x.....xx...".xx.....x
x.....x...x...x.....x
x..x.x.?.x.....x....@
@...x...x.{...x.x...x
x..'.x.".x...x..._..x
x.'...x...x.x.x.....x
x....x.x...x...x....x
x...x...x...x...x...x
x..x.....x...?...x..x
x.x.......x.......x.x
xx.................xx
xxxxxxxxxx@xxxxxxxxxx
ENDMAP

##############################################################
# A Maze of Twisty Passages, All Alike
NAME:    elethiomel_arrival_maze
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: WXYZ
SUBST:   W=., X=X., X=x+., Y=Y., Y=x., Z=Z., Z=x+.
NSUBST:  { = 1:{ / *:.
MAP
xx@xxxxxxxxxxxxxxxxxxxxxxxxxx
xx.x.......xxxxx...xx......xx
xx.xXxxxxx.xxxxx.x.xxxxxxx.xx
xx...x{..x...x...x...x{xxx..@
xxxx.xxx.x.x.x.xxxxx.x.xxx.xx
@..x...x...x.....xxx...x...xx
xx.xxx.xxxxxxxxx.xxxxxxx.xxxx
xx.....xxxxx.x.....x.W...x..@
xxxx.xxxxxxx.x.xxx.x.xxxxx.xx
xx...xxxxxxx...xxx...x...Z.xx
xx.xxxxxxxxx.x.xxx.xxx.xxx.xx
xx...xxxxx...x.....x...x...xx
xxxx.x{xxx.xxxxxxxxx.xxx.xxxx
xxxx.....Y...........xxx...xx
xxxxxxxxxxxxxxxxxxxxxxxxxx@xx
ENDMAP

##############################################################
# It's quiet. Too quiet....
NAME:    elethiomel_arrival_quiet
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: '"XY/ABVW, !?/CD
SUBST:   ':., ":+, A:x, B:x, !:., ?:+, C:x, D:x, V=x, W=x, Y=@x
SUBST:   c : cxx
KFEAT:   X = teleport trap / floor
MAP
         x@x
         x.x
       ccc+ccc
       c.G.G.c
       c.....c
       c.....c
       c.G.G.c
       c.....c
xxcccccc.....ccccccxx
YX''''"..G.G..?!!!!!!
xxcccccc.....ccccccxx
      cG.....Gc
      c...{...c
      ccccccccc
ENDMAP

##############################################################
NAME:    onia_arrival_mini_maze
TAGS:    arrival
WEIGHT:  5
ORIENT:  float
ITEM:    fruit q:1 / nothing w:20 / stone
SHUFFLE: Aa, Bb, Cc, Ee, Ff
MONS:    w:30 nothing / w:10 plant / w:5 fungus / w:10 rat
SUBST:   A=x, B=x, C=x, E=x, F=x, a=., b=., c=., e=., f=.
SUBST:   X = xxxxxxxxxxxxxxxxx+
NSUBST:  { = 1:{ / *:.
MAP
xxxxxxxxx@xxxxxxx
x.......A.a.....x
x.xxxxXexXxBxxx.x
x.xd1.......x1f.x
x.xxxxxxxXxbx.x.x
x.X...x.......X.x
xEx.x.x.xxxxx.xFx
x...x.c...1dx...x
xxxxxCxxxxxxxx.xx
x...............x
x.xxxxxx.xxxxxx.x
x.....{x{x{.....x
xxxxxxxxxxxxxxxxx
ENDMAP

NAME:    onia_arrival_mini_maze_b
TAGS:    arrival
WEIGHT:  5
ORIENT:  float
ITEM:    fruit q:1 / nothing w:50 / stone
SHUFFLE: AB
SUBST:   A : x , B = +., X = x+
MONS:    w:30 nothing / w:10 plant / w:3 fungus / w:1 worm
NSUBST:  { = 1:{ / *:.
MAP
xxxxxxxxx@xxxxxxx
x.......A.B.....x
x.xxxxxBxxxAxxx.x
x.xd1........dx.x
x.xxxxxxxxxxx1x.x
x.x...B.......x.x
x.x.x.x.xxxxxAx.x
x...x.x...1dx...x
xxxxx.xxxxxxxx.xx
x...............x
x.xxxxxX.Xxxxxx.x
x.....{x{x{.....x
xxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
NAME:    onia_arrival_larder
TAGS:    arrival
ORIENT:  float
MONS:    w:100 rat / w:50 giant cockroach / quokka
SUBST:   ? = .:30 1:20
MAP
xxxxxxxxxx@xxxxxxxxxx
xxxx?x?x?x.x?x?x?xxxx
xxxx+x+x+x+x+x+x+xxxx
xxxx.x.x.x.x.x.x.xxxx
x?+.......{.......+?x
xxxx.x.x.x.x.x.x.xxxx
xxxx+x+x+x+x+x+x+xxxx
xxxx?x?x?x?x?x?x?xxxx
xxxxxxxxxxxxxxxxxxxxx
ENDMAP

#############################
NAME:    onia_arrival_handbag
TAGS:    arrival
ORIENT:  float
MONS:    w:30 rat / ooze / ball python
SHUFFLE: {[, [<(
SUBST:   (=.,[=.
MAP
.......@........
.xx..xx>x{x..xx.
.....xxxxxx.....
.xx1xxxxxxxx1xx.
.(xxxxxxxxxxxx<.
.xx1xxxxxxxx1xx.
.....xxxxxx.....
.xx..x[x>xx..xx.
.......@........
ENDMAP

##############################################################
NAME:     eino_arrival_cavern_a
TAGS:     arrival no_monster_gen no_rotate
ORIENT:   northwest
SUBST:    V = V T:2
SUBST:    ' : x.
WEIGHT:   5
MAP
xxxxxxxxxxxxxxxxx
xxxxx{xxxxxxxxxxx
xxxxx..xxxxxxxxxx
xxxxx...xxxxxxxxx
xxxx....'''xxxxxx
xxxx.....'''xxxxx
xxx......''''xxxx
xxx......'''xxxxx
xxx.......'xxxxxx
xx........xxxxxxx
xx.........xxxxx.
xx..............@
xx.........xxxxxx
xx.......V..xxxxx
x...........xxxxx
x.......V.V..xxxx
x.............xxx
xx.....V.V.V...xx
xx..............x
xxx@.@xxxxxxx@.xx
ENDMAP

NAME:     eino_arrival_cavern_b
TAGS:     arrival no_monster_gen no_rotate
ORIENT:   northwest
WEIGHT:   5
MAP
xxxxxxxxxxxxxxxx
xxxxxxx{xxxxxxxx
xxxxxxx..xxxxxxx
xxxxxxx...xxxwxx
xxxxxx....xwwwxx
xxxxxx..wwwwwwxx
xxwwwwwwWwwwxxxx
xwwwwwwww..xxxxx
xwwxx.......xxxx
xxxx........xxxx
xxxx.........xxx
xxxx..........xx
xxxx..........xx
xxxx...........@
xxxxxx.......xxx
xxxxxxxx.@.xxxxx
ENDMAP

NAME:     eino_arrival_cavern_c
TAGS:     arrival no_monster_gen no_rotate
ORIENT:   northwest
WEIGHT:   2
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x{.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xx...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxx.....xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxx........xxxxxxxxxxxxxxxxxxxxxxx
xxxxx.............xxxxxxxxxxxxxxxxx
xxxxxxx.................xxxxxxxxxxx
xxxxxxxxxx......................xxb
xxxxxxxxxxxxx.....................@
xxxxxxxxxxxxxxxxx.............xxxxb
xxxxxxxxxxxxxxxxxxxb.@.bxxxxxxxxxxx
ENDMAP

NAME:     eino_arrival_cavern_d
TAGS:     arrival no_rotate
ORIENT:   northeast
SHUFFLE:  bvc
SUBST:    ' : x.
WEIGHT:   4
SHUFFLE:  IJ
SUBST:    J = .
MAP
xxxxxxxxxxxxxx
xxxxxxxxx{xxxx
xxxxxxxx...xxx
xxxxxx.....xxx
xxxxx.....''xx
xxx.......''xx
xx.........'xx
......J.....'x
@.....ccI..''x
x....Icc...''x
x......J...''x
...........'xx
@.........''xx
x........''xxx
xx.....xxxxxxx
xxx.@.xxxxxxxx
ENDMAP

NAME:     eino_arrival_cavern_e
TAGS:     arrival no_monster_gen no_rotate
ORIENT:   northeast
SUBST:    ' : x. , " : x. , ? = x.
WEIGHT:   4
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx?{xx
xxxxxxxxxx""xxxxxxxxxxxxxxx?...?xx
xxxxxxxxx""""xxxxxxxxxxx?.......?x
xxxxxxx""""""""xxxx?............?x
xxxxxx""""""....??.............'xx
xxxxx""".....................''xxx
xG..x....................'''''xxxx
@.......?..........??.''''''xxxxxx
xG.%x..xxx?.......xxxxx''''xxxxxxx
xxxxxxxxxxxxxxG.Gxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx@xxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
NAME:    arrival_aquarium_a
TAGS:    arrival
ORIENT:  float
KMASK:   w = no_monster_gen
KMONS:   Z = electric eel
KFEAT:   Z = w
KMASK:   Z = no_monster_gen
SHUFFLE: TTV
WEIGHT:  5
KMASK:   T = no_monster_gen
KPROP:   wZ = no_submerge
KPROP:   wZ = no_tele_into
MAP
xxxxxxxxxxxxxxxxxxxxxxx
x.....................x
x.............G...G...x
x..nnnnnnnnn..........x
@..nwwwwwwwn.T......T.x
@..nwwwZwwwn....{.....x
@..nwwwwwwwn.T......T.x
x..nnnnnnnnn..........x
x.............G...G...x
x.....................x
xxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   arrival_aquarium_b
TAGS:   arrival
ORIENT: float
KMASK:  w = no_monster_gen
KMONS:  Z = electric eel
KFEAT:  Z = w
KMASK:  Z = no_monster_gen
KPROP:  wZ = no_submerge
KPROP:  wZ = no_tele_into
WEIGHT: 5
MAP
xxxxxxxxxxxxxxx
x......{......x
x.G.xxxxxxx.G.x
x.............x
x.nnnnn.nnnnn.x
x.nwwwn.nwwwn.x
x.nwZwn.nwZwn.x
x.nwwwn.nwwwn.x
x.nnnnn.nnnnn.x
x.............x
xx@xxxxxxxxx@xx
ENDMAP

###########################################################
# Eye to eye with the dorky.
NAME:    lordsloth_arrival_two_stooges
ORIENT:  float
TAGS:    arrival no_rotate no_monster_gen
SUBST:   ':l...' , '=l. , l:lllWw , n:nv , v:vvvc
KMONS:   1 = orc / goblin / kobold
KFEAT:   1 = .
KPROP:   1 = no_tele_into
KMASK:   1 = no_item_gen
MAP
.....................
.....................
..vvvvnnnnnnnnnvvvv..
..vlllllll{lllllllv..
..vlllllll.lllllllv..
..vllll''...''llllv..
..vll'.........'llv..
..vl'...........'lv..
..vl.............lv..
..vmm..G.vvv.G..mmv..
..v1m...........m1v..
..vvvvvvv+++vvvvvvv..
...vv...........vv...
 ..vv...........vv..
 ...vvv.......vvv...
   ...vv+++++vv...
    .............
ENDMAP

##############################################
# Angband town! (Shiori)
NAME:    shiori_angband_town
ORIENT:  float
WEIGHT:  1
TAGS:    arrival no_rotate
NSUBST:  { = 1:{ / 1:> / *:.
NSUBST:  = = 3=+ / *=x
SUBST:   x:xc
MAP
xxxx=xxxxxxxxxxxxxx=xxxxxxxxxxxxxx=xxxxx
x......................................x
x.xxxxx..xxxxx....xxxxx....xxxxx.xxxxx.x
=.xxxxx..xxxx+..{.xxxxx..{.xxxxx.+xxxx.=
x.xxxxx...........xxxxx....xxxx+.xxxxx.x
x.xxxxx..{........xxx+x................x
x.xx+xx......{.............{.....xxxxx.x
x.........xxxx+xx..xx+x...xxxxx..+xxxx.x
=.xxxx+...xxxxxxx..xxxx...xxxxx{.xxxxx.=
x.xxxxx.{.xxxxxxx..xxxx.{.+xxxx..xxxxx.x
x......................................x
xxxx=xxxxxxxxxxxxxx=xxxxxxxxxxxxxxx=xxxx
ENDMAP

##############################################################
# The zoo
# With low chance (10%), the monsters spell Crawl or crawl.
# This is considered harmless and not ASCII art. Don't hand
# in other vaults where monsters spell other words, though :)
NAME:    onia_arrival_zoo_common
TAGS:    arrival no_trap_gen
ORIENT:  float
: if crawl.one_chance_in(10) then
SHUFFLE: 16
MONS:    centaur / centaur warrior / yaktaur / yaktaur captain
MONS:    rat / river rat / hell rat / quokka
MONS:    worker ant / soldier ant / queen ant
MONS:    worm / tyrant leech
MONS:    komodo dragon / iguana / basilisk
MONS:    ettin / frost giant / stone giant / fire giant
: else
SHUFFLE: 1234567
MONS:    frilled lizard / leopard gecko / iguana / crocodile / komodo dragon
MONS:    hog / jackal / wolf / hound
MONS:    giant cockroach / worker ant / butterfly / killer bee /\
         redback
MONS:    swamp worm / lindwurm / worm
MONS:    swamp drake / hydra / wyvern / rime drake
MONS:    polar bear / black bear
MONS:    black mamba / water moccasin / anaconda / salamander
: end
MAP
xxxxxxxx@xxxxxxxx
xxxxxx.G.G.xxxxxx
xxx.G.......G.xxx
x...............x
x.G..ccccccc..G.x
x....c12345c....x
x.G..cnnnnnc..G.x
x...............x
xxx.............x
xxxxxxxxxxxx....x
xxxxx{........xxx
xxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
# There's a large version (with WEIGHT 2) in the huge maps section.
NAME:   minmay_arrival_arboretum_small
WEIGHT: 8
TAGS:   arrival no_monster_gen
ORIENT: float
MONS:   plant / bush w:3, fungus col:any, fungus col:yellow, fungus col:red
MONS:   fungus col:white, fungus col:magenta, fungus col:darkgrey
KPROP:  ' = no_tele_into
SUBST:  ' = DDD111t, 1:1112, 2=1111112, 2:234567
FTILE:  D1234567t = floor_moss
COLOUR: D = green
SUBST:  " = .
KMASK:  D1234567 = no_item_gen
MAP
xxxxxxxxxxxxxxxxx
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
xxxxmmmmmmmmmxxxx
"""""""""""""""""
@"""""""{"""""""@
"""""""""""""""""
ENDMAP

##############################################################
NAME:    minmay_arrival_cross
TAGS:    arrival no_monster_gen
ORIENT:  float
SUBST:   x : x:10 c:2 v:1
SHUFFLE: ABC
SUBST:   A=+, B=+x, C=+xx
MAP
xxxxxxx+xxxxxxx
xxxxxx...xxxxxx
xxxxxx...xxxxxx
xxxxxx...xxxxxx
xxxx{......xxxx
xxxx.......xxxx
x.............x
A......T......B
x.............x
xxxx.......xxxx
xxxx.......xxxx
xxxxxx...xxxxxx
xxxxxx...xxxxxx
xxxxxx...xxxxxx
xxxxxxxCxxxxxxx
ENDMAP

##############################################################
# Background for the Abyss portals: someone made them, probably with
# an evil mind. The upright citizens and adventurers were not able
# to destroy those portals, so they sealed them instead.
NAME:   minmay_arrival_glassed_features
TAGS:   arrival no_monster_gen
ORIENT: float
KPROP:  'I = no_tele_into
SUBST:  I : G T I:4 A:1
KFEAT:  A = enter_abyss
MAP
mmmmm.....mmmmm
m'''m.....m'''m
m'I'm.....m'I'm
m'''m.....m'''m
mmmmm.....mmmmm
...............
...............
.......{.......
...............
...............
mmmmm.....mmmmm
m'''m.....m'''m
m'I'm.....m'I'm
m'''m.....m'''m
mmmmm.....mmmmm
ENDMAP

##############################################################
# This map provides no space to run around/away, so there is
# either some staircase or a known shaft.
NAME:   minmay_arrival_hallway
TAGS:   arrival no_monster_gen
ORIENT: float
SUBST:  x : x:10 c:2 v:1
SUBST:  a : ab
NSUBST: a = 1:< / 1:> / *:.
NSUBST: b = 1:b / 1:b. / *:.
KFEAT:  b = known shaft trap
SUBST:  G : G T t
MAP
xxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx
@..xxx...xxx...xxx...xxx
@..xxx.a.xxx.a.xxx.G.xxx
@..xxx...xxx...xxx...xxx
@....................xxx
@..................[.xxx
@....................xxx
@..xxx...xxx...xxx...xxx
@..xxx.a.xxx.a.xxx.G.xxx
@..xxx...xxx...xxx...xxx
xxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
# The map can make it hard to get away, so reduced weight.
NAME:    minmay_arrival_floating_metal
TAGS:    arrival no_monster_gen
WEIGHT:  3
ORIENT:  southeast
NSUBST:  V = 2:+ / 2=v+ / *:v
NSUBST:  U = 2:+ / 2=v+ / *:v
SUBST:   ' = .W
NSUBST:  { = 1:{ / *:.
MAP
  '''''''''@'''''x
 ''wwwwwwwwWwwwwwx
''wwwwwvvvv+vwwwwx
'wvvvvwv....vwwwwx
@W+{>vwv.{>.vwwwwx
'wvvvvwv....vwwwwx
'wwwwwwvvvvvvwwwwx
'wwwwwwwwwwwwwvvwx
'wwwvvwwwwwwwwvvwx
'wwwvvwwwwwwwwwwwx
'wwwwwwwwwwwwwwwwx
'wwwwwwwwwVVVVwwwx
'WwwUUUwwwV<>Vwwwx
'WwwU<UwwwVVVVwwwx
'WwwU>Uwwwwwwwwwwx
'wwwUUUwwwwwwwwwwx
'wwwwWwwwwwwwwwwwx
xxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
# A simple fortress with double doors.
NAME:    saegor_arrival_fortress
TAGS:    arrival no_monster_gen no_pool_fixup
ORIENT:  float
MONS:    place:D:1 zombie
KPROP:   W = no_tele_into
NSUBST:  { = 1:{ / 1:> / *:.
MAP
wwwwwwwwwcccccwwwwwwwww
wcccccccwcWtWcwcccccccw
wc{...GccccGccccG...{cw
wc.....+.......+.....cw
wcG...GccGc+cGccG...Gcw
wccc+cccccc+cccccc+cccw
wcWc.cctt.....ttcc.cWcw
wttG.ctW...1...Wtc.Gttw
wcWc.Gc.........cG.cWcw
wccc.cc...WtW...cc.cccw
@++..++...tGt...++..++@
wccc.cc...WtW...cc.cccw
wcWc.Gc.........cG.cWcw
wttG.ctW...1...Wtc.Gttw
wcWc.cctt.....ttcc.cWcw
wccc+cccccc+cccccc+cccw
wcG...GccGc+cGccG...Gcw
wc.....+.......+.....cw
wc{...GccccGccccG...{cw
wcccccccwcWtWcwcccccccw
wwwwwwwwwcccccwwwwwwwww
ENDMAP

##############################################################
# A small-room arrival with wooden doors and big ring of trees and rocks.
NAME:    saegor_arrival_under_anarchy
TAGS:    arrival no_trap_gen no_monster_gen
ORIENT:  float
NSUBST:  @ = 3:@ / *:X
SUBST:   X = tc w:1
COLOUR:  # = lightgreen / green / yellow:1
NSUBST:  p = 2:+ / *:c
COLOUR:  + : brown
SUBST:   # = .. 1:1
MONS:    plant
KPROP:   w = no_tele_into
NSUBST:  { = 1:{ / *:.
MAP
   xxx @@@ xxx
   XXXX@.@XXXX
  @@XXX.X.XXX@@
xX@...........@Xx
xXX...........XXx
xXX..ccpppcc..XXx
xXX..c#####c..XXx
@X...p##{##p...X@
@.X..p#####p..X.@
@X...p#{#{#p...X@
xXX..c#####c..XXx
xXX..ccpppcc..XXx
xXX...........XXx
xX@...........@Xx
  @@XXX.X.XXX@@
   XXXX@.@XXXX
   xxx @@@ xxx
ENDMAP

##############################################################
# Moated graveyard arrival (oiseaux)
NAME:    moated_graveyard_arrival
TAGS:    arrival no_monster_gen no_trap_gen no_pool_fixup no_rotate
ORIENT:  float
FTILE:   t{G = FLOOR_PEBBLE_BROWN
TILE:    ' = FLOOR_PEBBLE_BROWN / FLOOR_PEBBLE_DARKGRAY
TILE:    t = DNGN_TREE_DEAD
TILE:    G = DNGN_GRAVESTONE
COLOUR:  ' = brown / darkgrey
COLOUR:  t = darkgrey
SUBST:   t = t.
: set_feature_name("granite_statue", "adventurer's gravestone")
MAP
    ....'....
  ...'....'..
 ..'....'...
  ..'..'.'..
  ....'''....
  ..www'www...
 ..wwt''''ww...
..ww''G''t'ww..
.ww't'''''''ww.
.w'''''t'''''w.
.w''t'''''G''w.
.ww''G'{''t'ww.
..ww''t''''ww..
 ..ww'''t'ww..
  ..wwwwwww..
   .........
ENDMAP

##############################################################################
#The maze will allways contain an exit to the rest of D:1, the player won't be
#forced to D:2.
NAME:    co_arrival_random_square_maze
TAGS:    arrival
ORIENT:  float
NSUBST:  A = 3:+ / x
NSUBST:  B = 1:+ / "
NSUBST:  C = 11:+ / x
NSUBST:  D = 1:+ / "
SUBST:   " = xxxxxxxxxxxxxxxxx+
SUBST:   ! = ....V
SUBST:   x : cxxx
MAP
xxDxxxDxxxDxxxDxx
x...x...x...x...x
D.!.C.!.C.!.C.!.D
x...x...x...x...x
xxCxxxBxxxBxxxCxx
x...x...x...x...x
D.!.B.!.A.!.B.!.D
x...x...x...x...x
xxCxxxAxxxAxxxCxx
x...x...x...x...x
D.!.B.{.A.!.B.!.D
x...x...x...x...x
xxCxxxBxxxBxxxCxx
x...x...x...x...x
D.!.C.!.C.!.C.!.D
x...x...x...x...x
xxDxxxDxxxDxxxDxx
ENDMAP

##############################################################################
NAME:   sevenhm_arrival_forest_fort_small_1
TAGS:   arrival no_monster_gen
ORIENT: float
WEIGHT: 1
NSUBST: { = { / T> , z = + / n+
SUBST:  T = TTG
: if crawl.one_chance_in(4) then
SUBST:  " : +, ' : . , - : ccc.
: else
SUBST:  "'- : c
: end
MONS:   bullfrog / spiny frog / yak / death yak w:1 / elephant
MAP
          ccccccccc
       cccctccccctcccc
      cctt1tc...ct1ttcc
    ccctttttc.{.ctttttccc
    ccccccccn...ncccccccc
    WWcc..+.......+..ccWW
   @WWz..."'-----'"...zWW@
    WWcc..+.......+..ccWW
    ccccccccn...ncccccccc
    ccctttttc.{.ctttttccc
      cctt1tc...ct1ttcc
       cccctccccctcccc
          ccccccccc
ENDMAP

##############################################################################
NAME:    sevenhm_arrival_forest_fort_small_2
TAGS:    arrival no_pool_fixup no_monster_gen
ORIENT:  float
WEIGHT:  1
SHUFFLE: rz
: if crawl.one_chance_in(4) then
SUBST:   w : x, u : x, z : @
: else
SUBST:   z : x, u : w
NSUBST:  w = @ / w
SUBST:   w : wwwwwwtt.
: end
SUBST:   r : x
NSUBST:  y = { / .....> / .
KMASK:   W = no_monster_gen
MONS:    anaconda / death yak / black mamba / elephant / hydra
MAP
  xxxxxx
xxxccccxxx
xtttyytttx
xt1tyyt1tx
xcnc..cncx
x..c++c..x
r........z
xuwwwwwwux
ENDMAP

##############################################################################
NAME:   sevenhm_arrival_forest_fort_small_3
TAGS:   arrival no_pool_fixup no_monster_gen
ORIENT: float
: if crawl.one_chance_in(4) then
SUBST:  z : @ , Z : + , X = xx. , p : tR , m : c
: else
SUBST:  X : @, Z : c, z : x, p : x, m : .
: end
NSUBST: {OA = { / . , y = + / c+ / c+ , r = + / c+
SUBST:  G = GGTTU, R : Ltt1Ww, L : tttw1
MONS:   plant / bush
MAP
xXXxxxxxxxxx
x..........x
x..cyyycL.Lx
x.Rc...cLGLx
x.Rc...cLLLx
xGRc...cxxxx
xxccrrccccx
 pcm..c{{cx
 pccc.+{{cx
  ppcccZccx
   pppxzxx
ENDMAP

##############################################################################
NAME:   sevenhm_arrival_forest_fort_small_4
TAGS:   arrival no_monster_gen
ORIENT: float
NSUBST: { = { / . , @ = @ / t
SUBST:  X : c@@@ , ' : 'tW
SUBST:  + = ++. , t = tttW1, W : WWW.
MONS:   plant / bush
MAP
 ccccccccc
 ccc{{{cccX
 'tcc.cct'X
 WWtc+ctWWX
@WWWWWWWWWX
 WWtc+ctWWX
 'tcc.cct'X
 ccc{{{cccX
 ccccccccc
ENDMAP

##############################################################################
NAME:    sevenhm_arrival_forest_fort_small_5
TAGS:    arrival no_pool_fixup
ORIENT:  float
: if crawl.one_chance_in(4) then
SUBST:   o = {
SUBST:   X : x , Y : @ , p : .
: else
NSUBST:  X = @ / @@xx.
SUBST:   Y = x .:1 , o : . , p : [
: end
NSUBST:  { = { / . , r = + / +c
: if crawl.one_chance_in(3) then
NSUBST:  R = + / +c
NSUBST:  N = n / *=nnc
SUBST:   z : c
: else
SUBST:   R : c, z : +
: end
NSUBST:  H = G / WWW. , J = G / WWW.  , K = G / WWW.
SUBST:   G = GGGW , N : c
SHUFFLE: ({[
MAP
   xxxxxxxxxxxxx
  xxtttWWWWttttxx
  xttWWWHHWWWWttxx
xxYWWWWJJJJWWWWWtxxx
xtttWWWWKKWWWWWccccx
xtt....WWWW.cccc.{cx
xt...cNNc...np+..{cx
x...cc{{cc..ccc..{cx
x...z...{c..+....{cx
xt..cc...c..cccccccx
xt..tccc+c.....ttttx
xxx..ttc.ctt......tx
  x...tc.cccccc....X
  xxx.tc..r...R..ooX
    xxtc..r...R..o.X
     xxcccccccctttxx
      xxxxxxtttttxx
           xxxxxxx
ENDMAP

##############################################################################
NAME:    sevenhm_arrival_forest_fort_small_6
TAGS:    arrival
ORIENT:  float
KPROP:   zC = no_tele_into
SUBST:   z : l:40 G , C : C:40 lG
KMASK:   l = no_monster_gen
SUBST:   R : +R
SUBST:   r : +ccc
NSUBST:  R = + / ccc+
SUBST:   = : cc+
SUBST:   t : tT.
SUBST:   T = tt.
NSUBST:  { = 1:{ / *:.
MAP
     xxxxxxxx
xxxxxxccccccxxxxxx
xcccccc{{{{ccccccx
xc>zll=....=llC>cx
xcccccc....ccccccx
 .ttttc....ctt..
  ....crRRrct.
     .........
ENDMAP

##############################################################################
NAME:   saegor_arrival_miasma
TAGS:   arrival no_monster_gen
ORIENT: float
COLOUR: W# = lightgreen
NSUBST: W = 6:% / 7:#
KITEM:  % = nothing / q:1 river rat chunk / w:5 river rat skeleton
MARKER: # = lua:fog_machine { cloud_type = "foul pestilence", pow_max = 10, \
                              delay = 5, size = 5, excl_rad = 0 }
KFEAT:  %# = W
KPROP:  W#% = no_tele_into
MAP
 xxxxxxxxxx
xxWWWWWWWWxx
xWWWWWWWWWWx
xWWvWW#WvWWx
xWWWWWWWWWWx
xWWWWWWWWWWx
xxxmmxxxmmxx
@+....{....x
xxxmmxxxmmxx
xWWWWWWWWWWx
xWWWWWWWWWWx
xWWvW#WWvWWx
xWWWWWWWWWWx
xxWWWWWWWWxx
 xxxxxxxxxx
ENDMAP

##############################################################################
NAME:   lightli_arrival_statuegarden
TAGS:   arrival no_monster_gen
ORIENT: float
MONS:   plant
MAP
@..xxxG+Gxxx..@
..xxwwW.Wwwxx..
.xxwww.1.wwwxx.
xxww1..G..1wwxx
xwwW.......wwwx
xw1.G..{..G.1wx
xwww.......wwwx
xxww1..G..1wwxx
.xxwww.1.wwwxx.
..xxwwwwwwwxx..
@..xxxxxxxxx..@
ENDMAP

##############################################################################

NAME:   lightli_arrival_noodles
ORIENT: float
TAGS:   arrival no_monster_gen
MAP
xxxxxxxxxx
xG..{...Gx
xl......lx
xl......lx
xl......lx
xl..GG..lx
xl......lx
xl......lx
xl......lx
xG......Gx
xxxx++xxxx
ENDMAP

##############################################################################

NAME:   lightli_arrival_isolated_outpost
ORIENT: float
TAGS:   arrival no_pool_fixup no_monster_gen
MAP
WWWWWWWW@@WWWWWWWW
W..G..G....G..G..W
W.cccccn++nccccc.W
WGcWWWWW..WWWWWcGW
W.cW..........Wc.W
w.cW.cccccccc.Wc.W
W.cW..........Wc.W
W.cWWWWW..WWWWWc.W
W.cccccn++nccccc.W
W.c............c.W
W.clllll..lllllc.W
W.clllll..lllllc.W
W.c............c.W
W.cvvvvv++vvvvvc.W
WGc.....{......cGW
W.cccccccccccccc.W
W..G..........G..W
WWWWWWWWWWWWWWWWWW
ENDMAP

##############################################################################

NAME:   lightli_arrival_windingriver
ORIENT: float
TAGS:   arrival no_monster_gen
KMONS:  P = plant
SUBST:  p = Pw
MAP
xxxxxxxxxxxxxxxxxxxxxxxx
xpwppwppppppwppp.p.p...x
xwwwwwwpppppwwppp..w...x
xwwwwwwwpwpwwwwppwwwwp.x
xwwwwwwwwwwwwwwwwww..w.+
xw.wwwwwwwwwwwwwww.....+
xp..w..wwwww..www..p...x
x.xp.px.w.w.xx.w.xxx...x
x{xxxxxx.p.xxxx.xxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################

NAME:   lightli_arrival_boilingwater
ORIENT: float
TAGS:   arrival no_monster_gen
MAP
  @@@@@@
WWWllllWWW
WGWWllWWGW
WWGWWWWGWW
lWWb++bWWl
llWm..mWll
llWm{.mWll
lWWbmmbWWl
WWGWWWWGWW
WGWWllWWGW
WWWllllWWW
ENDMAP

##############################################################################

NAME: lightli_arrival_quake
## No, door choice does not affect difficulty. Game is stuck on Nightmare as
## it is.
ORIENT: float
TAGS: no_rotate no_hmirror no_vmirror arrival no_monster_gen
MAP
cc+ccc+ccc+cc
c...c...c...c
c...cw.wc..lc
c...cw.wc.llc
c...cw.wclllc
c...cw.wcll.c
c...cw.wcl..c
c...c...c...c
c...........c
c...........c
c...........c
c...........c
c.....(.....c
c...........c
ccccccccccccc
ENDMAP

NAME: nicolae_arrival_washed_ashore
ORIENT: northwest
TAGS: arrival no_monster_gen no_pool_fixup
SUBST: B = bb., S = bWW, Z = WWw
COLOUR: b : brown
FTILE: b{ = floor_sand
KFEAT: O = open_sea
KFEAT: b = .
MAP
OOOOOOOOOOOOOOOOOOOOOx
Owwwwwwwwwwwwwwwwxxxxx
OwwwwwwwwZZZZZZZZxx
OwwwwwwZZZSSSSSSZxx
OwwwwwZZSSSSSSSSSxx
OwwwwZZSSSSSbbbbbxx
OwwwZZSSSSbbbbbbxxx
OwwZZSSSSbbbbbbbxxx
OwwZSSSS{bbbbbbbxxx
OwZZSSSbbbbbbbbbBxx
OwZSSSbbbbbbbbbbBxx
OwZSSSbbbbbbbbbbBxx
OwZSSbbbbbbbbbbbBBx
OwZSSbbbbbbbbbbbBBx
OwZSSbbbbbbbbbbbBB@
OwZSSbbbbbbbbbbbBB@
OwZZSbxxxBBBBBBBBB@
OxxxxxxxxxxxBBBBBB@
Oxxxxxxxxxxxxx@@@@@
Ox
Ox
xx
ENDMAP

##############################################################
NAME:    minmay_arrival_shattered_statue
TAGS:    arrival
ORIENT:  float
ITEM:    stone q:4 / stone q:2 / nothing
ITEM:    stone q:3 w:5 / stone q:1 / nothing
ITEM:    stone q:2 w:5 / stone q:1 w:5 / nothing
ITEM:    stone q:1 w:5 / nothing
ITEM:    stone q:1 w:1 / nothing w:20
MAP
   xxxxxxx
  xx.....xx
  x..h{h..x
  xxhggghxx
   xxx+xxx
 xxxeeeeexxx
xxgfedddefgxx
+hgfedGdefgh+
xxgfedddefgxx
 xxxeeeeexxx
   xxxxxxx
ENDMAP

NAME:   mainiacjoe_arrival_reflecting_pool
TAGS:   arrival no_monster_gen no_pool_fixup
ORIENT: float
SUBST:  G : G:30 T:20 t
SUBST:  D : t:30 T:20 G
SUBST:  w : w:30 W
SUBST:  c : xcvb
MAP
   x@x
   x.x
  ccAcc
ccc...ccc
c...w...c
c.G.w.G.c
c...w...c
c.D.w.D.c
c...w...c
c.G.w.G.c
c...w...c
c.D.w.D.c
c...w...c
c.G.w.G.c
c...w...c
ccc...ccc
  cc{cc
   ccc
ENDMAP

NAME:   kennysheep_arrival_forest
TAGS:   arrival no_monster_gen no_trap_gen
ORIENT: south
FTILE:  t'G = floor_grass
COLOUR: ' = green
MAP
        xx+++xx
       xxG...Gxx
xxx   xx''...''xx   xxx
xtxxxxx'''...'''xxxxxtx
xt'''''''G...G'''''''tx
xtt'''''''...'''''''ttx
xtt'''''''...''''''tttx
xtttttt''G.{.G''''ttttx
xtttttttt'...'''ttttttx
xtttttttttt.ttttttttttx
xtttttttttttttttttttttx
ENDMAP

NAME:   nzn_arrival_settlement
TAGS:   arrival no_monster_gen no_trap_gen
ORIENT: float
SUBST:  p = s ':30, q = s:30 '
SHUFFLE: TG
: if crawl.coinflip() then
SUBST:  uf = ', R = t, rfg = ., Hh = s, VQLid = x, C = @, IS = A
: else
SUBST: gHuCS = x, IirR = s, fQ = A, Ldh = ., V = @
: end
FTILE:  VsG = floor_dirt, t'T = floor_grass
COLOUR: ' = green, s = brown
MAP
               xVx
            xxxxQxxx
   xCx   xxxxLhiddxxx
  xxSxxxxxsx.LIL.qptxx
 xxgHHh..xAx.r..qpq'tx
 xggg..hh...r..qptpqpx
 xuuu....h.r....qpqptx
 xuRfrrrrrs......q.qpx
 xufu..'..s.sss.....xx
xxpr..'t'.sssTsssssxx
xt'.r..'.s..sss.ppxAx
xt'..rr.s......ptxxx@
xxp....s.......pxx
 xwss.s....qpqqpx
 xxx.{..q.qpt''tx
   xx..qpqpq'''px
    xxqptpqpptpxx
     xxxxxxxxxxx
ENDMAP

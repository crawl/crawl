##############################################################################
# Simple maps for D:1 arrival vaults.
# See arrival_guide.txt for where your new arrival vault belongs.
#
# Please do not rearrange the vaults. The order is chosen so that readers are
# introduced to new features one by one.
##############################################################################

NAME:   minmay_arrival_doors
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
     x+x
     x.x
     x+x
     x.x
    xx+xx
xxxxx...xxxxx
+.+.+.{.+.+.+
xxxxx...xxxxx
    xx+xx
     x.x
     x+x
     x.x
     x+x
ENDMAP

##############################################################
NAME:    minmay_arrival_three_doors
TAGS:    arrival
ORIENT:  float
MAP
x+xxxx+xxxx+x
x...........x
xxxxxx{xxxxxx
     xxx
ENDMAP

##############################################################
NAME:   minmay_arrival_diagonal_passages
TAGS:   arrival
ORIENT: float
MAP
   @xxxxx@
   x.xxx.x
  xxx.x.xxx
@xxxxx.xxxxx@
x.xxx.x.xxx.x
xx.x.x.x.x.xx
xxx.x.{.x.xxx
xx.x.x.x.x.xx
x.xxx.x.xxx.x
@xxxxx.xxxxx@
  xxx.x.xxx
   x.xxx.x
   @xxxxx@
ENDMAP

##############################################################
NAME:    minmay_arrival_wavy_path
TAGS:    arrival no_monster_gen
ORIENT:  float
MAP
xxxxxxxxxxxxxxxxxxxxxx
xx.{xxxxxxxxxxxxxxxxxx
x....xx....xxxx....xxx
x....x......xx......xx
xx..xx..xx..xx..xx..xx
xx..xx..xx..xx..xx..xx
xx..xx..xx..xx..xx..xx
xx..xx..xx..xx..xx..xx
xx......xx......xx...@
xxx....xxxx....xxxx..@
xxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    matt_arrival_spirals
TAGS:    arrival
ORIENT:  float
SHUFFLE: {[(
SUBST:   [=., (=.
MAP
xxxxxxxx@xxxxxxxxx
x......x.x.x.....x
x.xxxx.x.x.x.xxx.x
x.x{.x.x.x.x..(x.x
x.xx.x.x.x.xxxxx.x
x....x.x.x.......x
xxxxxx.x.x.xxxxxxx
@................@
x.xxxxxxxxxxxxxx.x
x.x..........x...x
x.x.xxxxxxxx.x.x.x
x.x.x...[....x.x.x
x.x.x.xxxxxxxx.x.x
x...x..........x.x
xxxxxxxxxxxxxxxxxx
ENDMAP

###########################################################
NAME:    eino_arrival_druids_welcome
TAGS:    arrival no_monster_gen
ORIENT:  float
MONS:    plant
MAP
x1......@......1x
xw1...........1wx
xww1.........1wwx
xwww1.......1wwwx
xwwww1.....1wwwwx
xwwwww1.{.1wwwwwx
xwwww1.....1wwwwx
xwww1.......1wwwx
xww1.........1wwx
xw1...........1wx
x1......@......1x
ENDMAP

###########################################################
NAME:   eino_arrival_water_cross
TAGS:   arrival no_monster_gen
ORIENT: float
MONS:   plant
MAP
...................
..1xWx1.....1xWx1..
.1xxWxx1...1xxWxx1.
..WWWWW..{..WWWWW..
.1xxWxx1...1xxWxx1.
..1xWx1.....1xWx1..
...................
ENDMAP

###########################################################
NAME:   eino_arrival_water_star
TAGS:   arrival no_monster_gen no_rotate
ORIENT: float
MAP
..xxxxxxx@xxxxxxx..
....xxxx...xxxx....
x.....x..W..x.....x
xG..W....W....W..Gx
xx...W..WWW..W...xx
x.....WWWWWWW.....x
....WWWW...WWWW....
..WWWWW..{..WWWWW..
....WWWW...WWWW....
x.....WWWWWWW.....x
xx...W..WWW..W...xx
xG..W....W....W..Gx
x.....x..W..x.....x
....xxxx...xxxx....
..xxxxxxx@xxxxxxx..
ENDMAP

###########################################################
NAME:    eino_arrival_tree_alley
TAGS:    arrival no_monster_gen
ORIENT:  float
MONS:    plant
SHUFFLE: 1t
MAP
xxxxxxxxxxxxxxxxx
.111111111111111.
@.......{.......@
.111111111111111.
xxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:   lemuel_arrival_funnel
TAGS:   arrival no_monster_gen
ORIENT: float
SUBST:  c : ccccwlx.
MAP
xxxxxxxxxxxxxxx
x......{......x
x.............x
xx...ccccc...xx
 x...ccccc...x
 xx...ccc...xx
  x...ccc...x
  xx...c...xx
   x...c...x
   xx.....xx
    x.....x
    xx...xx
     x...x
     x...x
     x+++x
     x.@.x
ENDMAP

##############################################################
# Reduced weight because of size and shape.
NAME:    minmay_arrival_pointy
TAGS:    arrival no_monster_gen
WEIGHT:  4
ORIENT:  float
MAP
xxx                     xxx
x{xx                   xx.x
xx.xx                 xx.xx
 x..xx               xx..x
 xx..xx             xx..xx
  x...xx           xx...x
  xx...xx         xx...xx
   x....xx       xx....x
   xx....xx     xx....xx
    x.....xx   xx.....x
    xx.....xx xx.....xx
     x......xxx......x
     xx......x......xx
      x.............x
ENDMAP

##############################################################
NAME:    minmay_arrival_up_and_down
TAGS:    arrival
ORIENT:  float
SHUFFLE: )]}
MAP
ccccccccc
c.......c
+...{...+
c.......c
cnnnnnnnc
c.......c
+.).].}.+
c.......c
ccccccccc
ENDMAP

##############################################################
NAME:    onia_arrival_pyramid
TAGS:    arrival no_rotate no_monster_gen
ORIENT:  float
SHUFFLE: xxxcvlw
MAP
        .........
       .....x.....
      .....xxx.....
     .....xxxxx.....
    .....x.....x.....
   .....xxx...xxx.....
  .....xxxxx.xxxxx.....
 .....x...........x.....
.....xxx....{....xxx.....
....xxxxx.......xxxxx....
...x.....x.....x.....x...
..xxx...xxx...xxx...xxx..
.xxxxx.xxxxx.xxxxx.xxxxx.
............@............
ENDMAP

##############################################################################
NAME:    lemuel_arrival_cross
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: >d
ITEM:    stone
MAP
      xxx
      x{x
      x.x
      x.x
      x.x
xxxxxxx.xxxxxxx
x.............x
xxxxxxx.xxxxxxx
      x.x
      x.x
      x.x
     xx.xx
     xG>Gx
     xx.xx
      x.x
      x@x
ENDMAP

##############################################################################
NAME:   lemuel_arrival_lava_pool
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
    xxxxxxx
   xx..{..xx
   x.......x
   x.......x
   x.......x
   x..lll..x
   x.lllll.x
   x.ll%ll.x
   x.ll>ll.x
   x.ll%ll.x
   x.lllll.x
   x..lll..x
  xx.......xx
xxx.........xxx
x.............x
x@.....@.....@x
ENDMAP

##############################################################
NAME:    minmay_arrival_four_houses
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: xcvb, {[(>
SUBST:   [=., (=.
MAP
.............
.xxxxx.ccccc.
.x...x.c...c.
.x.{.x.+.[.c.
.x...x.c...c.
.xx+xx.ccccc.
.............
.vvvvv.bb+bb.
.v...v.b...b.
.v.(.+.b.>.b.
.v...v.b...b.
.vvvvv.bbbbb.
.............
ENDMAP

##############################################################################
NAME:    dpeg_arrival_radiant
TAGS:    arrival no_rotate
ORIENT:  float
NSUBST:  ' = 1:@ / *=.x
MAP
'xxxxxxx'xxxxxxx'
x.xxxxxx.xxxxxx'x
xx..xxxx+xxxx..xx
xxxx..+...+..xxxx
xxxxxxx.{.xxxxxxx
xxxx..+...+..xxxx
xx..xxxx+xxxx..xx
x.xxxxxx.xxxxxx.x
'xxxxxxx'xxxxxxx'
ENDMAP

##############################################################################
NAME:    dpeg_arrival_rooms_with_view
TAGS:    arrival no_monster_gen no_pool_fixup
ORIENT:  float
SHUFFLE: {[, lw
SUBST:   [=.
MAP
.......xxxxxx@0
@+xlllxx......@
.x.....x......x
.l..{..l......x
.l..T..l..%...x
.x.....x......x
xxxlllxxxwwwxxx
x......x.....x.
x...%..w..T..w.
x......w..[..w.
x......x.....x.
@......xxwwwx+.
0@xxxxxx......@
ENDMAP

##############################################################################
NAME:   dpeg_arrival_tiny_a
TAGS:   arrival
ORIENT: float
MAP
{
ENDMAP

NAME:   dpeg_arrival_tiny_b
TAGS:   arrival
WEIGHT: 2
ORIENT: float
MAP
xxx
+{+
xxx
ENDMAP

NAME:   dpeg_arrival_tiny_c
TAGS:   arrival
WEIGHT: 4
ORIENT: float
MAP
@....
.xxx.
.+{+.
xxxxx
ENDMAP

NAME:   dpeg_arrival_tiny_d
TAGS:   arrival
WEIGHT: 4
ORIENT: float
MAP
@....
.xxx.
.+{+.
.xxx.
.....
ENDMAP

NAME:   dpeg_arrival_tiny_e
TAGS:   arrival
WEIGHT: 1
ORIENT: float
MAP
xxxxx
x...x
+.{.+
x...x
xxxxx
ENDMAP

NAME:   dpeg_arrival_tiny_f
TAGS:   arrival
WEIGHT: 3
ORIENT: float
MAP
@......
.xxxxx.
.x...x.
.+.{.+.
xx...xx
xxxxxxx
ENDMAP

NAME:   dpeg_arrival_tiny_g
TAGS:   arrival
WEIGHT: 3
ORIENT: float
MAP
@...xx
.xx+xx
.x...x
.+.{.+
xx...x
xxxxxx
ENDMAP

NAME:   dpeg_arrival_tiny_h
TAGS:   arrival
WEIGHT: 3
ORIENT: float
MAP
@xxxxxx
.xxxxxx
.x...xx
.+.{.+.
xx...x.
xxxxxx@
ENDMAP

NAME:   dpeg_arrival_tiny_i
TAGS:   arrival
WEIGHT: 4
ORIENT: float
MAP
xxxxx
..{.@
xxxxx
ENDMAP

NAME:   dpeg_arrival_tiny_j
TAGS:   arrival
WEIGHT: 3
ORIENT: float
SUBST:  v : vcbxxx
MAP
xxxxx
xvvvx
..{.@
xvvvx
xxxxx
ENDMAP

NAME:   dpeg_arrival_tiny_k
TAGS:   arrival
WEIGHT: 3
ORIENT: float
MAP
xxxx
x{.@
xxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_windows_large_door
TAGS:    arrival no_monster_gen no_rotate
ORIENT:  float
MAP
.......
.xxmmmxx.
.x.....x.
.m..{..m.
.m.....m.
.x.....x.
.xx+++xx.
.........
ENDMAP

##############################################################################
NAME:    dpeg_chamrin_arrival_tetris
TAGS:    arrival
ORIENT:  float
MAP
          x.@.x
         xx...xx
        xx..x..xx
       xx..xxx..xx
      xx..xxxxx..xx
     xx..xxxxxxx..xx
    xx..xxxxxxxxx..xx
xxxxx..xxxxxxxxxxx..xxxxx
x.......................x
x.b..bbb.b..b.bb.bbb..b.x
x.bb.b..bbb.b.bb...b.bb.x
x..b........b........b..x
xx...xxxxxx.b.xxxxxx...xx
 xx...xxxxx...xxxxx...xx
  xx...xxxxxxxxxxx...xx
   xx...xxx...xxx...xx
    xx...x..{..x...xx
     xx...........xx
      xxx...x....xx
        xxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_boxorama
TAGS:    arrival
ORIENT:  float
SHUFFLE: {[(<
SUBST:   [=., (=.
MAP
xxxxxxxxxxxxxxxx@xxxxxxxxxxxxxxxxxx
x.................................x
x.................................x
x...xxxxxxx...xxxxx...xxxx...xx...x
x...xxxxxxx...xxxxx...xxxx...xx...x
x...xxxxxxx...xxxxx...xxxx........x
x...xxxxxxx...xxxxx........<..[...x
x...xxxxxxx.......................x
x.......................xxxxxxx...x
x...(..{........xxxxx...xxxxxxx...x
x........xxxx...xxxxx...xxxxxxx...x
x...xx...xxxx...xxxxx...xxxxxxx...x
x...xx...xxxx...xxxxx...xxxxxxx...x
x.................................x
x.................................x
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_boxes_within_box
TAGS:    arrival
ORIENT:  float
SHUFFLE: {[(
SUBST:   [=., (=.
MAP
xxxxxxxxxxxxxxxx@xxxxxxxxxxxxxxxxxx
x.................................x
x.................................x
x...xxxxxxx..xxxxxxxxxxx..xxxxx...x
x...xxxxxxx..xxxxxxxxxxx..xxxxx...x
x...xxx........xxxxxxxxx..xxxxx...x
x............{.xxxxx......xxxxx...x
x...xxx..[........................x
x...xxx........x........(.xxxxx...x
x...xxxxxx..xxxx.xxx......xxxxx...x
x...xxxxxx..xxxx.xxx......xxxxx...x
x...xxxxxx..xxxx.xxxx...xxxxxxx...x
x...xxxxxx..xxxx.xxxx...xxxxxxx...x
x.................................x
x.................................x
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_shelter
TAGS:    arrival
ORIENT:  float
SHUFFLE: cvxxxx
MAP
@...........@
.............
....cc+cc....
...cc...cc...
...c..{..c...
...cc...cc...
....cc+cc....
.............
@...........@
ENDMAP

##############################################################################
NAME:    dpeg_arrival_triangle_fractal
TAGS:    arrival no_monster_gen no_rotate
WEIGHT:  2
ORIENT:  float
MAP
xxxxxxxxxxxxxxxxxxxxxxx@x
xxxxx.xxxxx.xxxxxxxxxxx.x
xxxx..xxxx..xxxxxxxxxx..x
xxx...xxx...xxxxxxxxx...x
xx..{.xx....xxxxxxxx....x
x.....+.....xxxxxxx.....x
xxxxx+xxxxx+xxxxxx...x..x
xxxxx.xxxxx.xxxxx...xx..x
xxxx..xxxx..xxxx...xxx..x
xxx...xxx...xxx...xxxx..x
xx....xx..T.xx...xxxxx..x
x.....+.....+...........x
xxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_long_road
TAGS:    arrival
ORIENT:  float
SHUFFLE: {[(
SUBST:   [=., (=.
MAP
xx.(.@x..xx@
xx...xx...+.
.....xxx..xx
....xxx...x.
x...xx....x.
x..xxx...xx.
...xx....x..
..xxx.{.xxx.
..xx....xx..
.xxxx..xx...
xxx....xx@.[
ENDMAP

##############################################################################
NAME:    dpeg_arrival_ternary
TAGS:    arrival
ORIENT:  float
SHUFFLE: cv
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxx
xcxcxcxcxcxcxcxcxcxcxcxxcx
x.x..c.....x..c.....x..c.x
x.......x..x..x..c..c..c.x
@.........................
..x..x..x.....x..x..x..x.@
x.c..c..x.....x..........x
x.x.....c..{.....c..x....x
xcxcxcxcxcxcxcxcxcxcxcxxcx
xxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:   erik_arrival_triangle_small
TAGS:   arrival
WEIGHT: 4
ORIENT: float
MAP
            xxx
            x{x
          xxx.xxx
          x.+.+.x
        xxx.xxx.xxx
        x.+x+.+x+.x
        x@xxx@xxx@x
ENDMAP

NAME:   erik_arrival_triangle_medium
WEIGHT: 3
TAGS:   arrival
ORIENT: float
MAP
            xxx
            x{x
          xxx.xxx
          x.+.+.x
        xxx.xxx.xxx
        x.+x+.+x+.x
      xxx.xxx.xxx.xxx
      x.+x+.+x+.+x+.x
      x@xxx@xxx@xxx@x
ENDMAP

NAME:   erik_arrival_triangle_large
TAGS:   arrival
WEIGHT: 2
ORIENT: float
MAP
            xxx
            x{x
          xxx.xxx
          x.+.+.x
        xxx.xxx.xxx
        x.+x+.+x+.x
      xxx.xxx.xxx.xxx
      x.+x+.+x+.+x+.x
    xxx.xxx.xxx.xxx.xxx
    x.+x+.+x+.+x+.+x+.x
    x@xxx@xxx@xxx@xxx@x
ENDMAP

NAME:   erik_arrival_triangle_huge
TAGS:   arrival
WEIGHT: 1
ORIENT: float
MAP
            xxx
            x{x
          xxx.xxx
          x.+.+.x
        xxx.xxx.xxx
        x.+x+.+x+.x
      xxx.xxx.xxx.xxx
      x.+x+.+x+.+x+.x
    xxx.xxx.xxx.xxx.xxx
    x.+x+.+x+.+x+.+x+.x
  xxx.xxx.xxx.xxx.xxx.xxx
  x.+x+.+x+.+x+.+x+.+x+.x
  x@xxx@xxx@xxx@xxx@xxx@x
ENDMAP

##############################################################################
NAME:    erik_arrival_blinker_ship
TAGS:    arrival
ORIENT:  float
SHUFFLE: cxxx
MAP
           ......
          ..c..c.
          .c.....
  ....   ..c...c.
...cc......cccc..
..cccc...........
.cc.cc...........
..cc.....cc.ccc...............
........c.....cc.....c....ccc.
.......cc.{.....c....c....c.c.
........c.....cc.....c....ccc.
..cc.....cc.ccc...............
.cc.cc...........
..cccc...........
...cc......cccc..
...........c...c.
          .c.....
          ..c..c.
           ......
ENDMAP

##############################################################################
NAME:   matt_arrival_refuge
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxxx@xxxxxxxx@xxxx
x.................x
x.xxxxxxx+xxxxxxx.x
x.x.............x.x
x.x.xxxxxxxxxxx.x.x
x.x.x.........x.x.x
x.x.x.xxxxxxx.x.x.x
x.x.x.x.....x.x.x.x
@.x...x..{..x...x.@
x.x.x.x.....x.x.x.x
x.x.x.xxx.xxx.x.x.x
x.x.x.........x.x.x
x.x.xxxxxxxxxxx.x.x
x.x.............x.x
x.xxxxxxx+xxxxxxx.x
x.................x
xxxx@xxxxxxxx@xxxxx
ENDMAP

##############################################################################
NAME:   matt_arrival_los_fun
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
        x+x
        x.x
  xxxxxxx+xxxxxxx
  x.............x
  x.xxxxxxxxxxx.x
  x..x.......x..x
  x.x.xxxxxxx.x.x
  x.xx.......xx.x
  x.x.xxx+xxx.x.x
  x.x.x.....x.x.x
xxx.x.x.....x.x.xxx
+.+.x.+..{..+.x.+.+
xxx.x.x.....x.x.xxx
  x.x.x.....x.x.x
  x.x.xxx+xxx.x.x
  x.xx.......xx.x
  x.x.xxxxxxx.x.x
  x..x.......x..x
  x.xxxxxxxxxxx.x
  x.............x
  xxxxxxx+xxxxxxx
        x.x
        x+x
ENDMAP

##############################################################
NAME:   minmay_arrival_open_corners
TAGS:   arrival no_monster_gen
ORIENT: float
SUBST:  x : x:10 c:2 v:1 m:1
MAP
xxx...xxx...xxx
x.............x
x.............x
...xxx...xxx...
...x{......x...
...x.......x...
x.............x
x......b......x
x.............x
...x.......x...
...x......>x...
...xxx...xxx...
x.............x
x.............x
xxx...xxx...xxx
ENDMAP

##############################################################
NAME:    minmay_arrival_plants_in_alcoves
TAGS:    arrival
ORIENT:  float
SHUFFLE: 11123t
MONS:    plant, fungus, bush
MAP
xxxxxxxxxxx
x1x1x1x1x1xxx
@...........x
...........{x
@...........x
x1x1x1x1x1xxx
xxxxxxxxxxx
ENDMAP

##############################################################
NAME:    minmay_arrival_radiant_lines
TAGS:    arrival no_rotate
ORIENT:  float
SHUFFLE: xX
SUBST:   X:xxxX, X:.lw, x:xxxvc
MAP
    .............
  ........X........
 .....x...X...x.....
 ..X...x..X..x...X..
....X..x..X..x..X....
.....X..x.X.x..X.....
..x...X.x.X.x.X...x..
...xx..X.x.x.X..xx...
.....xx.X...X.xx.....
.......x.....x.......
.XXXXXX...{...XXXXXX.
.......x.....x.......
.....xx.X...X.xx.....
...xx..X.x.x.X..xx...
..x...X.x.X.x.X...x..
.....X..x.X.x..X.....
....X..x..X..x..X....
 ..X...x..X..x...X..
 .....x...X...x.....
  ........X........
    .............
ENDMAP

##############################################################
NAME:   noname_arrival_rooms_in_the_neighbourhood
TAGS:   arrival no_monster_gen
RTILE:  x = wall_hall
FTILE:  .([{ = floor_hall
SUBST:   [=., (=.
ORIENT: float
MAP
 xxxxx             xxxxx
xx...xx           xx...xx
x.....x           x.....x
x..(..+     V     +..[..x
x.....x           x.....x
xx...xx   xx+xx   xx...xx
 xxxxx   xx...xx   xxxxx
         x.....x
         x..{..x
         x.....x
         xx...xx
          xxxxx
ENDMAP

##############################################################
NAME:    onia_arrival_mini_spirals
TAGS:    arrival
ORIENT:  float
SHUFFLE: ({[<, GT, vcxxxx
SUBST:   [=., (=., <=.
MAP
vvvvvvvvvvv
vv...v...vv
vv[v.v.v(vv
vvvv.v.vvvv
v.........v
v.vv...vv.v
v.<v...v{.v
vvvv.@.vvvv
ENDMAP

###########################################################
NAME:    dpeg_arrival_line_fractal
TAGS:    arrival no_monster_gen no_rotate
ORIENT:  float
WEIGHT:  8
SHUFFLE: cv
MAP
x@xxxxxxxxxxxxxxxxxxxx@x
x......................x
x.cccccc......cccccc...x
x......................x
x.vv..vv......vv..vv...x
x......................x
xxxxxxxxxx{}xxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_rooms_a
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: cvb
MAP
xxxxxxxxx@.xxxxxx
xvvvvvvvx..xbbbbx
xv.....vx..xb..bx
xv..{..+...xb..bx
xv.....vx..xb..bx
xvvvvvvvx...+..bx
xxxxxxxxx..xb..bx
@..........xb..bx
.........T.xb..bx
xxxxxxxx...xbbbbx
xxxxxxxx.@.xxxxxx
ENDMAP

############################################################################
# A spiral sometimes made entirely of glass
NAME:    zelgadis_glass_arrival_small
TAGS:    arrival
ORIENT:  float
SHUFFLE: mvvcccxxxx
MAP
...mmmmmmmm...
..............
.mm.mmmmmm.mm.
.mm.m....m.mm.
.mm.m.mm.m.mm.
.mm.m..m.m.mm.
.mm.m.{m.m.mm.
.mm.mmmm.m.mm.
.........m.mm.
...mmmmmmm....
ENDMAP

##############################################################################
NAME:   dpeg_arrival_snail
TAGS:   arrival no_monster_gen no_rotate
ORIENT: float
NSUBST: . = 1:d / *:.
ITEM:   stone
MAP
         xxxxxxxxxxxxxxxxx
      xxxxxxxx.......xxxxxxx
   xxxxxxx.............xxxxxx
  xxxxxx.................xxxxx
 xxxxxx.........xxxx......xxxx
xxxxxx........xxxxxxxx.....xxx
xxxxx.......xxxx....xxx.....xx
xxxxx......xxx........xx....xx
xxxx.......xx...xxx...xxx....x
xxxx......xxx..xxxxx...xx....x
xxx.......xx..xxx.{xx..xx....x
xxx......xxx..xxx.xx..xxx....x
xxx......xxx...xx.....xx....xx
xx........xx....xxxxxxx....xxx
xx........xxx....xxxx......xxx
xx........xxxx............xxxx
xx........xxxxxxx......xxxxxx
x....>.....xxxxxxxxxxxxxxxxx
x....@.....xxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    dpeg_arrival_three_smaller_balls
TAGS:    arrival no_rotate
ORIENT:  float
WEIGHT:  5
MAP
   xxxxx  x@x  xxxxx
 xxx...xxxx+xxxx...xxx
xx.......xx.x........xx
x.........x.x.........x
x.........+.+.........x
x.........x.x.........x
xx.......xx.xx.......xx
 xxx...xxxx+xxx....xxx
  xx+xxxxx...xxxxx+xx
  xx.xxx.......xxx.xx
  x...x.........x...x
  x...+....{....+...x
  x...x.........x...x
  x..xxx.......xxx.xx
  xx.xxxxx...xxxxx.x
   x..xxxxx+xxxxx..x
   xx.............xx
    xxxxxxxxxxxxxxx
ENDMAP

NAME:    dpeg_arrival_three_bigger_balls
TAGS:    arrival no_rotate no_monster_gen
ORIENT:  float
WEIGHT:  5
SHUFFLE: }>
MAP
         xxxxx
        xx...xx
        x..{..x
   xxxxxx.....xxxxxx
 xxx...xxx...xxx...xxx
xx.......xx+xx.......xx
x.........x.x.........x
x.........+.+.........x
x.........x.x.........x
xx.......xx+xx.......xx
 xxx...xxxx.xxxx...xxx
  xxxxxx.......xxxxxx
  xxx.............xxx
  xx...............xx
  x.................x
  x........T........x
  x.......T}T.......x
  x........T........x
  xx...............xx
   xx.............xx
    xxxx.......xxxx
       xxxx@xxxx
ENDMAP

##############################################################################
NAME:    matt_arrival_grand_opening
TAGS:    arrival
ORIENT:  float
SHUFFLE: TV
KMASK:   T = no_monster_gen
MAP
xxxxxxxxxx@xxxxxxxxxx
x..x.............x..x
x.x...............x.x
xx.......x.x.......xx
x......x.x.x.x......x
x.....xx.x.x.xx.....x
x.......Tx.xT.......x
x..xxxxxxx.xxxxxxx..x
@.........{.........@
x..xxxxxxx.xxxxxxx..x
x.......Tx.xT.......x
x.....xx.x.x.xx.....x
x......x.x.x.x......x
xx.......x.x.......xx
x.x...............x.x
x..x.............x..x
xxxxxxxxxx@xxxxxxxxxx
ENDMAP

##############################################################################
NAME:   co_arrival_two_rings
TAGS:   arrival
ORIENT: float
WEIGHT: 1
SUBST:  c : ccb
MAP
 ..........
...ccccccc..
..cc.....cc....
.cc.......cc....
.c.........c....
.c.........c....
.c......ccc+ccc..
.c.....cc..c..cc..
.c....cc.>.c...cc.
.cc...c.{.cc....c.
..cc..c..cc.....c.
 ..ccc+ccc......c.
  ....c.........c.
  ....c.........c.
  ....cc.......cc.
   ....cc.....cc..
      ..ccccccc...
       ..........
ENDMAP

##############################################################################
NAME:   co_arrival_square_in_a_square_in_a_square
TAGS:   arrival
ORIENT: float
MAP
x@xxxxxxxxxxxxxxx@x
@.c.c.c.c.c.c.c.c.@
xc...............cx
x..........m......x
xc.......m.......cx
x......m....m.....x
xc...m...........cx
x..m...c.c.c.m....x
xc...............cx
x...m..c.{.c..m...x
xc...............cx
x....m.c.c.c...m..x
xc...........m...cx
x.....m....m......x
xc.......m.......cx
x......m..........x
xc...............cx
@.c.c.c.c.c.c.c.c.@
x@xxxxxxxxxxxxxxx@x
ENDMAP

##############################################################################
NAME:   roderic_dead_forest
TAGS:   arrival no_monster_gen
ORIENT: float
SUBST:  T : t.TTTTT
SUBST:  T = t:66 .:33 >:1
COLOUR: . = brown
COLOUR: t = w:2 darkgrey / w:1 brown
TILE:   t = dngn_tree_dead
: set_feature_name("tree", "dead tree")
MAP
xxxxxxxx@xxxxxxxx
xttttttT.TTtttxxx
xttttttTT.Ttttttx
xttttttT.TTtttttxx
xxtttttT.TTttttttx
xttttttTT.Tttttttx
xttttttTT.Tttttttx
xxtttttTT.TTttttx
 xtttttT...Tttttx
 xtttttT.{.Tttttx
 xtttttT...Tttttx
 xtttttTT.TTtttxx
 xttttTT.TTttttx
xxttttT.TTttttxx
xtttttTT.Ttttttx
xttttTT.TTttttxx
xttttT..Tttttttx
xxxxxx@@xxxxxxxx
ENDMAP

##############################################################################
NAME:    roderic_crossed_star
TAGS:    arrival no_monster_gen
ORIENT:  float
SHUFFLE: ?', !"
SUBST:   ?:.x , ':. , !:.x, ":.
MAP
   .........
  ..x.....x..
  ..xx...xx..
....xxx.xxx....
.xxx'xx?xx'xxx.
..xxx'x?x'xxx..
...xxx"!"xxx...
....??!{!??....
...xxx"!"xxx...
..xxx'x?x'xxx..
.xxx'xx?xx'xxx..
....xxx.xxx....
  ..xx...xx..
  ..x.....x..
   .........
ENDMAP

##############################################################################
NAME:   roderic_chinese_lattice_arrival
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
    x.xxx.x
    x.....x
  xxx.x.x.xxx
  x.........x
xxx.x.x.x.x.xxx
......x.x......
x.x.xxx.xxx.x.x
x......{......x
x.x.xxx.xxx.x.x
......x.x......
xxx.x.x.x.x.xxx
  x.........x
  xxx.x.x.xxx
    x.....x
    x.xxx.x
ENDMAP

##############################################################################
# denvon's houses (hot, water, plant)

NAME:    denvon_arrival_planthouse
TAGS:    arrival no_monster_gen
ORIENT:  float
WEIGHT:  6
MONS:    plant
SHUFFLE: {[
SUBST:   [=.
MAP
          ......
        ..xx++xx..
      ...xx1..1xx...
     ..xxxxt11txxxx..
    ..xxccccccccccxx..
    .xxcc1tt1t111ccxx.
    .xxc11..{[..t1cxx.
    .xxc..........cxx.
    .xxc+cc...1cc+cxx.
   ..xcc..cc++cc1.ccx..
  ..xxct.1c....c.t1cxx..
 ..xxxccccc...1cccccxxx..
 .xxx11..............xxx.
 .xt....t..1t...1t.....x.
 .....1.........t........
   ...tt...1t.....t1.
      .......1........
        ... ...
ENDMAP

NAME:    denvon_arrival_waterhouse
TAGS:    arrival no_monster_gen no_pool_fixup
ORIENT:  float
WEIGHT:  3
SHUFFLE: {[
SUBST:   [=.
MAP
         ........
        ..xx++xx..
      ...xxW..Wxx...
     ..xxxxWWWWxxxx..
    ..xxccccccccccxx..
    .xxccwWWWWWWwccxx.
    .xxcWWW.{[.WWWcxx.
    .xxc..........cxx.
    .xxc+cc....cc+cxx.
   ..xcc..cc++cc..ccx..
  ..xxcT..c....c..Tcxx..
 ..xxxccccc....cccccxxx..
 .xxxwWW..........WWwxxx.
 .xwwwW........x...Wwwwx.
..WWWW...xxW........WWWW.
.WWW.....Wxx...xx....WW..
..W.. ..WWW..xxWWx...W...
 ...  .Wxx....WW... ...
      .....  .Wxx.
             .....
ENDMAP

NAME:    denvon_arrival_hothouse
TAGS:    arrival no_monster_gen no_pool_fixup
ORIENT:  float
WEIGHT:  1
SHUFFLE: {[
SUBST:   [=.
MAP
         ........
        ..xx++xx..
      ...xxl..lxx...
     ..xxxxllllxxxx..
    ..xxccccccccccxx..
    .xxccllllllllccxx.
    .xxclll.{[.lllcxx.
    .xxc..........cxx.
    .xxc+cc....cc+cxx.
   ..xcc..cc++cc..ccx..
  ..xxcV..c....c..Vcxx..
 ..xxxccccc....cccccxxx..
 .xxxlll.....x....lllxxx.
 .xllll...x........llllx.
....ll...x...x..x...ll....
.x..........lx.........lx.
.....x....xxl..xxl.....xl.
 .xl.....x....xl....x..lx.
 ....   ...  ....  .......
ENDMAP

##############################################################################
NAME:   ncdulo_lavacross
TAGS:   arrival no_monster_gen no_trap_gen
ORIENT: float
MAP
xx+xxx+xx
xl.lll.lx
xll.l.llx
xll...llx
@.l.{.l.@
xll...llx
xll.l.llx
xl.lll.lx
xx+xxx+xx
ENDMAP

NAME:   ncdulo_wetmaze
TAGS:   arrival no_monster_gen no_trap_gen
ORIENT: float
MAP
xxxx+xxxx
xwwwwwwwx
xWxW{Wxwx
xWxxxWxWx
xxWWWWWWx
xWxxxxxxx
xxWWWx..+
xWxxWxx.x
x.xx....x
x+xxxxxxx
ENDMAP

NAME:   ncdulo_watergridlock
TAGS:   arrival no_monster_gen no_pool_fixup
ORIENT: float
MAP
x.x.x.x.x.x.x
.x.x.x.x.x.x.
x.x.wwwww.x.x
.x.xw.{.wx.x.
x.x.w...w.x.x
.x.xw...wx.x.
x.x.WWWWW.x.x
.x.x.x.x.x.x.
x.x.x.x.x.x.x
ENDMAP

NAME:   ncdulo_smallfort
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxxxxxxx
x.......x
x.xx+xx.x
x.x...x.x
x.x.{.x.x
x.xx+xx.x
x.....+.+
xxxxxxx+x
ENDMAP

NAME:   ncdulo_halloftheorcs
TAGS:   arrival
ORIENT: float
MAP
     ccc+ccc
    cc.....cc
   cc.T...T.cc
  cc....I....cc
ccc...T...T...ccc
c...............c
c.G.G.vvvvv.G.G.c
c..I..v.{.v..I..c
c.G.G.vv+vv.G.G.c
c...............c
ccccccccccccccccc
ENDMAP

##############################################################################
# ldierk's gardens

NAME:    ldierk_walled_garden
TAGS:    arrival no_monster_gen no_pool_fixup
ORIENT:  float
SHUFFLE: 123t
SUBST:   W : wW
SUBST:   R : RWW, R = W.
SUBST:   T : t
MONS:    plant, toadstool, bush
MAP
xxxxxx@xxxxxx
xWWWWTRTWWWWx
xW.1.....2.Wx
xW111...222Wx
xT.1.....2.Tx
@R....{....R@
xT.3.....t.Tx
xW333...tttWx
xW.3.....t.Wx
xW...T.T...Wx
xWWWWT.TWWWWx
xxxxxx@xxxxxx
ENDMAP

NAME:    ldierk_walled_garden_2
TAGS:    arrival no_monster_gen no_pool_fixup
ORIENT:  float
SHUFFLE: 123t
SUBST:   W : wW
SUBST:   T : W.
MONS:    plant, toadstool, bush
MAP
  xxx xxx
 xxWx@xWxx
xxW1WTW2Wxx
xW1G1T2G2Wx
xxW1.{.2Wxx
xxW3...tWxx
xW3G3.tGtWx
xxW3...tWxx
 xxWx@xWxx
  xxx xxx
ENDMAP

NAME:    ldierk_water_garden
TAGS:    arrival no_monster_gen no_pool_fixup
ORIENT:  float
SUBST:   W : wW
MONS:    toadstool
MAP
  W       W
 WtW     WtW
WtTtWWWWWtTtW
.Wt.......tW.
@.W...{...W.@
.Wt.......1W.
WtTtWWWWW1T1W
 WtW     W1W
  W       W
ENDMAP

##############################################################################
# tarquinn's gardens

NAME:   tarquinn_simple_flora_water_a
TAGS:   arrival no_monster_gen
ORIENT: float
SHUFFLE: ({[
MONS:   plant
MAP
ttt@ttttt
t{t.....t
t.11111.t
t.1www1.t
t.1wGw1.t
t.1www1.t
t.11111.t
t.......t
ttttttttt
ENDMAP

NAME:   tarquinn_simple_flora_water_b
TAGS:   arrival no_monster_gen
ORIENT: float
MONS:   plant
MAP
xxxxxxxxxxxxx
+...wwwww...+
xx..xxxxx..xx
x..tt1{1tt..x
xx..tt.tt..xx
xt.........tx
xt.t.www.t.tx
xt..wwGww..tx
xt.t.www.t.tx
xt.........tx
xx..tt.tt..xx
x..tt1}1tt..x
xx..xxxxx..xx
+...wwwww...+
xxxxxxxxxxxxx
ENDMAP

NAME:   tarquinn_simple_flora_water_c
TAGS:   arrival no_monster_gen
ORIENT: float
SHUFFLE: ([{ABC
SUBST:  A = }11
SUBST:  ABC = 1
MONS:   plant
MAP
xxxxxxxxxxxxxxxxxxxx
xttttt..C.wwwwwwwwwx
xttA.....ww11wwww..+
xt...B.www111ww....x
x.....ww111www....tx
+....wwwwwww.....ttx
xwwwwww..{.....ttttx
xxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   tarquinn_simple_flora_water_d
TAGS:   arrival no_monster_gen
ORIENT: float
MONS:   plant, fungus
MAP
xxxxxxxxxx
+....t1x2xxx
xWWW..t1x.txx
xxWG....{txx
xWWW..t1x.txx
+....t1x2xxx
xxxxxxxxxx
ENDMAP

NAME:   tarquinn_simple_flora_water_e
TAGS:   arrival no_monster_gen
ORIENT: float
FTILE:  1 = floor_grass
MONS:   plant
MAP
  xxx
xxx.xx
x1w.tx
x1w{.+
x1w.tx
xxx.xx
  xxx
ENDMAP

NAME:   tarquinn_simple_flora_water_f
TAGS:   arrival no_monster_gen
ORIENT: float
SHUFFLE: 123wwttt
MONS:   plant, bush, fungus
MAP
xxxxxx+xxx
x12213.1tx
xw13.....+
x.....w12x
x{.w3132wx
xxxxxxxxxx
ENDMAP

##############################################################################
# Saegor's simple entries

NAME:   saegor_arrival_simple_a
TAGS:   arrival
ORIENT: float
MAP
ccc
c{c
.@.
ENDMAP

NAME:   saegor_arrival_simple_b
TAGS:   arrival
ORIENT: float
MAP
ttttt
ttttt
xt{tx
xG.Gx
xx+xx
ENDMAP

NAME:   saegor_arrival_simple_c
TAGS:   arrival no_monster_gen
ORIENT: float
SHUFFLE: {([
MAP
xxxxxxx
x..{..x
xx...xx
x.vvv.x
xG+++Gx
xxv@vxx
ENDMAP

NAME:   saegor_arrival_simple_d
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxcccxxx
xccc{cccx
xc.c.c.cx
xt.....tx
xwt...twx
xwwt.twwx
xwwt.twwx
xxwt.twxx
xxxx+xxxx
ENDMAP

NAME:   saegor_arrival_simple_e
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxxxxxxxx
x.......xx
x.xxxxGx.x
xx....xx.x
xxx.{.xx.x
  x...xx.x
  xx.xx.xx
  xxx++xxx
ENDMAP

NAME:   saegor_arrival_simple_f
TAGS:   arrival
ORIENT: float
MAP
  xxx
  x.@
  x.xxxxxxx
  x.x.....x
  x....xx@x
x@x..{..x
x.xx....x
x.....x.x
xxxxxxx.x
      @.x
      xxx
ENDMAP

##############################################################################

NAME:   scummos_arrival_simple_fountains
TAGS:   arrival no_monster_gen transparent
ORIENT: float
MAP
.......
.T...T.
..mmm..
..m{+..
..mmm..
.T...T.
.......
ENDMAP

NAME:   scummos_arrival_simple_crosswalls
TAGS:   arrival no_monster_gen transparent
ORIENT: float
MAP
...........
..x.....x..
.xxGxxxGxx.
..x........
..x.....x..
..x..{..x..
..x.....x..
........x..
.xxGxxxGxx.
. x.....x..
...........
ENDMAP

NAME:   scummos_arrival_hexagon
TAGS:   arrival no_monster_gen no_rotate transparent
ORIENT: float
MAP
    .......
   ..xxxxx..
  ..x.....x..
 ..x.......x..
..x....{....x..
 ..x.......x..
  ..x.....x..
   ..xxxxx..
    .......
ENDMAP

NAME:   scummos_arrival_star
TAGS:   arrival no_monster_gen no_rotate transparent
ORIENT: float
MAP
       ...
      ..m..
     ..m.m..
......m...m......
.mmmmm.....mmmmm.
..m...........m..
 ..m.........m..
 ..m....{....m..
..m...........m..
.mmmmm.....mmmmm.
......m...m......
     ..m.m..
      ..m..
       ...
ENDMAP

NAME:   scummos_arrival_forest
TAGS:   arrival no_monster_gen no_rotate transparent
ORIENT: float
MAP
...............
..xxttt.tttxx..
..xttttTttttx..
..ttttt.ttttt..
.....T.{.T.....
..ttttt.ttttt..
..xttttTttttx..
..xxttt.tttxx..
...............
ENDMAP

NAME:   scummos_arrival_gate
TAGS:   arrival no_monster_gen
ORIENT: float
KFEAT:  y = floor
FTILE:  y = floor_sandstone
COLOUR: y = yellow
MAP
xxxxxxxxxxxxxxxxxx
x.....x...WWWW...x
x..{..x..........x
x.....x..........xG.
x+xxxxxyyyyyyyyyy+.@
x.xtt.+yyyyyyyyyy+.@
x.xt.txyyyyyyyyyy+.@
x.xt.tx..........xG.
x.+.ttx..........x
xtxtttx...llll...x
xxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   scummos_arrival_checkerplants
TAGS:   arrival no_monster_gen transparent
ORIENT: float
KMONS:  b = fungus
MAP
............
.ttttttttt..
.tbb..bb..t.
.tbb..b...t.
.t..bb...bt.
.t..b.{.bbt.
.tbb...b..t.
.tb...bb..t.
.t...b..bbt.
.t..bb..bbt.
..ttttttttt.
............
ENDMAP

##############################################################################

NAME:   lightli_molten_gateway
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxxxxx++xxxxxxx
xxxxxll..llxxxxx
xxxllll..llllxxx
xxlllvl..lvlllxx
xxlllll..lllllxx
xllllll..llllllx
xlllllv..vlllllx
xllvllv..vllvllx
xlllllv{.vlllllx
xlllllvvvvlllllx
xllllllllllllllx
xxllvllllllvllxx
xxllllllllllllxx
xxxllllllllllxxx
xxxxxllllllxxxxx
xxxxxxxxxxxxxxxx
ENDMAP

##############################################################################

NAME:   mrwooster_arrival_arrow
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxxxxxxxxxxx
xxxxxx.xxxxxx
xxxxx...xxxxx
xxxx.....xxxx
xxx.......xxx
xx.........xx
+.....{.....+
xxxxx...xxxxx
    xx.xx
     x.x
     x.x
     x.x
     x+x
ENDMAP

NAME:   mrwooster_arrival_statue
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxxxxxxxxxxx
xxxxxG{Gxxxxx
xx.........xx
xx.........xx
xx.........xx
.............
ENDMAP

NAME:   mrwooster_arrival_statue_2
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxxxxxxxxxxx
xxxxxG{Gxxxxx
xx.........xx
xG.........Gx
x...........x
xG.........Gx
xx.........xx
.............
ENDMAP

NAME:   mrwooster_arrival_statue_3
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxxxxxxxxxxx
xxxxxG{Gxxxxx
xx.........xx
xG.........Gx
x...........x
xG.........Gx
xx.........xx
xx.........xx
xxxxxx+xxxxxx
ENDMAP

##############################################################################

NAME: lightli_arrival_statue_moat
TAGS: arrival no_monster_gen
ORIENT: float
MAP
xxxx.xxxx
xx.....xx
x.xGwGx.x
x.xwwwx.x
x.xGwGx.x
x.xwwwx.x
x.xGwGx.x
xx.....xx
xxx.{.xxx
xxxxxxxxx
ENDMAP

NAME: lightli_arrival_chessboard
TAGS: arrival no_monster_gen
ORIENT: float
MAP
.c.c.c.c
c.c.c.c.
.c.c.c.c
c.c{c.c.
.c.c.c.c
c.c.c.c.
.c.c.c.c
c.c.c.c.
ENDMAP

NAME: lightli_iron_boxes
TAGS: arrival no_monster_gen
ORIENT: float
MAP
..vv..vv..
..vv..vv..
vv..vv..vv
vv..vv..vv
..vv{.vv..
..vv..vv..
vv..vv..vv
vv..vv..vv
..vv..vv..
..vv..vv..
ENDMAP

NAME: lightli_arrival_maybe_door_vault
ORIENT: float
TAGS: arrival
MARKER: + = lua: props_marker { connected_exclude="true" }
MAP
   @@
 ++++++
 +....+
@+.{..+@
@+....+@
 +....+
 ++++++
   @@
ENDMAP

##############################################################
NAME:    grunt_arrival_crossed_arrows
ORIENT:  float
TAGS:    arrival no_monster_gen transparent
MAP
....G....
.xx...xx.
..xx.xx..
 ..x+x..
 ..x+x..
..xxTxx..
.++T{T++.
..xxTxx..
 ..x+x..
 ..x+x..
..xx.xx..
.xx...xx.
....G....
ENDMAP

# Come back for the item later if you want it.
NAME:    grunt_arrival_lava_bath
ORIENT:  float
TAGS:    arrival no_monster_gen
KITEM:   % = %
KFEAT:   % = >
MAP
    .@...xxxxx
   ....G.ccccx
  .......lllcx
 .......llllcx
@.G..G{.lll%cx
 .......llllcx
  .......lllcx
   ....G.ccccx
    .@...xxxxx
ENDMAP

###############################################################################

NAME:   wheals_arrival_linesprint
ORIENT: float
TAGS:   arrival no_monster_gen
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxx
x{.......................x
xxxxxxxxxxxxxxxxxxxxxxxx.x
x........................x
x.xxxxxxxxxxxxxxxxxxxxxxxx
x........................@
xxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   wheals_arrival_shading
ORIENT: float
TAGS:   arrival no_monster_gen
SUBST:  a = xc
SUBST:  b = cv
MAP
xxxxaaacccbbbvvv
x..............@
x{.............@
x..............@
xxxxaaacccbbbvvv
ENDMAP

###############################################################################

NAME: lightli_arrival_flooded_house
ORIENT: float
TAGS: arrival no_monster_gen no_tele_into no_pool_fixup
SUBST: 1 = 1WWw.
MONS: plant
MAP
ccccccccc
c1111111c
c1111111c
c1111111c
c1....11c
c.c111.1c
c.c1111.c
c(c11111+
ccccccccc
ENDMAP

NAME: lightli_arrival_lavapool
ORIENT: float
TAGS: arrival no_monster_gen
MAP
   vvvv
  vvvvvv
 vv....vv
vvv.ll.vvv
v{+.ll.+.+
v.+.ll.+.+
vvv.ll.vvv
 vv....vv
  vvvvvv
   vvvv
ENDMAP

###############################################################################

NAME:   mushboom_english_gardens
ORIENT: float
TAGS:   arrival no_monster_gen
FTILE:  -T = floor_grass
COLOUR: -T = green
MAP
xxxxx+xxxxx
x.........x
x.---.---.x
x.-T-.-T-.x
x.---.---.x
+....{....+
x.---.---.x
x.-T-.-T-.x
x.---.---.x
x.........x
xxxxx+xxxxx
ENDMAP

NAME:   doesnt_arrival_eight_statues
TAGS:   arrival no_monster_gen
ORIENT: float
MAP
xxxx+xxxx
x.......x
x.G.G.G.x
x.......x
+.G.{.G.+
x.......x
x.G.G.G.x
x.......x
xxxx+xxxx
ENDMAP

NAME:   doesnt_arrival_goblin_jail
TAGS:   arrival no_monster_gen
ORIENT: float
KMASK:  ? = no_item_gen
KPROP:  ? = no_tele_into
KFEAT:  A = iron_grate
MONS:   goblin ; nothing, hobgoblin ; nothing, kobold ; nothing
MONS:   gnoll ; nothing, orc ; nothing
NSUBST: ? = 7:1 / 7:2 / 4:3 / 2:4 / 2:5 / *= .:150 12345
MAP
xxxxxxxxxxxxxxxxxxx
x>x???x???x???x???x
x.x???x???x???x???x
x+xAAAxAAAxAAAxAAAx
x.................+
x+xAAAxAAAxAAAxAAAx
x.x???x???x???x???x
x{x???x???x???x???x
xxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# -3- Huge maps.
##############################################################################
#
# The maps in this section are not complicated, but rather big... possibly
# too big. We are talking about sizes 25x25 and larger.

##############################################################
# 35 x 19
# Ruined Dwarven Fortress Basement
NAME:    elethiomel_arrival_fortress_basement
TAGS:    arrival no_monster_gen no_rotate transparent
ORIENT:  float
WEIGHT:  2
SHUFFLE: {[(
SUBST:   [( = .
SUBST:   c : cx
TILE:    c = wall_stone_smooth
FTILE:   .{0$+ = floor_cobble_blood
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xcccccccxxxxxxxxxxxxxxxxxxxcccccccx
xc.....ccccccccccccccccccccc.....cx
xc.....+...................+.....cx
xc....[cccccccccc+cccccccccc{....cx
xccc+cccxxxxxxccc.cccxxxxxxccc+cccx
xxxc.cxxxxxxxxc.....cxxxxxxxxc.cxxx
xxxc.cxxxxxxxxc.0...cxxxxxxxxc.cxxx
xxxc.cxxxxxxxxc$....cxxxxxxxxcx.xxx
xxxc.cxxxxxxxxcccccccxxxxxxxxc...xx
xxxc.cxxxxxxxxxxxxxxxxxxxxxxxc..x.x
xxxc.cxxxxxxxxxxxxxxxxxxxxxxxcx..x.
xxxc.cxxxxxxxxxxxxxxxxxxxxxxxc..xxx
xccc+ccxxxxxxxxxxxxxxxxxxxxccc+cccx
xxx...xxcccccccccccccccccccc(....cx
xxx.x..+...................+.....cx
xc...x.ccccccccccccccccccccc.....cx
xccx.xccxxxxxxxxxxxxxxxxxxxcccccccx
xxxx@xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
# 46 x 14
NAME:    elethiomel_arrival_infinity_welcomes_careful_drivers
TAGS:    arrival no_rotate transparent
ORIENT:  northwest
KMASK:   a = no_monster_gen
SUBST:   a : .
SUBST:   ' : . x ':40, '=x.
SUBST:   c : cxxx
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxx....xxxxxxxxxxxxxxx....xx..xxx.''xxxxxx
xxxxx''........xx...''.........''...'..xx...xx
xxx'....ccccc...xxx.....ccccc......'......x..@
x....ccccaaacccc.....ccccaaacccc.......''..xxx
x..cccaaaaaaaaaccc+cccaaaaaaaaaccc....''..xxxx
x'.caaaaaa{aaaaaa+a+aaaaaaTaaaaaa+.........xxx
x'.cccaaaaaaaaaccc+cccaaaaaaaaaccc...''...xxxx
x....ccccaaacccc.....ccccaaacccc......'..'.xxx
x.......ccccc......'....ccccc......'.....xxxxx
x'................'..xx........'....'....xxxxx
x.''..xxx...''.....'..xx....'''..''.xxx''.xxxx
x'xxxxxxxxxxxxxx..xxx.xxx..xxx....xxxxxx...xxx
xxxxxxxxxxxxxxxxx@xxxxxxxx@xxxxxx@xxxxxxxx@xxx
ENDMAP

##############################################################################
# 33 x 25
NAME:    dpeg_arrival_central
TAGS:    arrival transparent
ORIENT:  float
SHUFFLE: @ABC
SUBST:   A=@x, B=@x, C=@x
MAP
          xxxxxx@xxxxxx
        xxx...........xxx
      xxx...xxx...xxx...xxx
     xx...xxxx.....xxxx...xx
    xx..xxxxx...]...xxxxx..xx
   xx..xxxxx.........xxxxx..xx
  xx..xxxxx...........xxxxx..xx
 xx..xxxxx......x......xxxxx..xx
xx..xxxxx......xxx......xxxxx..x
xx.xxxxx......xxxxx......xxxxx.xx
x..xxxx......xxxxxxx......xxxx..x
x.xxxx......xxxxxxxxx......xxxx.x
A..........xxxxxxxxxxx..........C
x.xxxx......xxxxxxxxx......xxxx.x
x..xxxx......xxxxxxx......xxxx..x
xx.xxxxx......xxxxx......xxxxx.xx
 x..xxxxx......xxx......xxxxx..x
 xx..xxxxx......x......xxxxx..xx
  xx..xxxxx...........xxxxx..xx
   xx..xxxxx.........xxxxx..xx
    xx..xxxxx...{...xxxxx..xx
     xx...xxxx.....xxxx...xx
      xxx...xxx...xxx...xxx
        xxx...........xxx
          xxxxxxBxxxxxx
ENDMAP

##############################################################################
# 29 x 22
NAME:    dpeg_arrival_castle
TAGS:    arrival no_monster_gen no_rotate transparent
ITEM:    stone, nothing, nothing
ORIENT:  float
SHUFFLE: ]def
SUBST:   c : cx
TILE:    c = wall_stone_smooth
TILE:    x = wall_pebble_lightgray
KFEAT:   ' = floor
FTILE:   '+]def{T = floor_cobble_blood
SUBST:   X = T
MAP
  .........................
  .........................
...wwwwwwwwwwwwwwwwwwwwwww...
...wwwwwwwwwwwwwwwwwwwwwww...
...wwwwwwwwwwwwwwwwwwwwwww...
...wwwc+c+cwwwwwwwc+c+cwww...
...www+]''cc+ccc+cc''d+www...
...www+'''c'''''''c'''+www...
...wwwc+ccc'xxxxx'c+c+cwww...
...wwwww+'''x'''x'''cwwww....
...wwwwwc'xxx'{'+'T'+WwW..X..
...wwwwwc'xxx'''+'T'+wWw..X..
...wwwww+'''x'''x'''cwwww....
...wwwc+ccc'xxxxx'ccc+cwww...
...www+'''+'''''''+'''+www...
...www+f''cc+ccc+cc''e+www...
...wwwc+c+cwwwwwwwc+c+cwww...
...wwwwwwwwwwwwwwwwwwwwwww...
...wwwwwwwwwwwwwwwwwwwwwww...
...wwwwwwwwwwwwwwwwwwwwwww...
  .........................
  .........................
ENDMAP

#############################################################################
# 42x28
NAME:    dpeg_cavernosity
TAGS:    arrival no_monster_gen no_rotate transparent
ORIENT:  float
MONS:    fungus
SHUFFLE: {[(, UVW, XYZ
SUBST:   [( = T
SUBST:   '=.x, U=+, V=x, W=x, X=+, Y=x, Z=x, b:xxxbcv
RTILE:   x = wall_pebble_lightgray
SUBST:   = : + x
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxx''xxxxxxxxxxxxxxxxx.xxxxxxxxxxxxx
xxx...x''...x...xxxxxxxxxxx.0..xxxxxxxxxxx
xx..(....xx..''..xxxxxxxxxx.x.xxxxxxxxxxxx
xx.....xxx'.xxx..xxxx''xxxx...xxxxxxxxxxxx
xxx...xxxx=xxxxx.'xx'..'xxxx0xxxxxxxxxxxxx
xxxxxxx'x=xxxxxx'.x..x.'xxx.xxxxxxxxxxxx..
xxxxx''...'xxxxxx'..xx..'xxx..xxxxxxx.....
xxxxx'..x'..xxxxxx'x'..x'xxxxx.xxxx.......
xxxx..'xxx..'xxxxxxx'.xxxxxxx0x.Wx........
xxxx..xxxxx..'xxxxx.....xxxx.xxxx.........
xxxxx...xx'x..'xxx.......xxx0xxxx.........
xxxx.....x'x..'xx...bbb...xUxVxx..........
xxxx..{..xx..x''x..bbbbb....x'...........@
xxxxx...xx''.xxx...bbbbb..x....x..........
xxxxxxxxxxxx.'x.....bbb...xxxYxxx.........
xxxxxxxxxxxxx....x.......xXx.xxxx.........
xxxxxxxxxxxxxxx'xxx.....xxx..xxxxx........
xxxxxxxxxxxxxx=xxxxxx..xxx..xxxxxxx.......
xxxxxx'xxxxxx=xxxxxx.''xxx.xxxxxxxxxx.....
xx...x.'xxxxx'.xxxxx'...Z1.xxxxxxxxxxxxx..
x..[.......'...'x'xxx..'xx..xxxxxwxxwwxxxx
x.....xxx...'x'..xxx..'xxxx1...wwwwwxxwxxx
xx...xxxx''x'xxx'..'.xxxxxxxxx.wwwxxxxxxxx
xxxxxxxxxxxxxxxxxx..xxxxxxxxxxwwxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
# 30x25
# The Diamond Age, or A Young Lady's Illustrated Primer
NAME:    elethiomel_arrival_diamonds
TAGS:    arrival no_monster_gen no_rotate transparent
ORIENT:  west
WEIGHT:  2
MONS:    rat, goblin, bat, kobold
SHUFFLE: {[(, 234
SUBST:   [( = .
MAP
xxxxxxxxxxxxxxxxxxxxxxx@x
xxxxxxxxxxxxxxxxxxxxxxx+x
xxxxxxxxxxx.xxxxxxxxxxx.xx
xxxxxxxxxx...xxxxxxxxx...xx
xxxxxxxxx.....xxxxxxx.....xx
xxxxxxxx.......xxxxx.......xx
xxxxx.+....{....xxx....2....xx
xxxxx+xx.......xxxxx.......xxxx
xxxxx.xxx.....xxx.xxx.....xxx.xx
xxxx...xxx...xxx...xxx...xxx...xx
xxx.....xxx.xxx.....xxx.xxx.....xx
xx.......xx+xx..1.1..xx+xx.......xx
x....[....+.+...1d....+.+....3....+@
xx.......xx+xx...1...xx+xx.......xx
xxx.....xxx.xxx.....xxx.xxx.....xx
xxxx...xxx...xxx...xxx...xxx...xx
xxxxx.xxx.....xxx.xxx.....xxx.xx
xxxxx+xx.......xxxxx.......xxxx
xxxxx.+....(....xxx....4....xx
xxxxxxxx.......xxxxx.......xx
xxxxxxxxx.....xxxxxxx.....xx
xxxxxxxxxx...xxxxxxxxx...xx
xxxxxxxxxxx.xxxxxxxxxxx.xx
xxxxxxxxxxxxxxxxxxxxxxx+x
xxxxxxxxxxxxxxxxxxxxxxx@x
ENDMAP

##############################################################################
# 29x23
# Morbid curiosity
# Monsters are easier. Made it harder for them to get out.
NAME:    dpeg_arrival_morbid_curiosity
TAGS:    arrival no_rotate no_monster_gen transparent
ORIENT:  float
MONS:    goblin ; stone
MONS:    hobgoblin ; stone
MONS:    kobold ; mace
SHUFFLE: ABCD
NSUBST:  X = 1:+ / *:x
NSUBST:  Y = 1:+ / *:x
SUBST:   A=@, B=@xx, C=@xxxx, D=x
MAP
     xxxxxxxxxBxxxxxxxxx
    xxxxx...........xxxxx
   xxx...xxxxx+xxxxx...xxx
  xx..xxxxxlllllllxxxxx..xx
 xx.xxxxxlll.....lllxxxxx.xx
xx.xxxxxx3..........xxxxxx.xx
xx.xxxxxxxxxxx+xxxxxxxxxxx.xx
xx.xxxxxxxxxGXGXGxxxxxxxxx.xx
x.xx...xxx.........xxx..lxx.x
x.x....xx...........xx1.llx.x
x.x....x............Yx...lx.x
A.+...........{.....G+..1l+.C
x.x....x............Yx...lx.x
x.x....xx...........xx..llx.x
x.xx...xxx.........xxx..lxx.x
xx.xxxxxxxxxxG+Gxxxxxxxxxxx.x
xx.xxxxxxxxxxx+xxxxxxxxxxx.xx
xx.xxxxxx...........xxxxxx.xx
xxx.xxxxxlll2..2.lllxxxxx.xx
 xxx..xxxxxlllllllxxxxx..xx
   xxx...xxxxx+xxxxx...xxx
    xxxxx...........xxxxx
     xxxxxxxxxDxxxxxxxxx
ENDMAP

##############################################################################
# 29 x 21
NAME:    matt_arrival_debris
TAGS:    arrival transparent
ORIENT:  float
WEIGHT:  2
SHUFFLE: {[(
SUBST:   [( = .
SUBST:   ' : . x ':40
SUBST:   '=x.
MAP
xxxxxxx@xxxxxxxxxxxxx@xxxxxxx
x....x..'.x.................x
x...'.xxxx...........'x.....x
x'..''.''.........xx..x'....x
x'...'...x'...'....'xx.xx...x
x'...x'......x.x.....x..''..x
x'.'x'x'.......'....'x......x
x'..'x.......x'x.....''..x..x
x'...'...xxx..{...x'....'x'.x
@''.......'.....'....'..'x'.x
x''......xxx...xx........x..x
x....x.......'xx.x...[....x.@
x...'....x''..x.xx........x.x
x'....x....'..x'x..'''...x..x
x...'.'....'.xx.x...x...'x.'x
x..x.....(...x.xx.......'x.'x
x..'..'x.........xx.....'x..x
x...x...........xx.x..x..x..x
x.......'x.'''..x.xx........x
x.........'......xx.........x
xxxxxxxxxxx@xxxxxxxxxxxxxxxxx
ENDMAP

###########################################################
# 43 x 26 -- but it feels smaller because it's not open and
#            starts in a corner and is diagonally shaped
# There's a forest outside the dungeon
NAME:   kilobyte_arrival_exit_the_forest
TAGS:   arrival no_rotate no_pool_fixup transparent
ORIENT: northwest
MONS:   plant
FTILE:  .t1 = floor_moss
KMASK:  Ww = no_monster_gen
MAP
ttttttttttttttttttttttttttttttttttttttttttt
ttttttttt1.t1ttttttwwtttttttttttttttttttttt
tttt...1..t....tttwwwttt.tttttttt....ttttt
ttt..{..ttttt.t.tttwwwt...tttt.....t...tt
tt......tttttt.ttttttwww...t.....ttttt..
tttt..t.tttttt.ttttttttwww...ttttttttt..
ttttt..tttttt.ttttttttttwwwWttttttttttt..
ttttt.tttttt.tttttttttttttwWWtttttttttt.
tttt..tttttt..tttttttttttt..wWwtttttt..
tttt.ttttttttt..tttttttt...ttWttt.....
tttt.ttttttttttt.ttttt...ttt.tt.....
tttt.tttttttttttt.ttt.tttt...tt.....
ttttt..ttttttttttt...ttttt.ttt....
ttttttt..ttttt....tttttttt.t.....
ttttttttt.....ttttttttttttt.t...@
ttttttttttt1ttttttttttttttt...
tttttttttttttttttttttttt....
tttttttttttttttttttttt....
tttttttttttttttttttt.....
tttttttttttttttttt......
ttttttttttttt..........
tttttttt............
ttt..............
tt....
t..
t.
ENDMAP

#############################################################################
# 33 x 23
NAME:   dpeg_arrival_circle_labyrinth_a
TAGS:   arrival no_monster_gen no_rotate transparent
WEIGHT: 3
ORIENT: float
NSUBST: . = d / .
NSUBST: + = 3=+ / x
ITEM:   stone
MAP
          xxxxxxxx.@.xx
       xxxx......+....xxxx
    xxxx.........+.......xxxx
   xx.......xxxx.x+xx.......xx
  xx.....xxxx....x..xx+x.....xx
 xx....xxx.......x.....xxx....xx
 x....xx.....xxx+x.x.....xx....x
xx...xx....x+x...x.x+x....xx...xx
x....+....xx.....+...xx....+....x
x...xx...xx...xx.xx...xx...xx...x
x...x....x...xx...xx...x....x...x
x...+....+...xx.{.xx...+....+...x
x...x....x...xx...xx...x....x...x
x...xx...xx...xx+xx...xx...xx...x
x....+....xx.........xx....+....x
xx...xx....x+x.....x+x....xx...xx
 x....xx.....xxx+xxx.....xx....x
 xx....xxx.............x+x....xx
  xx.....x+xx.......x+xx.....xx
   xx.......xx+xxx+xx.......xx
    xxxx.................xxxx
       xxxx...........xxxx
          xxxxxxxxxxxxx
ENDMAP

NAME:   dpeg_arrival_circle_labyrinth_b
TAGS:   arrival no_monster_gen no_rotate transparent
WEIGHT: 1
ORIENT: float
NSUBST: . = 1:d / *:.
ITEM:   stone
MAP
          xxxxxxxxxxxxx
       xxxx......x....xxxx
    xxxx.........x.......xxxx
   xx.......xxxx.xxxx.......xx
  xx.....xxxx..x....xxxx.....xx
 xx....xxx.....x.......xxx....xx
 x....xx.....xxxxxxx.....xx....x
xx...xx....xxx.....xxx....xx...xx
.....x....xx.........xx....x....x
@...xx...xx...xx.xx...xx...xx...x
....x........xx...xx...x....x...x
xxxxxxxxxxxxxxx.{.xx...x........x
x............xx...xx...xxxxxx...x
x...xxxxxxx...xxxxx...xx...xx...x
x....x....xx.........xx....x....x
xx...xx....xxx.....xxx....xx...xx
 x...........xxx.xxx.....xx....x
 xx....xxx.....x.......x.x....x
  xx..xx.xxxx..x....xxxx.....xx
   xxxx.....xxxxxxxxx.......xx
      xx.................xxxx
       xxxx...........xxxx
          xxxxxxxxxxxxx
ENDMAP

NAME:    dpeg_arrival_circle_labyrinth_c
TAGS:    arrival no_monster_gen no_rotate transparent
WEIGHT:  6
ORIENT:  float
SHUFFLE: ab, cd, ef, gh, ij, kl, mn
# It's important this subst comes after the shuffle, otherwise it's possible
# none of these passages will be opened, which can create a closet.
NSUBST:  X = 1:acgi / *:x
SUBST:   a=., c=., e=., g=., i=., k=., m=.
SUBST:   b=x, d=x, f=x, h=x, j=x, l=x, n=x
NSUBST:  . = 1:d / *:.
ITEM:    stone
MAP
          xxxxxxxxxxxxx
       xxxx.....h.....xxxx
    xxxx........h........xxxx
   xx.......xxXxxxxxx.......xx
  xx.....xXxx.......xxxx.....xx
 xx....xXx.............xxx....xx
 x....xx.....xxxxxxx.....gg....x
xx....i....xxx.....xxx....xx...xx
x....xxxxjjx.........xx....x....x
x...xx........xxxxx...xx...xx...x
x...x....xlllxx...xx...x....x...x
x...X....x...k..{..m...xffffx...x
x...x....x...xx...xx...x....e...x
x...xx...xx...xxnxx...xx...xxxxxx
x....c....xx...x.....xx....x.....
xx...xx....xxx.x...xxx....xx....@
 x....xx.....xxxxxxx.....xx......
 xx....xXx.....d.......xxx....xxx
  xx.....xxXx..d....xxxx.....xx
   xx.......xxxxxaxxx.......xx
    xxxx.......b.........xxxx
       xxxx....b......xxxx
          xxxxxxxxxxxxx
ENDMAP

##############################################################
# 35 x 20
# Zaba's second arrival vault ever
NAME:    arrival_zaba_pyramid
TAGS:    arrival
ORIENT:  float
KMASK:   a = no_monster_gen
SUBST:   a : .
SHUFFLE: ([{>
SUBST:   ([ = d
ITEM:    skeleton
SUBST:   ?:???!,  ?=?., ?=!, !:cx, ===cc+
MAP
.................@.................
.cccccc...cccccc...cccccc...cccccc.
.c....c...c....c...c....c...c....c.
.c.(..c...c.{..c...c..[.c...c..>.c.
.c....cc+cc....cc+cc....cc+cc....c.
.cc+cccaaacc+cccaaaccc+ccaaaccc+cc.
..!aaa+aaa+aaaa+aaa+aaaa+aaa+aaa!..
..!!??!???!????!?.?!????!???!??!!..
 ..!?.??.???.????.????.???.??.?!..
  .!!?..??..???.....???..??..?!!.
  @.!??...???...???...???...??!.@
   .!!???....????.????....???!!.
   ..!!!???....??.??....???!!!..
    ..!!!!???....?....???!!!!..
     ..!!!!!????...????!!!!!..
      ...!!!!!?..?..?!!!!!...
        ..!!!!???.???!!!!..
         ...!!!!!+!!!!!...
           ......@......
ENDMAP

##############################################################
# 23x17
# The large aboretum
NAME:   minmay_arrival_arboretum_large
WEIGHT: 2
TAGS:   arrival no_monster_gen
ORIENT: float
MONS:   plant / bush w:3, fungus col:any, fungus col:yellow, fungus col:red
MONS:   fungus col:white, fungus col:magenta, fungus col:darkgrey
KPROP:  ' = no_tele_into
SUBST:  ' = D111t, 1 : 1112, 2 = 1111112, 2 : 234567
FTILE:  D1234567t = floor_moss
COLOUR: D = green
SUBST:  " = .
KMASK:  D1234567 = no_item_gen
MAP
xxxxxxxxxxxxxxxxx
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
xxxxmmmmmmmmmxxxx
"""""""""""""""""
@"""""""{"""""""@
"""""""""""""""""
xxxxmmmmmmmmmxxxx
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
x'''''''''''''''x
xxxxxxxxxxxxxxxxx
ENDMAP

##############################################################
# 32x29
NAME:    minmay_arrival_shoal_huts
WEIGHT:  5
TAGS:    arrival transparent
ORIENT:  float
SHUFFLE: {[(
SUBST:   ([ = .
SUBST:   y = w:39 W:1, Y = A:5 w:1, W = W:19 .:1, A = W
TILE:    x = wall_pebble_lightgray
MAP
             ..........@.......
         .....WWWWW........WWW...
      ....WWWWYYYYYWW.....WYYYWW...
    ...WWWYYYYyyyyyYYW....WYyyYYWW..
   ..WWYYYyyywwwwwwyYW....xxxxxyYYW..
  ..WYYyyywwwwwwwwyYW....xx...xxyyYW..
  .WYyywwwwwwwwwwwyYW....x.....xwwyYW.
 ..WYywwwwwwwwwwyyyYW....+..(>.xwwyYW.
 .WYywwwwwwwwyyyYYYW.....x.....xwyYW..
 .WYywwxxxxxyYYYWWW......xx...xxwyYW.
..WYywxx...xxWWW........WWxxxxxwwyYW.
.WYywwx.....x.........WWYYYywwwwwyYW.
.WYywwx.>{..+........WYYyyywwwwwyYW..
.WYywwx.....x.WWW....WYywwwwwwwwyYW.
..WYywxx...xxWYYYW...WYywwwwwwwwyYW.
 .WYywwxxxxxYYyyYW....WYywwwwwwwyYW.
 .WYywwwwwwwyywwyYW...WYywwwwwwyYW..
 .WYywwwwwwwwwwwyYxx+xxYywwwwwyYW..
 ..WYywwwwwwwwwwwxx...xxywwwwwyYW.
  .WYywwwwwwwwwwwx.....xwwwwwwyYW..
  ..WYywwwwwwwwwwx..[..xwwwwwwwyYW.
   ..WYywwwwwwwwwx..>..xwwwwwwwyYW..
    ..WYyywwwwwwwxx...xxwwwwwwwwyYW.
     ..WYYyyywwwwwxxxxxwwwwwwwwyYW..
      ..WWYYYyyyywwwwwyyyywwwyyYW..
       ...WWWYYYYyyyyyYYYYyyyYYW..
         ....WWWWYYYYYWWWWYYYWW..
            .....WWWWW....WWW...
                .......  .....
ENDMAP

##############################################################################
# 25x25
NAME:   co_arrival_hexagon_in_a_hexagon
TAGS:   arrival transparent
ORIENT: float
WEIGHT: 1
MONS:   plant
MAP
     xxxxx@xxxxxxxxx
    xxxxxx.xxxxxxxxxx
   xxxxxxx.xxxxxxxxxxx
  xxxxxxxx.xxxxxxxxxxxx
 xxxxxxxxx.1111xxxxxxxxx
xxxxxxxxx1.....1xxxxxxxxx
xxxxxxxx1.......1xxxxxxxx
xxxxxxx1...WWW...1xxxxxxx
xxxxxx1...WWWWW...1xxxxxx
@........WWW{WWW........@
xxxxxx1...WWWWW...1xxxxxx
xxxxxxx1...WWW...1xxxxxxx
xxxxxxxx1.......1xxxxxxxx
xxxxxxxxx1.....1xxxxxxxxx
 xxxxxxxxx1111.xxxxxxxxx
  xxxxxxxxxxxx.xxxxxxxx
   xxxxxxxxxxx.xxxxxxx
    xxxxxxxxxx.xxxxxx
     xxxxxxxxx@xxxxx
ENDMAP

##############################################################################
NAME:   co_arrival_hall_of_pillars
TAGS:   arrival transparent
ORIENT: float
WEIGHT: 1
SUBST:  c : cccccvvb
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxx...{...xxxxxxxxxx
xxxxxxx.............xxxxxxx
xxxxx...c..c...c..c...xxxxx
xxxx...................xxxx
xxxx...................xxxx
xxx..c..c..c...c..c..c..xxx
xxx.....................xxx
xxx.....................xxx
xxx..c..c..c...c..c..c..xxx
xxx.....................xxx
xxx.....................xxx
xxx..c..c..G...G..c..c..xxx
xxx.....................xxx
xxx.....................xxx
xxx..c..c..c...c..c..c..xxx
xxx.....................xxx
xxx.....................xxx
xxx..c..c..c...c..c..c..xxx
xxx.....................xxx
xxx.....................xxx
xxx..c..c..c...c..c..c..xxx
xx.......................xx
x.........................x
...........................
...........................
@...@...@...@@@...@...@...@
ENDMAP

##############################################################################
NAME:    roderic_lyre
TAGS:    arrival no_monster_gen no_trap_gen no_rotate transparent
ORIENT:  float
WEIGHT:  1
SHUFFLE: EF
SUBST:   E = +
SUBST:   F = x
MAP
    xxxxxxxx+xxxxxxxx
  xxx....xxx.xxx....xxx
  xx.xxxx.xx.xx.xxxx.xx
  x.xx..xx.x.x.xx..xx.x
  x.x.xx.x.x.x.x.xx.x.x
  x.xx>x.x.E.F.x.x}xx.x
  xx.xxx.x.x.x.x.xxx.xx
  xxx...xx.x.x.xx...xxx
  xxxxxxxx.x.x.xxxxxxxx
   xxxxxx.xx.xx.xxxxxx
   xxxxx.xxx+xxx.xxxxx
   xxxx.xxx...xxx.xxxx
   xxx.xxxx.{.xxxx.xxx
   xx.xxxxx...xxxxx.xx
  xx.xxxxxxx+xxxxxxx.xx
  x.xxxxxxxx.xxxxxxxx.x
  x.xx...xxx.xxx...xx.x
  x.x.xxx.xx.xx.xxx.x.x
  x.x.x)xx.x.x.xx]x.x.x
  x.x.xx.x.F.E.x.xx.x.x
  x.xx..xx.x.x.xx..xx.x
  xx.xxxx.xx.xx.xxxx.xx
   xx....xxx.xxx....xx
    xxxxxxxx+xxxxxxxx
ENDMAP

##############################################################################
NAME:    sevenhm_arrival_forest_fort_large_1
TAGS:    arrival no_monster_gen no_pool_fixup no_rotate transparent
ORIENT:  southeast
WEIGHT:  3
SUBST:   z = W., I = IG, r : cccvt., E = wW, ' : @x', ' = @x
MONS:    rat w:20 / ball python / frilled lizard / bat / nothing w:25
SHUFFLE: {[(
SUBST:   ([ = G
MAP
xxx'WWWxxxxxxx''xxxxxxxxxxxxxxxxxxx
xxwwwwWWxxxxxx..xxxxxxxxxxxxxxxtxxx
wwwwcccccccccc..ccxxxxxxxxxxxxtttxx
EwwwWc.1..............xxxxxxxtttttx
EwwWWc...cccc+cc.cc+cccccxxxxcccctx
EwwwWcc++c.....c.c......cccccc.(ctx
xxwwwWW..c..1..cIczzz...+....+..ntx
@+.......cccccccccWWWzz.cccccc..ctx
.+.............1.cWWWWzzc....c+cctx
xxwwwwwW.........cccccccc........tx
WwwwwwwwWWwwwww.................ttx
wwwwWwwWWwrrrrwWW..............ttxx
EwwW..wWwrr..rrwwwwWW.........ttxxx
EwW.1..Wwr.{.................tttxxx
 E....WWwrr..rrwwwwwwwwW.....tttxxx
 ...tttwwwrrrrwwwwwwwWW..[.ttttxxxx
@.ttttttxxxxxxxxxxx........ttxxxxxx
 xtttttxxxxxxxxxxxxxxxtttttttxxxxxx
 xxxttxxxxxxxxxxxxxxxxxtttttxxxxxxx
   xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    sevenhm_arrival_forest_fort_large_2
TAGS:    arrival no_rotate no_monster_gen transparent
ORIENT:  northwest
SUBST:   T : TTTV, p = tt., G = GGGG., z = ...T
SHUFFLE: {[
SUBST:   [ = T
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xccccccccccccccccccctttttxxxx
xc....c...G...c....cttppttxxx
xc.{..c.G...G.+..[.cttpzpttxx
xc....c...T...c....cttp...ptx
xc....+.G...G.c....cttp...ptx
xccc..c.......c..ccctp....ptx
xxxcccc..G.G..cccctpp......px
xxtttcc.......ccttp.........x
xxp.ttccc...ccctp...........@
xxp..pptc+++ctpp....pppxxxxxx
xxp..............pxxxxxx
xxxxtt.......pttxxx
xxxxxtttt...pptxx
xxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    sevenhm_arrival_forest_fort_large_3
TAGS:    arrival transparent
ORIENT:  float
WEIGHT:  3
SUBST:   T : TTTV, p = tt., G = GGG.
NSUBST:  z = + / c, Z = + / c, X = + / c, > = > / .
SHUFFLE: {[(
SUBST:   ([ = .
MAP
            xxxxx@@@@xxxxxx
           xxtttp....pppp.xxx
          xx................xxxx
        xxx...pcccc+cccctt...ppxx
     xxxxp....cc.......cctp....pxx
    xxpp....ptc..czzzc..cttp....pxx
    xtp....ttcc..z...z..ccttp...ptx
    xtp....ttc>..z.(.z..>cttp....tx
    xpp....ttc>..z...z..>cttp....tx
   xxp...pccccccccccccccccccctt..txx
   xt...ttc....X...G...Z....cttp..txx
   xt...ttc.{..X.G...G.Z..[.cttp..ttxx
   xxp..ttc....X...T...Z....cttp...ptx
    xp..ttc....X.G...G.Z....cttp...ptx
    xt..ttccc..X.......Z..ccctp....ptx
    xt...tttcccc..G.G..cccctpp...pptxx
    xxt...ptttcc.......ccttp....pptxx
     xtp.....ttccc...ccctp.....pptxx
     xxxxt....pptc+++ctpp....pppxxx
        xxxp..............pxxxxxx
          xxxtt.......pttxxx
            xxtttt...pptxx
             xxxxxxxxxxxx
ENDMAP

##############################################################################
NAME:    sevenhm_arrival_forest_fort_large_4
TAGS:    arrival transparent
ORIENT:  northwest
SUBST:   p = 11., G = G..
NSUBST:  { = { / ., [ = [ / .
NSUBST:  + = 1:c / *=+
SHUFFLE: {[
SUBST:   [ = .
MONS:    plant / bush
MAP
xxxxxxxxxxxxxxxxxxxxxxx
xxccccxxxxxxxxccccxxxxx
xcc[[cccccccccc{{ccxxxx
xc{...+......c...[ccxxx
xc{...c......+...[cxxxx
xcc..cccccccccc..ccttxx
xxcc+cG..G...Gcc+c..ttxxxxxx
xxc..c.p.p.p..c..c...tttxxxx
xxc..c..p.p.p.c..c.....ttxxx
xxc..c.p.p.p.Gc..c......ttxx
xxc..cG.p.p.p.c..c.......txx
xxc..c.p.p.p..c..c.......txx
xxc..c..p.p...............xx
xxc+ccG..G...............xxx
xcc..ccccccc...............@
xc{...c....................@
xc{...+..........ttttxxxxxxx
xcc[[ccccccc...ttttxxx
xxccccxxxxxtttttxxxxxx
xxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Crop circle vaults (Roderic)
# Season indicated by colouring. Something special for tiles wanted? --dpeg
NAME:   roderic_crop_circles_arrival_small
TAGS:   arrival no_trap_gen transparent
ORIENT: float
MONS:   col:green plant, col:brown plant
COLOUR: . = brown
: if crawl.one_chance_in(5) then
SUBST:  1 = 2:100 1:20
: else
SUBST:  1 = 1:100 2:10
: end
KMASK:  ' = no_item_gen
MAP
11111111111111111
111111..{..111111
1111..11111..1111
111.111'''111.111
111.11'111'11.111
11.11'11'11'11.11
11.1'11'1'11'1.11
11.1'1'111'1'1.11
11.1'11'1'11'1.11
11.11'11'11'11.11
111.11'111'11.111
111.111'''111.111
1111..11111..1111
111111.....111111
11111111@11111111
ENDMAP

# This vault has over 400 monsters (as plants) -- can this be too many? --dpeg
NAME:   roderic_crop_circles_arrival_large
TAGS:   arrival no_trap_gen transparent
ORIENT: float
WEIGHT: 1
MONS:   col:green plant, col:brown plant
SUBST:  '=111.
COLOUR: . = brown
: if crawl.one_chance_in(5) then
SUBST:  1 = 2:100 1:20
: else
SUBST:  1 = 1:100 2:10
: end
MAP
111111.111111111'111111
1111.....11111.....1111
111.11111.111.11111.111
11.1111111.1.1111111.11
1.111111111.111111111.1
1.111111111.111111111.1
'.1111111..{..1111111.'
1.111111.11.11.111111.1
1.11111.111.111.11111.1
11.111.111.1.111.111.11
111.11.11.111.11.11.111
1111.....11111.....1111
111.11.11.111.11.11.111
11.111.111.1.111.111.11
1.11111.111.111.11111.1
1.111111.11.11.111111.1
'.1111111.....1111111.'
1.111111111.111111111.1
1.111111111.111111111.1
11.1111111.1.1111111.11
111.11111.111.11111.111
1111.....11111.....1111
111111'111111111'111111
ENDMAP

##############################################################################
# arrival octogonal star
NAME:   roderic_octagonal_star
TAGS:   arrival transparent
ORIENT: float
MAP
   xxxx+xxxxxxx+xxxx
   xxxx.........xxxx
   xxx...xxxxx...xxx
xxxxx.x.x.xxx.x.x.xxxxx
xxxx.xx.xx.x.xx.xx.xxxx
xxx.xxx.xxx.xxx.xxx.xxx
xx.xxxx.xx.x.xx.xxxx.xx
+......{..xxx.........+
x..xxxx.xxxxxxx.xxxx..x
x.x.xxx.xxxxxxx.xxx.x.x
x.xx.x.xxxxxxxxx.x.xx.x
x.xxx.xxxxxxxxxxx.xxx.x
x.xx.x.xxxxxxxxx.x.xx.x
x.x.xxx.xxxxxxx.xxx.x.x
x..xxxx.xxxxxxx.xxxx..x
+.........xxx.........+
xx.xxxx.xx.x.xx.xxxx.xx
xxx.xxx.xxx.xxx.xxx.xxx
xxxx.xx.xx.x.xx.xx.xxxx
xxxxx.x.x.xxx.x.x.xxxxx
   xxx...xxxxx...xxx
   xxxx.........xxxx
   xxxx+xxxxxxx+xxx
ENDMAP

##############################################################################

NAME:   lightli_arrival_farm
ORIENT: float
TAGS:   no_monster_gen arrival
MONS:   plant
KPROP:  F1 = no_tele_into
SUBST:  1 = 1:2 t:2 .:1
MARKER: F = lua:fog_machine { cloud_type = "flame", pow_min = 100, \
                              pow_max = 100, delay = 1, size = 1, \
                              excl_rad = -1 }
MAP
         xxxxxxx
         x..{..x
         x.....x
         x.....x
xxxxxxxxxxxx+xxxxxxxxxxxx
xWWWWWWWWWWW.WWWWWWWWWWWx
xW111111111W.W111111111Wx
xW111111111W.W111111111Wx
xWWWWWWWWWWW.WWWWWWWWWWWx
xWWWWWWWWW.....WWWWWWWWWx
xW1111111W.mmm.W1111111Wx
xW1111111W.mFm.W1111111Wx
xW1111111W.mmm.W1111111Wx
xWWWWWWWWW.....WWWWWWWWWx
xWWWWWWWWWWW.WWWWWWWWWWWx
xW111111111W.W111111111Wx
xW111111111W.W111111111Wx
xWWWWWWWWWWW.WWWWWWWWWWWx
xxxxxxxxxxxx+xxxxxxxxxxxx
ENDMAP

##############################################################################

NAME: nicolae_arrival_somewhat_less_tricky_arrival
ORIENT: float
TAGS: arrival no_monster_gen no_trap_gen transparent
SHUFFLE: GTt, ab, cd, ef, gh, ij, kl
SUBST: acegik = ., bdfhjl = x
MAP
@.......................@
.........xxx+xxx.........
..t....+xxxx.xxxx+....t..
......xx.xx..xx..xx......
....xxx..xx.xxx.xxxxx....
....x...xxx.a...x...x....
...xx.xxxxx.xbxxx.x.xx...
..+xx.xxxx..x.....x..x+..
..x.l....f.xx.xxxxxx..x..
.xxkxxxxex..x.xx...xxxxx.
.xxk...xexx+x+xx.x.x...x.
.xxx.x.xexx...xx.x.x.x.x.
.+.x.x.x..+.{.+..x.x.x.+.
.x.x.x.x.xx...xxhx.x.xxx.
.x...x.x.xx+x+xxhx...jxx.
.xxxxx...xx.x..xhxxxxjxx.
..x..xxxxxx.xx.g....i.x..
..+x..x.....x..xxxx.xx+..
...xx.x.xxxcx.xxxxx.xx...
....x...x...d.xxx...x....
....xxxxx.xxx.xx..xxx....
......xx..xx..xx.xx......
..t....+xxxx.xxxx+....t..
.........xxx+xxx.........
@.......................@
ENDMAP

#       arrival/large

NAME:   kennysheep_arrival_seaside
TAGS:   arrival no_monster_gen no_trap_gen transparent
ORIENT: southeast
KMONS:  p = plant
KFEAT:  W = open_sea
FTILE:  ' = floor_sand
FTILE:  .pt{ = floor_dirt
COLOUR: .p = brown
COLOUR: ' = yellow
MAP
             xxxxxxxxxxx
             xptp''wwwwW
             xx..''wwwwW
             xx...''wwwW
             +...t''wwwW
             +....''wwwW
             +...''wwwwW
             xxp..'wwwwW
             xx..'''wwwW
             x...'''wwwW
             xt..''wwwwW
             xt..''wwwwW
             xp..'''wwwW
             x....'''wwW
             xp...'wwwwW
xxxxxxxxxxxxxxt...''wwwW
xtttttttt.p.pt....''wwwW
xtttttttt........''wwwwW
xttttt{t.....t.p..''wwwW
xtttttp..........''wwwwW
xttttt.....''..'''wwwwwW
xttttt..'''''''''wwwwwwW
xttttttt''ww''wwwwwwwwwW
xttttwwwwwwwwwwwwwwwwwwW
xttttwwwwwwwwwwwwwwwwwwW
xtwwwwwwwwwwwwwwwwwwwwwW
xWWWWWWWWWWWWWWWWWWWWWWW
ENDMAP

################################################################################
NAME:       psy_arrival_wood_between_worlds
TAGS:       arrival no_monster_gen no_trap_gen no_pool_fixup transparent
ORIENT:     float
WEIGHT:     3
FTILE:      t.qG^P{ = floor_grass
COLOUR:     . = green
KMONS:      P = plant / bush
KFEAT:      ^ = teleport trap
KFEAT:      p = W
KMONS:      p = plant
NSUBST:     p = 10=p / W, q = 10=P / .
MAP
 xxxxxxxxx@@@xxxxxxxxx
xxtttttttx...xtttttttxx
xttpW.qttx...xttq.Wpttx
xtGWW^...........^WWGtx
xttpW.qtqqq.qqqtq.Wpttx
xxttttttttq.qttttttttxx
xxtttqPtttq.qtttPqtttxx
xttpW.qtqqq.qqqtq.Wpttx
xtGWW^...........^WWGtx
xttpW.qt.......tq.Wpttx
xxtttttt.......ttttttxx
xxtttttt...{...ttttttxx
xttpW.qt.......tq.Wpttx
xtGWW^...........^WWGtx
xttpW.qt.......tq.Wpttx
xxtttqPtt.ttt.ttPqtttxx
 xxxttttP.PtP.Pttttxxx
   xxxxt.^.t.^.txxxx
      xtWWWtWWWtx
      xtpWptpWptx
      xttGtttGttx
      xxtttxtttxx
       xxxxxxxxx
ENDMAP

NAME:   wander_arrival_estate
TAGS:   arrival no_monster_gen no_item_gen transparent
ORIENT: float
MAP
.................
.......T.T.......
........U........
.................
.................
..ccccc...ccccc..
..c...ccncc...c..
..c.G.......G.c..
..c...........c..
..ccc.......ccc..
....c...{...c....
....c.......c....
....c.......c....
....c.......c....
....c.......c....
....ccc...ccc....
......c+++c......
.................
.....t.....t.....
.................
.....t.....t.....
.................
.....t.....t.....
.................
.....t.....t.....
.................
........@........
ENDMAP

NAME:    wander_arrival_abandoned_jail
TAGS:    arrival no_monster_gen no_item_gen transparent
ORIENT:  float
ITEM:    human skeleton / orc skeleton, \
         goblin skeleton / gnoll skeleton, \
         kobold skeleton / ogre skeleton
SHUFFLE: {def
KFEAT:   x = iron_grate
MAP
 cc++cc
ccG..Gcc
cxx..xxc
c{+..+dc
cxx..xxc
cG....Gc
cxx..xxc
ce+..+dc
cxx..xxc
cG....Gc
cxx..xxc
ce+..+fc
cxx..xxc
ccG..Gcc
 cc++cc
ENDMAP

##############################################################################
NAME:    heliophobic_arrival_dark_side_of_the_moon
TAGS:    arrival no_monster_gen no_trap_gen transparent
ORIENT:  float
MARKER:  F = lua:fog_machine{ \
             pow_max = 32, delay_min = 20, delay_max = 200, \
             size = 7, start_clouds = 7, cloud_type = "black smoke"}
MAP
        xxxxxxxxx
      xxxxxxGxxxxxx
     xxx.........xxx
    xx.....F.......xx
   xx...............xx
  xx........xxxx.....xx
  xx......xxxxxxxx...xx
 xx......xxxxxxxxxx...xx
 xx.....xxxxxxxxxxxx...@
 xx.....xxxxxxxxxxxx..xx
 xx....xxxxxxxxxxxxxx.xx
 xG{...xxxxxxxxxxxxxx.xx
 xx....xxxxxxxxxxxxxx.xx
 xx....xxxxxxxxxxxxxx.xx
 xx.....xxxxxxxxxxxx...@
 xx.....xxxxxxxxxxxx..xx
  xx.....xxxxxxxxxx..xx
  xx......xxxxxxxx...xx
   xx.......xxxx....xx
    xx....F........xx
     xxx.........xxx
      xxxxxxGxxxxxx
        xxxxxxxxx
ENDMAP

##############################################################################
# 37x22
NAME:   beargit_arrival_diablo_church
TAGS:   arrival no_trap_gen no_item_gen no_monster_gen
ORIENT: float
MONS:   rat / nothing w:50
FTILE:  `t = floor_grass, 1'G_> = floor_limestone
COLOUR: ` = green, 1'G = white
KPROP:  ` = no_tele_into
SUBST:  V = VVVT, H = ....H
# Why elves..?
KITEM:  H = human skeleton / elf skeleton
MAP
       ccccccc
       ctttttc
       ct```tc
       ccnnncc
        c.{.c
        c...c
  ccccccc+++ccccccccc
  cV............+...+
  cV............ccccc
  ccc..cc...cc..ccc
  ccc..cc...cc..ccc
  ctc...........ctc
  ctn...........ntc
 cctc...........ctc
 cttc...........ctc
 ct`n...........ntccc
 cttc...........ctc1+
  ccc..cc...cc..cccHc
  ctc..cc...cc..ctcHc
  ctn...........ntcHc
  ctc...........ctcHc
  ctc...........ctc.cc
  ctn...........ntc..c
  ctc...........ctcc.c
 cccc..cc...cc..ccc..c
ccccc..cc...cc..ccc+cc
+...................c
+...................c
cccccccc+c+c+cccccccc
      cG'''''Gc
      c'''''''c
      cG''>''Gc
      cc'''''cc
       cc'''cc
        cc'cc
        ccncc
        ctttc
        ccccc
ENDMAP

NAME:   tekkud_arrival_beachfront_property
TAGS:   arrival no_monster_gen no_trap_gen no_pool_fixup
ORIENT: north
KMASK:  .T = no_item_gen
KFEAT:  z = open_sea
KFEAT:  _ = abandoned_shop
KFEAT:  D = W
MARKER: W = lua:fog_machine { pow_max = 8, delay_min = 50, delay_max = 200, \
                              size = 4, start_clouds = 1 }
FTILE:  '_t{ = floor_sand
COLOUR: ' = yellow
MAP
       zzzzzzzzzzzzzzzzzzzzz
      xzwwwwwwwwwwwwwwwwwwwzx
    xxxwwwwwwDWWWDWWWDwwwwwwxxx
   xxxxwwwWWWWWWWWWWWWWWWDwwwxxxxxx
  xxxxxwWDWWWWW'''''''WWWWWWDxxxxxxx
 xxxxxWWWWWWW''''''''''''WWWWWxxxxxxx
 xxxxDWWW'''''''''{''t'_'''WWWWDxxxxx
xxxxWWW''''''_'''''''''''''''''WWWxxxx
xxx'''''''_'''''t'''t''''_''t''''''xx
xx''t''_''''''''''''''''''''''_''''xx
 x''''''''''.............''''''''t'xx
xx''_''''..................''t'''''x
xxx'''........................'''''x
 @@................................@
  @@..............................@@
   xxxx........................xxxx
    xxxx......T........T.....xxxxx
     xxxxxx................xxxxxx
      xxxxxxxxxx......xxxxxxxxx
          xxxxxxxx++xxxxxxx
ENDMAP

# 25 x 25
NAME:   orjb1_clovarrival
TAGS:   arrival transparent no_monster_gen no_trap_gen
ORIENT: float
MONS:   bush
FTILE:  _1Wt = floor_grass
MAP
.......tttt...tttt
......t____t.t____t
.....t______t______t
.....t_____________t
.....t_____________t
..tttWt____11_____tWttt
.t___tWt__1111___tWt___t
t_____tWt1111111tWt_____t
t______tWt11111tWt______t
t______1tWt111tWt1______t
t______11tWtttWt111_____t
.t____1111tWWWt11111___t
..t__11111tW{Wt11111__t
.t___11111tWWWt1111____t
t_____111tWtttWt11______t
t______1tWt111tWt1______t
t______tWt11111tWt______t
t_____tWt1111111tWt_____t
.t___tWt__1111___tWt___t
..tttWt____11_____tWttt
.....t_____________t
.....t_____________t
.....t______t______t
......t____t.t____t
.......tttt...tttt
ENDMAP

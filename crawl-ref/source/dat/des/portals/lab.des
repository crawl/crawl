###############################################################################
# lab.des: minivaults particular to labyrinths.
#          There are two types: labyrinth exits (tagged by 'minotaur') and
#                                flavour vaults (tagged by 'lab').
###############################################################################


#############################################################################
# Labyrinth entry vaults

{{
function lab_portal(e)
  local messager =
    timed_msg {
      initmsg = { "You hear a distant snort.",
                  "Hark! There is an entrance to a minotaur's labyrinth "
                  .. "on this level. Find the entrance quickly before "
                  .. "the gate is sealed!" },
      finalmsg = "You hear the last, dying ticks of the clock.",
      verb = 'ticking',
      noisemaker = 'clock'
    }

    e.lua_marker('O',
      timed_marker {
        low = 400,
        high = 600,
        msg = messager,
        floor = 'stone_arch',
        feat_tile = 'dngn_portal_labyrinth_gone',
        single_timed = true,
        dstname = 'lab' })
    e.kfeat("O = enter_labyrinth")
    e.tile("O = dngn_portal_labyrinth")
end
}}

NAME:   lab_entry_generic
TAGS:   transparent extra uniq_lab
# (Jan 2010) With the reduction in placement of Labyrinths to 10-20 (roughly
# across 24 levels) and changing to game-unique, chance of NOT having a
# Labyrinth was (1-285/10^4)^24 = ~49%. Increasing chance to 5% gives you
# (1-500/10^4)^24 = ~29% chance of NOT placing a Labyrinth, ~71% chance
# of getting a Labyrinth every game, skewed towards earlier depths.
# (Mar 2013) A long while later, there is general agreement that something
# should be changed about labyrinths to address perceived tedium.
# For now, trimming spawning depths to keep the minotaur more dangerous.
CHANCE: 5%
DEPTH:  D:11-, Orc:$, Lair
: lab_portal(_G)
MAP
O
ENDMAP

NAME:   lab_entry_trowel
TAGS:   transparent trowel_portal unrand can_overwrite
DEPTH:  *, !Lab
veto {{ return you.is_level_on_stack("Lab") }}
: lab_portal(_G)
MAP
O
ENDMAP

default-depth: Lab
#############################################################################
# Labyrinth exit minivaults
#############################################################################
# These are generated by the TAG: minotaur.
# You *must* place the minotaur yourself! Only one minotaur per map, please.
# There must be an exit (<), leading back to the dungeon.
#
# You can use the "generate_loot" tag to indicate that you're not explicitly
# placing the loot and that the dungeon builder should generate random loot
# (on the upstair). Note that this is not the default, and if you neither use
# this tag nor provide loot in the map definition, the player will be
# disappointed.
#
# One layer of floor space *must* surround the minivault, or the player could
# be trapped in the labyrinth (the dummy is exempt from this requirement).
#############################################################################

#############################################################################
# Dummy balancer
NAME: labyrinth_0
TAGS: dummy
ORIENT: float
WEIGHT: 20
MAP
x
ENDMAP

#############################################################################
# Watery exit
NAME:    labyrinth_watery
TAGS:    generate_loot no_pool_fixup allow_dup
ORIENT:  float
MONS:    patrolling minotaur
SHUFFLE: def
SUBST:   d=~, e=~, f=., b:vvb
MAP
.........
.bbbbbbb.
.bwwwwwb.
.bww<wwb.
.bwdefwb.
.bbb1bbb.
.b.....b.
.bbb+bbb.
.........
ENDMAP

#############################################################################
# Green exit
NAME:   labyrinth_green
TAGS:   generate_loot allow_dup
ORIENT: float
MONS:   patrolling minotaur
WEIGHT: 2
MAP
........
.bbbbbb.
.+..1<b.
.bbbbbb.
........
ENDMAP

#############################################################################
# Spiral exit
NAME:   labyrinth_spiral
TAGS:   generate_loot allow_dup
ORIENT: float
MONS:   patrolling minotaur
SUBST:  b : bvz, z = vb
MAP
............
.bbbbbbbbbb.
.b........b.
.b.bbbbbb.b.
.b.b<1..b.b.
.b.bbbb.b.b.
.b......b.b.
.bbbbbbbb.b.
..........b.
.bbbbbbbbbb.
............
ENDMAP

#############################################################################
# Hidden exit, and trapped loot
NAME:   labyrinth_hidden_loot
TAGS:   generate_loot allow_dup
ORIENT: float
MONS:   patrolling minotaur, minotaur zombie
SUBST:  d = 2%*
SUBST:  b : bvv
MAP
............
.bbbbbbbbbb.
.bxxxxdxxxb.
.bxxxx+xxxb.
.bxx..U.xxb.
.bd+U...xxb.
.bxx...U+db.
.bxx.<..xxb.
.bxxxx+xxxb.
.bxxxx1xxxb.
.bbbbb+bbbb.
............
ENDMAP

#############################################################################
# Mini labyrinth exit
NAME:    labyrinth_mini_lab
TAGS:    generate_loot allow_dup
ORIENT:  float
SHUFFLE: def, ghi, klm
SUBST:   d : b, e : ., f : b
SUBST:   g : b, h : ., i : b
SUBST:   k = <, l = ., m = .
KMONS:   < = patrolling minotaur
KFEAT:   < = <
SUBST:   b : vvb

# should not be necessary
validate {{ return has_exit_from_glyph('<') }}

MAP
...............
.bbbbbbbbbbbbb.
.bm....k....lb.
.bgbebbbbdbbfb.
.b.b.........b.
.b.bbbfb.bbbbb.
.b.....b.b...b.
.bbebb.b.h.bfb.
.b...b.b.iibfb.
.b.b.b.b.f.bfb.
.b.b.g.b.b.bfb.
.b.b.bdbhb.bfb.
.b.b.......gfb.
.bdbbbebbbbbfb.
...............
ENDMAP

#############################################################################
# Chooses four corpses, each with their own set of loot, themed around some
# particular archetypical species/background. Each set of loot has at least
# three guaranteed items.
# The dead octopode has a 62% chance of generating only 3 rings,
# a 93% chance of generating 4 or less rings, and a 99% chance of
# 5 or less. The chance of all 8 is 0.0006%.

NAME: nicolae_lab_dead_adventurers
TAGS: allow_dup
ORIENT: float
KMONS: m = patrolling minotaur
KPROP: ABCD'm = bloody
KFEAT: ' = floor
SHUFFLE: bxvc, ABCDEFHIJKLM
{{
 -- Dwarven fighter. Maybe the minotaur got the last MD. 3 items.
dgn.delayed_decay(_G, 'A', 'dwarf skeleton, battleaxe race:dwarven / '
   .. "eveningstar race:dwarven / executioner's axe race:dwarven w:2 / "
   .. 'great mace race:dwarven w:2, helmet good_item / '
   .. 'pair of gloves good_item / pair of boots good_item, '
   .. 'plate armour race:dwarven / crystal plate armour w:2' )

 -- Elf caster. 4 items.
dgn.delayed_decay(_G, 'B', 'elf skeleton, robe race:elven, randbook, '
   .. 'any magical staff, any jewellery' )

 -- Spriggan stabber. 4 items.
dgn.delayed_decay(_G, 'C', 'spriggan skeleton, dagger good_item / '
   .. 'quick blade w:1, bread ration, cloak, randbook disc:hexes / any book' )

 -- Centaur hunter of Fedhas. 3-4 (stacks of) items.
dgn.delayed_decay(_G, 'D', 'centaur skeleton, bow good_item / longbow w:2, '
   .. 'arrow good_item, apple / orange / pear / apricot / lemon / nothing, '
   .. 'helmet / centaur barding / pair of gloves' )

 -- Dwarven artificer. 3 items.
dgn.delayed_decay(_G, 'E', 'dwarf skeleton, any wand, any wand, '
   .. 'any wand / any misc / any rod' )

 -- Dead octopode. 3 rings, possibly up to 8, though with a very low chance.
dgn.delayed_decay(_G, 'F', 'octopode corpse, any ring good_item, '
   .. 'any ring good_item / any ring, any ring, any ring w:1 / nothing w:10, '
   .. 'any ring w:1 / nothing w:10, any ring w:1 / nothing w:10, '
   .. 'any ring w:1 / nothing w:10, any ring w:1 / nothing w:10' )

 -- Merfolk skald. 3 items.
dgn.delayed_decay(_G, 'H', 'merfolk skeleton, leather armour good_item w:30 / '
   .. 'swamp dragon armour w:2 / fire dragon armour w:2 / '
   .. 'ice dragon armour w:2, trident / demon trident / glaive / bardiche, '
   .. 'randbook disc:charms / any book' )

 -- Human wanderer. 3-5 items.
dgn.delayed_decay(_G, 'I', 'human skeleton, any armour, any weapon, '
   .. 'any good_item / any book / any jewellery, '
   .. 'nothing / any scroll, nothing / any potion' )

 -- Orc fighter. 3 items.
dgn.delayed_decay(_G, 'J', 'orc skeleton, shield / large shield w:2, '
   .. 'long sword race:orcish / eveningstar race:orcish / '
   .. 'broad axe race:orcish / demon blade, '
   .. 'plate armour race:orcish / crystal plate armour w:2' )

 -- Dwarven heavy armour earth elementalist. 3-4 items.
dgn.delayed_decay(_G, 'K', 'dwarf skeleton, plate armour race:dwarven, '
   .. 'randbook disc:earth / any book, staff of earth / any magical staff, '
   .. 'ring of wizardry / any jewellery / nothing w:20' )

 -- Halfling warper. 4-5 items.
dgn.delayed_decay(_G, 'L', 'halfling skeleton, dart ego:dispersal / dart, '
   .. 'randbook disc:translocation / any book, scroll of blinking / '
   .. 'scroll of teleportation / ring of teleportation / '
   .. 'ring of teleport control w:5 / nothing w:30, robe / leather armour, '
   .. 'short sword good_item' )

 -- Ogre, maybe an ogre mage of some kind. 4-5 items.
dgn.delayed_decay(_G, 'M', 'ogre skeleton, giant club good_item / '
   .. 'giant spiked club good_item, robe, large rock, meat ration, '
   .. 'any book / nothing' )
}}
MAP
...........
.xxxxxxxxx.
.xGAGBG.Gx.
.x<'''m..+.
.xGCGDG.Gx.
.xxxxxxxxx.
...........
ENDMAP

#############################################################################
# Labyrinth flavour minivaults
#############################################################################
# One layer of floor space *must* surround the minivault, or the player could
# be trapped in the labyrinth (the dummy is exempt from this requirement).
#
# These minivaults can be placed anywhere onto the labyrinth, making for
# easier navigation (as the number of connections increases) but can also add
# to confusion or despair (use teleportation very sparingly, and abstain from
# unthematic monster sets).
#############################################################################

############################################################################
# Labyrinth dummy decorator
NAME: lab_dummy
TAGS: dummy
WEIGHT: 90
MAP
x
ENDMAP

############################################################################
# Labyrinth furniture
NAME:    lab_block
TAGS:    allow_dup
SHUFFLE: vcx
MAP
.....
.xxx.
.xxx.
.xxx.
.....
ENDMAP

############################################################################
# Labyrinth furniture II
NAME: lab_fountain
TAGS: allow_dup
MAP
.......
..b.b..
.bb.bb.
...T...
.bb.bb.
..b.b..
.......
ENDMAP

############################################################################
# Labyrinth hedge
NAME:    lab_hedge
TAGS:    allow_dup
SHUFFLE: 1l
MONS:    plant
MAP
.......
.11111.
.1ccc1.
..1c1..
..1c1..
..1c1..
.1ccc1.
.11111.
.......
ENDMAP

############################################################################
# Labyrinth furniture
NAME:    lab_statue
TAGS:    allow_dup
MAP
...
.G.
...
ENDMAP

############################################################################
# Teaser: inaccessible loot
NAME:  labyrinth_glass_1
TAGS:  allow_dup
SUBST: % = %*
MAP
......
.mmmm.
.m%%m.
.m%%m.
.mmmm.
......
ENDMAP

NAME:   labyrinth_glass_2
TAGS:   allow_dup
WEIGHT: 1
MAP
......
.nnnn.
.n||n.
.n||n.
.nnnn.
......
ENDMAP

############################################################################
# The other minotaur's lava lair
NAME: labyrinth_lava_lair
TAGS: allow_dup
MONS: minotaur zombie
MAP
.......
.lllll.
.l***l.
.l*1*l.
.l***l.
.lllll.
.......
ENDMAP

############################################################################
# Baited teleport trap - this is evil!
NAME:    labyrinth_baited_teleportation_trap
TAGS:    allow_dup
KFEAT:   Y = known teleport trap / known zot trap / .
KITEM:   Y = any good_item
SHUFFLE: cxv
WEIGHT:  1
NSUBST:  = = 1:+ / *:+xx
MAP
.....
.x=x.
.=Y=.
.x=x.
.....
ENDMAP

############################################################################
# Teaser: fake exit
NAME:  labyrinth_fake_exit
TAGS:  allow_dup
KFEAT: X = exit_through_abyss
MAP
........
.vvvvvv.
.v...Xv.
.v.vvvv
.v....v.
.vvvv+v.
........
ENDMAP
# Disheartened?

############################################################################
# A few monsters: Nothing is as it seems.
NAME:   labyrinth_single_monster
TAGS:   allow_dup generate_awake
WEIGHT: 20
KFEAT:  x = .
KMONS:  x = trapdoor spider / w:2 wandering mushroom
MAP
x
ENDMAP

# Death by starvation?
NAME:   labyrinth_hungry_ghost
TAGS:   allow_dup generate_awake
WEIGHT: 40
KFEAT:  x = .
KMONS:  x = hungry ghost
MAP
x
ENDMAP

############################################################################
# The remains of a previous adventurer.
# No items. Maybe the minotaur got them.
NAME:   labyrinth_skeletal_remains
TAGS:   allow_dup
WEIGHT: 40
KFEAT:  x = .
: dgn.delayed_decay(_G, 'x', 'human skeleton / troll skeleton / centaur skeleton / ogre skeleton')
MAP
x
ENDMAP

############################################################################

# Some basic furniture.

NAME: nicolae_lab_plus
TAGS: allow_dup
SUBST: x : cvxb
MAP
  ...
 ..x..
...x...
.xxxxx.
...x...
 ..x..
  ...
ENDMAP

NAME: nicolae_lab_t_cross
TAGS: allow_dup
SUBST: x : cvxb
MAP
  .....
  .xxx.
....x....
.x..x..x.
.xxxxxxx.
.x..x..x.
....x....
  .xxx.
  .....
ENDMAP

NAME: nicolae_lab_arrow
TAGS: allow_dup
SUBST: x : cvxb
MAP
  ....
 ...x..
.....x..
.xxxxxx.
.....x..
 ...x..
  ....
ENDMAP

NAME: nicolae_lab_dead_end
TAGS: allow_dup
SUBST: x : cvbx
MAP
.......
.xxx.x.
.x.x.x.
.x.x.x.
.x...x.
.xxxxx.
.......
ENDMAP

NAME: nicolae_lab_very_dead_end
TAGS: allow_dup
SUBST: ' = ''.
KPROP: z' = bloody
: dgn.delayed_decay(_G, 'z', 'human skeleton / elf skeleton / dwarf skeleton')
MAP
.......
.xxxxx.
.x'''x.
.x'z'x.
.x'''x.
.xx.xx.
.......
ENDMAP

# Maybe it's time to try starting over from someplace else?
NAME: nicolae_lab_do_over
TAGS: allow_dup
KFEAT: t = known teleport trap
MAP
.....
.x.x.
..t..
.x.x.
.....
ENDMAP

NAME: nicolae_lab_lava
TAGS: allow_dup
MAP
....
.ll.
.ll.
....
ENDMAP

NAME: nicolae_lab_garden
TAGS: allow_dup
MONS: plant w:3 / nothing
SUBST: w = wW
MAP
11111
1www1
1wtw1
1www1
11111
ENDMAP

NAME: nicolae_lab_stuck
TAGS: allow_dup
: dgn.delayed_decay(_G, 'v', 'human skeleton')
KITEM: w = wand of digging charges:0 / wand of teleportation charges:0
KPROP: w'v = no_tele_into
MAP
......
.mmmm.
.mv'm.
.m'wm.
.mmmm.
......
ENDMAP

##########
# These furniture vaults all have non-empty outlines.
# They're still traversible, though: every spot next to the vault
# orthogonally is still adjacent to at least one empty square
# inside the vault, and all squares in the vault are connected.

NAME: nicolae_lab_checkerboard
TAGS: allow_dup
MAP
x.x.x
.x.x.
x.x.x
.x.x.
x.x.x
ENDMAP

NAME: nicolae_lab_starburst
TAGS: allow_dup
SUBST: m = .xGT
SUBST: x : cvxb
MAP
x..x..x
.x.x.x.
.......
xx.m.xx
.......
.x.x.x.
x..x..x
ENDMAP

NAME: nicolae_lab_diamond
TAGS: allow_dup
MONS: plant
SUBST: x : cvxb1
MAP
..x..
.x.x.
x...x
.x.x.
..x..
ENDMAP

NAME: nicolae_lab_quarters
TAGS: allow_dup
SUBST: m = .xGT
SUBST: x : cvxb
MAP
.xx.xx.
x..x..x
x..x..x
.xxmxx.
x..x..x
x..x..x
.xx.xx.
ENDMAP

NAME: nicolae_lab_blocks
TAGS: allow_dup
SUBST: x : cvxb
MAP
x.xx.xx.x
.........
x.xx.xx.x
x.xx.xx.x
.........
x.xx.xx.x
x.xx.xx.x
.........
x.xx.xx.x
ENDMAP

NAME: nicolae_lab_grate
TAGS: allow_dup
SUBST: x : cvxb
MAP
.x.x.x.x.x.
...x.x...x.
.x.x.x.x.x.
.x.x...x.x.
.x.x.x.x.x.
.x...x.x...
.x.x.x.x.x.
ENDMAP

NAME: nicolae_lab_grate_2
TAGS: allow_dup
SUBST: x : cvxb
MAP
x.x...x
..x.x..
x...x.x
x.x...x
..x.x..
x...x.x
ENDMAP

NAME: nicolae_lab_statue
TAGS: allow_dup
MAP
 .
.G.
 .
ENDMAP

NAME: nicolae_lab_x
TAGS: allow_dup
SUBST: x : cvxb
MAP
x.     .x
.x.   .x.
 .x. .x.
  .x.x.
   .x.
  .x.x.
 .x. .x.
.x.   .x.
x.     .x
ENDMAP

NAME: nicolae_lab_flowing_pool
TAGS: allow_dup
SHUFFLE: wP/wP/wT/lA
KFEAT: P = w
KFEAT: A = exit_through_abyss
MAP
 .ww. .w.
  ..w..w.
.w.ww.www.
w.w...w..
w..www..w
. .wPwww.
 wwwww.ww.
ww...w.w.
.. .w. .w
ENDMAP

NAME: nicolae_lab_hexagon
TAGS: allow_dup
MONS: plant
SUBST: m : Gxcvb1wl.
MAP
 .G.
G...G
..m..
G...G
 .G.
ENDMAP

NAME: nicolae_lab_swirl
TAGS: allow_dup
SUBST: x : cvxb
MAP
 .x.
  .x.   .
  .x. ..x
   .x.xx.
 ..xxx..
.xx.x.
x.. .x.
.   .x.
     .x.
ENDMAP

NAME: nicolae_lab_big_swirl
TAGS: allow_dup
SUBST: x : cvxb
MAP
  .x.
   .x.
   .x.     .
    .x.  ..x
    .x...xx.
   ..xxxx..
 ..xxxx..
.xx...x.
x..  .x.
.     .x.
      .x.
       .x.
ENDMAP

##############################################################################
# hive.des - Hive entry vaults and layouts.
#
# The hive offers food for the player, but they will have to face berserking
# bees and grab as much as they can before the larvae eat all the spoils and
# transform into bees themselves.
##############################################################################

##############################################################################
# Utility functions
##############################################################################

lua {{

function place_hive_portal(e, perm)
  e.kfeat("O = enter_portal_vault")
  e.tile("O = dngn_portal_hive")
  e.lua_marker('O', hive_portal())
end

function hive_portal()
  local d = "You hear a manifold buzzing. The waxy walls of this " ..
  "place are dripping with honey, but the larvae are hungry, and the queen " ..
  "bee will not tolerate intruders."
  return one_way_stair { desc = 'gateway to a hive', desc_long = d,
                           dst = 'hive', floor = 'stone_arch',
                           feat_tile = 'dngn_portal_hive_gone' }
end

function hive_message(e)
  crawl.mpr("You enter a furiously buzzing hive!")
end

function hive_milestone(e)
  crawl.mark_milestone("br.enter", "entered a Hive.", true)
end

function random_hive_colour()
  local colours = {"blue", "red", "lightblue", "magenta", "green"}
  return colours[crawl.random2(#colours) + 1]
end

function fixup_hive()
  dgn.fixup_stairs("stone_arch", "exit_portal_vault")

  if (dgn.get_floor_colour() == "black") then
    dgn.change_floor_colour(random_hive_colour())
  end

  if (dgn.get_rock_colour() == "black") then
    dgn.change_rock_colour("yellow")
  end

  dgn.change_floor_tile("floor_hive")
  dgn.change_rock_tile("wall_hive")
end

dgn.set_lt_callback("hive", "fixup_hive")

function hive_epilogue(e)
  e.hook("epilogue",
         function ()
           hive_message(_G)
           hive_milestone(_G)
         end)
end

}}

default-depth: D:11-16

##############################################################################
# Hive entries
##############################################################################

##############################################################################
# Standard hive
#
NAME: david_hive_1
TAGS: uniq_hive patrolling no_monster_gen
MONS: queen bee, killer bee, killer bee larva
KITEM: R = w:2 honeycomb / w:1 royal jelly
ORIENT: float
CHANCE: 5%
: place_hive_portal(_G)
MAP
............
....aaaa....
..a2a2aaaa..
..aaRa3a2a..
.aa2aRa2aaa.
.a3aOa1aRa2.
.aa3aRaRa2a.
.aaa2a2a3aa.
..a3aRa2aa..
...aa2aa2a..
....aaaa....
............
ENDMAP

##############################################################################
# Hive monsters near stair
#
NAME: david_hive_2
TAGS: uniq_hive patrolling no_monster_gen
MONS: nothing / killer bee / killer bee larva
MONS: queen bee
ORIENT: float
CHANCE: 5%
: place_hive_portal(_G)
MAP
1111..@.
1211....
1111....
............1
@.....O.....@
   ..........
   ......1
   .......
   1@.....
ENDMAP

#############################################################################
# Cooperation Not Competition (Rot)
#
NAME:    hive_david_3
TAGS:    uniq_hive patrolling no_monster_gen
ORIENT:  float
ITEM:    honeycomb, royal jelly
MONS:    killer bee larva / killer bee, queen bee
MONS:    worker ant / soldier ant, queen ant
MONS:    yellow wasp / nothing, red wasp / yellow wasp
MONS:    bumblebee
SHUFFLE: XY, VW, 34/56
NSUBST:  O = 1:O / *:de.
SUBST:   V==, W=a, X==, Y=a
SUBST:   ? = a .:20
SUBST:   1 : 1 7:2
SUBST:   7 = 7 .
CHANCE: 5%
: place_hive_portal(_G)
MAP
   .......   ...............
   ..??...   ..??aaa?aaa???.
...?aaa?..   .?aaOaaaaOaa?..
.?aaa.aa??.....?aa=.aa=Oaa?.
.aa333.aaa.......a?a11aaa?..
.aaO43..=...T...a.1aOa1.a?..
.?a.33.aaa......aa.1a2a1aa?.
.?aa.3aa??.....aaaVa1a1aYaa.
..?aaaa?.......?aaOW..aXOaa.
   ????.........?aaaaaaaaa?.
   ......     ...??aaa?a??
   ......     ............
ENDMAP

##############################################################################
# Bears Won't Share
#
NAME:    erik_hive_1
TAGS:    uniq_hive patrolling no_monster_gen
ORIENT:  float
MONS:    polar bear / grizzly bear w:200 / black bear w:100
MONS:    killer bee, queen bee, killer bee larva
ITEM:    honeycomb, royal jelly
SUBST:   ? = a.
SUBST:   ; = 1:1 .
SHUFFLE: deO
CHANCE: 5%
: place_hive_portal(_G)
MAP
  ...............
 ;;;;;...?aaa???.
.;;;;;..aaa2aaaaa..
.;;;;;.?a2e4d342a?.
.;;;;;.?a4?2?4O4a?.
.;;;;;..?a42?a2a...
.;;;;;..?aaaaaa?
 ;;;;;....????..
  ..............
ENDMAP

##############################################################################
# Bucolic
#
NAME:   erik_hive_2
TAGS:   uniq_hive patrolling no_monster_gen
ORIENT: float
MONS:   plant, killer bee
SUBST:  1 = 1.
SUBST:  2 = 2.
CHANCE: 5%
: place_hive_portal(_G)
MAP
 1.....2.......1.1
 ..1.....1...2..1.
 ....2.........1..
 1.2....1....2.....2
 .....2.....1....21
 1...1...1.........
  ...2.....1.21...2
  .....1.O......21.
  .1.......1.......
  .2.....1....1..2.
  ...11.2....2...11
  .1..1....1......
    2.......2....1
    1...1.....1
ENDMAP

##############################################################################
# Object lesson
#
NAME:    erik_hive_3
TAGS:    uniq_hive patrolling no_monster_gen
MONS:    killer bee larva, killer bee, queen bee
MONS:    fire drake / w:5 nothing
SHUFFLE: UVTXYZ, lL4/lL4/lL4/wW5
SUBST:   l=.l, L=l, w=.w, W=w, U==, V=a, T=a, X=a, Y=a, Z=a, '=.a
KMONS:   l = fire drake / w:200 nothing
KFEAT:   l = l
ORIENT:  float
CHANCE: 5%
: place_hive_portal(_G)
MAP
 .............
.'.aa'aa.'.'....
..aaRa'...a'.'a..
.'a2'Y.aaaX.aa.'..
.aV.1aaa'1aaa.'.a.
..aa21'2O2aaa4.aa.
.'.Zaa2131Waaaa...
.'aaaaaRaUa..aa'..
..aa.4aaa.a.'.a...
.aa...aaa.aa......
.a..aaaa.'........
..''..a..a.llllll
 ...'..aaa.lLLLLl
    ...''..llllll
       ........
ENDMAP

#############################################################################
# Another hive entry (Eino)
# This one appeared to me in a dream.
#
NAME:      eino_hive_1
TAGS:      uniq_hive patrolling no_monster_gen
ORIENT:    float
MONS:      killer bee / killer bee larva w:2
MONS:      queen bee / killer bee w:2
MONS:      oklob plant / plant w:1
MONS:      plant / nothing
KITEM:     R = w:2 honeycomb / w:1 royal jelly
KMONS:     R = killer bee / killer bee larva w:2
KFEAT:     R = .
KMONS:     M = plant
KMONS:     N = plant
KFEAT:     M = *
KFEAT:     N = | / * w:20
KMONS:     Z = plant / nothing
: if crawl.coinflip() then
SUBST:     W : .
SUBST:     w : .
KFEAT:     Z = W
: else
KFEAT:     Z = .
: end
SUBST:     q=w
CHANCE: 5%
: place_hive_portal(_G)
MAP
   x   x
 xxx...xxx
xx4.a1a.4xx
x4.aRaRa.4x
x441aOa244x
x4.aRaRa.4x
xx4.a1a.4xx
 x4.....4x
 xxZ4.44xx
  xZWW.4x
  xxZWZxx
   xxWxx
    xWx
xxxxxWxxxxx
@....W....@
xxxxxWxxxxx
    xWx
   xxWxx
  xx4W4xx
  x4.WW4x
 xx4.WWZxx
 xM4WWww4x
xxN3wwwwwxx
xM4wwwwwqqx
x4.wwwwqqqx
x4..wwqqqqx
xx...44qqxx
 xxx..4xxx
   x   x
ENDMAP

##########################################
# Bees in trees by the pond
NAME:      eino_hive_2
TAGS:      uniq_hive patrolling no_monster_gen
ORIENT:    float
KFEAT:     N = w / w:5 .
KFEAT:     Z = w
KMONS:     Z = plant / w:3 nothing
KFEAT:     V = w
KMONS:     V = killer bee w:20 / nothing w:6 / vampire mosquito w:1
MONS:      plant / nothing w:3
MONS:      killer bee w:20 / nothing w:6 / vampire mosquito w:1
CHANCE: 5%
: place_hive_portal(_G)
MAP
     ....  ...NNNZNN.
  ....11.....NZZwwZZNN.
 . 11.2111.1NZwwVwwwwwN
.1.1.11.22ZZw.wwwwVwwwZN
..1212211ZZOZwwwwwwwVZZ.
.1..111.21wV1.ZwVwwwZNN
 ..1.2211.1Zw1.ZwwZNN.
  ...11.1...  11.NN..
     ....
ENDMAP

###############################################################################
# Hive portal vaults
###############################################################################

########################################################
# Combs of tragedy (mumra)
# Hive portal map
NAME:     hive_mumra_combs
TAGS:     hive
ORIENT:   encompass
# Guarantee a certain amount of combs and larva to demonstrate
# NSUBST:   % : %:5 / . # FIXME: syntax error
# NSUBST:   ! : !:4 / .
KITEM: % = honeycomb
KMONS: ! = killer bee larva
# Perform rest of item/mons substitutions
KITEM: $ = honeycomb w:5 / nothing w:10
KITEM: * = royal jelly / honeycomb w:5 / nothing w:3
KMONS: 1 = killer bee larva w:3 / nothing
KMONS: 2 = killer bee / killer bee larva w:5 / nothing w:2
KMONS: 3 = queen bee
# Either b or c
SHUFFLE: bc/de
SUBST: c=a, e=a
NSUBST: b = 1:. / *:aa.
# Either d or e
NSUBST: d = 1:. / *:aa.
# At least 1 of f
NSUBST: f = 1:. / *:aa.
# At least 1 of g
NSUBST: g = 1:. / *:aa.
# At least 1 of h
NSUBST: h = 1:. / *:aa.
# At least 1 of i
NSUBST: i = 1:. / *:aa.
# At least 1 of j
NSUBST: j = 1:. / *:aa.
# At least 1 of k
NSUBST: k = 1:. / *:aa.
# At least 1 of l
NSUBST: l = 1:. / *:aa.
# At least 1 of m
NSUBST: m = 1:. / *:aa.
# At least 1 of n
NSUBST: n = 1:. / *:aa.
# At least 1 of o
NSUBST: o = 1:. / *:aa.
# At least 1 of p
NSUBST: p = 1:. / *:aa.
# At least 1 of q
NSUBST: q = 1:. / *:aa.
: hive_epilogue(_G)
MAP
             aaaaa     aaaaa
            aa.<>aa   aa...aa
           aa.....aaaaa..$..aa
          aa!%%%%%!aaa..$*$..aa
        aaaaa!%%%!aaaaa..$..aaaaa
       aa...bb!!!cc...dd...ee...aa
      aa..$..aaaaa..$..aaaaa..$..aa
     aa..$*$..aaa..$*$..aaa..$*$..aa
   aaaaa..$..aaaaa..$..aaaaa..$..aaaaa
  aa...ff.1.ff.1.gg.1.gg.1.hh.1.hh...aa
 aa.$$$.aaaaa.$$$.aaaaa.$$$.aaaaa.$$$.aa
aa.$***$.aaa.$***$.aaa.$***$.aaa.$***$.aa
 aa.$$$.aaaaa.$$$.aaaaa.$$$.aaaaa.$$$.aa
  aa1.1ii1.1jj...jj1.1kk1.1kk1.1ll1.1aa
   aaaaa.$$$.aaaaa.$$$.aaaaa.$$$.aaaaa
    aaa1$***$1aaa1$***$1aaa1$***$1aa
      aa.$$$.aaaaa.$$$.aaaaa.$$$.aa
       aa1.1mm...nn1.1nn...oo1.1aa
        aaaaa.....aaaaa.....aaaaa
         aaa111$111aaa111$111aa
           aa11111aaaaa11111aa
            aa111pp$$$qq111aa
             aaaaa11111aaaaa
               aa1122211aa
                aa22322aa
                 aa***aa
                  aaaaa
ENDMAP
